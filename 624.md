Boxy Ebony Rattlesnake

High

# Inflated lendSupplierIndex

### Summary
In the `Lendtroller`, the `lendSupplierIndex` is increased based on `lToken.totalSupply`. However, in the `CoreRouter::redeem` function, the LEND distribution is executed after redeeming from lToken and before deducting from `totalInvestment`. As a result, the `lendSupplierIndex` becomes inflated, meaning the required LEND for users is greater than the actual supplied LEND.

### Root Cause
The root cause is that LEND is distributed after the user's assets are redeemed from lToken.

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1262-L1265
```solidity
    function triggerSupplyIndexUpdate(address lToken) external {
        require(msg.sender == lendStorageAddress, "access denied");
        updateLendSupplyIndex(lToken);
    }
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1286
```solidity
    function updateLendSupplyIndex(address lToken) internal {
        LendMarketState storage supplyState = lendSupplyState[lToken];
        uint256 supplySpeed = lendSupplySpeeds[lToken];
        uint32 blockNumber = safe32(getBlockNumber(), "block number exceeds 32 bits");
        uint256 deltaBlocks = sub_(uint256(blockNumber), uint256(supplyState.block));
        if (deltaBlocks > 0 && supplySpeed > 0) {
            uint256 supplyTokens = LToken(lToken).totalSupply();
            uint256 lendAccrued = mul_(deltaBlocks, supplySpeed);
1286:       Double memory ratio = supplyTokens > 0 ? fraction(lendAccrued, supplyTokens) : Double({mantissa: 0});
            supplyState.index =
                safe224(add_(Double({mantissa: supplyState.index}), ratio).mantissa, "new index exceeds 224 bits");
            supplyState.block = blockNumber;
        } else if (deltaBlocks > 0) {
            supplyState.block = blockNumber;
        }
    }
```
In the `Lendtroller::updateLendSupplyIndex`, the `supplyState.index` is increased by `deltaBlocks * supplySpeed / lToken.totalSupply`.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L303-L335
```solidity
    function distributeSupplierLend(address lToken, address supplier) external onlyAuthorized {
        // Trigger supply index update
        LendtrollerInterfaceV2(lendtroller).triggerSupplyIndexUpdate(lToken);

        // Get the appropriate lend state based on whether it's for supply or borrow
        (uint224 supplyIndex,) = LendtrollerInterfaceV2(lendtroller).lendSupplyState(lToken);

        // Get the relevant indexes and accrued LEND for the account
        uint256 supplierIndex = lendSupplierIndex[lToken][supplier];

        lendSupplierIndex[lToken][supplier] = supplyIndex;

        // Update the account's index to the current index since we are distributing accrued LEND
        if (supplierIndex == 0 && supplyIndex >= LEND_INITIAL_INDEX) {
            // Covers the case where users borrowed tokens before the market's borrow state index was set.
            // Rewards the user with LEND accrued from the start of when borrower rewards were first
            // set for the market.
            supplierIndex = LEND_INITIAL_INDEX;
        }

        // Calculate change in the cumulative sum of the LEND per lToken accrued
        Double memory deltaIndex = Double({mantissa: sub_(supplyIndex, supplierIndex)});

        // Calculate the appropriate account balance and delta based on supply or borrow
327:    uint256 supplierTokens = totalInvestment[supplier][lToken];

        // Calculate LEND accrued: lTokenAmount * accruedPerLToken
        uint256 supplierDelta = mul_(supplierTokens, deltaIndex);

        // Update the accrued LEND for the account
        uint256 supplierAccrued = add_(lendAccrued[supplier], supplierDelta);
        lendAccrued[supplier] = supplierAccrued;
    }
```
In the `LendStorage::distributeSupplierLend`, LEND is distributed based on users' `totalInvestment`.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L100-L138
```solidity
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
        // Redeem lTokens
        address _token = lendStorage.lTokenToUnderlying(_lToken);

        require(_amount > 0, "Zero redeem amount");

        // Check if user has enough balance before any calculations
        require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");

        // Check liquidity
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
        require(collateral >= borrowed, "Insufficient liquidity");

        // Get exchange rate before redeem
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        // Calculate expected underlying tokens
        uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;

        // Perform redeem
121:    require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");

        // Transfer underlying tokens to the user
        IERC20(_token).transfer(msg.sender, expectedUnderlying);

        // Update total investment
127:    lendStorage.distributeSupplierLend(_lToken, msg.sender);
        uint256 newInvestment = lendStorage.totalInvestment(msg.sender, _lToken) - _amount;
129:    lendStorage.updateTotalInvestment(msg.sender, _lToken, newInvestment);

        if (newInvestment == 0) {
            lendStorage.removeUserSuppliedAsset(msg.sender, _lToken);
        }

        emit RedeemSuccess(msg.sender, _lToken, expectedUnderlying, _amount);

        return 0;
    }
```
In the `CoreRouter::redeem` function, LEND is distributed after the user's asset is redeemed from lToken and before updating `totalInvestment`.

As a result, the Lendtroller does not provide LEND for the redeeming user, but the CoreRouter accounts for the LEND for that user.

### Impact
1. Early claimants to receive more LEND, while later users receive nothing.
2. Posing a risk to governance roles

### Mitigation
