Winning Cinnabar Gorilla

Medium

# User may not be able to cross-chain borrow if they have already initiated a borrow from the destination chain

### Summary

User may not be able to cross-chain borrow if they have already initiated a borrow from the destination chain.

### Root Cause

When a user initiates a cross-chain from `ChainA` to borrow tokens on `ChainB`, `CrossChainRouter::_handleBorrowCrossChainRequest()` is triggered on `ChainB` when LayerZero is received, and `LendStorage::crossChainCollaterals` will be updated.

[CrossChainRouter.sol#L640-L653](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L640-L653):
```solidity
        } else {
@>          lendStorage.addCrossChainCollateral(
                payload.sender,
@>              destUnderlying,
                LendStorage.Borrow({
                    srcEid: srcEid,
                    destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
        }
```

[LendStorage.sol#L215-L220](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L215-L220):
```solidity
    function addCrossChainCollateral(address user, address underlying, Borrow memory newCollateral)
        external
        onlyAuthorized
    {
        crossChainCollaterals[user][underlying].push(newCollateral);
    }
```

[LendStorage.sol#L84](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L84):
```solidity
    mapping(address borrower => mapping(address underlying => Borrow[])) public crossChainCollaterals;
```

As a result, the length of `crossChainCollaterals[user][underlying]` becomes `1`.

Later, LayerZero message is received by `CrossChainRouter` on `ChainA`, and `CrossChainRouter::_handleValidBorrowRequest()` is triggered,  `LendStorage::crossChainBorrows` is updated.

[CrossChainRouter.sol#L716-L725](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L716-L725):
```solidity
        } else {
            // Add new borrow record
            lendStorage.addCrossChainBorrow(
                payload.sender,
@>              payload.srcToken,
                LendStorage.Borrow(
                    srcEid, currentEid, payload.amount, payload.borrowIndex, payload.destlToken, payload.srcToken
                )
            );
        }
```

[LendStorage.sol#L254-L256](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L254-L256):
```solidity
    function addCrossChainBorrow(address user, address underlying, Borrow memory newBorrow) external onlyAuthorized {
        crossChainBorrows[user][underlying].push(newBorrow);
    }
```

[LendStorage.sol#L83](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L83):
```solidity
    mapping(address borrower => mapping(address underlying => Borrow[])) public crossChainBorrows;
```

By then, the length of `crossChainBorrows[user][underlying]` becomes `1`.

The user status on both chains is as below:

|  Blockchain  | Underlying Address | Size of crossChainCollaterals | Size of crossChainBorrows |
|:-----------|:-------------:|:---------------:|:---------------:|
| ChainA | addrA | 0 | 1 |
| ChainB | addrB | 1 | 0 |

Supposing the user again initiates cross-chain borrow from `ChainB` to borrow the tokens on `ChainA`, then similarly, the status would be updated  as following:

|  Blockchain  | Underlying Address | Size of crossChainCollaterals | Size of crossChainBorrows |
|:-----------|:-------------:|:---------------:|:---------------:|
| ChainA | addrA | 1 | 1 |
| ChainB | addrB | 1 | 1 |

This is problematic as there is a check in `LendStorage::borrowWithInterest()` to ensure only one mapping (crossChainCollaterals or crossChainBorrows) should be populated for the same user and underlying asset.

[LendStorage.sol#L482-L485](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L482-L485):
```solidity
        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
```

As a result, the second cross-chain borrow would be reverted when protocol tries to distribute LEND rewards on destination chain.

[CrossChainRouter.sol#L658-L659](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L658-L659):
```solidity
        // Distribute LEND rewards on destination chain
        lendStorage.distributeBorrowerLend(payload.destlToken, payload.sender);
```

[LendStorage.sol#L364-L368](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L364-L368):
```solidity
        // Calculate the appropriate account balance and delta based on supply or borrow
        uint256 borrowerAmount = div_(
@>          add_(borrowWithInterest(borrower, lToken), borrowWithInterestSame(borrower, lToken)),
            Exp({mantissa: LTokenInterface(lToken).borrowIndex()})
        );
```

For example, a user supplies some DAI on `Ethereum` and initiates a cross-chain borrow to borrow WETH from `Base`, this cross-chain borrow will success. Then, the user supplies USDC on `Base` and initiates another cross-chain borrow to borrow WETH from `Ethereum`, the cross-chain borrow would eventually fail due to this issue.

### Internal Pre-conditions

User initiates a cross-chain borrow on `ChainB` to borrow tokens from `ChainA`, when they have already initiated a cross-chain borrow from `ChainA` to borrow tokens from `ChainB`.

### External Pre-conditions

NA

### Attack Path

NA

### Impact

User cannot cross-chain borrow tokens as expected.

### PoC

Please run `forge test --mt testPOC_CrossChainBorrowFail`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import "forge-std/Test.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

import {ILayerZeroEndpointV2} from "@layerzerolabs/lz-evm-protocol-v2/contracts/interfaces/ILayerZeroEndpointV2.sol";

import {Lend} from "../src/Governance/Lend.sol";
import {Lendtroller, LendtrollerInterface} from "../src/Lendtroller.sol";
import {LErc20Immutable, LToken} from "../src/LErc20Immutable.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel, InterestRateModel} from "../src/WhitePaperInterestRateModel.sol";

import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {CrossChainRouter, Origin} from "../src/LayerZero/CrossChainRouter.sol";

contract POC is Test {
    using SafeERC20 for IERC20;

    uint32 constant chainAEid = 30101; // Ethereum
    uint32 constant chainBEid = 30184; // Base

    address owner;
    address mockDestLToken;
    address mockPeer;

    IERC20 btc;
    IERC20 dai;
    IERC20 weth;

    LErc20Immutable lTokenBTC;
    LErc20Immutable lTokenDAI;
    LErc20Immutable lTokenWETH;

    Lend lend;

    ILayerZeroEndpointV2 endpoint;

    Lendtroller lendtroller;
    SimplePriceOracle priceOracle;
    WhitePaperInterestRateModel interestRateModel;

    LendStorage lendStorage;
    CoreRouter coreRouter;
    CrossChainRouter crossChainRouter;

    function setUp() public {
        vm.createSelectFork("https://eth.drpc.org");

        owner = makeAddr("Owner");
        mockDestLToken = makeAddr("MockDestLToken");
        mockPeer = makeAddr("ChainBPeer");
        endpoint = ILayerZeroEndpointV2(
            0x1a44076050125825900e736c501f859c50fE728c
        );

        btc = IERC20(0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599);
        dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
        weth = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

        vm.startPrank(owner);

        // Deploy Lendtroller
        lendtroller = new Lendtroller();
        // Deploy Lend
        lend = new Lend(address(lendtroller));
        // Deploy PriceOracle
        priceOracle = new SimplePriceOracle();
        // Deploy InterestRateModel
        interestRateModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);
        // Deploy lTokens
        lTokenBTC = new LErc20Immutable(
            address(btc),
            lendtroller,
            interestRateModel,
            1e8, // 1:1
            "Lending BTC",
            "lBTC",
            18,
            payable(owner)
        );
        lTokenDAI = new LErc20Immutable(
            address(dai),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending DAI",
            "lDAI",
            18,
            payable(owner)
        );
        lTokenWETH = new LErc20Immutable(
            address(weth),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending WETH",
            "lWETH",
            18,
            payable(owner)
        );

        // Deploy LendStorage
        lendStorage = new LendStorage(
            address(lendtroller),
            address(priceOracle),
            chainAEid
        );
        // Deploy CoreRouter
        coreRouter = new CoreRouter(
            address(lendStorage),
            address(priceOracle),
            address(lendtroller)
        );
        // Deploy CrossChainRouter
        crossChainRouter = new CrossChainRouter(
            address(endpoint),
            owner,
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            chainAEid
        );

        // Configurations
        {
            priceOracle.setUnderlyingPrice(lTokenBTC, 100000e18);
            priceOracle.setUnderlyingPrice(lTokenDAI, 1e18);
            priceOracle.setUnderlyingPrice(lTokenWETH, 2500e18);

            lendtroller._setPriceOracle(priceOracle);
            lendtroller.setLendStorage(address(lendStorage));
            lendtroller._supportMarket(lTokenBTC);
            lendtroller._supportMarket(lTokenDAI);
            lendtroller._supportMarket(lTokenWETH);
            lendtroller.setLendToken(address(lend));
            lendtroller._setCollateralFactor(lTokenBTC, 0.8e18);
            lendtroller._setCollateralFactor(lTokenDAI, 0.8e18);
            lendtroller._setCollateralFactor(lTokenWETH, 0.8e18);
            lendtroller._setCloseFactor(0.5e18);
            lendtroller._setLiquidationIncentive(1.08e18);

            LToken[] memory lTokens = new LToken[](3);
            lTokens[0] = lTokenBTC;
            lTokens[1] = lTokenDAI;
            lTokens[2] = lTokenWETH;
            uint256[] memory supplySpeeds = new uint256[](3);
            supplySpeeds[0] = 1e18;
            supplySpeeds[1] = 1e18;
            supplySpeeds[2] = 1e18;
            uint256[] memory borrowSpeeds = new uint256[](3);
            borrowSpeeds[0] = 2e18;
            borrowSpeeds[1] = 2e18;
            borrowSpeeds[2] = 2e18;
            lendtroller._setLendSpeeds(lTokens, supplySpeeds, borrowSpeeds);

            lendStorage.addSupportedTokens(address(btc), address(lTokenBTC));
            lendStorage.addSupportedTokens(address(dai), address(lTokenDAI));
            lendStorage.addSupportedTokens(address(weth), address(lTokenWETH));
            lendStorage.addUnderlyingToDestlToken(
                address(btc),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(dai),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(weth),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.setAuthorizedContract(address(coreRouter), true);
            lendStorage.setAuthorizedContract(address(crossChainRouter), true);

            crossChainRouter.setPeer(
                chainBEid,
                bytes32(uint256(uint160(mockPeer)))
            );

            coreRouter.setCrossChainRouter(address(crossChainRouter));

            deal(address(crossChainRouter), 1 ether);
        }

        vm.stopPrank();

        {
            vm.label(address(btc), "BTC");
            vm.label(address(dai), "DAI");
            vm.label(address(weth), "WETH");
            vm.label(address(lTokenBTC), "lToken BTC");
            vm.label(address(lTokenDAI), "lToken DAI");
            vm.label(address(lTokenWETH), "lToken WETH");
            vm.label(address(endpoint), "Endpoint");
            vm.label(address(lendtroller), "Lendtroller");
            vm.label(address(lend), "Lend");
            vm.label(address(priceOracle), "PriceOracle");
            vm.label(address(lendStorage), "LendStorage");
            vm.label(address(coreRouter), "CoreRouter");
            vm.label(address(crossChainRouter), "CrossChainRouter");
        }
    }

    function testPOC_CrossChainBorrowFail() public {
        // Supply liqudity
        {
            address supplier = makeAddr("Supplier");
            deal(address(weth), supplier, 2e18);

            vm.startPrank(supplier);
            weth.safeApprove(address(coreRouter), 2e18);
            coreRouter.supply(2e18, address(weth));
            vm.stopPrank();
        }

        address alice = makeAddr("Alice");

        // User initiates cross-chain borrow from Ethereum (currentEid)
        // Mock layerzero data to trigger `_handleValidBorrowRequest()`
        {
            Origin memory _origin = Origin({
                srcEid: chainBEid,
                sender: bytes32(uint256(uint160(address(mockPeer)))),
                nonce: 1
            });

            lTokenWETH.accrueInterest();
            bytes memory _payload = abi.encode(
                1e8, // amount
                lTokenWETH.borrowIndex(), // borrowIndex
                100000 * 1e8, // collateral
                alice, // sender
                mockDestLToken, // destLToken
                address(0), // liquidator
                address(weth), // srcToken
                uint8(CrossChainRouter.ContractType.ValidBorrowRequest) // contractType
            );

            vm.prank(address(endpoint));
            crossChainRouter.lzReceive(
                _origin, 
                bytes32(0), 
                _payload, 
                makeAddr("Executor"), 
                ""
            );
        }

        // User initiates cross-chain borrow from Base
        // Mock layerzero data to trigger `_handleBorrowCrossChainRequest()`
        {
            Origin memory _origin = Origin({
                srcEid: chainBEid,
                sender: bytes32(uint256(uint160(address(mockPeer)))),
                nonce: 1
            });

            bytes memory _payload = abi.encode(
                1e18, // amount
                0, // borrowIndex
                2500 * 1e18, // collateral
                alice, // sender
                address(lTokenWETH), // destLToken
                address(0), // liquidator
                makeAddr("WETH on Base"),
                uint8(CrossChainRouter.ContractType.BorrowCrossChain) // contractType
            );

            // Cross-chain borrow fail
            vm.prank(address(endpoint));
            vm.expectRevert(bytes("Invariant violated: both mappings populated"));
            crossChainRouter.lzReceive(
                _origin, 
                bytes32(0), 
                _payload, 
                makeAddr("Executor"), 
                ""
            );
        }
    }
}
```

### Mitigation

It is recommended to add a nested mapping to `crossChainCollaterals` and `crossChainBorrows`.

```diff
-   mapping(address borrower => mapping(address underlying => Borrow[])) public crossChainCollaterals;
+   mapping(address borrower => mapping(uint256 srcEid) => mapping(address underlying => Borrow[])) public crossChainCollaterals;
```

```diff
-   mapping(address borrower => mapping(address underlying => Borrow[])) public crossChainBorrows;
+   mapping(address borrower => mapping(uint256 srcEid) => mapping(address underlying => Borrow[])) public crossChainBorrows;
```