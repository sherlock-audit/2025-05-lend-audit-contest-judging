Zesty Bamboo Lobster

High

# No minimum Borrow Enforcement in the Protocol leads to small liquiditable positions which has no incentives to be liquidated by others

### Summary

In the borrow functionality of the protocol we don't have a minimum borrow i.e there should be an minimum amount of borrowable position under which no user can borrow.

The reason is if an extremely small position goes undercollateralized or become insolvent there will be no major incentives or no incentives at all for liquidators to liquidate those functions leading to bad debt accumulation.


Here are snippets of `borrow` function on CoreRouter and CrossChainRouter

[CoreRouter](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L145)

```solidity
    /**
     * @notice Borrows tokens using supplied collateral
     * @param _amount Amount of tokens to borrow
     * @param _token Address of the token to borrow
     */
    function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");

        address _lToken = lendStorage.underlyingTolToken(_token);

        LTokenInterface(_lToken).accrueInterest();

        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);

        uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;

        require(collateral >= borrowAmount, "Insufficient collateral");

        // Enter the Compound market
        enterMarkets(_lToken);

        // Borrow tokens
        require(LErc20Interface(_lToken).borrow(_amount) == 0, "Borrow failed");

        // Transfer borrowed tokens to the user
        IERC20(_token).transfer(msg.sender, _amount);  //@audit No boolean check

        lendStorage.distributeBorrowerLend(_lToken, msg.sender);

        // Update records
        if (currentBorrow.borrowIndex != 0) {
            uint256 _newPrinciple =
                (currentBorrow.amount * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex;

            lendStorage.updateBorrowBalance(
                msg.sender, _lToken, _newPrinciple + _amount, LTokenInterface(_lToken).borrowIndex()
            );
        } else {
            lendStorage.updateBorrowBalance(msg.sender, _lToken, _amount, LTokenInterface(_lToken).borrowIndex()); //@audit same bug, borrowIndex() changes after accrue() and we are updating it to the cached index
        }

        lendStorage.addUserBorrowedAsset(msg.sender, _lToken);

        // Emit BorrowSuccess event
        emit BorrowSuccess(msg.sender, _lToken, lendStorage.getBorrowBalance(msg.sender, _lToken).amount);
    }

```

[CrossChainRouter.sol](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113)



```solidity


    /**
     * @notice Initiates a cross-chain borrow. Initiated on the source chain (Chain A)
     * @param _amount Amount to borrow
     * @param _borrowToken Token to borrow on destination chain
     * @param _destEid Destination chain's layer zero endpoint id
     */
    function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
        require(msg.sender != address(0), "Invalid sender");
        require(_amount != 0, "Zero borrow amount");
        require(address(this).balance > 0, "Out of money");

        // Get source lToken for collateral
        address _lToken = lendStorage.underlyingTolToken(_borrowToken);
        require(_lToken != address(0), "Unsupported source token");

        // Get the destination chain's version of the token
        address destLToken = lendStorage.underlyingToDestlToken(_borrowToken, _destEid);
        require(destLToken != address(0), "Unsupported destination token");

        // Accrue interest on source token (collateral token) on source chain
        LTokenInterface(_lToken).accrueInterest();

        // Add collateral tracking on source chain
        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        if (!isMarketEntered(msg.sender, _lToken)) {
            enterMarkets(_lToken);
        }

        // Get current collateral amount for the LayerZero message
        // This will be used on dest chain to check if sufficient
        (, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
    }
```

Current Validation: Both functions only validate `_amount != 0`, allowing micro-borrows (e.g., 1 wei).


No minimum borrow amount enforcement is there and if those positions go undercollateralized there won't be any liquidators willing to repay them.


Malicious users can create borrow a lot of extremely small positions/micro positions which would get undercollateralized as soon as the market moves a little. 

In such cases the gas cost of repaying the debt will be higher than the amount itself.

It will lead to  a lot of bad debt getting accumulated in the protocol and no legitimate liquidator liquidating it.

### Root Cause

- No minimum borrow amount enforced.

### Internal Pre-conditions

-  A lot of extremely small positions are created with very small amount of collateral


### External Pre-conditions

- Collateral value fluctuates/Goes down a bit and position becomes undercollateralized.

### Attack Path

- A malicious user creates positions worth <$50 or extremely low as $5 in a calculated way which leads to the position getting undercollateralized as soon as the market fluctuates a little.

### Impact

- A lot of bad debt being created in the protocol.
- No liquidator would liquidate that position as gas costs would exceed the liquidator rewards and the liquidator incentives.
- Position remains unliquidated forever.
- Protocol health lowers down.


 **Example Economic Analysis using Compound's liquidation bonus threshold:**


2. Position value: $100 (undercollateralized)
3. Liquidation reward: $5.00 (Compound's 5% liquidation bonus)
4. Gas costs: $5-20 (depending on network congestion)
5. Net liquidator result: $0 to -$15.00

### PoC

- 

### Mitigation

Add to both CoreRouter and CrossChainRouter


```solidity
// Add to both CoreRouter and CrossChainRouter
mapping(address => uint256) public minimumBorrowAmount;

modifier validBorrowAmount(uint256 _amount, address _token) {
    require(_amount >= minimumBorrowAmount[_token], "Below minimum borrow amount");
    _;
}

function setMinimumBorrowAmount(address _token, uint256 _minAmount) external onlyOwner {
    minimumBorrowAmount[_token] = _minAmount;
}
```