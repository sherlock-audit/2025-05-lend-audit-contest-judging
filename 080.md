Massive Plastic Raven

Medium

# `CoreRouter:redeem()` function doesn't account for accrued interest which could lead to user losses

### Summary

`CoreRouter:redeem()` function uses `exchangeRateStored()` to calculate the underlying token amount to transfer to user and since it doesn't account for accrued interest user will get amount without latest accrued interest.

### Root Cause

In [CoreRouter.sol#L115-L124](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L115-L124) the redeem function uses the following calculation for the underlying tokens.

```solidity
  // @audit exchangeRateStored() doesn't account for interest
  uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
  uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
  // ....
   IERC20(_token).transfer(msg.sender, expectedUnderlying);
```

Implementation of `exchangeRateStored`
```solidity
    /**
     * @notice Calculates the exchange rate from the underlying to the LToken
     * @dev This function does not accrue interest before calculating the exchange rate
     * @return Calculated exchange rate scaled by 1e18
     */
    function exchangeRateStored() public view override returns (uint256) {
        return exchangeRateStoredInternal();
    }
```

This leads to user not getting the correct amount with accrued interest.

### Internal Pre-conditions

None

### External Pre-conditions

- users borrow tokens after the supplier deposits
- some time passes without accrueInterest() is called on LToken

### Attack Path

- User supplies some tokens
- Some time passes and exchangeRate goes up in the meantime
- accrueInterest() isn't called on Ltoken for a while
- Supplier decides to redeem tokens for underlying


### Impact

Supplier gets less underlying tokens due to redeem() function not accounting for interest. Depending on the amount of borrows and days passed since supply the losses will be higher.

### PoC

Paste following test inside `TestRedeeming.t.sol`
```solidity
function test_redeem_exchange_rate_difference() public {
    uint256 supplyAmount = 1000e18;
    
    vm.startPrank(deployer);
    // Initial supply
    (address token, address lToken) = _supply(supplyAmount);
    uint256 initialLTokenBalance = lendStorage.totalInvestment(deployer, lToken);
    vm.stopPrank();
    // Setup borrower
    address borrower = makeAddr("borrower");
    vm.startPrank(borrower);
    // Borrower supplies double the amount as collateral
    ERC20Mock(token).mint(borrower, supplyAmount * 2);
    IERC20(token).approve(address(coreRouter), supplyAmount * 2);
    coreRouter.supply(supplyAmount * 2, token);
    
    // Borrower takes out a large loan (75% of their collateral)
    uint256 borrowAmount = (supplyAmount * 2 * 75) / 100;
    coreRouter.borrow(borrowAmount, token);
    vm.stopPrank();

    // advance time 2 days to accrue interest
   // note if this is higher the losses will be higher too, due to the interest going up
    vm.warp(block.timestamp + 2 days);
    vm.roll(block.number + 7200 * 2); // ~2 days of blocks
    
    // Calculate redemption amounts using both rates
    uint256 storedRate = LTokenInterface(lToken).exchangeRateStored();
    uint256 currentRate = LTokenInterface(lToken).exchangeRateCurrent();
    
    uint256 redeemAmount = initialLTokenBalance / 2; // Redeem half of supply
    
    uint256 expectedWithStored = (redeemAmount * storedRate) ;
    uint256 expectedWithCurrent = (redeemAmount * currentRate) ;
    
    console.log("Initial supply (lTokens):", initialLTokenBalance );
    console.log("Redeeming amount (lTokens):", redeemAmount );
    console.log("Stored exchange rate:", storedRate );
    console.log("Current exchange rate:", currentRate );
    console.log("Expected underlying with stored rate:", expectedWithStored );
    console.log("Expected underlying with current rate:", expectedWithCurrent );
    console.log("Difference in underlying:", expectedWithCurrent - expectedWithStored);
    
    // Verify rates and amounts
    assertGt(currentRate, storedRate, "Current rate should be higher than stored rate");
    assertGt(expectedWithCurrent, expectedWithStored, "Current rate should yield more tokens");
    
    // Calculate percentage difference
    uint256 percentageDiff = ((expectedWithCurrent - expectedWithStored) * 10000) / expectedWithStored;
    console.log("Percentage difference (basis points):", percentageDiff);
    
    vm.stopPrank();
}
```

Run Poc with
```bash
forge test --match-test test_redeem_exchange_rate_difference -vv

Expected logs:
  Logs:
  Initial supply (lTokens): 5000000000000
  Redeeming amount (lTokens): 2500000000000
  Stored exchange rate: 200000000000000000000000000
  Current exchange rate: 200085616438353920000000000
  Expected underlying with stored rate: 500000000000000000000000000000000000000
  Expected underlying with current rate: 500214041095884800000000000000000000000
  Difference in underlying: 214041095884800000000000000000000000
  Percentage difference (basis points): 4
```


### Mitigation

Use `LToken.exchangeRateCurrent()` which accounts for interest.

```diff
    // Get exchange rate before redeem
-    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
+    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateCurrent();

    // Calculate expected underlying tokens
    uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;

    // Perform redeem
    require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");

    // Transfer underlying tokens to the user
    IERC20(_token).transfer(msg.sender, expectedUnderlying);

```