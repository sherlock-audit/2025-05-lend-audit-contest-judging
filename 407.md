Magnificent Holographic Camel

Medium

# Interest is accrued twice in same-chain borrow

### Summary

Interest is accrued twice in same-chain borrow, causing the collateral sufficiency check unexpected

### Root Cause

The function [`CoreRouter::borrow()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L145) firstly calls `LToken.accrueInterest()` contract to accrue interest and then call `lendStorage.getHypotheticalAccountLiquidityCollateral()` to fetch the borrower's liquidity states, including total borrow amount (plus the borrow effect) and the total collateral. About the total borrow calculation, the function tries to sum borrows from all borrower's debt position with the support of 2 helper functions `borrowWithInterest()` to calculate cross chain borrow with interest and `borrowWithInterestSame()` to calculate same-chain borrow with interest.

Note that at this execution step, the market `_lToken` already accrued interest, so the value `LTokenInterface(_lToken).borrowIndex()` is the updated value, meaning the borrow amount is with up-to-date interest.
```solidity
    function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;

        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
        // Only one mapping should be populated:
        if (borrows.length > 0) {
            for (uint256 i = 0; i < borrows.length; i++) {
                if (borrows[i].srcEid == currentEid) {
@>                    borrowedAmount +=
                        (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
                }
            }
        } else {
            for (uint256 i = 0; i < collaterals.length; i++) {
                // Only include a cross-chain collateral borrow if it originated locally.
                if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
                }
            }
        }
        return borrowedAmount;
    }

    function borrowWithInterestSame(address borrower, address _lToken) public view returns (uint256) {
        uint256 borrowIndex = borrowBalance[borrower][_lToken].borrowIndex;
@>        uint256 borrowBalanceSameChain = borrowIndex != 0
            ? (borrowBalance[borrower][_lToken].amount * uint256(LTokenInterface(_lToken).borrowIndex())) / borrowIndex
            : 0;
        return borrowBalanceSameChain;
    }
```

After fetching the total borrow and collateral from LendStorage, the function `CoreRouter::borrow()` accrues interest once more before the checking of collateral sufficiency. This is incorrect because borrow interest is accrued twice, causing the final borrow amount higher than expected.
```solidity
    function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");

        address _lToken = lendStorage.underlyingTolToken(_token);

        LTokenInterface(_lToken).accrueInterest();

        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);

@>        uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;

@>        require(collateral >= borrowAmount, "Insufficient collateral");
        ...
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

NA

### Impact

Borrowers may be unable to borrow due to insufficient collateral despite having enough collateral

### PoC

_No response_

### Mitigation

```diff

        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);

-        uint256 borrowAmount = currentBorrow.borrowIndex != 0
-            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
-            : 0;

-        require(collateral >= borrowAmount, "Insufficient collateral");
+        require(collateral >= borrowed, "Insufficient collateral");
```