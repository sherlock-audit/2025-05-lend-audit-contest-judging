Skinny Charcoal Chinchilla

High

# Unnecessary line in `CoreRouter.sol::borrow()` calculate borrow amount wrong and allows for multi-token borrows with insufficient collateral.

### Summary

When a user borrow for the second time via `borrow()`, when borrowing the same token, the function check if the collateral can cover previous borrow + interest + current borrow. However the unnecessary line calculate the total borrow (previous borrow + interest + current borrow) amount wrong. If the user borrow a different token this time, he is allowed to borrow an amount far greater than his collateral, stealing from the protocol. 

In [CoreRouter.sol::borrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L145), we get the collateral and total borrow amount via [LendStorage::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L385).

```solidity 
    function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");
        address _lToken = lendStorage.underlyingTolToken(_token);
        LTokenInterface(_lToken).accrueInterest();

@>      (uint256 borrowed, uint256 collateral) = 
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
            uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
            vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()}); 
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
@>          vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);
            unchecked {
                ++i;
            }
        }
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);
@>          uint256 totalBorrow = borrowWithInterestSame(account, address(asset));
            totalBorrow += borrowWithInterest(account, address(asset));
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
@>          vars.sumBorrowPlusEffects = 
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);
            unchecked {
                ++i;
            }
        }
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
                vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()}); 
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }
            if (borrowAmount > 0) {
@>              vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }
        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```

We can see that the function first gets the first borrow + interest using borrowWithInterestSame(), which returns the values in storage. This is convereted into USD value and added to `sumBorrowPlusEffects`. Then the borrowing amount in USD value is added. So the function returns the correct total borrow needed to compare with the collateral. Which is first borrow + interest + current borrow. 

However, after getting these correct values, in `borrow()`, the function gets the borrowBalance of the token the user's borrowing via [LendStorage::getBorrowBalance()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L286). There are two cases and both cases are handled incorrectly. 

```solidity 
    function borrow(uint256 _amount, address _token) external {
        ...
@>      LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);
@>      uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0; 
        require(collateral >= borrowAmount, "Insufficient collateral");
```

The first case, when the user borrow a different token from the first borrow, the `currentBorrow.borrowIndex` is 0, since it has not been stored yet (first time borrowing). This makes the borrowAmount 0, no matter the amount we're borrowing. So the require will always pass and the user is able to borrow as much as he wants of the second token. 

The second case, when the user borrow the same token from the first borrow, the `currentBorrow.borrowIndex` is non-zero. In this case, interest is applied on the `borrowed` amount, which is first borrow + interest + current borrow. This makes the borrowing value a lot larger than the actual amount, the longer the interest is accrued. 

### Root Cause

```solidity 
    function borrow(uint256 _amount, address _token) external {
        ...
        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);
@>      uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0; 
        require(collateral >= borrowAmount, "Insufficient collateral");
```

These lines are unnecessary, they only cause problems. The correct collateral and borrow value is already fetched from `getHypotheticalAccountLiquidityCollateral()`, we only need to compare those two.


### Internal Pre-conditions


1. There must be at least two tokens avaliable to supply and borrow. 


### External Pre-conditions

1. The user must borrow for a second time.

### Attack Path


To steal from the protocol, 

1. Token A and B are avaliable to use in the protocol.
2. A user supply token A.
2. The attacker supply a small amount of Token B.
3. The attacker borrow token A while having very little Token B which is not enough for his borrow.
4. The borrow is sucessful because of the bug. 

### Impact

For second borrows, same token borrows might fail even when the user has enough collateral. Differnet token borrows allows anyone to steal from the protocol by supplying little collateral.

### PoC

Paste this test in src/test/TestBorrowing.t.sol. This is a test showing an attacker depositing 100 of tokenA and sucessfully borrowing 7e18 of tokenB.

```solidity 
    function test_borrow_more_than_collateral() public {
        vm.startPrank(deployer);
        (address tokenA, ) = _supply(10e18);
        vm.stopPrank();

        address attacker = makeAddr("attacker");
        address tokenB = supportedTokens[1];
        ERC20Mock(tokenB).mint(attacker, 100);
        vm.startPrank(attacker);
        IERC20(tokenB).approve(address(coreRouter), 100);
        coreRouter.supply(100, tokenB);
        coreRouter.borrow(7e18, tokenA);
    }
```


### Mitigation

Remove the line. There is no need to do it. Instead, compare the `borrowed` and `collateral` values obtained from `getHypotheticalAccountLiquidityCollateral()`.