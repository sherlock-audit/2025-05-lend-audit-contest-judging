Broad Hazel Frog

Medium

# M-01 Cross-chain borrow requests can lead to loss of funds for the requester

### Summary

The `CrossChainRouter` contract assumes that the user sending the cross-chain borrow request has control over the address on the destination chain, which is not always the case for smart contract wallets.

### Root Cause

In [CrossChainRouter.sol:148](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L148) the protocol wrongly assumes that the user that initiated the borrow process has control of the same address in the destination chain.

The `CrossChainRouter` coordinates borrow, repayment, and liquidation operations with its counterparts on other chains by sending different types of messages using the LayerZero API. This is done in the internal `_send` function:

```solidity
     /**
     * @notice Sends LayerZero message
     */
    function _send(
        uint32 _dstEid,
        uint256 _amount,
        uint256 _borrowIndex,
        uint256 _collateral,
        address _sender,
        address _destlToken,
        address _liquidator,
        address _srcToken,
        ContractType ctype
    ) internal {
		bytes memory payload =
            abi.encode(_amount, _borrowIndex, _collateral, _sender, _destlToken, _liquidator, _srcToken, ctype);
		// ... rest of code
	}

```

When a user wants to perform a cross-chain borrow, they do so by calling the `CrossChainRouter.borrowCrossChain` function. After validating that the user is able to take the borrow, it will call the `_send` function to initiate the cross-chain borrow process between Chain A (the chain from where the user is initiating the process) and Chain B (the chain where the user ultimately intends to receive the borrowed assets):

```solidity
	/**
     * @notice Initiates a cross-chain borrow. Initiated on the source chain (Chain A)
     * @param _amount Amount to borrow
     * @param _borrowToken Token to borrow on destination chain
     * @param _destEid Destination chain's layer zero endpoint id
     */
    function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
        // ... rest of code

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
    }

```

> Notice how `msg.sender` is used as the `address _sender` parameter for the `_send` function. 

When the `CrossChainRouter` on Chain B receives the message, it proceeds to process it in the `_handleBorrowCrossChainRequest` internal function, which at some point distributes the borrowed tokens through the local CoreRouter using the _sender in the payload:

```solidity
	// Execute the borrow on destination chain
    CoreRouter(coreRouter).borrowForCrossChain(payload.sender, payload.amount, payload.destlToken, destUnderlying);
```

If the user who initiated the borrow process doesn't have control over the address on Chain B, they will lose the full amount of the loan. This scenario is particularly common with smart contract wallets, where it may be impossible to deploy the wallet to the same address across different chains due to varying deployment conditions, nonce differences, or chain-specific factors. Even if deployment is technically possible, the user may not have deployed their smart contract wallet on Chain B at the time of the cross-chain borrow, leaving the borrowed funds inaccessible at an address they cannot control.

### Internal Pre-conditions

N/A: the issues exists from the very beginning.

### External Pre-conditions

This issue arises specifically when a user performs a cross-chain borrow using a smart contract wallet on Chain A, but the same smart contract wallet address either doesn't exist or is not controlled by the user on Chain B.

### Attack Path

1. User supplies collateral to the CoreRouter on chain A.
2. User starts a borrow on Chain B.
3. User expects to receive the borrowed assets on chain B, but since he doesn't control the address, assets are lost.

### Impact

The affected users lose the full amount of the requested borrow.

### Mitigation

Given that the protocol already supports other accounts than original borrower paying cross-chain loans, you might want to consider adding an extra field to the struct `LZPayload`:
```solidity
// Struct for LayerZero payload
    struct LZPayload {
        uint256 amount;
        uint256 borrowIndex;
        uint256 collateral;
        address sender;
        address destlToken;
        address liquidator;
        address srcToken;
@>   address receiver;
        uint8 contractType;
    }
```
In that way the users can select which address should receive the assets in the destination chain.