Colossal Chrome Woodpecker

Medium

# CoreRouter underestimates token amounts by using the exchangeRateStored instead of exchangeRateCurrent

### Summary

CoreRouter underestimates token amounts by using the `exchangeRateStored()` instead of `exchangeRateCurrent()`

### Root Cause

We take for example the `supply()` function that computes the minted token amount as follows:

```solidity
// Get exchange rate before mint
uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();(); // @audit-issue does not accrue interest

// Mint lTokens
require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

// Calculate actual minted tokens using exchangeRate from before mint
uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;
```

The issue stems from the fact that `exchangeRateBefore` will surely be less than the exchangeRate used by `LToken:mint()` because the latter accrues interest while the first one do not.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L74

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. user calls `supply()` and he gets accredited less LToken that the actually minted amount
2. when he'll eventually calls `redeem()` he won't get the full accrued interest as some of it remains stuck in the CoreRouter due to the underestimation above

### Impact

Token amounts will be underestimated, leading to some interest stuck in the contract with no way to claim it.

### PoC

Add the following test case inside `test/TestBorrowingCrossChain.t.sol`:

```solidity
function test_supply_interest_stuck() public { // @note
        uint256 supplyAmount = 1000e18;
        address token = supportedTokensA[0];
        address LTokentoken = lendStorageA.underlyingTolToken(token);
        console2.log("", IERC20Metadata(LTokentoken).name());
        console2.log("", LTokenInterface(LTokentoken).exchangeRateStored());

        vm.deal(address(routerA), 1 ether);

        console2.log("LToken before: ", IERC20(LTokentoken).balanceOf(address(coreRouterA)));
        vm.startPrank(deployer);
        ERC20Mock(token).mint(deployer, supplyAmount);
        ERC20Mock(token).approve(address(coreRouterA), supplyAmount);
        coreRouterA.supply(supplyAmount, token);
        console2.log("LToken after: ", IERC20(LTokentoken).balanceOf(address(coreRouterA)));

        vm.roll(block.number + 20);

        coreRouterA.borrow(500e18, token);
        console2.log("exchangeRate after borrow: ", LTokenInterface(LTokentoken).exchangeRateStored());

        vm.roll(block.number + 5000);

        ERC20Mock(token).mint(deployer, supplyAmount);
        ERC20Mock(token).approve(address(coreRouterA), supplyAmount);
        coreRouterA.supply(supplyAmount, token);
        console2.log("exchangeRate after 2nd supply: ", LTokenInterface(LTokentoken).exchangeRateStored());

        vm.roll(block.number + 5000);

        // redeem
        vm.startPrank(deployer);
        coreRouterA.redeem(5000000000000, payable(LTokentoken));
        console2.log("token after in coreRouter ", IERC20(token).balanceOf(address(coreRouterA)));
        console2.log("token after ", IERC20(token).balanceOf(deployer));
    }
```

Execution results on the current codebase:
```js
[PASS] test_supply_interest_stuck() (gas: 1058127)
Logs:
  Lending ERC20Mock
  200000000000000000000000000
  LToken before:  0
  LToken after:  5000000000000
  exchangeRate after borrow:  200000000000000000000000000
  exchangeRate after 2nd supply:  200029727929998204146366307
  token after in coreRouter  66914356558976733
  token after  1500148639649991020731
```

Execution results by changing `exchangeRateStored()` with `exchangeRateCurrent()` in `supply()` and `redeem()`:
```js
[PASS] test_supply_interest_stuck() (gas: 1040487)
Logs:
  Lending ERC20Mock
  200000000000000000000000000
  LToken before:  0
  LToken after:  5000000000000
  exchangeRate after borrow:  200000000000000000000000000
  exchangeRate after 2nd supply:  200029727929998204146366307
  token after in coreRouter  0
  token after  1500215554006549997464
```

We can see that no tokens are left stuck in the CoreRouter anymore after applying the fix.

### Mitigation

Replace `exchangeRateStored()` with `exchangeRateCurrent()` in all token amount calculations so that the same exchangeRate is used by the CoreRouter and the LToken.