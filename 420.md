Magnificent Holographic Camel

High

# Max close calculation does not include interest

### Summary

Using wrong borrow amount can cause liquidation max close to be incorrect

### Root Cause

Function [`CoreRouter::liquidateBorrow()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L230-L244) can liquidate a borrower's position for the same chain borrows. The liquidation is validated by the [function `liquidateBorrowAllowedInternal()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L329-L361), in which the max close is calculated as `maxClose = mul_ScalarTruncate(Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount)` with `borrowBalance.amount` is the borrow balance fetched from LendStorage `lendStorage.getBorrowBalance(borrower, lTokenBorrowed)`. Indeed, at this execution step, the borrow balance is not yet updated with the new borrow index from `LTokenBorrowed`, meaning that the value `borrowBalance.amount` has not yet accrued interest. So, the calculation of max close will be incorrect.
```solidity
    function liquidateBorrowAllowedInternal(
        address payable lTokenBorrowed,
        address borrower,
        uint256 repayAmount,
        uint256 collateral,
        uint256 borrowed
    ) internal view returns (uint256) {
        // Determine borrowIndex and borrowAmount based on chain type
@>        LendStorage.BorrowMarketState memory borrowBalance = lendStorage.getBorrowBalance(borrower, lTokenBorrowed); // <<<<< not yet accrued interest

        // Allow accounts to be liquidated if the market is deprecated
        if (LendtrollerInterfaceV2(lendtroller).isDeprecated(LToken(lTokenBorrowed))) {
            require(borrowBalance.amount >= repayAmount, "Repay > total borrow");
        } else {
            // The borrower must have shortfall in order to be liquidatable
            uint256 borrowedAmount;

            // For same-chain liquidations, calculate borrowed amount using the borrowBalance's index
            borrowedAmount =
                (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex;

            require(borrowedAmount > collateral, "Insufficient shortfall");

            /* The liquidator may not repay more than what is allowed by the closeFactor */
@>            uint256 maxClose = mul_ScalarTruncate(
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
            );

            require(repayAmount <= maxClose, "Too much repay");
        }

        return 0;
    }
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

NA

### Impact

- Liquidators can only close less debt than expected
- Core functionality broken because liquidation does not work properly

### PoC

_No response_

### Mitigation

```diff
-uint256 maxClose = mul_ScalarTruncate(
-               Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
-            );
+uint256 maxClose = mul_ScalarTruncate(
+               Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount * uint256(LTokenInterface(lTokenBorrowed).borrowIndex()) / borrowBalance.borrowIndex
+            );
```