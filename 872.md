Tall Sepia Marmot

High

# Unable to find the cross-chain borrowing/debt position due to incorrect `srcEid` and `destEid` during the search via `findCrossChainCollateral`

### Summary

-

### Root Cause

-

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

Assume that Bob supplied collateral in Chain A, and perform a cross-chain borrowing in Chain B. In this case, when the cross-chain borrowing happen in Chain B, the `_handleBorrowCrossChainRequest` will be executed, and the new debt or borrowing position will be created and stored in Chain B side. More specifically, a new debt/borrowing position will be stored in `crossChainCollaterals` mapping as shown below. 

An important point to note here, which is crucial in understanding this issue later, is that the `LendStorage.Borrow.srcEid` is set to Chain A (`srcEid`), and `LendStorage.Borrow.destEid` is set to Chain B (`destEid`).

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L581

```solidity
File: CrossChainRouter.sol
581:     function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
..SNIP..
641:             lendStorage.addCrossChainCollateral(
642:                 payload.sender,
643:                 destUnderlying,
644:                 LendStorage.Borrow({
645:                     srcEid: srcEid, // @audit-info Chain A
646:                     destEid: currentEid, // @audit-info Chain B
647:                     principle: payload.amount,
648:                     borrowIndex: currentBorrowIndex,
649:                     borrowedlToken: payload.destlToken,
650:                     srcToken: payload.srcToken
651:                 })
652:             );
```

Assume that the value of Bob's supplied collateral drops significantly and his account is subjected to liquidation. Liquidator will initiate a cross-chain liquidation by calling Chain B's `CrossChainRouter.liquidateCrossChain()` function against Bob's account. 

During the liquidation process, numerous cross-chain LayerZero messages will be sent back and forth between Chain B and Chain A. At some point, the `_handleLiquidationSuccess` on Chain B's `CrossChainRouter` will be executed.

In Line 448 of the `_handleLiquidationSuccess` function below, it will attempt to find Bob's position in Chain B, which is needed for the repayment of debts, by calling the `lendStorage.findCrossChainCollateral` function. An important point to know and the main bug here is that the `srcEid` is set to Chain B (`currentEid`)(See Line 451 below) and the `destEid` is set to zero (0)(See Line 452 below), which is incorrect.

As noted earlier, Bob's cross-chain position is actually stored with `LendStorage.Borrow.srcEid` is set to Chain A, and `LendStorage.Borrow.destEid` is set to Chain B. As a result, the `lendStorage.findCrossChainCollateral()` function in Line 448 will never be able to find Bob's cross-chain position, and the validation check in Line 457 will fail and revert the entire transaction.

As a result, the liquidation cannot be completed, leaving bad debt in the protocol and leading to a loss of funds for the protocol.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L457

```solidity
File: CrossChainRouter.sol
443:     function _handleLiquidationSuccess(LZPayload memory payload) private {
444:         // Find the borrow position on Chain B to get the correct srcEid
445:         address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);
446: 
447:         // Find the specific collateral record
448:         (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
449:             payload.sender,
450:             underlying,
451:             currentEid, // srcEid is current chain // @audit-info Chain B
452:             0, // We don't know destEid yet, but we can match on other fields
453:             payload.destlToken,
454:             payload.srcToken
455:         );
456: 
457:         require(found, "Borrow position not found");
458: 
459:         LendStorage.Borrow[] memory userCollaterals = lendStorage.getCrossChainCollaterals(payload.sender, underlying);
460:         uint32 srcEid = uint32(userCollaterals[index].srcEid);
461: 
462:         // Now that we know the borrow position and srcEid, we can repay the borrow using the escrowed tokens
463:         // repayCrossChainBorrowInternal will handle updating state and distributing rewards.
464:         repayCrossChainBorrowInternal(
465:             payload.sender, // The borrower
466:             payload.liquidator, // The liquidator (repayer)
467:             payload.amount, // Amount to repay
468:             payload.destlToken, // lToken representing the borrowed asset on this chain
469:             srcEid // The chain where the collateral (and borrow reference) is tracked
470:         );
471:     }
```

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L683

```solidity
File: LendStorage.sol
683:     function findCrossChainCollateral(
684:         address user,
685:         address underlying,
686:         uint256 srcEid,
687:         uint256 destEid,
688:         address borrowedlToken,
689:         address srcToken
690:     ) public view returns (bool, uint256) {
691:         Borrow[] memory userCollaterals = crossChainCollaterals[user][underlying];
692: 
693:         for (uint256 i = 0; i < userCollaterals.length;) {
694:             if (
695:                 userCollaterals[i].srcEid == srcEid && userCollaterals[i].destEid == destEid
696:                     && userCollaterals[i].borrowedlToken == borrowedlToken && userCollaterals[i].srcToken == srcToken
697:             ) {
698:                 return (true, i);
699:             }
700:             unchecked {
701:                 ++i;
702:             }
703:         }
704:         return (false, 0);
705:     }
```

### Impact

Liquidation cannot be completed, leaving bad debt in the protocol and leading to a loss of funds for the protocol.

### PoC

_No response_

### Mitigation

```diff
- function _handleLiquidationSuccess(LZPayload memory payload) private {
+ function _handleLiquidationSuccess(LZPayload memory payload, uint32 srcEid) private {
    // Find the borrow position on Chain B to get the correct srcEid
    address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);

    // Find the specific collateral record
    (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
        payload.sender,
        underlying,
-       currentEid, // srcEid is current chain
+       srcEid
-       0, // We don't know destEid yet, but we can match on other fields
+       currentEid        
        payload.destlToken,
        payload.srcToken
    );
```