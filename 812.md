Skinny Charcoal Chinchilla

High

# Usage of outdated values causes unfair cross-chain liquidations.

### Summary

When a user's cross-chain position is getting liquidated, his total borrwed USD value and total collateral USD values are compared to ensure he can be rightfully liquidated. However the comparison uses up-to-date borrowing USD value and outdated collateral USD value, allowing unfair liquidations.

In [CrossChainRouter.sol::_executeLiquidationCore()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L264), the collateral value to seize in terms of Ltoken is calculated using [Lendtroller.sol::liquidateCalculateSeizeTokens()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/Lendtroller.sol#L852). The Compound protocol updates the colalteral value before the call to this function. Here, the protocol only make sure the borrowed token value is up-to-date while using outdated collateral value to calculate the collateral to seize.

```solidity 


    function _executeLiquidationCore(LendStorage.LiquidationParams memory params) private {
        address borrowedlToken = lendStorage.underlyingTolToken(params.borrowedAsset);
@>      (uint256 amountSeizeError, uint256 seizeTokens) = LendtrollerInterfaceV2(lendtroller)
            .liquidateCalculateSeizeTokens(borrowedlToken, params.lTokenToSeize, params.repayAmount);
        require(amountSeizeError == 0, "Seize calculation failed");
        _send(
            params.srcEid,
            seizeTokens,
            params.storedBorrowIndex, //old index
            0,
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid), // Convert to Chain A version before sending
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
    }
```

Because of this the `seizeTokens` returned from the function will be larger than in actuality, since it calculates using the outdated exchange rate.

```solidity 
     
    function liquidateCalculateSeizeTokens(address lTokenBorrowed, address lTokenCollateral, uint256 actualRepayAmount) 
        external
        view
        override
        returns (uint256, uint256)
    {
        uint256 priceBorrowedMantissa = oracle.getUnderlyingPrice(LToken(lTokenBorrowed));
        uint256 priceCollateralMantissa = oracle.getUnderlyingPrice(LToken(lTokenCollateral));
        if (priceBorrowedMantissa == 0 || priceCollateralMantissa == 0) {
            return (uint256(Error.PRICE_ERROR), 0);
        }
@>      uint256 exchangeRateMantissa = LToken(lTokenCollateral).exchangeRateStored(); 
        uint256 seizeTokens;
        Exp memory numerator;
        Exp memory denominator;
        Exp memory ratio;
        numerator = mul_(Exp({mantissa: liquidationIncentiveMantissa}), Exp({mantissa: priceBorrowedMantissa}));
        denominator = mul_(Exp({mantissa: priceCollateralMantissa}), Exp({mantissa: exchangeRateMantissa}));
        ratio = div_(numerator, denominator);
        seizeTokens = mul_ScalarTruncate(ratio, actualRepayAmount); 
        return (uint256(Error.NO_ERROR), seizeTokens);
    }
```

The borrowed token's value is updated in [_prepareLiquidationValues()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L245), the collateral token's exchange rate is not updated anywhere.

```solidity 
    function _prepareLiquidationValues(LendStorage.LiquidationParams memory params)
        private
        returns (uint256 maxLiquidation)
    {
@>      LTokenInterface(params.borrowedlToken).accrueInterest();
        uint256 currentBorrowIndex = LTokenInterface(params.borrowedlToken).borrowIndex(); 
        uint256 currentBorrow = (params.borrowPrinciple * currentBorrowIndex) / params.storedBorrowIndex;
        maxLiquidation = mul_ScalarTruncate(
            Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), currentBorrow
        );
        return maxLiquidation;
    } 
```


### Root Cause


[Ltoken.sol::accrueInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L324), is NOT called on the collateral token. only on the borrowed token.


### Internal Pre-conditions

none

### External Pre-conditions

none

### Attack Path

none

### Impact

Cross-chain borrowers will be unfairly liquidated as their borrowed value is up-to-date, while their collateral value is not.

### PoC

none

### Mitigation


Make sure the collateral token is up-to-date as well.

```diff
    function _prepareLiquidationValues(LendStorage.LiquidationParams memory params)
        private
        returns (uint256 maxLiquidation)
    {
        LTokenInterface(params.borrowedlToken).accrueInterest();
+       LTokenInterface(params.lTokenToSeize).accrueInterest();        
        uint256 currentBorrowIndex = LTokenInterface(params.borrowedlToken).borrowIndex(); 
        uint256 currentBorrow = (params.borrowPrinciple * currentBorrowIndex) / params.storedBorrowIndex;
        maxLiquidation = mul_ScalarTruncate(
            Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), currentBorrow
        );
        return maxLiquidation;
    }    
```
