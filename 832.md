Fierce Eggplant Dalmatian

High

# Incorrect borrow amount scaling in `liquidateBorrowAllowedInternal` leads to improper liquidation limits

## Title 
Incorrect borrow amount scaling in `liquidateBorrowAllowedInternal` leads to improper liquidation limits

## Summary
The `liquidateBorrowAllowedInternal` function incorrectly calculates the maximum allowed liquidation amount (`maxClose`) by using the unscaled `borrowBalance.amount` instead of the interest-adjusted borrow amount (`borrowedAmount`). This inconsistency with other functions like `_prepareLiquidationValues` could allow liquidators to repay less than the intended limit based on the close factor.

## Root Cause 
The [`liquidateBorrowAllowedInternal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L329-L361) function uses `borrowBalance.amount` (unscaled principal) instead of `borrowedAmount` (principal + accrued interest) when calculating `maxClose`
```solidity
    function liquidateBorrowAllowedInternal(
        address payable lTokenBorrowed,
        address borrower,
        uint256 repayAmount,
        uint256 collateral,
        uint256 borrowed
    ) internal view returns (uint256) {
        // Determine borrowIndex and borrowAmount based on chain type
        LendStorage.BorrowMarketState memory borrowBalance = lendStorage.getBorrowBalance(borrower, lTokenBorrowed);

        // Allow accounts to be liquidated if the market is deprecated
        if (LendtrollerInterfaceV2(lendtroller).isDeprecated(LToken(lTokenBorrowed))) {
            require(borrowBalance.amount >= repayAmount, "Repay > total borrow");
        } else {
            // The borrower must have shortfall in order to be liquidatable
            uint256 borrowedAmount;

            // For same-chain liquidations, calculate borrowed amount using the borrowBalance's index
            borrowedAmount =
                (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex; 

            require(borrowedAmount > collateral, "Insufficient shortfall");

            /* The liquidator may not repay more than what is allowed by the closeFactor */
            uint256 maxClose = mul_ScalarTruncate(
>>              Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount 
            );

            require(repayAmount <= maxClose, "Too much repay");
        }

        return 0;
    }

```

Unlike [`_prepareLiquidationValues`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L245-L262) which properly scales the borrow amount using the current borrow index, this function uses the raw amount.

## Internal Pre-Conditions
1. The protocol must have accrued interest since the last borrow index update
2. The borrower must have an outstanding loan position
3. The liquidator must attempt to liquidate the position

## External Pre-Conditions
None

## Attack Path
1. Interest accrues on a borrow position over time
2. A liquidator calls `liquidateBorrow` when the borrower becomes undercollateralized
3. The system calculates `maxClose` using the unscaled principal amount
4. This allows the liquidator to repay less than the intended close factor percentage of the actual debt (principal + interest)

## Impact
Liquidators can repay less debt than intended by the close factor hence preventing maximum liquidation.

## POC
1. Modify `test/TestLiquidations.t.sol` as follows:
```diff
    // Add these new imports
+   import "../src/LayerZero/interaces/LendtrollerInterfaceV2.sol";
+   import "../src/ExponentialNoError.sol";

-   contract TestLiquidations is LayerZeroTest {
+   contract TestLiquidations is LayerZeroTest, ExponentialNoError {
```
2. Add the following test in the same file
```solidity
    function test_liquidateBorrowAllowedInternal_incorrect_maxClose_calculation_prevents_maxLiquidation(uint256 supplyAmount uint256 borrowAmount, uint256 newPrice) public {
        // Bound inputs to reasonable values
        supplyAmount = bound(supplyAmount, 100e18, 1000e18);
        borrowAmount = bound(borrowAmount, 50e18, supplyAmount * 60 / 100); // Max 60% LTV
        newPrice = bound(newPrice, 1e16, 5e16); // 1-5% of original price

        // Supply token0 as collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount, 0);

        // Supply token1 as liquidity on Chain A from random wallet
        (address tokenB, address lTokenB) = _supplyA(address(1), supplyAmount, 1);

        vm.prank(deployer);
        coreRouterA.borrow(borrowAmount, tokenB);

        // Advance some blocks to accrue interest
        vm.warp(block.timestamp + 1000);
        vm.roll(block.number + 1000);

        // Simulate price drop of collateral (tokenA) only on first chain
        priceOracleA.setDirectPrice(tokenA, newPrice);
        // Simulate price drop of collateral (tokenA) only on second chain
        priceOracleB.setDirectPrice(
            lendStorageB.lTokenToUnderlying(lendStorageB.crossChainLTokenMap(lTokenA, block.chainid)), newPrice
        );

        // ACCRUE INTRERST
        LToken(lTokenB).accrueInterest();

        // get max repayable amount
        uint256 maxRepayAmount = lendStorageA.getMaxLiquidationRepayAmount(deployer, lTokenB, true);

        // retrieve borrow details
        LendStorage.BorrowMarketState memory borrowBalance = lendStorageA.getBorrowBalance(deployer, lTokenB);

        // calculate max amount as done in liquidateBorrowAllowedInternal()
        uint256 maxClose = mul_ScalarTruncate(
            Exp({mantissa: LendtrollerInterfaceV2(address(lendtrollerA)).closeFactorMantissa()}), borrowBalance.amount
        );

        // assert maxClose < maxRepayAmount: This means that liquidators attempting to close max as calculated by 
        // getMaxLiquidationRepayAmount() may face a revert due to internal flawed constraint
        assertLt(maxClose, maxRepayAmount);


        // Attempt liquidation
        vm.startPrank(liquidator);
        ERC20Mock(tokenB).mint(liquidator, maxRepayAmount);
        IERC20(tokenB).approve(address(coreRouterA), maxRepayAmount);

        // expect revert when liquidator attempts to repay maxRepayAmount
        vm.expectRevert(bytes("Too much repay"));
        coreRouterA.liquidateBorrow(deployer, maxRepayAmount, lTokenA, tokenB);

        vm.stopPrank();
    }
```

## Mitigation
Use Scaled borrow amount
```diff
            // For same-chain liquidations, calculate borrowed amount using the borrowBalance's index
            borrowedAmount =
                (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex; 

            require(borrowedAmount > collateral, "Insufficient shortfall");

            /* The liquidator may not repay more than what is allowed by the closeFactor */
            uint256 maxClose = mul_ScalarTruncate(
-               Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount 
+               Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowedAmount
            );
```