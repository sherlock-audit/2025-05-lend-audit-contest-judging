Scrawny Red Sloth

High

# Cross-chain liquidation pulls `seizeTokens` instead of `repayAmount` from liquidator

### Summary

During liquidation, the repayment of the borrower's debt on `ChainB` (debt chain) incorrectly uses the `seizeTokens` value (the number of collateral tokens to seize on `ChainA`) instead of the `repayAmount` specified by the `liquidator`. This results in the `liquidator` paying an incorrect amount of the borrowed asset and the borrower's debt being improperly adjusted.


### Root Cause

The issue originates in the `_handleLiquidationSuccess()` function, which processes the successful liquidation message on `ChainB`. It incorrectly passes `payload.amount` (representing `seizeTokens`, the collateral amount) to `repayCrossChainBorrowInternal()` instead of the original `repayAmount` specified in `liquidateCrossChain()`.

1. Initiation in [`liquidateCrossChain()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L172-L192) (Chain B):
```solidity
    function liquidateCrossChain(
        address borrower,
>>      uint256 repayAmount,
        uint32 srcEid,
        address lTokenToSeize,
        address borrowedAsset
    ) external {
        LendStorage.LiquidationParams memory params = LendStorage.LiquidationParams({
            borrower: borrower,
>>          repayAmount: repayAmount,
            srcEid: srcEid,
            lTokenToSeize: lTokenToSeize,
            borrowedAsset: borrowedAsset,
            storedBorrowIndex: 0,
            borrowPrinciple: 0,
            borrowedlToken: address(0)
        });
        _validateAndPrepareLiquidation(params);
        _executeLiquidation(params);
    }
```
- The `liquidator` specifies `repayAmount`, the amount of the borrowed asset (e.g., `USDC`) they intend to repay to seize collateral.

2. Seize Calculation in [`_executeLiquidationCore()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L264-L285) (Chain B):
```solidity
    function _executeLiquidationCore(LendStorage.LiquidationParams memory params) private {
        address borrowedlToken = lendStorage.underlyingTolToken(params.borrowedAsset);
        (uint256 amountSeizeError, uint256 seizeTokens) = LendtrollerInterfaceV2(lendtroller)
            .liquidateCalculateSeizeTokens(borrowedlToken, params.lTokenToSeize, params.repayAmount);
        require(amountSeizeError == 0, "Seize calculation failed");
        _send(
            params.srcEid,
>>          seizeTokens,
            params.storedBorrowIndex,
            0,
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid),
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
    }
```
- `repayAmount` is used to calculate `seizeTokens` (collateral tokens, e.g., lETH) via `liquidateCalculateSeizeTokens()`.
- The `_send()` function sends `seizeTokens` as `payload.amount` to `ChainA`, which is correct for collateral seizure but loses track of `repayAmount`.

3. Liquidation Success Handling in [`_handleLiquidationSuccess()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443-L471) (Chain B):
```solidity
    function _handleLiquidationSuccess(LZPayload memory payload) private {
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            currentEid,
            0,
            payload.destlToken,
            payload.srcToken
        );
        require(found, "Borrow position not found");
        LendStorage.Borrow[] memory userCollaterals = lendStorage.getCrossChainCollaterals(payload.sender, underlying);
        uint32 srcEid = uint32(userCollaterals[index].srcEid);
        repayCrossChainBorrowInternal(
            payload.sender,
            payload.liquidator,
>>          payload.amount, // @audit-issue Incorrect: Uses seizeTokens
            payload.destlToken,
            srcEid
        );
    }
```
- The function uses `payload.amount` (which is `seizeTokens`) as the repayment amount in `repayCrossChainBorrowInternal()`, instead of the original `repayAmount`.

4. Repayment in [`repayCrossChainBorrowInternal()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L368-L395) (Chain B):
```solidity
    function repayCrossChainBorrowInternal(
        address borrower,
        address repayer,
        uint256 _amount,
        address _lToken,
        uint32 _srcEid
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest();
        (uint256 borrowedAmount, uint256 index, LendStorage.Borrow memory borrowPosition) =
            _getBorrowDetails(borrower, _token, _lToken, _srcEid);
>>      uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
        require(repayAmountFinal <= borrowedAmount, "Repay amount exceeds borrow");
>>      _handleRepayment(borrower, repayer, _lToken, repayAmountFinal);
        _updateRepaymentState(
            borrower, _token, _lToken, borrowPosition, repayAmountFinal, borrowedAmount, index, _srcEid
        );
        emit RepaySuccess(borrower, _token, repayAmountFinal);
    }
```
- `_amount` (set to `seizeTokens`) is used as `repayAmountFinal`, causing the `liquidator` to transfer `seizeTokens` units of the borrowed asset (e.g., `USDC`) instead of `repayAmount`.

### Internal Pre-conditions

1. A borrower must have an active cross-chain borrow, with debt on Chain B (e.g., in USDC) and collateral on Chain A (e.g., in lETH)
2. The borrower’s position must be undercollateralized, making it eligible for liquidation.


### External Pre-conditions

1. An external actor (liquidator) must initiate the liquidation by calling `liquidateCrossChain()`


### Attack Path

1. A liquidator calls `liquidateCrossChain()` on Chain B, specifying a `repayAmount` (e.g., `500 USDC`) to repay part of a borrower’s debt and seize collateral (e.g., lETH on Chain A).
2. The contract validates the liquidation and calculates `seizeTokens` (e.g., `1 lETH`) using `LendtrollerInterfaceV2.liquidateCalculateSeizeTokens()`.
3. A LayerZero message is sent to Chain A with `seizeTokens` as `payload.amount`, but `repayAmount` is not included in the `LZPayload`.
4. Chain A processes the liquidation, seizes `seizeTokens` (`1 lETH`) from the `borrower`, and sends a `LiquidationSuccess` message back to Chain B with `payload.amount = seizeTokens`.
5. On Chain B, `_handleLiquidationSuccess()` calls `repayCrossChainBorrowInternal()` with `payload.amount` (1 lETH), incorrectly using it as the repayment amount.
6. `repayCrossChainBorrowInternal()` transfers `seizeTokens` units (`1 USDC`) from the liquidator instead of `repayAmount` (`500 USDC`) and reduces the borrower’s debt by `1 USDC`.
7. A `DestRepay` message is sent to Chain A with the incorrect repayment amount (`1 USDC`), causing inconsistent borrow record updates.


### Impact

- The liquidator transfers an incorrect amount of the borrowed asset
- The borrower’s debt is reduced by `seizeTokens` instead of `repayAmount`, leading to incorrect accounting.


### PoC

_No response_

### Mitigation

The contract must propagate and use the original `repayAmount` during the repayment step.
