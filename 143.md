Beautiful Gingerbread Beetle

Medium

# LEND tokens are distributed twice when borrower repay cross chain loan

### Summary

When borrower repays cross loan using [repayCrossChainBorrow](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L156) the `LEND` tokens are distributed to borrower twice

### Root Cause

LEND tokens are distributed to borrowers twice when borrower repays cross chain loan using `repayCrossChainBorrow`. The LEND tokens are distributed to borrowers [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L487) and [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L529).

### Internal Pre-conditions

The borrower should repay cross -chain borrowed amount using `repayCrossChainBorrow` function


### Attack Path

When a borrower calls `repayCrossChainBorrow` function the [repayCrossChainBorrowInternal](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L161C9-L161C38) is called - 

```solidity 
function repayCrossChainBorrowInternal(
        address borrower,
        address repayer,
        uint256 _amount,
        address _lToken,
        uint32 _srcEid
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest();

        // Get borrow details and validate
        (uint256 borrowedAmount, uint256 index, LendStorage.Borrow memory borrowPosition) =
            _getBorrowDetails(borrower, _token, _lToken, _srcEid);

        // Calculate and validate repay amount
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
        require(repayAmountFinal <= borrowedAmount, "Repay amount exceeds borrow");

        // Handle token transfers and repayment
        _handleRepayment(borrower, repayer, _lToken, repayAmountFinal);

        // Update state
        _updateRepaymentState(
            borrower, _token, _lToken, borrowPosition, repayAmountFinal, borrowedAmount, index, _srcEid
        );

        emit RepaySuccess(borrower, _token, repayAmountFinal);
    }
```

The `_handleRepayment` calls `repayCrossChainLiquidation` in the `CoreRouter` contract - 

```solidity 
function _handleRepayment(address _borrower, address repayer, address _lToken, uint256 repayAmountFinal) private {
        // Execute the repayment
        CoreRouter(coreRouter).repayCrossChainLiquidation(_borrower, repayer, repayAmountFinal, _lToken);
    }
```

The `repayCrossChainLiquidation` then calls [repayBorrowInternal](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L220C9-L220C28)

The repayBorrowInternal then distributes LEND tokens to borrower [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L487)


Now returning back to the [repayCrossChainBorrowInternal](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L368) after  `_handleRepayment`  the `_updateRepaymentState` function is called

```solidity 
function _updateRepaymentState(
        address borrower,
        address _token,
        address _lToken,
        LendStorage.Borrow memory borrowPosition,
        uint256 repayAmountFinal,
        uint256 borrowedAmount,
        uint256 index,
        uint32 _srcEid
    ) private {
        uint256 currentBorrowIndex = LTokenInterface(_lToken).borrowIndex();
        LendStorage.Borrow[] memory userCrossChainCollaterals = lendStorage.getCrossChainCollaterals(borrower, _token);

        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeCrossChainCollateral(borrower, _token, index);
            if (userCrossChainCollaterals.length == 1) {
                lendStorage.removeUserBorrowedAsset(borrower, _lToken);
            }
        } else {
            userCrossChainCollaterals[index].principle = borrowedAmount - repayAmountFinal;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;
            lendStorage.updateCrossChainCollateral(borrower, _token, index, userCrossChainCollaterals[index]);
        }

       // LEND Tokens are distributed again to borrower
        lendStorage.distributeBorrowerLend(_lToken, borrower);

        _send(
            _srcEid,
            repayAmountFinal,
            currentBorrowIndex,
            0,
            borrower,
            _lToken,
            _token,
            borrowPosition.srcToken,
            ContractType.DestRepay
        );
    }

```

Now, LEND tokens are again distributed to the borrower in [_updateRepaymentState](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L529)

### Impact

LEND tokens are distributed twice when borrower repay cross chain loan  using [repayCrossChainBorrow](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L156) function


### Mitigation

```diff
function _updateRepaymentState(
        address borrower,
        address _token,
        address _lToken,
        LendStorage.Borrow memory borrowPosition,
        uint256 repayAmountFinal,
        uint256 borrowedAmount,
        uint256 index,
        uint32 _srcEid
    ) private {
        uint256 currentBorrowIndex = LTokenInterface(_lToken).borrowIndex();
        LendStorage.Borrow[] memory userCrossChainCollaterals = lendStorage.getCrossChainCollaterals(borrower, _token);

        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeCrossChainCollateral(borrower, _token, index);
            if (userCrossChainCollaterals.length == 1) {
                lendStorage.removeUserBorrowedAsset(borrower, _lToken);
            }
        } else {
            userCrossChainCollaterals[index].principle = borrowedAmount - repayAmountFinal;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;
            lendStorage.updateCrossChainCollateral(borrower, _token, index, userCrossChainCollaterals[index]);
        }

       // LEND Tokens are distributed again to borrower
-      lendStorage.distributeBorrowerLend(_lToken, borrower);

        _send(
            _srcEid,
            repayAmountFinal,
            currentBorrowIndex,
            0,
            borrower,
            _lToken,
            _token,
            borrowPosition.srcToken,
            ContractType.DestRepay
        );
    }
```