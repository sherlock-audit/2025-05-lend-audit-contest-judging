Wobbly Wintergreen Gerbil

High

# The protocol fails to accrue reward for users who supply assets as promised

## Impact
### Summary 
The protocol is designed to reward users who supply assets to the protocol by accruing some `LEND` due to them. The amount of `LEND` due to a user is tracked using the `LendStorage::lendAccrued` mapping which can later be claimed by the user. However, due to logic errors, no amount of `LEND` will accrue to users who supply assets on the protocol.

### Vulnerability Details

The vulnerability is multifaceted and is summarized below:
*Facet A:*
1. `LEND` due to a user who supplies assets is calculated in the `LendStorage::distributeSupplierLend` which is called by the `CoreRouter::supply` function (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L84)).

2. The problem here is the `LendStorage::distributeSupplierLend` function depends on `totalInvestment` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L327)) 

3. but the `CoreRouter::supply` function calls the `LendStorage::distributeSupplierLend` function before updating `totalInvestment` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L84-L89)). This logic error will prevent timely accrual of user rewards in `LEND` as a result, a user has to supply asset a second time before their reward is accrued for the first supply and any user who supplies asset only once will not have any rewards accrued to them.

*Facet B:*
1. When the `CoreRouter::supply` function is called, this triggers a call to the the `Lerc20::mint` function (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L77)), which calls `LToken::mintInternal` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LErc20.sol#L54)) which also calls `LToken::mintFresh` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L387)) which in turn calls the `Lendtroller::mintAllowed` function (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L398)). 

2. The `Lentroller::mintAllowed` function then calls `Lentroller::updateLendSupplyIndex` and `Lentroller::distributeSupplierLend` functions (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L269-L270)). This way, the `Lentroller::updateLendSupplyIndex` updates the `LendMarketState.block` for `lToken` to the current `block.number` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1278-L1293)) but since `totalInvestment` is not updated, the `Lentroller::distributeSupplierLend` function will not accrue any reward to the user.

3. By the the time the `CoreRouter::supply` function calls the `LendStorage::distributeSupplierLend` function proper on line 84 (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L84)), which calls the `Lendtroller::triggerSupplyIndexUpdate` function and then the `Lendtroller::updateLendSupplyIndex` funtion, the `LendMarketState.block` for `lToken` is the same as the current `block.number` resulting in `deltaBlocks = 0` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1278-L1293)). As a result, the `index` and `block` of `LendMarketState` will not be updated because [this](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1283-L1292) section of the code will be omitted.

4. In the `LendStorage::distributeSupplierLend` function, because the `totalInvestment` was not updated prior to the preceeding function calls, [this](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L327) line returns `0` which results in [this](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L330) line also yielding `0` so that the `LEND` reward acrued to the user is `0` despite they have supplied assets to the protocol. 

*Facet C:*
1. Note that the `Lendtroller::updateLendSupplyIndex` function relies on positive block number differences (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1278-L1293)) to update the `index` and `block` of `LendMarketState` which in turn determines whether or not a user will accrue any rewards.
2. If user A calls the `CoreRouter::supply` function supplying the same asset before user B but within the same block, user A will be rewarded while user B will not. 
3. In general, because the `Lendtroller::updateLendSupplyIndex` function uses block number differences to calculate rewards, only the first user in a block is rewarded while all other users after the first user who supply assets within the same block will not be rewarded


To see all these explanations play out, see the PoC section where `console2.log` were added to various sections of the code to see what happens under the hood when the `CoreRouter::supply` function is called.


### Impact
It is the protocol's design to reward users who supply asset with `LEND` in addition to the reward they will get from interest acrued from borrowers. Now, if users do not get what was promised, they will boycot the protocol and explore other alternatives where they will get due reward for their services.
This will hinder the protocol from achieving the underlying development objectives.

## Proof of Concept

To see all these explanations play out, we introduce `console2.log` at various points in the several functions to see what happens at the background when a user calls the `CoreRouter::supply` function.

Edit the `CoreRouter::supply` function by introducing `console2.log` at various points as follows
```javascript
function supply(uint256 _amount, address _token) external {
        address _lToken = lendStorage.underlyingTolToken(_token); 

        require(_lToken != address(0), "Unsupported Token"); 

        require(_amount > 0, "Zero supply amount"); 

        // Transfer tokens from the user to the contract
        IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount); 

        _approveToken(_token, _lToken, _amount); 

        // Get exchange rate before mint
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored(); 

        // Mint lTokens
        console2.log("Calling Lerc20::mint (from supply function) .... "); // @audit-addition
        require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed"); 
        console2.log("Lerc20::mint call ends  .... \n"); // @audit-addition

        // Calculate actual minted tokens using exchangeRate from before mint
        uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore; 

        lendStorage.addUserSuppliedAsset(msg.sender, _lToken); 
        
        console2.log("Calling LendStorage::distributeSupplierLend (from supply function) .... "); // @audit-addition

        lendStorage.distributeSupplierLend(_lToken, msg.sender); 

        console2.log("LendStorage::distributeSupplierLend call ends .... \n"); // @audit-addition

        // Update total investment using calculated mintTokens
        lendStorage.updateTotalInvestment(
            msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
        );

        emit SupplySuccess(msg.sender, _lToken, _amount, mintTokens);
    }
```

Edit the `LendStorage::distributeSupplierLend` function by introducing `console2.log` at various points as follows:

```javascript
function distributeSupplierLend(address lToken, address supplier) external onlyAuthorized {
        // Trigger supply index update
        console2.log("Calling the Lendtroller::triggerSupplyIndexUpdate function (dSL function): "); // @audit-addition

        LendtrollerInterfaceV2(lendtroller).triggerSupplyIndexUpdate(lToken); 

        // Get the appropriate lend state based on whether it's for supply or borrow
        (uint224 supplyIndex,) = LendtrollerInterfaceV2(lendtroller).lendSupplyState(lToken); 

        // Get the relevant indexes and accrued LEND for the account
        uint256 supplierIndex = lendSupplierIndex[lToken][supplier];
        console2.log("The initial supplier index (dSL function): ", supplierIndex); // @audit-addition
        

        lendSupplierIndex[lToken][supplier] = supplyIndex;
        console2.log("The current supplier index (dSL function): ", supplyIndex); // @audit-addition

        // Update the account's index to the current index since we are distributing accrued LEND
        if (supplierIndex == 0 && supplyIndex >= LEND_INITIAL_INDEX) {
            // Covers the case where users borrowed tokens before the market's borrow state index was set.
            // Rewards the user with LEND accrued from the start of when borrower rewards were first
            // set for the market.
            supplierIndex = LEND_INITIAL_INDEX;
        }

        // Calculate change in the cumulative sum of the LEND per lToken accrued
        Double memory deltaIndex = Double({mantissa: sub_(supplyIndex, supplierIndex)}); 
        console2.log("current minus initial index (dSL function): ", deltaIndex.mantissa); // @audit-addition

        // Calculate the appropriate account balance and delta based on supply or borrow
        uint256 supplierTokens = totalInvestment[supplier][lToken]; // @audit this will be zero for a first time supplier
        console2.log("user totalInvestment (dSL function): ", supplierTokens); // @audit-addition

        // Calculate LEND accrued: lTokenAmount * accruedPerLToken
        uint256 supplierDelta = mul_(supplierTokens, deltaIndex);

        // Update the accrued LEND for the account
        uint256 supplierAccrued = add_(lendAccrued[supplier], supplierDelta);
        lendAccrued[supplier] = supplierAccrued; 
        console2.log("LEND accrued to user (dSL function): ", supplierAccrued); // @audit-addition
    }
```

```javascript
function triggerSupplyIndexUpdate(address lToken) external {
        require(msg.sender == lendStorageAddress, "access denied");

        console2.log("Lendtroller::triggerSupplyIndexUpdate function calls updateLendSupplyIndex ..." ); // @audit-addition

        updateLendSupplyIndex(lToken);

        console2.log("Lendtroller::updateLendSupplyIndex function call ends  ..." ); // @audit-addition
    }
```

```javascript
function updateLendSupplyIndex(address lToken) internal {
       
        LendMarketState storage supplyState = lendSupplyState[lToken];
        
        console2.log("The initial block number (uLSIndex function): ", supplyState.block); // @audit-addition
        
        uint256 supplySpeed = lendSupplySpeeds[lToken]; 
        
        console2.log("lendSupplySpeeds for lToken (uLSIndex function): ", supplySpeed); // @audit-addition

        uint32 blockNumber = safe32(getBlockNumber(), "block number exceeds 32 bits");
        
        console2.log("The current block number (uLSIndex function): ", blockNumber); // @audit-addition

        uint256 deltaBlocks = sub_(uint256(blockNumber), uint256(supplyState.block)); 

        console2.log("current block - initial block (uLSIndex function): ", deltaBlocks); // @audit-addition

        if (deltaBlocks > 0 && supplySpeed > 0) {
            uint256 supplyTokens = LToken(lToken).totalSupply(); 
            uint256 lendAccrued = mul_(deltaBlocks, supplySpeed); 
            Double memory ratio = supplyTokens > 0 ? fraction(lendAccrued, supplyTokens) : Double({mantissa: 0}); 
            supplyState.index =
                safe224(add_(Double({mantissa: supplyState.index}), ratio).mantissa, "new index exceeds 224 bits"); 
            supplyState.block = blockNumber;

            console2.log("The updated index (uLSIndex function): ", supplyState.index); // @audit-addition
        } else if (deltaBlocks > 0) {
            supplyState.block = blockNumber;
        }
    }
```

Now, in the `TestSupplying.t.sol`, write the following simple test to see what happens under the hood when the `CoreRouter::supply` function is called

```javascript

function test_spomaria_see_supply_under_the_hood() public {

        console2.log("Contracts are deployed at block", block.number);
        // roll the blockchain by some blocks

        vm.warp(block.timestamp + 60*60);
        vm.roll(block.number + 200);

        // Assume reasonable amount bounds
        uint256 amount = 1e25;
        vm.startPrank(deployer);

        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        // Mint tokens to deployer
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouter), amount);

        // Calculate expected tokens based on exchange rate
        uint256 exchangeRate = LTokenInterface(lToken).exchangeRateStored();
        uint256 expectedTokens = (amount * 1e18) / exchangeRate;

    

        // Expect SupplySuccess event with correct token amounts
        vm.expectEmit(true, true, true, true);
        emit SupplySuccess(deployer, lToken, amount, expectedTokens);

        console2.log("User calls supply at block", block.number);
        
        coreRouter.supply(amount, token);

        vm.stopPrank();
    }
```

Now run the command `forge test --match-test test_spomaria_see_supply_under_the_hood -vvvv`

Output:

```javascript
Ran 1 test for test/TestSupplying.t.sol:TestSupplying
[PASS] test_spomaria_see_supply_under_the_hood() (gas: 406284)
Logs:
  The initial block number (uLSIndex function):  1
  lendSupplySpeeds for lToken (uLSIndex function):  0
  The current block number (uLSIndex function):  1
  current block - initial block (uLSIndex function):  0
  The initial block number (uLSIndex function):  1
  lendSupplySpeeds for lToken (uLSIndex function):  0
  The current block number (uLSIndex function):  1
  current block - initial block (uLSIndex function):  0
  Contracts are deployed at block 1
  User calls supply at block 201
  Calling Lerc20::mint (from supply function) .... 
  The initial block number (uLSIndex function):  1
  lendSupplySpeeds for lToken (uLSIndex function):  100000000000000
  The current block number (uLSIndex function):  201
  current block - initial block (uLSIndex function):  200
  The updated index (uLSIndex function):  1000000000000000000000000000000000000
  Lerc20::mint ends  .... 

  Calling LendStorage::distributeSupplierLend (from supply function) .... 
  Calling the Lendtroller::triggerSupplyIndexUpdate function (dSL function): 
  Lendtroller::triggerSupplyIndexUpdate function calls updateLendSupplyIndex ...
  The initial block number (uLSIndex function):  201
  lendSupplySpeeds for lToken (uLSIndex function):  100000000000000
  The current block number (uLSIndex function):  201
  current block - initial block (uLSIndex function):  0
  Lendtroller::updateLendSupplyIndex function call ends  ...
  The initial supplier index (dSL function):  0
  The current supplier index (dSL function):  1000000000000000000000000000000000000
  current minus initial index (dSL function):  0
  user totalInvestment (dSL function):  0
  LEND accrued to user (dSL function):  0
  LendStorage::distributeSupplierLend call ends .... 


Traces:
  [488684] TestSupplying::test_spomaria_see_supply_under_the_hood()
    ├─ [0] console::log("Contracts are deployed at block", 1) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] VM::warp(3601)
    │   └─ ← [Return] 
    ├─ [0] VM::roll(201)
    .
    .
    .
```

For a more formal test, add the following inside the TestSupplying.t.sol file

```javascript
function test_spomaria_poc_error_updating_lendAccrued() public {
        // roll the blockchain by some blocks

        vm.warp(block.timestamp + 60*60);
        vm.roll(block.number + 20);

        // Assume reasonable amount bounds
        uint256 amount = 1e25;
        vm.startPrank(deployer);

        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        // Mint tokens to deployer
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouter), amount);

        // Calculate expected tokens based on exchange rate
        uint256 exchangeRate = LTokenInterface(lToken).exchangeRateStored();
        uint256 expectedTokens = (amount * 1e18) / exchangeRate;

        vm.warp(block.timestamp + 60*60);
        vm.roll(block.number + 20);

        // Expect SupplySuccess event with correct token amounts
        vm.expectEmit(true, true, true, true);
        emit SupplySuccess(deployer, lToken, amount, expectedTokens);

        // deployer calls supply the first time
        coreRouter.supply(amount, token);

        // Verify state changes
        assertEq(IERC20(token).balanceOf(lToken), amount, "Router should have received tokens");
        assertGt(lendStorage.totalInvestment(deployer, lToken), 0, "Total investment should be updated");
        assertEq(
            LTokenInterface(lToken).balanceOf(address(coreRouter)),
            expectedTokens,
            "Router should have received correct amount of lTokens"
        );

        console2.log("lendAccrued update after first supply ... ");
        assertEq(lendStorage.lendAccrued(deployer), 0);

        // Mint tokens to deployer a second time
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouter), amount);

        // deployer calls supply a second time
        coreRouter.supply(amount, token);
        console2.log("lendAccrued update after second supply ... ");
        assertEq(lendStorage.lendAccrued(deployer), 0);        

        vm.stopPrank();
    }
```

Now run `forge test --match-test test_spomaria_poc_error_updating_lendAccrued`

Output:

```javascript
Ran 1 test for test/TestSupplying.t.sol:TestSupplying
[PASS] test_spomaria_poc_error_updating_lendAccrued() (gas: 491839)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 37.99ms (10.39ms CPU time)

Ran 1 test suite in 43.96ms (37.99ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tools Used

Manual Review


## Recommended Mitigation Steps
Recommendations are as follows:
1. Modify the `CoreRouter::supply` to update `totalInvestment` before calling the `distributeSupplierLend` function as below

```diff
    function supply(uint256 _amount, address _token) external {
    address _lToken = lendStorage.underlyingTolToken(_token);


    require(_lToken != address(0), "Unsupported Token");


    require(_amount > 0, "Zero supply amount");


    // Transfer tokens from the user to the contract
    IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);


    _approveToken(_token, _lToken, _amount);


    // Get exchange rate before mint
    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();


    // Mint lTokens
    require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");


    // Calculate actual minted tokens using exchangeRate from before mint
    uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;


    lendStorage.addUserSuppliedAsset(msg.sender, _lToken);


-   lendStorage.distributeSupplierLend(_lToken, msg.sender);


    // Update total investment using calculated mintTokens
    lendStorage.updateTotalInvestment(
        msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
    );

+   lendStorage.distributeSupplierLend(_lToken, msg.sender); // @audit this call should come after updating totalInvestment


    emit SupplySuccess(msg.sender, _lToken, _amount, mintTokens);
}
```

2. Modify the `Lentroller::mintAllowed` function to omit the `updateLendSupplyIndex` and `distributeSupplierLend` function calls as below
```diff
function mintAllowed(address lToken, address minter, uint256 mintAmount) external override returns (uint256) {
        // Pausing is a very serious situation - we revert to sound the alarms
        require(!mintGuardianPaused[lToken], "mint is paused");

        // Shh - currently unused
        minter;
        mintAmount;

        if (!markets[lToken].isListed) {
            return uint256(Error.MARKET_NOT_LISTED);
        }

        
        // Keep the flywheel moving
-        updateLendSupplyIndex(lToken);
-        distributeSupplierLend(lToken, minter);


        return uint256(Error.NO_ERROR);
    }
```

3. Modify the the `Lendtroller::updateLendSupplyIndex` function to use `block.timestamp` instead of `block.number`. This way all users will be duly rewarded for supplying assets to the protocol. 
