Mini Graphite Moth

High

# Cross-chain liquidation allows liquidators to seize collateral without repaying debt

## Description:
The cross-chain liquidation mechanism in the protocol contains a critical vulnerability where liquidators can obtain collateral without actually repaying the borrower's debt. This occurs due to the asynchronous nature of LayerZero cross-chain messaging, where collateral seizure and debt repayment happen in separate transactions.
The vulnerability exists in the `liquidateCrossChain()` function flow:
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L172
```solidity
// Step 1: Liquidation initiation (Chain B) - NO escrow of liquidator funds
// liquidateCrossChain -> _executeLiquidation -> _executeLiquidationCore -> _send()
function liquidateCrossChain(
    address borrower,
    uint256 repayAmount,
    uint32 srcEid,
    address lTokenToSeize,
    address borrowedAsset
) external {
    // Only validation and message sending - NO fund transfer
    _validateAndPrepareLiquidation(params);
    _executeLiquidation(params);
}
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L312
```solidity
// Step 2: Collateral seizure (Chain A) - Liquidator receives collateral
// _lzReceive() -> _handleLiquidationExecute() -> _send()
function _handleLiquidationExecute(LZPayload memory payload, uint32 srcEid) private {
    // ... calculations ...
    
    // Liquidator receives collateral HERE
    lendStorage.updateTotalInvestment(
        payload.liquidator,
        payload.destlToken,
        lendStorage.totalInvestment(payload.liquidator, payload.destlToken) + liquidatorShare
    );
    
    // Transaction completes - liquidator now owns collateral
    _send(srcEid, ..., ContractType.LiquidationSuccess);
}
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443
```solidity
// Step 3: Debt repayment attempt (Chain B) - Can fail
// _lzReceive() -> _handleLiquidationSuccess() -> repayCrossChainBorrowInternal() -> _handleRepayment() -> repayCrossChainLiquidation() -> repayBorrowInternal()
function _handleLiquidationSuccess(LZPayload memory payload) private {
    // Attempts to charge liquidator for debt repayment
    repayCrossChainBorrowInternal(
        payload.sender,    // borrower
        payload.liquidator, // liquidator (source of funds)
        payload.amount,
        payload.destlToken,
        srcEid
    );
}

// Final step: Actual fund transfer attempt
function repayBorrowInternal(
    address borrower,
    address liquidator,
    uint256 _amount,
    address _lToken,
    bool _isSameChain
) internal {
    // This can REVERT if liquidator has no allowance/balance
    IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
    // ... rest of repayment logic
}
```

## Attack path:
**Transaction №1 (Chain B - Liquidation Initiation):**
1. Attacker calls `liquidateCrossChain()` without providing any token allowance to the contract
2. Function flow: `liquidateCrossChain` → `_executeLiquidation` → `_executeLiquidationCore` → `_send()` 
3. LayerZero message is sent to Chain A with liquidation parameters
4. **Transaction №1 completes successfully** - no funds are escrowed from the attacker

**Transaction №2 (Chain A - Collateral Seizure):**
5. LayerZero relayer delivers the message and calls `_lzReceive()`
6. Function flow: `_lzReceive()` → `_handleLiquidationExecute()` 
7. **Critical point**: Attacker receives collateral through `lendStorage.updateTotalInvestment(liquidator, collateralToken, balance + liquidatorShare)`
8. Success message is sent back to Chain B via `_send()`
9. **Transaction №2 completes successfully** - attacker now owns the seized collateral

**Transaction №3 (Chain B - Repayment Attempt):**
10. LayerZero relayer delivers the success message and calls `_lzReceive()`
11. Function flow: `_lzReceive()` → `_handleLiquidationSuccess()` → `repayCrossChainBorrowInternal()` → `_handleRepayment()` → `repayCrossChainLiquidation()` → `repayBorrowInternal()`
12. **Failure point**: `IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal)` reverts due to insufficient allowance
13. **Transaction №3 reverts** - debt repayment fails

**Final state**: Attacker retains the collateral from Transaction №2 while the borrower's debt remains unpaid since Transaction №3 failed. The protocol has lost collateral without receiving repayment.


## Impact:
Liquidators can steal collateral worth more than the debt amount without paying anything
Unpaid debts accumulate while collateral is drained from the system
Borrowers lose their collateral but their debt remains, creating bad debt for the protocol
This vulnerability can be exploited repeatedly, potentially draining all cross-chain collateral

## Recommended Mitigation
Implement fund escrow at liquidation initiation to ensure atomicity.
This ensures that liquidators must provide funds upfront, making the cross-chain liquidation atomic from an economic perspective.