Scrawny Red Sloth

Medium

# Unhandled case in `repayBorrowInternal()` results in underflow revert

### Summary

`repayBorrowInternal()` calculates `repayAmountFinal` by handling only two cases: when `_amount is type(uint256).max` (indicating full repayment) or any other specific amount. However, it doesn't account for cases where `_amount` might be greater than `borrowedAmount`, which could lead to underflow issues later when subtracting from the borrow balance.

`NOTE`: 
Users can possibly specify an amount greater than their actual borrow because the borrow amount will be scaled within the function during repay based on current borrow index making it impossible to know beforehand the actual amount. Therefore, if they just intended to repay whatever amount they specified and it happen to be larger than their scaled debt, the function will revert with underflow. This is not user mistake.

### Root Cause

When [determining `repayAmountFinal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L480), the following is done:
```solidity
    uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
```
As seen, `repayAmountFinal` will be set to `_amount` when `_amount == type(uint256).max` evaluates to `false`.

Then downstream when [updating the borrow balance](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L498), the following is done:
```solidity
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                //@audit Potential underflow
>>              borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }
```
From this, when the passed `_amount` is greater than `borrowedAmount`, the `repayAmountFinal` will be set to `_amount` and the subtraction done i.e `borrowedAmount - repayAmountFinal` will revert due to underflow.


### Internal Pre-conditions

1. User has active debt
2. Flawed amount handling mechanism

### External Pre-conditions

1. User attempts to repay full amount

### Attack Path

1. User attempts to repay full amount providing `_amount` that exceeds `borrowedAmount` yet to be calculated
2. This is handled incorrectly 
3. Tx reverts with underflow error

### Impact

`borrowedAmount` is determined based on the index difference between the current index during the repayment and the borrow index stored:
```solidity
        if (_isSameChain) {
>>          borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        }
```
As such, users would mostly specify any amount which could be greater than the actual borrowed amount. And since the system should be in a position to handle this, there should be no revert when the `_amount` is greater than the `borrowedAmount`.
However, since the amount handling is flawed, users will potentially experience repayment failures.


### PoC

1. In `test/TestBorrowing.t.sol`, add the following test:
```solidity
    function test_underflow_blocks_repay_due_to_incorrect_amount_handling(uint256 amount) public {
        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
        amount = bound(amount, 1e18, 1e30);
        vm.startPrank(deployer);

        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        // mint tokens
        ERC20Mock(token).mint(deployer, 2 * amount);

        IERC20(token).approve(address(coreRouter), amount);

        // supply tokens as collateral
        coreRouter.supply(amount, token);

        // Calculate maximum allowed borrow (70% of collateral to leave some safety margin)
        uint256 maxBorrow = (amount * 70) / 100;

        // Get initial balances
        uint256 initialTokenBalance = IERC20(token).balanceOf(deployer);

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, lToken, maxBorrow);

        // Borrow tokens
        coreRouter.borrow(maxBorrow, token);

        // Verify balances after borrowing
        assertEq(
            IERC20(token).balanceOf(deployer) - initialTokenBalance,
            maxBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        // Verify borrow balance is tracked correctly
        assertEq(
            lendStorage.borrowWithInterestSame(deployer, lToken),
            maxBorrow,
            "Borrow balance should be tracked correctly"
        );

        // Repay borrow

        // approve contract to pull tokens
        IERC20(token).approve(address(coreRouter), maxBorrow + 1);

        // expect revert with underflow
        vm.expectRevert(abi.encodeWithSignature("Panic(uint256)", 0x11));
        coreRouter.repayBorrow(maxBorrow + 1, lToken);

        vm.stopPrank();
    }
```
2. Run: `forge test --mt test_underflow_blocks_repay_due_to_incorrect_amount_handling -vvvv`


### Mitigation

Modify the amount handling as follows:
```diff
-   uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
+   uint256 repayAmountFinal = (_amount == type(uint256).max || _amount > borrowedAmount) ? borrowedAmount : _amount;
```