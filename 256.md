Itchy Fiery Cyborg

High

# CoreRouter will misrecord lToken amounts for suppliers and redeemers.

### Summary

The missing call to `accrueInterest` before fetching exchange rates in CoreRouter's `supply` and `redeem` functions will cause incorrect accounting as the operations use stale exchange rates while the actual mint/redeem uses updated rates after interest accrual.

### Root Cause

In CoreRouter.sol:
1. In the `supply` function, the exchange rate is fetched without accruing interest ([CoreRouter.sol#L74](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L74)), then `mint` is called ([CoreRouter.sol#L77](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L)) which internally accrues interest ([LToken.sol#L385](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L385)). This causes the `mintTokens` calculation in the `supply` function ([CoreRouter.sol#L80](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L80)) to use a stale exchange rate, resulting in an overestimated value compared to the actual minted amount ([LToken.sol#L429](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L429)).
```solidity
    function supply(uint256 _amount, address _token) external {
        address _lToken = lendStorage.underlyingTolToken(_token);

        require(_lToken != address(0), "Unsupported Token");

        require(_amount > 0, "Zero supply amount");

        // Transfer tokens from the user to the contract
        IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);

        _approveToken(_token, _lToken, _amount);

        // Get exchange rate before mint
74:     uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        // Mint lTokens
77:     require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

        // Calculate actual minted tokens using exchangeRate from before mint
80:     uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;

        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        lendStorage.distributeSupplierLend(_lToken, msg.sender);

        // Update total investment using calculated mintTokens
87:     lendStorage.updateTotalInvestment(
88:         msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
89:     );

        emit SupplySuccess(msg.sender, _lToken, _amount, mintTokens);
    }
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L61-L92


2. Similarly in the `redeem` function, the exchange rate is fetched before accruing interest while the redeem operation uses the post-accrual rate.

### Internal Pre-conditions

1. Borrow activity has occurred to generate interest.

### External Pre-conditions

1. Time have elapsed and some blocks have been mined since last accrual.

### Attack Path

1. There exists borrow activities and interest has accrued
2. User calls `CoreRouter.supply(amount, token)`
3. CoreRouter gets stored exchange rate (without accruing interest)
4. CoreRouter calls `mint` which first accrues interest (updating exchange rate)
5. Actual mint uses new higher exchange rate (resulting in fewer tokens)
6. CoreRouter calculates `mintTokens` using old lower rate (overestimating amount)
7. The inflated `mintTokens` value is stored in user's `totalInvestment`

### Impact


The protocol suffers from:
1. **Accounting Mismatch**: Suppliers' `totalInvestment` records more `lTokens` than actually minted.
2. **Reward Inflation**: Suppliers receive inflated `LEND` token rewards due to overstated collateral positions.
3. **Redemption Issues**: Users may be unable to redeem full recorded amounts when exchange rates diverge.
4. **Collateral Risks**: Overstated collateral values could lead to under-collateralized positions.
5. **Systemic Risk**: Protocol-wide accounting inaccuracies that compound over time.

### PoC

1. The following test case demonstrates the accounting discrepancy caused by the missing accrueInterest call:

```solidity
// TestBorrowing.t.sol: add this test case in TestBorrowing.t.sol

    function test_supply_borrow_supply() public {
        uint256 amount = 100e18;
        vm.startPrank(deployer);

        // First supply, no accrued interest
        console2.log("First supply:");
        (address token, address lToken) = _supply(amount);

        // Calculate maximum allowed borrow (70% of collateral to leave some safety margin)
        uint256 maxBorrow = (amount * 70) / 100;

        // Borrow tokens to accrue interest
        coreRouter.borrow(maxBorrow, token);

        // 10 blocks mined to accrue interest
        vm.roll(block.number + 10);

        // Second supply, with accrued interest, the `mintTokens` differs
        console2.log("Second supply:");
        _supply(amount);

        vm.stopPrank();
    }
```

2. To show the difference of `mintTokens` in `CoreRouter.supply` and `LToken.mintFresh`, we log the values in these two functions:
```solidity
// CoreRouter.supply

        // Get exchange rate before mint
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        // Mint lTokens
        require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

        // Calculate actual minted tokens using exchangeRate from before mint
        uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;
+       console2.log("supply: mintTokens=%d, exchangeRateBefore=%d", mintTokens, exchangeRateBefore);
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L73-L80


```solidity
        Exp memory exchangeRate = Exp({mantissa: exchangeRateStoredInternal()});

        /////////////////////////
        // EFFECTS & INTERACTIONS
        // (No safe failures beyond this point)

        /*
         *  We call `doTransferIn` for the minter and the mintAmount.
         *  Note: The lToken must handle variations between ERC-20 and ETH underlying.
         *  `doTransferIn` reverts if anything goes wrong, since we can't be sure if
         *  side-effects occurred. The function returns the amount actually transferred,
         *  in case of a fee. On success, the lToken holds an additional `actualMintAmount`
         *  of cash.
         */
        uint256 actualMintAmount = doTransferIn(minter, mintAmount);

        /*
         * We get the current exchange rate and calculate the number of lTokens to be minted:
         *  mintTokens = actualMintAmount / exchangeRate
         */

        uint256 mintTokens = div_(actualMintAmount, exchangeRate);
+       console2.log("mintFresh: mintTokens=%d, exchangeRate=%d", mintTokens, exchangeRate.mantissa);
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L408-L429


3. Run the above test case with `forge test -vv --match-test test_supply_borrow_supply`, and the logs show the difference in `mintTokens` of the second supply:
```solidity
Ran 1 test for test/TestBorrowing.t.sol:TestBorrowing
[PASS] test_supply_borrow_supply() (gas: 911704)
Logs:
  First supply:
  mintFresh: mintTokens=500000000000, exchangeRate=200000000000000000000000000
  supply: mintTokens=500000000000, exchangeRateBefore=200000000000000000000000000
  Second supply:
  mintFresh: mintTokens=499997752578, exchangeRate=200000898972602710000000000
  supply: mintTokens=500000000000, exchangeRateBefore=200000000000000000000000000
```

### Mitigation

Call `accrueInterest` before fetching the exchange rate in `CoreRouter.supply` and `CoreRouter.redeem`:
```solidity
// In supply function:
        // Get exchange rate before mint
+       LTokenInterface(_lToken).accrueInterest();
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();


// In redeem function:
        // Get exchange rate before redeem
+       LTokenInterface(_lToken).accrueInterest();
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
```