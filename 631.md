Boxy Ebony Rattlesnake

High

# Missing Check for Same-Chain Repayment in Updating Same-Chain Borrow Balances

### Summary
The `borrowBalance` refers to the same-chain borrow balance. For cross-chain, the borrow balance is recoded to `crossChainBorrows` and `crossChainCollaterals`.
In the `repayBorrowInternal` function, the `borrowBalance` is incorrectly updated even when the function is executed for a cross-chain repayment.

### Root Cause
The root cause is that the `borrowBalance` is updated to reflect the cross-chain borrow amount during a cross-chain repayment.

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L492-L500
```solidity
    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);

        LTokenInterface(_lToken).accrueInterest();

        uint256 borrowedAmount;

        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }

        require(borrowedAmount > 0, "Borrowed amount is 0");

        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;

        // Transfer tokens from the liquidator to the contract
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);

        _approveToken(_token, _lToken, repayAmountFinal);

        lendStorage.distributeBorrowerLend(_lToken, borrower);

        // Repay borrowed tokens
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");

492:    // Update same-chain borrow balances
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
500:    }

        // Emit RepaySuccess event
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }
```
As illustrated in the code snippet above, the `removeBorrowBalance` or `updateBorrowBalance` is executed regardless of whether the repayment is same-chain or cross-chain, causing unintended changes to the `borrowBalance`.

### Impact
When users repay a cross-chain borrow, the same-chain borrow balance is incorrectly modified, which can result in financial losses or gains for users.

### Mitigation
```diff
492:    // Update same-chain borrow balances
+     if (_isSameChain) {
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
500:    }
+     }
```