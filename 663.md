Winning Cinnabar Gorilla

High

# Incorrect borrowed amount is used for checking if a cross-chain liquidation is valid

### Summary

Incorrect borrowed amount is used for checking if a cross-chain liquidation is valid.

### Root Cause

When a cross-chain liquidation is initialized on `ChainB`, `repayAmount` is set in `LendStorage.LiquidationParams`.

[](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L179-L188):
```solidity
        LendStorage.LiquidationParams memory params = LendStorage.LiquidationParams({
            borrower: borrower,
@>          repayAmount: repayAmount,
            srcEid: srcEid,
            lTokenToSeize: lTokenToSeize, // Collateral lToken from the user's position to seize
            borrowedAsset: borrowedAsset,
            storedBorrowIndex: 0,
            borrowPrinciple: 0,
            borrowedlToken: address(0)
        });
```

Later in `CrossChainRouter::_executeLiquidationCore()`, `repayAmount` is used to retrieve `seizeTokens` from `Lendtroller`.

[CrossChainRouter.sol#L268-L269](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L268-L269):
```solidity
        (uint256 amountSeizeError, uint256 seizeTokens) = LendtrollerInterfaceV2(lendtroller)
            .liquidateCalculateSeizeTokens(borrowedlToken, params.lTokenToSeize, params.repayAmount);
```  

Then `seizeTokens` is sent in a LayerZero message to `ChainA`.

[CrossChainRouter.sol#L273-L284](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L273-L284):
```solidity
        // Send message to Chain A to execute the seize
        _send(
            params.srcEid,
@>          seizeTokens,
            params.storedBorrowIndex,
            0,
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid), // Convert to Chain A version before sending
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
```

When the message is received by `CrossChainRouter` on `ChainA`, protocol firstly checks the validity of the liquidation in `_checkLiquidationValid()`.

[CrossChainRouter.sol#L431-L436](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L431-L436):
```solidity
    function _checkLiquidationValid(LZPayload memory payload) private view returns (bool) {
        (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
@>          payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );
        return borrowed > collateral;
    }
```

It's worth noting that `seizeTokens` is limited by Liquidation Close Factor, assuming a borrower's debt is 2000, Liquidation Close Factor is 0.5 and Liquidation Incentive is 1.08, then `seizeTokens` can only be up to 1080. Therefore, if the borrower's collateral is 1999, then they are supposed to be successfully liquidated, however, in `_checkLiquidationValid()`, `borrowed` would be 1080 and is less than collateral, the check will fail and no liquidation would be done.



### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

NA

### Impact

Cross-chain liquidation will always fail, this increases more risk to the protocol.

### PoC

Add `console.log()` in the code base so we can track the issue more easily.

[CrossChainRouter.sol#L431-L436](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L431-L436):
```diff
    function _checkLiquidationValid(LZPayload memory payload) private view returns (bool) {
        (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );

        console2.log();
        console2.log("borrowed:", borrowed);
        console2.log("collateral:", collateral);

        return borrowed > collateral;
    }
```

[CrossChainRouter.sol#L491-L503](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L491-L503):
```diff
    function _sendLiquidationFailure(LZPayload memory payload, uint32 srcEid) private {
+       console2.log("Liquidation Invalid");

        _send(
            srcEid,
            payload.amount,
            0,
            0,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.LiquidationFailure
        );
    }
```

Run `forge test --mt testPOC_CrossChainLiquidationWillAlwaysFail -vv`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import "forge-std/Test.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

import {ILayerZeroEndpointV2} from "@layerzerolabs/lz-evm-protocol-v2/contracts/interfaces/ILayerZeroEndpointV2.sol";

import {Lend} from "../src/Governance/Lend.sol";
import {Lendtroller, LendtrollerInterface} from "../src/Lendtroller.sol";
import {LErc20Immutable, LToken} from "../src/LErc20Immutable.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel, InterestRateModel} from "../src/WhitePaperInterestRateModel.sol";

import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {CrossChainRouter, Origin} from "../src/LayerZero/CrossChainRouter.sol";

contract POC is Test {
    using SafeERC20 for IERC20;

    uint32 constant chainAEid = 30101; // Ethereum
    uint32 constant chainBEid = 30184; // Base

    address owner;
    address mockDestLToken;
    address mockPeer;

    IERC20 btc;
    IERC20 dai;
    IERC20 weth;

    LErc20Immutable lTokenBTC;
    LErc20Immutable lTokenDAI;
    LErc20Immutable lTokenWETH;

    Lend lend;

    ILayerZeroEndpointV2 endpoint;

    Lendtroller lendtroller;
    SimplePriceOracle priceOracle;
    WhitePaperInterestRateModel interestRateModel;

    LendStorage lendStorage;
    CoreRouter coreRouter;
    CrossChainRouter crossChainRouter;

    function setUp() public {
        vm.createSelectFork("https://eth.drpc.org");

        owner = makeAddr("Owner");
        mockDestLToken = makeAddr("MockDestLToken");
        mockPeer = makeAddr("ChainBPeer");
        endpoint = ILayerZeroEndpointV2(
            0x1a44076050125825900e736c501f859c50fE728c
        );

        btc = IERC20(0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599);
        dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
        weth = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

        vm.startPrank(owner);

        // Deploy Lendtroller
        lendtroller = new Lendtroller();
        // Deploy Lend
        lend = new Lend(address(lendtroller));
        // Deploy PriceOracle
        priceOracle = new SimplePriceOracle();
        // Deploy InterestRateModel
        interestRateModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);
        // Deploy lTokens
        lTokenBTC = new LErc20Immutable(
            address(btc),
            lendtroller,
            interestRateModel,
            1e8, // 1:1
            "Lending BTC",
            "lBTC",
            18,
            payable(owner)
        );
        lTokenDAI = new LErc20Immutable(
            address(dai),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending DAI",
            "lDAI",
            18,
            payable(owner)
        );
        lTokenWETH = new LErc20Immutable(
            address(weth),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending WETH",
            "lWETH",
            18,
            payable(owner)
        );

        // Deploy LendStorage
        lendStorage = new LendStorage(
            address(lendtroller),
            address(priceOracle),
            chainAEid
        );
        // Deploy CoreRouter
        coreRouter = new CoreRouter(
            address(lendStorage),
            address(priceOracle),
            address(lendtroller)
        );
        // Deploy CrossChainRouter
        crossChainRouter = new CrossChainRouter(
            address(endpoint),
            owner,
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            chainAEid
        );

        // Configurations
        {
            priceOracle.setUnderlyingPrice(lTokenBTC, 100000e18);
            priceOracle.setUnderlyingPrice(lTokenDAI, 1e18);
            priceOracle.setUnderlyingPrice(lTokenWETH, 2500e18);

            lendtroller._setPriceOracle(priceOracle);
            lendtroller.setLendStorage(address(lendStorage));
            lendtroller._supportMarket(lTokenBTC);
            lendtroller._supportMarket(lTokenDAI);
            lendtroller._supportMarket(lTokenWETH);
            lendtroller.setLendToken(address(lend));
            lendtroller._setCollateralFactor(lTokenBTC, 0.8e18);
            lendtroller._setCollateralFactor(lTokenDAI, 0.8e18);
            lendtroller._setCollateralFactor(lTokenWETH, 0.8e18);
            lendtroller._setCloseFactor(0.5e18);
            lendtroller._setLiquidationIncentive(1.08e18);

            LToken[] memory lTokens = new LToken[](3);
            lTokens[0] = lTokenBTC;
            lTokens[1] = lTokenDAI;
            lTokens[2] = lTokenWETH;
            uint256[] memory supplySpeeds = new uint256[](3);
            supplySpeeds[0] = 1e18;
            supplySpeeds[1] = 1e18;
            supplySpeeds[2] = 1e18;
            uint256[] memory borrowSpeeds = new uint256[](3);
            borrowSpeeds[0] = 2e18;
            borrowSpeeds[1] = 2e18;
            borrowSpeeds[2] = 2e18;
            lendtroller._setLendSpeeds(lTokens, supplySpeeds, borrowSpeeds);

            lendStorage.addSupportedTokens(address(btc), address(lTokenBTC));
            lendStorage.addSupportedTokens(address(dai), address(lTokenDAI));
            lendStorage.addSupportedTokens(address(weth), address(lTokenWETH));
            lendStorage.addUnderlyingToDestlToken(
                address(btc),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(dai),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(weth),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.setAuthorizedContract(address(coreRouter), true);
            lendStorage.setAuthorizedContract(address(crossChainRouter), true);

            crossChainRouter.setPeer(
                chainBEid,
                bytes32(uint256(uint160(mockPeer)))
            );

            coreRouter.setCrossChainRouter(address(crossChainRouter));

            deal(address(crossChainRouter), 1 ether);
        }

        vm.stopPrank();

        {
            vm.label(address(btc), "BTC");
            vm.label(address(dai), "DAI");
            vm.label(address(weth), "WETH");
            vm.label(address(lTokenBTC), "lToken BTC");
            vm.label(address(lTokenDAI), "lToken DAI");
            vm.label(address(lTokenWETH), "lToken WETH");
            vm.label(address(endpoint), "Endpoint");
            vm.label(address(lendtroller), "Lendtroller");
            vm.label(address(lend), "Lend");
            vm.label(address(priceOracle), "PriceOracle");
            vm.label(address(lendStorage), "LendStorage");
            vm.label(address(coreRouter), "CoreRouter");
            vm.label(address(crossChainRouter), "CrossChainRouter");
        }
    }

    function testPOC_CrossChainLiquidationWillAlwaysFail() public {
        address alice = makeAddr("Alice");

        // Supply 1 WETH (2500u)
        {
            deal(address(weth), alice, 1 ether);

            vm.startPrank(alice);

            weth.safeApprove(address(coreRouter), 1 ether);
            coreRouter.supply(1 ether, address(weth));

            vm.stopPrank();
        }

        // Cross-chain borrow
        {
            vm.prank(alice);
            crossChainRouter.borrowCrossChain(2000e18, address(dai), chainBEid);
        }

        // Mock LayerZero message to trigger `_handleValidBorrowRequest()`
        {
            Origin memory _origin = Origin({
                srcEid: chainBEid,
                sender: bytes32(uint256(uint160(address(mockPeer)))),
                nonce: 1
            });

            bytes memory _payload = abi.encode(
                2000e18, // amount
                1e18, // borrowIndex
                2000e18, // collateral: 2500 * 0.8
                alice, // sender
                address(lTokenDAI), // destLToken
                address(0), // liquidator
                address(dai), // srcToken
                uint8(CrossChainRouter.ContractType.ValidBorrowRequest) // contractType
            );

            vm.prank(address(endpoint));
            crossChainRouter.lzReceive(
                _origin, 
                bytes32(0), 
                _payload, 
                makeAddr("Executor"), 
                ""
            );
        }

        // Mock LayerZero message to trigger liquidation
        {
            Origin memory _origin = Origin({
                srcEid: chainBEid,
                sender: bytes32(uint256(uint160(address(mockPeer)))),
                nonce: 2
            });

            bytes memory _payload = abi.encode(
                1080e18, // seizeToken: 1000 * 1.08
                1e18, // borrowIndex
                0, // collateral
                alice, // sender
                address(lTokenDAI), // destLToken
                address(0), // liquidator
                address(dai), // srcToken
                uint8(CrossChainRouter.ContractType.CrossChainLiquidationExecute) // contractType
            );

            vm.prank(address(endpoint));
            crossChainRouter.lzReceive(
                _origin, 
                bytes32(0), 
                _payload, 
                makeAddr("Executor"), 
                ""
            );
        }
    }
}

```

The output is as below:

> [PASS] testPOC_CrossChainLiquidationWillAlwaysFail() (gas: 1208485)
> Logs:
>  
>   borrowed: 1080000000000000000000
>   collateral: 2000000000000000000000
>  Liquidation Invalid

### Mitigation

Implement correctly to check the validity of a cross-chain liquidation on `ChainA`.