Old Chambray Rhino

High

# cross-chain borrowing functionality is broken

### Summary

The cross-chain borrowing functionality contains critical flaws that prevent proper collateral tracking and validation, leading to transaction failures. The primary issues involve missing cross-chain collateral registration on the source chain and incorrect collateral calculation due to unupdated investment mappings.

### Root Cause

The [`borrowCrossChain`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113C5-L154C6)  function fails to register cross-chain collateral on the source chain, which is required for destination chain validation.
````solidity
function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
        require(msg.sender != address(0), "Invalid sender");
        require(_amount != 0, "Zero borrow amount");
        require(address(this).balance > 0, "Out of money");

        // Get source lToken for collateral
        address _lToken = lendStorage.underlyingTolToken(_borrowToken);
        require(_lToken != address(0), "Unsupported source token");

        // Get the destination chain's version of the token
        address destLToken = lendStorage.underlyingToDestlToken(_borrowToken, _destEid);
        require(destLToken != address(0), "Unsupported destination token");

        // Accrue interest on source token (collateral token) on source chain
        LTokenInterface(_lToken).accrueInterest();

        // Add collateral tracking on source chain
        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        if (!isMarketEntered(msg.sender, _lToken)) {
            enterMarkets(_lToken);
        }

        // Get current collateral amount for the LayerZero message
        // This will be used on dest chain to check if sufficient
        (, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
    }
```
The function needs to add cross chain collateral in the source chain, so when it's checking in the destination chain for a correspondent `Borrow` it finds it.

Another problem as to do with the function [`getHypotheticalAccountLiquidityCollateral`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L385C5-L543C6):
```solidity
function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;

        // Calculate collateral value from supplied assets
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();

        // First loop: Calculate collateral value from supplied assets
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
      ->    uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];

            // Get collateral factor and price for this asset
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
            vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()});

            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);

            // Add to collateral sum
            vars.sumCollateral =
         ->     mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        } // .... Rest of code ....
```
This function uses the `totalInvestment` mapping to determine the sum of collateral a user has. This becomes a problem when in the function `borrowCrossChain` the mapping is never updated and therefore the collateral always returns 0.

When  sending the message to the destination chain it validates the borrow parameters in `_handleBorrowCrossChainRequest`:
```solidity
function _handleBorrowCrossChainRequest(
        LZPayload memory payload,
        uint32 srcEid
    ) private {
        // Accrue interest on borrowed token on destination chain
        LTokenInterface(payload.destlToken).accrueInterest();

        // Get current borrow index from destination lToken
        uint256 currentBorrowIndex = LTokenInterface(payload.destlToken)
            .borrowIndex();

        // Important: Use the underlying token address
        address destUnderlying = lendStorage.lTokenToUnderlying(
            payload.destlToken
        );

        // Check if user has any existing borrows on this chain
        bool found = false;
        uint256 index;

        LendStorage.Borrow[] memory userCrossChainCollaterals = lendStorage
            .getCrossChainCollaterals(payload.sender, destUnderlying);

        /**
         * Filter collaterals for the given srcEid. Prevents borrowing from
         * multiple collateral sources.
         */
        for (uint256 i = 0; i < userCrossChainCollaterals.length; ) {
            if (
                userCrossChainCollaterals[i].srcEid == srcEid &&
                userCrossChainCollaterals[i].srcToken == payload.srcToken
            ) {
                index = i;
                found = true;
                break;
            }
            unchecked {
                ++i;
            }
        }

        // Get existing borrow amount
        (uint256 totalBorrowed, ) = lendStorage
   ->       .getHypotheticalAccountLiquidityCollateral(
                payload.sender,
                LToken(payable(payload.destlToken)),
                0,
                payload.amount
            );

        // Verify the collateral from source chain is sufficient for total borrowed amount
    ->  require(payload.collateral >= totalBorrowed, "Insufficient collateral");

        // Execute the borrow on destination chain
     -> CoreRouter(coreRouter).borrowForCrossChain(
            payload.sender,
            payload.amount,
            payload.destlToken,
            destUnderlying
        ); // ... Rest of code ...
```
This require will always fail because `payload.collateral` will be 0.
```solidity
 require(payload.collateral >= totalBorrowed, "Insufficient collateral");
```
The function called by the router `borrowForCrossChain` has it's own vulnerabilities explained in other report.



### Internal Pre-conditions

Not applied.

### External Pre-conditions

Not apllied.

### Attack Path

Not applied.

### Impact

Protocol doesnt't work properly.

### PoC

Add this to the `script/Deploy.s.sol` file:
```solidity
// Use the provided endpoint instead
        layerZeroEndpoint = _endpoint != address(0)
            ? _endpoint
            : configuredEndpoint;

        // If a specific endpoint is provided (like in tests), use its chain ID
  ->      if (_endpoint != address(0)) {
  ->         currentEid = EndpointV2(_endpoint).eid();
  ->      }

        // Start broadcasting transactions
        vm.startBroadcast();
```

Change the file `TestBorrowingCrossChain.t.sol` to this:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "../lib/forge-std/src/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface, LErc20Interface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import {SendLibMock} from "@layerzerolabs/lz-evm-messagelib-v2/test/mocks/SendLibMock.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";

contract TestBorrowingCrossChain is LayerZeroTest {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokensA;
    address[] public supportedTokensB;
    address[] public supportedTokensC;
    bool public isTestnet;
    address public deployer;
    address public borrower;
    address public liquidator;

    // Chain A (Source)
    CrossChainRouterMock public routerA;
    LendStorage public lendStorageA;
    CoreRouter public coreRouterA;
    Lendtroller public lendtrollerA;
    InterestRateModel public interestRateModelA;
    SimplePriceOracle public priceOracleA;
    address[] public lTokensA;

    // Chain B (Destination)
    CrossChainRouterMock public routerB;
    LendStorage public lendStorageB;
    CoreRouter public coreRouterB;
    Lendtroller public lendtrollerB;
    InterestRateModel public interestRateModelB;
    SimplePriceOracle public priceOracleB;
    address[] public lTokensB;

    // Chain C (Destination)
    // CrossChainRouterMock public routerC;
    // LendStorage public lendStorageC;
    // CoreRouter public coreRouterC;
    // Lendtroller public lendtrollerC;
    // InterestRateModel public interestRateModelC;
    // SimplePriceOracle public priceOracleC;
    // address[] public lTokensC;

    uint32 constant CHAIN_A_ID = 31337; // Use LayerZero's actual test EID
    uint32 constant CHAIN_B_ID = 31338; // Use a different EID for Chain B
    // uint32 constant CHAIN_C_ID = 3;

    EndpointV2 public endpointA;
    EndpointV2 public endpointB;
    // EndpointV2 public endpointC;
    SimpleMessageLib public simpleMsgLibA;
    SimpleMessageLib public simpleMsgLibB;
    // SimpleMessageLib public simpleMsgLibC;

    // Events to test
    event BorrowSuccess(
        address indexed borrower,
        address indexed lToken,
        uint256 accountBorrow
    );

    // Instead of doing everything in setUp(), split into multiple functions:

    function setUp() public override(LayerZeroTest) {
        setupAddresses();
        setupEndpoints();
        deployChainA();
        deployChainB();
        fixRouterChainIds();
    }

    function setupAddresses() internal {
        deployer = makeAddr("deployer");
        vm.deal(deployer, 1000 ether);

        borrower = makeAddr("borrower");
        vm.deal(borrower, 1000 ether);

        liquidator = makeAddr("liquidator");
        vm.deal(liquidator, 1000 ether);
    }

    function setupEndpoints() internal {
        // Create endpoints with your desired chain IDs
        (endpointA, simpleMsgLibA) = setupEndpointWithSimpleMsgLib(CHAIN_A_ID);
        (endpointB, simpleMsgLibB) = setupEndpointWithSimpleMsgLib(CHAIN_B_ID);

        // Set up cross-chain communication between endpoints
        setDefaultMsgLib(endpointA, address(simpleMsgLibA), CHAIN_B_ID);
        setDefaultMsgLib(endpointB, address(simpleMsgLibB), CHAIN_A_ID);
    }

    function deployChainA() internal {
        Deploy deployA = new Deploy();

        // Unpack directly into storage variables to avoid locals
        (
            address priceOracleAddr,
            address lendtrollerAddr,
            address interestRateModelAddr,
            address[] memory lTokenAddrs,
            address payable routerAddr,
            address payable coreRouterAddr,
            address lendStorageAddr,
            ,
            address[] memory supportedTokensAddr
        ) = deployA.run(address(endpointA));

        // Store in storage variables
        priceOracleA = SimplePriceOracle(priceOracleAddr);
        lendtrollerA = Lendtroller(lendtrollerAddr);
        interestRateModelA = InterestRateModel(interestRateModelAddr);
        lTokensA = lTokenAddrs;
        // Don't store routerA yet - will create new one
        coreRouterA = CoreRouter(coreRouterAddr);
        lendStorageA = LendStorage(lendStorageAddr);
        supportedTokensA = supportedTokensAddr;
    }

    function deployChainB() internal {
        Deploy deployB = new Deploy();

        // Unpack directly into storage variables to avoid locals
        (
            address priceOracleAddr,
            address lendtrollerAddr,
            address interestRateModelAddr,
            address[] memory lTokenAddrs,
            address payable routerAddr,
            address payable coreRouterAddr,
            address lendStorageAddr,
            ,
            address[] memory supportedTokensAddr
        ) = deployB.run(address(endpointB));

        // Store in storage variables
        priceOracleB = SimplePriceOracle(priceOracleAddr);
        lendtrollerB = Lendtroller(lendtrollerAddr);
        interestRateModelB = InterestRateModel(interestRateModelAddr);
        lTokensB = lTokenAddrs;
        // Don't store routerB yet - will create new one
        coreRouterB = CoreRouter(coreRouterAddr);
        lendStorageB = LendStorage(lendStorageAddr);
        supportedTokensB = supportedTokensAddr;
    }

    function fixRouterChainIds() internal {
        // ✅ Create routers with correct chain IDs (minimal local variables)
        routerA = new CrossChainRouterMock(
            address(lendStorageA),
            address(priceOracleA),
            address(lendtrollerA),
            payable(address(coreRouterA)),
            CHAIN_A_ID
        );

        routerB = new CrossChainRouterMock(
            address(lendStorageB),
            address(priceOracleB),
            address(lendtrollerB),
            payable(address(coreRouterB)),
            CHAIN_B_ID
        );

        // Verify
        require(routerA.currentEid() == CHAIN_A_ID, "Router A wrong chain");
        require(routerB.currentEid() == CHAIN_B_ID, "Router B wrong chain");
        // routerC = CrossChainRouterMock(payable(routerAddressC));

        // Set up pair relationships
        routerA.setPairContract(payable(address(routerB)));
        // routerA.setPairContract(payable(address(routerC)));
        routerB.setPairContract(payable(address(routerA)));

        address coreRouterAOwner = coreRouterA.owner();
        address coreRouterBOwner = coreRouterB.owner();

        // Set the crossChainRouter in CoreRouter for both chains
        vm.startPrank(coreRouterAOwner); // or whoever owns CoreRouter A
        coreRouterA.setCrossChainRouter(address(routerA));
        vm.stopPrank();

        vm.startPrank(coreRouterBOwner); // or whoever owns CoreRouter B
        coreRouterB.setCrossChainRouter(address(routerB));
        vm.stopPrank();
    }

    // Helper function to supply tokens before testing borrowing
    // Fixed helper function to supply tokens and get lTokens
    function _supplyA(
        uint256 amount
    ) internal returns (address token, address lToken) {
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[0];
        lToken = lendStorageA.underlyingTolToken(token);

        // === DEPLOYER SUPPLY ===
        vm.startPrank(deployer);
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(lToken, amount); // Approve lToken directly
        LErc20Interface(lToken).mint(amount);

        // Enter market to enable as collateral
        address[] memory markets = new address[](1);
        markets[0] = lToken;
        lendtrollerA.enterMarkets(markets);
        vm.stopPrank();

        // === BORROWER SUPPLY ===
        vm.startPrank(borrower);
        ERC20Mock(token).mint(borrower, amount);
        IERC20(token).approve(lToken, amount); // Approve lToken directly
        LErc20Interface(lToken).mint(amount);
        vm.stopPrank();
        vm.startPrank(address(routerA)); // Switch to admin

        // Update the totalInvestment mapping that LendStorage uses for collateral calculation
        lendStorageA.updateTotalInvestment(borrower, lToken, amount);
        // OR try alternative function names:
        // lendStorageA.setTotalInvestment(borrower, lToken, borrowerBalance);
        // lendStorageA.trackSupply(borrower, lToken, borrowerBalance);

        vm.stopPrank(); // Stop admin

        // Enter market to enable as collateral
        lendtrollerA.enterMarkets(markets);
        // === CRITICAL: REGISTER CROSS-CHAIN COLLATERAL ===
        console2.log("=== REGISTERING CROSS-CHAIN COLLATERAL ===");

        // Register the borrower's collateral for cross-chain use
        // This tells the system that this user's collateral on Chain A can be used for borrowing on Chain B
        address destToken = lendStorageA.underlyingToDestUnderlying(
            token,
            CHAIN_B_ID
        );
        require(
            destToken != address(0),
            "Destination token mapping should exist"
        );

        // Verify borrower got lTokens
        uint256 borrowerBalance = IERC20(lToken).balanceOf(borrower);
        console2.log("Borrower lToken balance after mint: ", borrowerBalance);

        // Create the Borrow struct for cross-chain collateral registration
        // Even though this is collateral, the function uses Borrow struct format
        LendStorage.Borrow memory collateralInfo = LendStorage.Borrow({
            srcEid: 31337, // Source chain's layer zero endpoint id (Chain A)
            destEid: CHAIN_B_ID, // Destination chain's layer zero endpoint id (Chain B)
            principle: borrowerBalance, // Amount of lTokens as collateral
            borrowIndex: 1e18, // Standard borrow index (1.0)
            borrowedlToken: lToken, // lToken address holding the collateral
            srcToken: token // Source token address (underlying)
        });

        vm.startPrank(address(routerA)); // Switch to admin account
        // Register cross-chain collateral as admin
        lendStorageA.addCrossChainCollateral(borrower, token, collateralInfo);
        vm.stopPrank();

        // Register cross-chain collateral (the exact function name may vary)
        // This is the missing piece that makes getCrossChainCollaterals() return non-empty

        console2.log("Registered cross-chain collateral for borrower");
        console2.log("Token: ", token);
        console2.log("LToken: ", lToken);
        console2.log("Amount: ", borrowerBalance);
        vm.stopPrank();

        // === LIQUIDATOR SUPPLY ===
        vm.startPrank(liquidator);
        ERC20Mock(token).mint(liquidator, amount * 50);
        IERC20(token).approve(lToken, amount * 50); // Approve lToken directly
        LErc20Interface(lToken).mint(amount * 50);

        // Enter market to enable as collateral
        lendtrollerA.enterMarkets(markets);
        vm.stopPrank();
    }

    // Struct to group related test data and avoid stack issues
    struct TestScenario {
        address ethToken;
        address ethLToken;
        address usdcToken;
        address usdcLToken;
        uint256 collateralAmount;
        uint256 borrowAmount;
        uint256 ethPrice;
        uint256 usdcPrice;
    }

    struct LiquidationData {
        uint256 repayAmount;
        uint256 liquidatorBalanceBefore;
        uint256 borrowerCollateralBefore;
        uint256 liquidatorBalanceAfter;
        uint256 borrowerCollateralAfter;
    }

    function _setupCrossChainMappings() private {
        address ethToken = supportedTokensA[0];
        address ethLToken = lendStorageA.underlyingTolToken(ethToken);
        address usdcToken = supportedTokensB[0];
        address usdcLToken = lendStorageB.underlyingTolToken(usdcToken);
        address ownerStorageA = lendStorageA.owner();
        address ownerStorageB = lendStorageB.owner();

        vm.startPrank(ownerStorageA);

        // ✅ Map ETH (Chain A) → USDC lToken (Chain B)
        lendStorageA.addUnderlyingToDestUnderlying(
            ethToken,
            usdcToken,
            CHAIN_B_ID
        );
        lendStorageA.addUnderlyingToDestlToken(
            ethToken,
            usdcLToken,
            CHAIN_B_ID
        );

        vm.stopPrank();

        vm.startPrank(ownerStorageB);

        // ✅ Reverse mappings for Chain B
        lendStorageB.addUnderlyingToDestUnderlying(
            usdcToken,
            ethToken,
            CHAIN_A_ID
        );
        lendStorageB.addUnderlyingToDestlToken(
            usdcToken,
            ethLToken,
            CHAIN_A_ID
        );

        vm.stopPrank();
    }

    function setupChainBLiquidity() private {
        address usdcToken = supportedTokensB[0];
        address usdcLToken = lendStorageB.underlyingTolToken(usdcToken);

        console2.log("=== SETTING UP CHAIN B USDC LIQUIDITY ===");

        // Create a liquidity provider
        address liquidityProvider = 0x1234567890123456789012345678901234567890;

        vm.startPrank(liquidityProvider);

        // 1. Give liquidity provider USDC
        ERC20Mock(usdcToken).mint(liquidityProvider, 10000e18); // 10,000 USDC

        // 2. Supply USDC to the lToken (this gives the lToken USDC to lend)
        IERC20(usdcToken).approve(usdcLToken, 10000e18);
        LErc20Interface(usdcLToken).mint(10000e18);

        vm.stopPrank();

        // 3. Verify the lToken now has USDC to lend
        uint256 usdcBalance = IERC20(usdcToken).balanceOf(usdcLToken);
        console2.log(
            "Chain B USDC lToken now has:",
            usdcBalance,
            "USDC available"
        );
        require(usdcBalance >= 50e18, "Not enough USDC for test");

        console2.log(" Chain B liquidity ready!");
    }

    // Test ONLY the protocol (no manual collateral registration)
    function test_protocol_only_diagnosis() public {
        console2.log(" TESTING PROTOCOL ONLY (no manual registration)");

        _setupCrossChainMappings();

        setupChainBLiquidity();

        address ethToken = supportedTokensA[0];
        address ethLToken = lendStorageA.underlyingTolToken(ethToken);
        address usdcToken = supportedTokensB[0];

        // Setup borrower with ETH collateral
        vm.startPrank(borrower);
        ERC20Mock(ethToken).mint(borrower, 200e18);
        IERC20(ethToken).approve(ethLToken, 200e18);
        LErc20Interface(ethLToken).mint(200e18);

        address[] memory markets = new address[](1);
        markets[0] = ethLToken;
        lendtrollerA.enterMarkets(markets);
        vm.stopPrank();

        // Setup prices
        priceOracleA.setDirectPrice(ethToken, 2000e18);
        priceOracleB.setDirectPrice(usdcToken, 1e18);

        uint256 ethCollateralAmount = IERC20(ethLToken).balanceOf(borrower);
        console2.log(" PRE-BORROW STATE:");
        console2.log("ETH lToken balance (borrower):", ethCollateralAmount);
        console2.log("Expected collateral principle:", ethCollateralAmount);

        // Check that NO collateral is registered yet
        LendStorage.Borrow[] memory beforeA = lendStorageA
            .getCrossChainCollaterals(borrower, ethToken);
        LendStorage.Borrow[] memory beforeB = lendStorageB
            .getCrossChainCollaterals(borrower, ethToken);
        console2.log(
            "Chain A collaterals before:",
            beforeA.length,
            "(should be 0)"
        );
        console2.log(
            "Chain B collaterals before:",
            beforeB.length,
            "(should be 0)"
        );

        uint256 borrowAmount = 50e18; // Borrow 500 USDC

        console2.log("\n EXECUTING PROTOCOL borrowCrossChain:");

        console2.log("Borrow amount:", borrowAmount);

        address lendStorageAOwner = lendStorageA.owner();

        vm.startPrank(lendStorageAOwner);

        lendStorageA.setAuthorizedContract(address(routerA), true);

        vm.stopPrank();

        address lendStorageBOwner = lendStorageB.owner();

        vm.startPrank(lendStorageBOwner);

        lendStorageB.setAuthorizedContract(address(routerB), true);

        vm.stopPrank();

        // Execute ONLY the protocol function (no manual setup)
        vm.startPrank(borrower);
        routerA.borrowCrossChain{value: 1 ether}(
            borrowAmount,
            ethToken,
            CHAIN_B_ID
        );
        vm.stopPrank();

        console2.log("\n WHAT DID THE PROTOCOL REGISTER?");

        console2.log("\n WHAT DID THE PROTOCOL REGISTER?");

        // ✅ FIX: Use Borrow[] instead of CrossChainCollateral[]
        LendStorage.Borrow[] memory chainABorrows = lendStorageA
            .getCrossChainCollaterals(borrower, ethToken);
        LendStorage.Borrow[] memory chainBBorrows = lendStorageB
            .getCrossChainCollaterals(borrower, usdcToken);

        console2.log("Chain A borrows after:", chainABorrows.length);
        console2.log("Chain B borrows after:", chainBBorrows.length);

        if (chainABorrows.length > 0) {
            console2.log("Chain A borrow amount:", chainABorrows[0].principle);
            console2.log("Chain A destination:", chainABorrows[0].destEid);
        }

        if (chainBBorrows.length > 0) {
            console2.log("Chain B borrow amount:", chainBBorrows[0].principle);
            console2.log("Chain B source:", chainBBorrows[0].srcEid);
        }

        uint256 ethLCollateralAmountAfter = IERC20(ethLToken).balanceOf(
            borrower
        );
        uint256 ethCollateralAmountAfter = IERC20(ethToken).balanceOf(borrower);
        uint256 usdcTokenCollateralAmountAfter = IERC20(usdcToken).balanceOf(
            borrower
        );

        // Updated assertions
        require(chainABorrows.length > 0, "Chain A should register borrow");
        require(chainBBorrows.length > 0, "Chain B should register borrow");

        console2.log(" CROSS-CHAIN BORROW REGISTRATION WORKING!");
    }
}
```
Logs of the test:
```bash
//... final logs
CrossChainRouterMock::_lzReceive(Origin({ srcEid: 31337 [3.133e4], sender: 0x0000000000000000000000007d8a6b343f153d9d026466636ed35ca13c367fd2, nonce: 0 }), 0x0000000000000000000000000000000000000000000000000000000000000000, 0x000000000000000000000000000000000000000000000002b5e3af16b1880000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000007d8a6b343f153d9d026466636ed35ca13c367fd200000000000000000000000077443101f739bf8b771b96a7dd92e89088379d1f000000000000000000000000000000000000000000000000000000000000000000000000000000000000000034a1d3fff3958843c43ad80f30b94c510645c3160000000000000000000000000000000000000000000000000000000000000000, 0x0000000000000000000000000000000000000000, 0x)
    │   │   ├─ [417] LErc20Immutable::accrueInterest()
    │   │   │   └─ ← [Return] 0
    │   │   ├─ [2406] LErc20Immutable::borrowIndex() [staticcall]
    │   │   │   └─ ← [Return] 1000000000000000000 [1e18]
    │   │   ├─ [2661] LendStorage::lTokenToUnderlying(LErc20Immutable: [0x77443101F739bf8b771B96a7DD92E89088379D1F]) [staticcall]
    │   │   │   └─ ← [Return] ERC20Mock: [0xe08990C15f4Ed27B45651A5b4F12cD0c739836F2]
    │   │   ├─ [3073] LendStorage::getCrossChainCollaterals(borrower: [0x7d8A6b343f153D9d026466636ED35cA13C367fD2], ERC20Mock: [0xe08990C15f4Ed27B45651A5b4F12cD0c739836F2]) [staticcall]
    │   │   │   └─ ← [Return] []
    │   │   ├─ [15621] LendStorage::getHypotheticalAccountLiquidityCollateral(borrower: [0x7d8A6b343f153D9d026466636ED35cA13C367fD2], LErc20Immutable: [0x77443101F739bf8b771B96a7DD92E89088379D1F], 0, 50000000000000000000 [5e19]) [staticcall]
    │   │   │   ├─ [6335] SimplePriceOracle::getUnderlyingPrice(LErc20Immutable: [0x77443101F739bf8b771B96a7DD92E89088379D1F]) [staticcall]
    │   │   │   │   ├─ [3255] LErc20Immutable::symbol() [staticcall]
    │   │   │   │   │   └─ ← [Return] "lE20M"
    │   │   │   │   ├─ [448] LErc20Immutable::underlying() [staticcall]
    │   │   │   │   │   └─ ← [Return] ERC20Mock: [0xe08990C15f4Ed27B45651A5b4F12cD0c739836F2]
    │   │   │   │   └─ ← [Return] 1000000000000000000 [1e18]
    │   │   │   └─ ← [Return] 50000000000000000000 [5e19], 0
    │   │   └─ ← [Revert] revert: Insufficient collateral
    │   └─ ← [Revert] revert: Insufficient collateral
    └─ ← [Revert] revert: Insufficient collateral

```


### Mitigation

Add this to the end of `borrowCrossChain` function, before sending the message:
```solidity
// .... rest of code
lendStorage.addCrossChainCollateral(
            msg.sender, // borrower
            _borrowToken, // ETH token
            LendStorage.Borrow({
                srcEid: currentEid, // Chain A (31337)
                destEid: _destEid, // Chain B (31338)
                principle: _amount, // 50 USDC being borrowed
                borrowIndex: 1e18, // Initial borrow index
                borrowedlToken: destLToken, // USDC lToken address
                srcToken: _borrowToken // ETH token address
            })
        );

_send(.....);
```

Update `totalInvestment` mapping before calculating collateral:

```solidity
function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
// ...
uint256 currentBalance = IERC20(_lToken).balanceOf(msg.sender);
    lendStorage.updateTotalInvestment(msg.sender, _lToken, currentBalance);
// ... Rest of code
```