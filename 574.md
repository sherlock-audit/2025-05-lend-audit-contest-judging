Scrawny Red Sloth

Medium

# Liquidators are forced to supply the liquidated collateral to redeem their reward after liquidation

## Summary
Anyone can liquidate another user whether or not themselves have aleady supplied funds of the token they are liquidating. However, when they dont have this, their total investment for this token will be set based on the `seizeTokens - currentReward` but this asset wont be added for them as one of their supplied assets.

As such, during redeem, their collateral will be visibly `0` hence failure. Therefore to claim these rewards, they are forced to supply this asset to perform redeem as this remains as the only method to add this asset as one of their supplied assets.

## Root cause
During liquidation, [total investments are updated](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L306-L314) for both the `borrower` and `liquidator`  as follows:
```solidity
        // Update total investment
        lendStorage.updateTotalInvestment(
            borrower, lTokenCollateral, lendStorage.totalInvestment(borrower, lTokenCollateral) - seizeTokens
        );
>>      //@audit Fails to add the asset for liquidator prior
        // simply proceeds to create an invetment for them
>>      lendStorage.updateTotalInvestment(
            sender,
            lTokenCollateral,
            lendStorage.totalInvestment(sender, lTokenCollateral) + (seizeTokens - currentReward)
        );
```
Upto this point, the liquidator has 0 supply assets recorded in the protocol.

Now, when he attmepts to redeem his investment from this seized tokens, the [`lendStorage.getHypotheticalAccountLiquidityCollateral()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L393-L466) is invoked to return both `borrowed` and `collateral`:
```solidity
        // @audit These arrays are empty so far
>>      address[] memory suppliedAssets = userSuppliedAssets[account].values();
>>      address[] memory borrowedAssets = userBorrowedAssets[account].values();

        // First loop: Calculate collateral value from supplied assets
>>      // @audit Will not loop as there is nothing in the array and as such, vars.sumCollateral ==0
        for (uint256 i = 0; i < suppliedAssets.length;) {
            ---snip---

            // Add to collateral sum
            vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }

        // Second loop: Calculate borrow value from borrowed assets
        // @audit Will not loop as there is nothing in the array
        for (uint256 i = 0; i < borrowedAssets.length;) {
           ---snip---

            // Add to borrow sum
            vars.sumBorrowPlusEffects =
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }

        // Handle effects of current action
>>      // @audit Since address(lTokenModify) != address(0), and `redeemTokens > 0`, the vars.sumBorrowPlusEffects >0
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

            // Add effect of redeeming collateral
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
                vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()});
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }

            // Add effect of new borrow
            if (borrowAmount > 0) {
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }

>>      return (vars.sumBorrowPlusEffects, vars.sumCollateral);
```
Then in `redeem()`, the following check is made:
```solidity
    require(collateral >= borrowed, "Insufficient liquidity");
```
However, as seen, `collateral == 0` and `borrowed > 0`, this reverts.

## Internal Pre-conditions
1. Liquidator fails to have liquidated asset added for them as one of their supplied assets
2. `redeem()` requires `collateral` to exceed `borrowed` which is not the case

## External Pre-conditions
None

## Attack path
1. Liquidator attempts to redeem their seized tokens
2. `redeem()` fails
3. Funds stuck
4. To redeem therefore, the liquidator has to perform a supply which is unnecessary

## Impact
Liquidators are blocked from redeeming their seized tokens by being forced to first supply more of the asset they liquidated as collateral.

## Poc
1. In `test/TestLiquidations.t.sol`, add the following test:
```solidity
    function test_liquidator_forced_to_supply_tokens_to_redeem_their_reward
    (
        uint256 supplyAmount,
        uint256 borrowAmount,
        uint256 newPrice
    ) public {
        // Bound inputs to reasonable values
        supplyAmount = bound(supplyAmount, 100e18, 1000e18);
        borrowAmount = bound(borrowAmount, 50e18, supplyAmount * 60 / 100); // Max 60% LTV
        newPrice = bound(newPrice, 1e16, 5e16); // 1-5% of original price

        // Supply token0 as collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount, 0);

        // Supply token1 as liquidity on Chain A from random wallet
        (address tokenB, address lTokenB) = _supplyA(address(1), supplyAmount, 1);

        vm.prank(deployer);
        coreRouterA.borrow(borrowAmount, tokenB);

        // Simulate price drop of collateral (tokenA) only on first chain
        priceOracleA.setDirectPrice(tokenA, newPrice);
        // Simulate price drop of collateral (tokenA) only on second chain
        priceOracleB.setDirectPrice(
            lendStorageB.lTokenToUnderlying(lendStorageB.crossChainLTokenMap(lTokenA, block.chainid)), newPrice
        );

        // Attempt liquidation
        vm.startPrank(liquidator);
        ERC20Mock(tokenB).mint(liquidator, borrowAmount);
        IERC20(tokenB).approve(address(coreRouterA), borrowAmount);

        // Expect LiquidateBorrow event
        vm.expectEmit(true, true, true, true);
        emit LiquidateBorrow(liquidator, lTokenB, deployer, lTokenA);

        // Repay 1% of the borrow
        uint256 repayAmount = borrowAmount / 100;

        coreRouterA.liquidateBorrow(deployer, repayAmount, lTokenA, tokenB);
        vm.stopPrank();

        // Verify liquidation was successful
        assertLt(
            lendStorageA.borrowWithInterestSame(deployer, lTokenB),
            borrowAmount,
            "Borrow should be reduced after liquidation"
        );
        
        // retrieve liquidators investment in protocol from seized collateral
        uint256 liquidatorSeizureRewards = lendStorageA.totalInvestment(liquidator, lTokenA);

        // assert that is greater than 0 from liquidation
        assertGt(liquidatorSeizureRewards, 0, "Liquidator should have some seizure rewards");

        // get liquidator supply assets
        address[] memory liquidatorSupplyAssets = lendStorageA.getUserSuppliedAssets(liquidator);

        // should show that no asset was added for him
        assertEq(liquidatorSupplyAssets.length, 0, "Liquidator should have no supply assets");
        
        // Check liquidity for liquidator
        (uint256 borrowed, uint256 collateral) =
            lendStorageA.getHypotheticalAccountLiquidityCollateral(
                liquidator,
                LToken(lTokenA),
                liquidatorSeizureRewards,
                0
            );
        // should show that collateral is 0  and borrowed is positive from Adding effect of 
        // redeemTokens i.e liquidatorSeizureRewards
        assertEq(collateral, 0);
        assertGt(borrowed, 0);
        
        // liquidator attempts to redeem his seized collateral
        vm.startPrank(liquidator);
        // expect revert
        vm.expectRevert(bytes("Insufficient liquidity"));
        coreRouterA.redeem(liquidatorSeizureRewards, payable(lTokenA));

        vm.stopPrank();

        // Now liquidator has to supply some tokens to redeem his reward
        // Supply token0 as collateral on Chain A
        _supplyA(liquidator, supplyAmount, 0);

        // retrieve liquidators current investment
        uint256 liquidatorCurrentInvestment = lendStorageA.totalInvestment(liquidator, lTokenA);

        // assert current investemnt is greater than seized reward
        assertGt(liquidatorCurrentInvestment, liquidatorSeizureRewards);

        // liquidator now attempts to redeem
        vm.prank(liquidator);
        coreRouterA.redeem(liquidatorCurrentInvestment, payable(lTokenA));

        // Now assert that liquidator has 0 investment in protocol
        assertEq(lendStorageA.totalInvestment(liquidator, lTokenA), 0); 
    }
```
2. Run: `forge test --mt test_liquidator_forced_to_supply_tokens_to_redeem_their_reward -vvvv`


## Mitigation
Add this asset to the liquidator:
```diff
        // Update total investment
        lendStorage.updateTotalInvestment(
            borrower, lTokenCollateral, lendStorage.totalInvestment(borrower, lTokenCollateral) - seizeTokens
        );

        //@audit add the asset for liquidator prior
+       lendStorage.addUserSuppliedAsset(sender, lTokenCollateral);

>>      lendStorage.updateTotalInvestment(
            sender,
            lTokenCollateral,
            lendStorage.totalInvestment(sender, lTokenCollateral) + (seizeTokens - currentReward)
        );
```