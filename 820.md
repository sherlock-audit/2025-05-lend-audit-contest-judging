Skinny Charcoal Chinchilla

High

# Users who supplied USDT wont be able to redeem.

### Summary

When users supply on the same chain via CoreRouter.sol::supply(), they give the underlying token and receive Ltokens in LendStorage.sol accounting. When they supply USDT, they will receive L-USDT in their accounting ([LendStorage.sol::totalInvestment](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L87)). However, they wont be able to redeem the L-USDT to receive back USDT via CoreRouter.sol::redeem() as the function does not handle USDT correctly.


### Root Cause


In [CoreRouter.sol::redeem()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L100), the underlying token is fetched from the LToken via [LendStorage.sol::lTokenToUnderlying()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L68). Then the function tries to transfer this to the redeemer via transfer(). For USDT, this would revert as [IERC20::transfer()](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/dc44c9f1a4c3b10af99492eed84f83ed244203f6/contracts/token/ERC20/IERC20.sol#L41) expects a bool and USDT::transfer() doesnt return a bool. 

```solidity 
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        require(_amount > 0, "Zero redeem amount");
        require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
        require(collateral >= borrowed, "Insufficient liquidity");
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
        uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
        require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed"); 

@>      IERC20(_token).transfer(msg.sender, expectedUnderlying); 

        lendStorage.distributeSupplierLend(_lToken, msg.sender);
        uint256 newInvestment = lendStorage.totalInvestment(msg.sender, _lToken) - _amount;
        lendStorage.updateTotalInvestment(msg.sender, _lToken, newInvestment);
        if (newInvestment == 0) {
            lendStorage.removeUserSuppliedAsset(msg.sender, _lToken);
        }
        emit RedeemSuccess(msg.sender, _lToken, expectedUnderlying, _amount);
        return 0;
    }
```

### Internal Pre-conditions

Protocol allow users to supply USDT. (Stated in contest details page).

### External Pre-conditions


Users supply USDT via CoreRouter.sol

### Attack Path

none

### Impact

Users can supply USDT but cant get them back, permeanantly losing it. USDT will be stuck in the contract. 

### PoC

1. User A supply USDT via CoreRouter.sol::supply() and their `totalInvestment` mapping gets updated.
2. User A gains intrest on his supplied USDT and tries to redeem.
3. Redemption fails as IERC20(USDT).transfer() reverts.
4. User A cant redeem his L-USDT to receive his supplied USDT.


### Mitigation

OZ's SafeERC20 is imported and already used in supply(), use it in redeem() too.

```diff
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
        ...

-       IERC20(_token).transfer(msg.sender, expectedUnderlying); 
+       IERC20(_token).safeTransfer(msg.sender, expectedUnderlying); 
        ...
    }
```