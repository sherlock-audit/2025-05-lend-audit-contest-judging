Fun Shadow Parakeet

High

# Incorrect check in `_checkLiquidationValid` may cause healthy accounts to be liquidated

### Summary

In `liquidateCrossChain`, the `_checkLiquidationValid` function is used to determine whether a position is eligible for liquidation. However, it incorrectly passes `payload.amount` as a new borrow into `getHypotheticalAccountLiquidityCollateral`, which artificially inflates the total borrow value. As a result, the check `borrowed > collateral` may return true even when the account is still healthy, leading to incorrect liquidation.

### Root Cause

On Chain B, `liquidateCrossChain` sends a message to Chain A via [`_executeLiquidationCore`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L273-L284).


Then, Chain A receives this message and performs a validation in [`_checkLiquidationValid`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L773):

```solidity
        else if (cType == ContractType.CrossChainLiquidationExecute) {
@>          if (_checkLiquidationValid(payload)) {
                _handleLiquidationExecute(payload, srcEid);
            } else {
                _sendLiquidationFailure(payload, srcEid);
            }
        }
```

Inside [`_checkLiquidationValid`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L433) `payload.amount` is incorrectly passed as `borrowAmount`:

```solidity
    function _checkLiquidationValid(LZPayload memory payload) private view returns (bool) {
        (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
@>          payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );
        return borrowed > collateral;
    }
```
This leads to an inflated `sumBorrowPlusEffects` in [getHypotheticalAccountLiquidityCollateral](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L435-L436):

```solidity
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
@>      uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        ...
        if (borrowAmount > 0) {
@>          vars.sumBorrowPlusEffects =
                mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
        }
        ...
    }
```
As a result, the calculated borrow value becomes higher than the userâ€™s actual debt, which can incorrectly trigger the liquidation condition.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

A liquidator can exploit this logic flaw to falsely mark a healthy account as undercollateralized and trigger liquidation using a specified repay amount.

Example Scenario:

1. The borrower supplies `100` TokenA on Chain A.
2. The borrower initiates a cross-chain borrow, specifying `70` TokenA as the borrowAmount, to borrow on Chain B.
3. A liquidator attempts to liquidate by repaying `10` tokens on Chain B.

Due to the incorrect simulation logic, the liquidation is triggered even though the account is still healthy.

### Impact

This issue may cause healthy positions to be liquidated incorrectly. It undermines the accuracy of the liquidation process and may lead to unintended user fund losses due to falsely triggered liquidations.

### PoC

The following is a runnable PoC, Add the test case in test/TestLiquidations.t.sol:

```solidity
function test_check_liquidation_valid() public {
        address borrower = makeAddr("borrower");

        uint256 supplyAmount = 100e18;
        uint256 borrowAmount = 70e18;
        // 1. Borrower supplies 100 TokenA on Chain A
        (address tokenA, address lTokenA) = _supplyA(borrower, supplyAmount, 0);
        _supplyB(borrower, 2 * borrowAmount, 0);

        // 2. Borrower performs a cross-chain borrow
        vm.deal(address(routerA), 1 ether); // For LayerZero fees
        vm.startPrank(borrower);
        routerA.borrowCrossChain(borrowAmount, tokenA, CHAIN_B_ID);
        vm.stopPrank();

        // 3. Simulate liquidation by a liquidator and compare the results of the validity checks:
        //  - Current implementation: incorrectly treats `payload.amount` as new borrow, which inflates the borrow value and makes the position look undercollateralized.
        (uint256 borrowed1, uint256 collateral1) = lendStorageA.getHypotheticalAccountLiquidityCollateral(borrower, LToken(lTokenA), 0, 10e18);
        assertGt(borrowed1, collateral1, "Artificial borrow makes the position insolvent");
        //  - Correct check: no simulated borrow amount, position remains healthy
        (uint256 borrowed2, uint256 collateral2) = lendStorageA.getHypotheticalAccountLiquidityCollateral(borrower, LToken(lTokenA), 0, 0);
        assertLt(borrowed2, collateral2,  "Position is expected to be healthy");
    }
```

Run with:

```shell
forge test --mt test_check_liquidation_valid  -vvvv
```

Output:

<img width="764" alt="Image" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/563dfe18-3735-43ad-9386-2c0698804c2d" />


Under the current implementation, `borrowed1` is `8e19`, while collateral1 is `7.5e19`.

<img width="1729" alt="Image" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/419ec6cc-44aa-4dfe-8e1b-374fc7fc5687" />


Under the correct logic, `borrowed2` should be `7e19`, and `collateral2` remains `7.5e19`.

<img width="1536" alt="Image" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/d4be7109-a3c8-420b-805c-330fb3c9b2ba" />


This confirms that the current logic incorrectly inflates the borrow value and misclassifies a healthy position as undercollateralized.



### Mitigation

Pass `0` instead of `payload.amount` as the `borrowAmount` to avoid inflating debt during the liquidation check.

```diff
    function _checkLiquidationValid(LZPayload memory payload) private view returns (bool) {
+       (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
+            payload.sender, LToken(payable(payload.destlToken)), 0, 0
+       );
-       (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
-           payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
-       );
        return borrowed > collateral;
    }
```