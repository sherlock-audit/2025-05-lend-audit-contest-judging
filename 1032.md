Magnificent Holographic Camel

High

# Accounts borrow amount are incorrectly calculated

### Summary

Missing logics to accrue interest at markets causing the borrows are not up-to-date

### Root Cause

The function [`LendStorage::getHypotheticalAccountLiquidityCollateral()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L385) calculates account's borrow amount with the markets' current onchain states, not the interest-accrued states.  This can cause the calculated borrow amount less than expected.
```solidity
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;

        // Calculate collateral value from supplied assets
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();

        // First loop: Calculate collateral value from supplied assets
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
            uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];

            // Get collateral factor and price for this asset
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
            vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()});

            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);

            // Add to collateral sum
            vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }

        // Second loop: Calculate borrow value from borrowed assets
        for (uint256 i = 0; i < borrowedAssets.length;) {
@>            LToken asset = LToken(borrowedAssets[i]);

// <<<<<<<<< lack of logics to accrue interest on market

            // Get borrow balance for this asset
@>            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));

            // Add cross-chain borrows if any
@>            totalBorrow += borrowWithInterest(account, address(asset));

            // Get price for borrowed asset
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

            // Add to borrow sum
@>            vars.sumBorrowPlusEffects =
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }

        // Handle effects of current action
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

            // Add effect of redeeming collateral
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
                vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()});
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }

            // Add effect of new borrow
            if (borrowAmount > 0) {
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }

        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

NA

### Impact

- Borrow amount is wrongfully calculated in the system, hence all borrowing operations in the system may work unexpectedly

### PoC

_No response_

### Mitigation

```diff
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);
            
+            asset.accrueInterest();
            // Get borrow balance for this asset
            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));

            // Add cross-chain borrows if any
            totalBorrow += borrowWithInterest(account, address(asset));

            // Get price for borrowed asset
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

            // Add to borrow sum
            vars.sumBorrowPlusEffects =
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }
```