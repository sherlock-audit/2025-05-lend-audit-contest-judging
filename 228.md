Creamy Pine Cow

High

# User same-chain borrow is wrongfully cleared if the same token is borrowed and repayed cross-chain

### Summary

If the user borrows a token on `chain B` from `chain A`, on repaying this debt on `chain B` - the same-chain borrow for that token on `chain B` is also cleared. This clears the users same-chain debt without actually paying for it, thus leading to loss of funds for the protocol.

### Root Cause

On performing a cross-chain borrow from` chain A` to `chain B`, the borrowed debt is repaid from the `chain B` where the main borrowed tokens are issued to the user. [CrossChainRouter::repayCrossChainBorrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L156-L162) is called on `chain B` to repay this debt:

```solidity
    function repayCrossChainBorrow(address _borrower, uint256 _amount, address _lToken, uint32 _srcEid) external {
        require(_amount > 0, "Amount must be greater than 0");
        require(_lToken != address(0), "Invalid lToken address");

        // Pass to internal function for processing
        repayCrossChainBorrowInternal(_borrower, msg.sender, _amount, _lToken, _srcEid);
    }

    ...

    function repayCrossChainBorrowInternal(
        address borrower,
        address repayer,
        uint256 _amount,
        address _lToken,
        uint32 _srcEid
    ) internal {
        ...

        // Handle token transfers and repayment
        _handleRepayment(borrower, repayer, _lToken, repayAmountFinal);

       ...
    }

    /// @dev Repayer must've approved the CoreRouter to spend the tokens
    function _handleRepayment(address _borrower, address repayer, address _lToken, uint256 repayAmountFinal) private {
        // Execute the repayment
        CoreRouter(coreRouter).repayCrossChainLiquidation(_borrower, repayer, repayAmountFinal, _lToken);
    }
```

Within `CrossChainRouter::_handleRepayment()`, [CoreRouter::repayCrossChainLiquidation()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L216-L221) on the `CoreRouter` is called:

```solidity
    function repayCrossChainLiquidation(address _borrower, address _liquidator, uint256 _amount, address _lToken)
        external
    {
        require(msg.sender == crossChainRouter, "Access Denied");
        repayBorrowInternal(_borrower, _liquidator, _amount, _lToken, false);
    }

    ...

    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);

        LTokenInterface(_lToken).accrueInterest();

        uint256 borrowedAmount;

        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }

        require(borrowedAmount > 0, "Borrowed amount is 0");

        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;

        // Transfer tokens from the liquidator to the contract
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);

        _approveToken(_token, _lToken, repayAmountFinal);

        lendStorage.distributeBorrowerLend(_lToken, borrower);

        // Repay borrowed tokens
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");

        // Update same-chain borrow balances      // @audit: should not update same-chain borrow balance for cross-chain borrow repayment
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }

        // Emit RepaySuccess event
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }
```

Following the call stack, the same logic is used for same-chain and cross-chain repayments; the code executes in expected fashion, but does not return early before modifying same-chain borrow balances. This is undesired behaviour as the context is cross-chain repayments.

Two different outcomes are possible here:

1. If the user is doing a full repayment of his cross-chain debt, the token is removed from the same-chain balance, this effectively wipes clean his same-chain debt on that token.
2. If partial repayment, the same-chain debt balance is set to the remainder of the cross-chain debt.

By leveraging this, an attacker can wipe his same-chain debts clean by borrowing small amounts of the same token from another chain and repaying them.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

For simplicity, assume `1 BTC` collateral lets you borrow `$80k USDC` after all oracle and collateral ratio calculations are done on both chains, and borrow index on both chains remain constant (doesn't matter if it changes as the values will simply be adjusted in that case, but the exploit works as normal).

1. Our attacker - Alice, supplies `1 BTC` as collateral on `chain A`.
2. Alice makes a cross chain borrow of `$20k USDC` on `chain B`, from `chain A`.
3. Now, Alice has a cross-chain collateral of `$20k` on `chain B`.

> Note: Due to how collateral and borrow calculations are done, calls to [LendStorage::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L385-L467) which are used heavily, will show a debt balance of $20k and collateral of $80k `(1 BTC collateral being worth $80k after all collateral ratio and oracle price calculations)` on chain A, and a debt balance of 0 and collateral of 0 on chain B. A debt balance of zero is shown on chain B, because chain A initiated the cross-chain borrow and therefore on chain B it is saved as `crossChainCollateral` and not counted towards chain B's total debt.

4. Alice now supplies `1 BTC` on `chain B`.
5. Now Alice borrows the entire allowed `$80k` of the same previously cross-chain borrowed token `USDC` on chain B, putting her total debt at $80k USDC ($80k same-chain borrow balance).
6. Alice now calls [CrossChainRouter::repayCrossChainBorrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L156-L162) from chain B still to repay the original $20k cross-chain borrow that was made from chain A in step 2.
7. By repaying the full cross-chain borrow ($20k USDC), the same-chain borrow balance ($80k USDC) on chain B will be cleared:

```solidity
    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        ...

        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);    // @audit: this will be executed
        }

        require(borrowedAmount > 0, "Borrowed amount is 0");

        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;

        ...

        // Update same-chain borrow balances  
        if (repayAmountFinal == borrowedAmount) {    // @audit: will be executed
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }

        // Emit RepaySuccess event
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }
```

8. The rest of the call is executed, and the cross-chain debt of the user is cleared.

> Note: The chain calling a cross-chain borrow saves borrow using `crossChainBorrows` storage in `LendStorage`, while the recipient chain where the main borrow is issued saves within `crossChainCollaterals`. The latter does not count towards a chain's debt because the collateral does not originate from there, and is simply used as accounting.

9. On repaying this debt, confirmation is sent to the original `chain A`, and the cross chain debt is cleared which frees up the entire $80k collateral for withdrawal.
10. While on the chain B, the `$80k USDC` same-chain borrow from step 5 is cleared - along with the cross-chain borrow (`crossChainCollaterals[user][underlying][index]`).

With this, the entire collateral worth $80k on both chains are freed, and the full 1 BTC on each can be withdrawn - remember Alice made a `$80k USDC` same-chain borrow on `chain B` in step 5 of which she used `$20k USDC `to pay back her cross-chain borrow.

Thus she walks away with `$60k USDC` on chain B and both of her 1 BTC collaterals complete.

### Impact

Theft of funds.

### PoC

_No response_

### Mitigation

Either create a separate `CoreRouter::repayBorrowInternal()` function for calls to `CoreRouter::repayCrossChainLiquidation()` or you can keep as it is, and simply return early in the case of cross-chain redemptions:

```solidity
    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
       ...

        // Repay borrowed tokens
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");

        if (!_isSameChain) {  // @audit-fix: return early as same-chain balance should not be modified for cross-chain repayments
            return;
        }

        // Update same-chain borrow balances
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }

        // Emit RepaySuccess event
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }
```