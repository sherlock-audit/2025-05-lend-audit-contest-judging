Lone Champagne Hornet

High

# A user will receive over-minted lTokens affecting supply accuracy

### Summary

The missing call to `accrueInterest()` before reading `exchangeRateStored()` in `supply()` will cause an inaccurate mint amount for suppliers as a user will supply assets while the interest state is stale, resulting in incorrect exchange rate calculation.

### Root Cause

- In `CoreRouter.sol: 74` the exchange rate is retrieved from `exchangeRateStored()` without calling `accrueInterest()` first.
https://github.com/sherlock-audit/2025-05-lend-audit-contest-sylvarithos/blob/551944cd87d138620b89c11674a92f1dcbe0efbe/Lend-V2/src/LayerZero/CoreRouter.sol#L74
```solidity
function supply(uint256 _amount, address _token) external {
    address _lToken = lendStorage.underlyingTolToken(_token);

    require(_lToken != address(0), "Unsupported Token");

    require(_amount > 0, "Zero supply amount");

    // Transfer tokens from the user to the contract
    IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);

    _approveToken(_token, _lToken, _amount);

    // Get exchange rate before mint
74    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

    // Mint lTokens
    require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

    // Calculate actual minted tokens using exchangeRate from before mint
    uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;
```

- In `LToken.sol: 304`, the exchange rate depends on `totalBorrows` and `totalReserves`, which are updated in `accrueInterest()`
https://github.com/sherlock-audit/2025-05-lend-audit-contest-sylvarithos/blob/551944cd87d138620b89c11674a92f1dcbe0efbe/Lend-V2/src/LToken.sol#L304
```solidity
function exchangeRateStoredInternal() internal view virtual returns (uint256) {
    uint256 _totalSupply = totalSupply;
    if (_totalSupply == 0) {
        /*
            * If there are no tokens minted:
            *  exchangeRate = initialExchangeRate
            */
        return initialExchangeRateMantissa;
    } else {
        /*
            * Otherwise:
            *  exchangeRate = (totalCash + totalBorrows - totalReserves) / totalSupply
            */
        uint256 totalCash = getCashPrior();
304        uint256 cashPlusBorrowsMinusReserves = totalCash + totalBorrows - totalReserves;
        uint256 exchangeRate = cashPlusBorrowsMinusReserves * expScale / _totalSupply;

        return exchangeRate;
    }
}
```
- The stale values may result in over-minting of lTokens for the user

### Internal Pre-conditions

1. A user calls `supply()` with a valid `_amount` and supported `_token`
2. There has been at least one block since the last time `accrueInterest()` was called for that market
3. The market has non-zero borrows or reserves

### External Pre-conditions

1. There is volatility in the borrowing market — interest is accumulating across blocks
2. `accrueInterest()` has not been automatically called or externally triggered for the current market


### Attack Path

1. A user calls `supply()` to deposit a large amount of tokens
2. The contract reads a stale `exchangeRateStored()` value because `accrueInterest()` was not called
3. The contract mints lTokens using an outdated rate
4. The user receives more lTokens than they should based on current interest accruals

### Impact

The suppliers suffer inaccurate accounting of their deposit position.  
This can lead to:
- Over-minting: unfair dilution of existing lenders

**The user receives more than their fair share** of claim on the underlying assets. Over time, this can cause the protocol’s assets to be undercollateralized relative to lTokens in circulation.

### PoC

_No response_

### Mitigation

Always call `LToken(_lToken).accrueInterest()` before using `exchangeRateStored()` in the `supply()` function to ensure fresh and accurate values.  
```solidity
function supply(uint256 _amount, address _token) external {
    address _lToken = lendStorage.underlyingTolToken(_token);

    require(_lToken != address(0), "Unsupported Token");

    require(_amount > 0, "Zero supply amount");

    // Transfer tokens from the user to the contract
    IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);

    _approveToken(_token, _lToken, _amount);
+	LTokenInterface(_lToken).accrueInterest();
    // Get exchange rate before mint
    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
```
