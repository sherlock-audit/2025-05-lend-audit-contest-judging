Cheerful Taupe Ladybug

High

# Stale Exchange Rate Usage in `supply` and `redeem` Functions Leads to Incorrect Accounting and Value Discrepancies

### Summary

The supply and redeem functions take the exchange rate before calculating the accrued interest.
This allows for users that are supplying tokens using the `CoreRouter.supply()` function, before the `LToken.accrueInterest()` function for that `LToken` is called inside a new block and the accrued interests are calculated, to use the stale exchange rate. This results in the internal accounting o `lendStorage` to assign more more lTokens to the user than were actually minted to the `CoreRouter` by the `LToken` contract.
Additionally, when users try to redeem tokens using the `CoreRouter.redeem()` function inside a new block before the `LToken.accrueInterest()` function is called and accrued interest are calculated, the stale exchange rate is used, resulting in the user receiving fewer underlying tokens than their redeemed lTokens are actually worth post-interest accrual..

### Root Cause

In [CoreRouter.sol:74](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L74) the exchange rate is taken and stored inside `exchangeRateBefore`, then in [CoreRouter.sol:77](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L77) the mint function is called which calls `LToken.accrueInterest()`. This function calculates accrued interest and updates the exchange rate if the last update was from an older block. Subsequently, it mints the appropriate number of lTokens to the router using this updated rate. However, the number of lTokens accounted for the user is calculated using `exchangeRateBefore`. which in case of the `LToken.accrueInterest()` function updating the accrued interest, a different exchange rate would be used assigning an incorrect number of tokens to the user than what they should've been assigned and what was actually minted to the `CoreRouter`.
The same issue happens in [CoreRouter.sol:115](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L115) where the exchange rate is taken before redeeming the tokens in [CoreRouter.sol:121](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L121) resulting in `LToken.accrueInterest()` being called and the exchange rate potentially being updated. The `expectedUnderlying` tokens to be sent to the user are calculated using `exchangeRateBefore` (the stale rate), while the `LToken.redeem()` function itself would burn lTokens and internally account for underlying based on the now updated rate. This discrepancy leads to the user receiving fewer tokens than their lTokens are worth at the current rate.

### Internal Pre-conditions

1. Borrowed amount from the protocol must be higher than zero
2. Interest rate must be higher than zero
3. `LToken.accrualBlockNumber` must not be equal to the current `block.number`

### External Pre-conditions

none

### Attack Path

1. The users start borrowing from the protocol, and the protocol starts earning interest
2. A new block starts
3. User calls the `CoreRouter.supply()` function, which uses the old exchange rate from the last block `LToken.accrueInterest()` was last called directly or as a side effect, which uses the stale exchange rate from the previous block. This results in `lendStorage` crediting the user with more lTokens than were actually minted to the `CoreRouter` by the `LToken` contract (which used the updated rate).

OR

1. The users start borrowing from the protocol, and the protocol starts earning interest
2. A new block starts
3. User calls the `CoreRouter.redeem()` function, which uses the old exchange rate from the last block `LToken.accrueInterest()` was last called directly or as a side effect, sending them less tokens than they've earned as a result.

### Impact

when supplying tokens:
The protocol suffers a loss of funds or value. Specifically, `lendStorage.totalInvestment` for the user is inflated compared to the actual lTokens minted to and held by the `CoreRouter`. This could lead to the protocol owing more in lToken terms than it effectively has, potentially diluting other users or leading to a shortfall if many users exploit this.

when redeeming tokens:
The user suffers a loss of funds. They receive fewer underlying tokens than the true value of their redeemed lTokens (based on the exchange rate after interest accrual). The difference in underlying tokens effectively remains locked inside the `CoreRouter` contract.

### PoC

Append the following test for the `CoreRouter.supply()` function to `TestSupplying.t.sol` and run it using `forge test --mt "test_that_supplying_liquidity_accounting_is_wrong"`

```solidity
function test_that_supplying_liquidity_accounting_is_wrong() public {
    address user1 = makeAddr("user1");
    address user2 = makeAddr("user2");
    address user3 = makeAddr("user3");

    address token = supportedTokens[0];
    address lToken = lendStorage.underlyingTolToken(token);

    uint256 supplyAmount = 100e18;

    // User1 supplies some tokens and takes a loan
    vm.startPrank(user1);

    ERC20Mock(token).mint(user1, supplyAmount);
    IERC20(token).approve(address(coreRouter), supplyAmount);
    coreRouter.supply(supplyAmount, token);

    uint256 user1BorrowAmount = supplyAmount / 2;

    coreRouter.borrow(user1BorrowAmount, token);

    vm.stopPrank();

    // A few blocks passes without accrueInterest() being called
    vm.roll(block.number + 10);

    // User2 supplies some tokens, which uses the old exchange rate for accounting before updating the rate, getting assigned more tokens
    vm.startPrank(user2);

    ERC20Mock(token).mint(user2, supplyAmount);
    IERC20(token).approve(address(coreRouter), supplyAmount);
    coreRouter.supply(supplyAmount, token);

    vm.stopPrank();

    // User3 supplies some tokens as well within the same block, but as the exchange rate is already updated inside that block they receive the correct number of tokens
    vm.startPrank(user3);

    ERC20Mock(token).mint(user3, supplyAmount);
    IERC20(token).approve(address(coreRouter), supplyAmount);
    coreRouter.supply(supplyAmount, token);

    vm.stopPrank();

    uint256 totalUserInvestments = lendStorage.totalInvestment(user1, lToken) + lendStorage.totalInvestment(user2, lToken) + lendStorage.totalInvestment(user3, lToken);

    // The total user investments tracked by the protocol ends up being more than what was actually minted to the Core Router
    assertGt(lendStorage.totalInvestment(user2, lToken), lendStorage.totalInvestment(user3, lToken));

    // The amount that was assigned to User2 is higher than User3, since the old exchange rate was used for User2 and the correct updated exchange rate for User3
    assertGt(totalUserInvestments, LToken(lToken).balanceOf(address(coreRouter)));
}
```

Append the following test for the `CoreRouter.redeem()` function to `TestRedeeming.t.sol` and run it using `forge test --mt "test_that_user_can_receive_less_tokens"`

```solidity
function test_that_user_can_receive_less_tokens() public {
    address user1 = makeAddr("user1");
    address user2 = makeAddr("user2");
    address user3 = makeAddr("user3");

    address token = supportedTokens[0];
    address lToken = lendStorage.underlyingTolToken(token);

    uint256 supplyAmount = 100e18;

    // User1 supplies some tokens and takes a loan
    vm.startPrank(user1);

    ERC20Mock(token).mint(user1, supplyAmount);
    IERC20(token).approve(address(coreRouter), supplyAmount);
    coreRouter.supply(supplyAmount, token);

    uint256 user1BorrowAmount = supplyAmount / 2;

    coreRouter.borrow(user1BorrowAmount, token);

    vm.stopPrank();

    // User2 supplies some tokens
    vm.startPrank(user2);

    ERC20Mock(token).mint(user2, supplyAmount);
    IERC20(token).approve(address(coreRouter), supplyAmount);
    coreRouter.supply(supplyAmount, token);

    vm.stopPrank();

    // User3 supplies some tokens as well
    vm.startPrank(user3);

    ERC20Mock(token).mint(user3, supplyAmount);
    IERC20(token).approve(address(coreRouter), supplyAmount);
    coreRouter.supply(supplyAmount, token);

    vm.stopPrank();

    // A new block starts
    vm.roll(block.number + 1);

    uint256 tokensToRedeem = lendStorage.totalInvestment(user2, lToken);

    // User2 redeems some of their tokens, however since accrueInterest() isn't called and the exchange rate is not updated yet the tokens they receive is less than what was actually redeemed
    vm.startPrank(user2);

    coreRouter.redeem(tokensToRedeem, payable(lToken));

    vm.stopPrank();

    // User3 redeems the same number of tokens, however they receive the correct number of tokens, since the exchange rate was already updated within that block
    vm.startPrank(user3);

    coreRouter.redeem(tokensToRedeem, payable(lToken));

    vm.stopPrank();

    // Both User2 and User3 redeemed the same number of tokens within the same block but User3 ends up receiving more tokens
    assertGt(IERC20(token).balanceOf(user3), IERC20(token).balanceOf(user2));

    // The extra amount of tokens that was redeemed for User2 but wasn't actually sent to them is locked inside the CoreRouter
    assertGt(IERC20(token).balanceOf(address(coreRouter)), 0);
    }
```

### Mitigation

Consider getting the exchange rate after the token minting/redeeming is done and the exchange rate is properly updated

```diff
function supply(uint256 _amount, address _token) external {
    .
    .
    .
-   uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

    // Mint lTokens
    require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");
    
+   uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
    .
    .
    .
  }
```

By fetching `exchangeRateStored()` after `LErc20Interface(_lToken).mint(_amount)` (which triggers accrueInterest), the calculation for `totalUserInvestments` will use the up-to-date exchange rate, correctly reflecting the lTokens minted.

```diff
function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
    .
    .
    .
-   uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

-   uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;

    // Perform redeem
    require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");

+   uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

+   uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
    .
    .
    .
}
```

By fetching `exchangeRateStored()` and calculating `expectedUnderlying` after `LErc20Interface(_lToken).redeem(_amount)` (which triggers accrueInterest), the amount of underlying tokens transferred to the user will correctly reflect the value of their lTokens at the current, updated exchange rate.