Fun Shadow Parakeet

High

# `repayCrossChainBorrow` and `liquidateCrossChain` missing payable modifier cause cross-chain calls to fail

### Summary

Both `repayCrossChainBorrow` and `liquidateCrossChain` perform LayerZero cross-chain operations via `_lzSend`.
However, neither function is marked payable, so users cannot send native gas needed to cover LayerZero’s `MessagingFee.nativeFee`.

As a result, these cross-chain calls will either be rejected by the EVM (due to the function not being payable) or fail during `_lzSend` execution (due to insufficient native fees), making the feature entirely unusable on mainnet.

### Root Cause

LayerZero V2 requires the caller to provide a fee to cover the cross-chain message, passed in the form of native gas or LZ token via `MessagingFee`.

In [CrossChainRouter.sol#L820](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L820), the `_send` function implementation uses native gas by default:

```solidity
    function _send(
        uint32 _dstEid,
        uint256 _amount,
        uint256 _borrowIndex,
        uint256 _collateral,
        address _sender,
        address _destlToken,
        address _liquidator,
        address _srcToken,
        ContractType ctype
    ) internal {
        bytes memory payload =
            abi.encode(_amount, _borrowIndex, _collateral, _sender, _destlToken, _liquidator, _srcToken, ctype);

        bytes memory options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(1_000_000, 0);

@>      _lzSend(_dstEid, payload, options, MessagingFee(address(this).balance, 0), payable(address(this)));
    }

```

```solidity
    struct MessagingFee {
@>      uint256 nativeFee;
@>      uint256 lzTokenFee;
    }
```

This means the contract relies on receiving native tokens into `address(this).balance` to pay the message fee.

However, neither [`repayCrossChainBorrow`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L156) nor [`liquidateCrossChain`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L172-L178) is marked payable:

```solidity
@>  function repayCrossChainBorrow(address _borrower, uint256 _amount, address _lToken, uint32 _srcEid) external {
        require(_amount > 0, "Amount must be greater than 0");
        require(_lToken != address(0), "Invalid lToken address");

        // Pass to internal function for processing
        repayCrossChainBorrowInternal(_borrower, msg.sender, _amount, _lToken, _srcEid);
    }
```

```solidity
    function liquidateCrossChain(
        address borrower,
        uint256 repayAmount,
        uint32 srcEid,
        address lTokenToSeize,
        address borrowedAsset
@>  ) external {
        LendStorage.LiquidationParams memory params = LendStorage.LiquidationParams({
            borrower: borrower,
            repayAmount: repayAmount,
            srcEid: srcEid,
            lTokenToSeize: lTokenToSeize, // Collateral lToken from the user's position to seize
            borrowedAsset: borrowedAsset,
            storedBorrowIndex: 0,
            borrowPrinciple: 0,
            borrowedlToken: address(0)
        });

        _validateAndPrepareLiquidation(params);
        _executeLiquidation(params);
    }
```

Since users cannot fund the message fee, `_lzSend` will execute with zero balance, causing LayerZero to reject the transaction due to insufficient fee.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

No malicious intent is required — any user can unintentionally trigger this at any time:
1. A user calls `repayCrossChainBorrow` or `liquidateCrossChain` with `msg.value > 0`.
2. The EVM reverts immediately because the function is not `payable`.
3. Even without `msg.value`, the `_lzSend` call will fail due to insufficient native fee.
4. Result: cross-chain features are broken for all users.

### Impact

Users cannot perform key actions like `repayCrossChainBorrow` or `liquidateCrossChain` due to missing native fee support.
Cross-chain repay and liquidation become entirely non-functional, leading to severe protocol failure.


### PoC

See above.

### Mitigation

Mark `repayCrossChainBorrow` and `liquidateCrossChain` as payable so the contract can receive native tokens. Or alternatively, support LayerZero’s `ZRO` token payment system by modifying `_send` to allow `MessagingFee(0, zroFee)` when `ZRO` is used.