Wobbly Wintergreen Gerbil

High

# The protocol fails to accrue reward for users who borrow assets from the protocol as promised

## Impact
### Summary 
The protocol is designed to reward users who participate by borrowing assets from the protocol. Some amount of `LEND` is supposed to accrue to a user any time they borrow assets from the protocol. The amount of `LEND` due to a user is tracked using the `LendStorage::lendAccrued` mapping which can later be claimed by the user. However, due to logic errors, no amount of `LEND` will accrue to users who borrow assets from the protocol.

### Vulnerability Details

The vulnerability is multifaceted and is summarized below:
*Facet A:*
1. When a user borrows assets from the protocol, the `LEND` due to them is calculated in the `LendStorage::distributeBorrowerLend` which is called by the `CoreRouter::borrow` function (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L172)).

2. The problem here is the `LendStorage::distributeBorrowerLend` function depends on the amount of assets borrowed by the user both on the same chain or on other chains (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L365-L368)) 

3. but the `CoreRouter::borrow` function calls the `LendStorage::distributeBorrowerLend` function before updating the amount of assets borrowed by the user (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L172-L184)). This logic error will prevent timely accrual of user rewards in `LEND` as a result, a user will have to borrow asset a second time before their reward is accrued for the first borrow and any user who borrows asset only once will not have any rewards accrued to them.

*Facet B:*
1. When the `CoreRouter::borrow` function is called, this triggers a call to the the `Lerc20::borrow` function (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L167)), which calls `LToken::borrowInternal` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LErc20.sol#L86)) which also calls `LToken::borrowFresh` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L555)) which in turn calls the `Lendtroller::borrowAllowed` function (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L564)). 

2. The `Lentroller::borrowAllowed` function then calls `Lentroller::updateLendBorrowIndex` and `Lentroller::distributeBorrowLend` functions (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L429-L430)). This way, the `Lentroller::updateLendBorrowIndex` updates the `LendMarketState.block` for `lToken` to the current `block.number` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1305-L1311)) but since the amount of assets borrowed is not updated, the `Lentroller::distributeBorrowLend` function will not accrue any reward to the user.

3. By the the time the `CoreRouter::borrow` function calls the `LendStorage::distributeBorrowLend` function proper on line 172 (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L172)), which calls the `Lendtroller::triggerBorrowIndexUpdate` function and then the `Lendtroller::updateLendBorrowIndex` funtion, the `LendMarketState.block` for `lToken` is the same as the current `block.number` resulting in `deltaBlocks = 0` (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1301-L1314)). As a result, the `index` and `block` of `LendMarketState` will not be updated because [this](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1305-L1311) section of the code will not execute.

4. In the `LendStorage::distributeBorrowLend` function, because the amount of assets borrowed by user was not updated prior to the preceeding function calls, [this](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L365-L368) line returns `0` which results in [this](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L371) line also yielding `0` so that the `LEND` reward acrued to the user is `0` despite they have borrowed assets from the protocol. 

*Facet C:*
1. Note that the `Lendtroller::updateLendBorrowIndex` function relies on positive block number differences (see [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1303-L1304)) to update the `index` and `block` of `LendMarketState` which in turn determines whether or not a user will accrue any rewards.

2. If user A calls the `CoreRouter::supply` function supplying the `assetA` and another user B calls `CoreRouter::borrow` function to borrow `assetA` within the same block, user A will be rewarded while user B will not. 

3. If user A calls the `CoreRouter::borrow` function borrowing `assetA` and another user B  borrows the same asset within the same block, user A will be rewarded while user B will not.

4. In general, because the `Lendtroller::updateLendBorrowIndex` function uses block number differences to calculate rewards, only the first user in a block is rewarded while all other users after the first user who borrow the same asset within the same block will not be rewarded


To see all these explanations play out, see the PoC section where `console2.log` were added to various sections of the code to see what happens under the hood when the `CoreRouter::supply` function is called.


### Impact
It is the protocol's design to reward users who borrow asset with `LEND` as a way of rewarding users for participating on the protocol. Now, if users do not get what was promised, they will boycot the protocol and explore other alternatives where they will get due reward for their services.
This will hinder the protocol from achieving the underlying development objectives.

## Proof of Concept

To see all these explanations play out, we introduce `console2.log` at various points in the several functions to see what happens at the background when a user calls the `CoreRouter::borrow` function.

Edit the `CoreRouter::borrow` function by introducing `console2.log` at various points as follows
```javascript
function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");

        address _lToken = lendStorage.underlyingTolToken(_token);

        LTokenInterface(_lToken).accrueInterest();

        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);

        uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;

        require(collateral >= borrowAmount, "Insufficient collateral");

        // Enter the Compound market
        enterMarkets(_lToken);

        // Borrow tokens
        console2.log("Calling Lerc20::borrow (from borrow function) .... "); // @audit-addition

        require(LErc20Interface(_lToken).borrow(_amount) == 0, "Borrow failed");
        console2.log("Lerc20::borrow call ends  .... "); // @audit-addition
        console2.log(" "); // @audit-addition

        // Transfer borrowed tokens to the user
        IERC20(_token).transfer(msg.sender, _amount);

        console2.log("Calling LendStorage::distributeBorrowerLend (from borrow function) .... "); // @audit-addition
        lendStorage.distributeBorrowerLend(_lToken, msg.sender); 

        console2.log("LendStorage::distributeBorrowerLend call ends .... "); // @audit-addition
        console2.log(" "); // @audit-addition
        

        // Update records
        if (currentBorrow.borrowIndex != 0) {
            uint256 _newPrinciple =
                (currentBorrow.amount * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex;

            lendStorage.updateBorrowBalance(
                msg.sender, _lToken, _newPrinciple + _amount, LTokenInterface(_lToken).borrowIndex()
            );
        } else {
            lendStorage.updateBorrowBalance(msg.sender, _lToken, _amount, LTokenInterface(_lToken).borrowIndex());
        }

        lendStorage.addUserBorrowedAsset(msg.sender, _lToken);

        // Emit BorrowSuccess event
        emit BorrowSuccess(msg.sender, _lToken, lendStorage.getBorrowBalance(msg.sender, _lToken).amount);
    }
```

Edit the `LendStorage::distributeBorrowerLend` function by introducing `console2.log` at various points as follows:

```javascript
function distributeBorrowerLend(address lToken, address borrower) external onlyAuthorized {
        console2.log(" "); // @audit-addition
        console2.log("distributeBorrowerLend call execution begins ... "); // @audit-addition

        console2.log("triggerBorrowIndexUpdate call execution begins ... "); // @audit-addition
        // Trigger borrow index update
        LendtrollerInterfaceV2(lendtroller).triggerBorrowIndexUpdate(lToken);
        console2.log("triggerBorrowIndexUpdate call ends ... "); // @audit-addition

        // Get the appropriate lend state based on whether it's for supply or borrow
        (uint224 borrowIndex,) = LendtrollerInterfaceV2(lendtroller).lendBorrowState(lToken);

        uint256 borrowerIndex = lendBorrowerIndex[lToken][borrower];

        // Update borrowers's index to the current index since we are distributing accrued LEND
        lendBorrowerIndex[lToken][borrower] = borrowIndex;

        if (borrowerIndex == 0 && borrowIndex >= LEND_INITIAL_INDEX) {
            // Covers the case where users borrowed tokens before the market's borrow state index was set.
            // Rewards the user with LEND accrued from the start of when borrower rewards were first
            // set for the market.
            borrowerIndex = LEND_INITIAL_INDEX;
        }

        // Calculate change in the cumulative sum of the LEND per borrowed unit accrued
        Double memory deltaIndex = Double({mantissa: sub_(borrowIndex, borrowerIndex)});
        console2.log("deltaIndex is: ", deltaIndex.mantissa); // @audit-addition

        // Calculate the appropriate account balance and delta based on supply or borrow
        uint256 borrowerAmount = div_(
            add_(borrowWithInterest(borrower, lToken), borrowWithInterestSame(borrower, lToken)),
            Exp({mantissa: LTokenInterface(lToken).borrowIndex()})
        );

        console2.log("borrowWithInterest(borrower, lToken) is: ", borrowWithInterest(borrower, lToken)); // @audit-addition
        console2.log("borrowWithInterestSame(borrower, lToken) is: ", borrowWithInterestSame(borrower, lToken)); // @audit-addition
        console2.log("borrowerAmount is: ", borrowerAmount); // @audit-addition

        // Calculate LEND accrued: lTokenAmount * accruedPerBorrowedUnit
        uint256 borrowerDelta = mul_(borrowerAmount, deltaIndex);

        uint256 borrowerAccrued = add_(lendAccrued[borrower], borrowerDelta);
        lendAccrued[borrower] = borrowerAccrued;

        console2.log("LEND accrued to borrower is: ", borrowerAccrued); // @audit-addition

        console2.log("distributeBorrowerLend call ends ... "); // @audit-addition
    }
```

```javascript
function triggerBorrowIndexUpdate(address lToken) external {
        require(msg.sender == lendStorageAddress, "access denied");
        Exp memory borrowIndex = Exp({mantissa: LToken(lToken).borrowIndex()});

        console2.log("Lendtroller::triggerBorrowIndexUpdate function calls updateLendBorrowIndex ..." ); // @audit-addition

        updateLendBorrowIndex(lToken, borrowIndex);

        console2.log("Lendtroller::updateLendBorrowIndex function call ends  ..." ); // @audit-addition
    }
```

```javascript
function updateLendBorrowIndex(address lToken, Exp memory marketBorrowIndex) internal {
        
        console2.log(" "); // @audit-addition
        console2.log("The updateLendBorrowIndex function call execution ... "); // @audit-addition
        LendMarketState storage borrowState = lendBorrowState[lToken];
        console2.log("The initial block number (uLBIndex function): ", borrowState.block); // @audit-addition

        uint256 borrowSpeed = lendBorrowSpeeds[lToken];
        console2.log("lendBorrowSpeeds for lToken (uLBIndex function): ", borrowSpeed); // @audit-addition

        uint32 blockNumber = safe32(getBlockNumber(), "block number exceeds 32 bits");
        console2.log("The current block number (uLBIndex function): ", blockNumber); // @audit-addition

        uint256 deltaBlocks = sub_(uint256(blockNumber), uint256(borrowState.block)); 

        console2.log("current block - initial block (uLBIndex function): ", deltaBlocks); // @audit-addition

        if (deltaBlocks > 0 && borrowSpeed > 0) {
            uint256 borrowAmount = div_(LToken(lToken).totalBorrows(), marketBorrowIndex);
            uint256 lendAccrued = mul_(deltaBlocks, borrowSpeed);
            Double memory ratio = borrowAmount > 0 ? fraction(lendAccrued, borrowAmount) : Double({mantissa: 0});
            borrowState.index =
                safe224(add_(Double({mantissa: borrowState.index}), ratio).mantissa, "new index exceeds 224 bits");
            borrowState.block = blockNumber;
            console2.log("The updated borrow index (uLBIndex function): ", borrowState.index); // @audit-addition
        } else if (deltaBlocks > 0) {
            borrowState.block = blockNumber;
        }

        console2.log("The updateLendBorrowIndex function call ends ... "); // @audit-addition
    }
```

Now, in the `TestBorrowing.t.sol`, write the following simple test to see what happens under the hood when the `CoreRouter::borrow` function is called

```javascript

function test_spomaria_see_borrow_under_the_hood() public {
        uint256 amount = 1e25;
        vm.startPrank(deployer);

        // First supply tokens as collateral
        console2.log("deployer calls supply at block", block.number);
        (address token, address lToken) = _supply(amount);
        vm.stopPrank();

        address user = makeAddr("USER");
        // supply token (the second token) as collateral
        uint256 userAmount = (amount * 80 ) / 100;
        console2.log("User calls supply at block", block.number);
        (address token1, address lToken1) = _supplyOne(userAmount, user);
        
        uint256 initialInvestment = lendStorage.totalInvestment(user, lToken1);
        uint256 initialInvestment0 = lendStorage.totalInvestment(user, lToken);
        uint256 maxBorrow = (userAmount * 60) / 100;

        // Get initial balances
        uint256 initialTokenBalance = IERC20(token).balanceOf(user);

        // roll the blockchain by some blocks
        vm.warp(block.timestamp + 60*60);
        vm.roll(block.number + 20);

        vm.startPrank(user);

        // Borrow more than 70% of tokens
        // vm.expectRevert();
        console2.log("User calls borrow at block", block.number);
        coreRouter.borrow(maxBorrow, token);


        vm.stopPrank();
    }
```

Now run the command `forge test --match-test test_spomaria_see_borrow_under_the_hood -vvvv`

Output:

```javascript
Ran 1 test for test/TestBorrowing.t.sol:TestBorrowing
[PASS] test_spomaria_see_borrow_under_the_hood() (gas: 1142333)
Logs:
  The initial block number (uLSIndex function):  1
  lendSupplySpeeds for lToken (uLSIndex function):  0
  The current block number (uLSIndex function):  1
  current block - initial block (uLSIndex function):  0
   
  The updateLendBorrowIndex function call execution ... 
  The initial block number (uLBIndex function):  1
  lendBorrowSpeeds for lToken (uLBIndex function):  0
  The current block number (uLBIndex function):  1
  current block - initial block (uLBIndex function):  0
  The updateLendBorrowIndex function call ends ... 
  The initial block number (uLSIndex function):  1
  lendSupplySpeeds for lToken (uLSIndex function):  0
  The current block number (uLSIndex function):  1
  current block - initial block (uLSIndex function):  0
   
  The updateLendBorrowIndex function call execution ... 
  The initial block number (uLBIndex function):  1
  lendBorrowSpeeds for lToken (uLBIndex function):  0
  The current block number (uLBIndex function):  1
  current block - initial block (uLBIndex function):  0
  The updateLendBorrowIndex function call ends ... 
  deployer calls supply at block 1
  Calling Lerc20::mint (from supply function) .... 
  Lerc20::mint ends  .... 

  Calling LendStorage::distributeSupplierLend (from supply function) .... 
  LendStorage::distributeSupplierLend call ends .... 

  Calling the Lendtroller::triggerSupplyIndexUpdate function (dSL function): 
  Lendtroller::triggerSupplyIndexUpdate function calls updateLendSupplyIndex ...
  The initial block number (uLSIndex function):  1
  lendSupplySpeeds for lToken (uLSIndex function):  100000000000000
  The current block number (uLSIndex function):  1
  current block - initial block (uLSIndex function):  0
  Lendtroller::updateLendSupplyIndex function call ends  ...
  The initial supplier index (dSL function):  0
  The current supplier index (dSL function):  1000000000000000000000000000000000000
  current minus initial index (dSL function):  0
  user totalInvestment (dSL function):  50000000000000000
  LEND accrued to user (dSL function):  0
  User calls supply at block 1
  Calling Lerc20::mint (from supply function) .... 
  Lerc20::mint ends  .... 

  Calling LendStorage::distributeSupplierLend (from supply function) .... 
  LendStorage::distributeSupplierLend call ends .... 

  Calling the Lendtroller::triggerSupplyIndexUpdate function (dSL function): 
  Lendtroller::triggerSupplyIndexUpdate function calls updateLendSupplyIndex ...
  The initial block number (uLSIndex function):  1
  lendSupplySpeeds for lToken (uLSIndex function):  100000000000000
  The current block number (uLSIndex function):  1
  current block - initial block (uLSIndex function):  0
  Lendtroller::updateLendSupplyIndex function call ends  ...
  The initial supplier index (dSL function):  0
  The current supplier index (dSL function):  1000000000000000000000000000000000000
  current minus initial index (dSL function):  0
  user totalInvestment (dSL function):  40000000000000000
  LEND accrued to user (dSL function):  0
  User calls borrow at block 21
  Calling Lerc20::borrow (from borrow function) .... 
   
  The updateLendBorrowIndex function call execution ... 
  The initial block number (uLBIndex function):  1
  lendBorrowSpeeds for lToken (uLBIndex function):  100000000000000
  The current block number (uLBIndex function):  21
  current block - initial block (uLBIndex function):  20
  The updated borrow index (uLBIndex function):  1000000000000000000000000000000000000
  The updateLendBorrowIndex function call ends ... 
  Lerc20::borrow call ends  .... 
   
  Calling LendStorage::distributeBorrowerLend (from borrow function) .... 
   
  distributeBorrowerLend call execution begins ... 
  triggerBorrowIndexUpdate call execution begins ... 
  Lendtroller::triggerBorrowIndexUpdate function calls updateLendBorrowIndex ...
   
  The updateLendBorrowIndex function call execution ... 
  The initial block number (uLBIndex function):  21
  lendBorrowSpeeds for lToken (uLBIndex function):  100000000000000
  The current block number (uLBIndex function):  21
  current block - initial block (uLBIndex function):  0
  The updateLendBorrowIndex function call ends ... 
  Lendtroller::updateLendBorrowIndex function call ends  ...
  triggerBorrowIndexUpdate call ends ... 
  deltaIndex is:  0
  borrowWithInterest(borrower, lToken) is:  0
  borrowWithInterestSame(borrower, lToken) is:  0
  borrowerAmount is:  0
  LEND accrued to borrower is:  0
  distributeBorrowerLend call ends ... 
  LendStorage::distributeBorrowerLend call ends .... 
   

Traces:
  [1329833] TestBorrowing::test_spomaria_see_borrow_under_the_hood()
    ├─ [0] VM::startPrank(deployer: [0xaE0bDc4eEAC5E950B67C6819B118761CaAF61946])
    │   └─ ← [Return] 
    ├─ [0] console::log("deployer calls supply at block", 1) [staticcall]
    │   └─ ← [Stop] 
    ├─ [2615] LendStorage::underlyingTolToken(ERC20Mock: [0x34A1D3fff3958843C43aD80F30b94c510645C316]) [staticcall]
    .
    .
    .
```

For a more formal test, add the following inside the TestBorrowing.t.sol file

```javascript
function test_spomaria_poc_borrow_fails_to_accrue_reward() public {
        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
        uint256 amount = 1e25;
        vm.startPrank(deployer);

        // First supply tokens as collateral
        (address token, address lToken) = _supply(amount);
        vm.stopPrank();

        address user = makeAddr("USER");
        // supply token (the second token) as collateral
        uint256 userAmount = (amount * 80 ) / 100;
        (address token1, address lToken1) = _supplyOne(userAmount, user);

        // Calculate maximum allowed borrow (70% of collateral to leave some safety margin)
        
        uint256 initialInvestment = lendStorage.totalInvestment(user, lToken1);
        uint256 initialInvestment0 = lendStorage.totalInvestment(user, lToken);
        uint256 maxBorrow = (userAmount * 60) / 100;
        // uint256 maxBorrow0 = (initialInvestment * 10_000_000_000) / 100;

        // Get initial balances
        uint256 initialTokenBalance = IERC20(token).balanceOf(user);

        // roll the blockchain by some blocks
        vm.warp(block.timestamp + 60*60);
        vm.roll(block.number + 20);

        vm.startPrank(user);

        // Borrow more than 70% of tokens
        // vm.expectRevert();
        coreRouter.borrow(maxBorrow, token);

        // Verify balances after borrowing
        assertGt(
            IERC20(token).balanceOf(user) - initialTokenBalance,
            0,
            "Should receive correct amount of borrowed tokens"
        );

        // Verify borrow balance is tracked correctly
        assertGt(
            lendStorage.borrowWithInterestSame(user, lToken),
            0,
            "Borrow balance should be tracked correctly"
        );

        uint256 userBalanceAfter = IERC20(token).balanceOf(user) - initialTokenBalance;

        assertEq(lendStorage.lendAccrued(user), 0, "lendAccrued has accrued for user");


        vm.stopPrank();
    }
```

Now run `forge test --match-test test_spomaria_poc_borrow_fails_to_accrue_reward`

Output:

```javascript
Ran 1 test for test/TestBorrowing.t.sol:TestBorrowing
[PASS] test_spomaria_poc_borrow_fails_to_accrue_reward() (gas: 1148378)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 27.15ms (4.06ms CPU time)

Ran 1 test suite in 33.84ms (27.15ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tools Used

Manual Review and `console2.log`


## Recommended Mitigation Steps
Recommendations are as follows:
1. Modify the `CoreRouter::borrow` to call the `distributeBorrowLend` function after updating the amount of asset borrowed by user as below

```diff
    function borrow(uint256 _amount, address _token) external {
    require(_amount != 0, "Zero borrow amount");


    address _lToken = lendStorage.underlyingTolToken(_token);


    LTokenInterface(_lToken).accrueInterest();


    (uint256 borrowed, uint256 collateral) =
        lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);


    LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);


    uint256 borrowAmount = currentBorrow.borrowIndex != 0
        ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
        : 0;


    require(collateral >= borrowAmount, "Insufficient collateral");


    // Enter the Compound market
    enterMarkets(_lToken);


    // Borrow tokens
    require(LErc20Interface(_lToken).borrow(_amount) == 0, "Borrow failed");


    // Transfer borrowed tokens to the user
    IERC20(_token).transfer(msg.sender, _amount);


-   lendStorage.distributeBorrowerLend(_lToken, msg.sender);


    // Update records
    if (currentBorrow.borrowIndex != 0) {
        uint256 _newPrinciple =
            (currentBorrow.amount * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex;


        lendStorage.updateBorrowBalance(
            msg.sender, _lToken, _newPrinciple + _amount, LTokenInterface(_lToken).borrowIndex()
        );
    } else {
        lendStorage.updateBorrowBalance(msg.sender, _lToken, _amount, LTokenInterface(_lToken).borrowIndex());
    }

+   lendStorage.distributeBorrowerLend(_lToken, msg.sender); // @audit call this function after updating amount borrowed by user

    lendStorage.addUserBorrowedAsset(msg.sender, _lToken);


    // Emit BorrowSuccess event
    emit BorrowSuccess(msg.sender, _lToken, lendStorage.getBorrowBalance(msg.sender, _lToken).amount);
}
```

2. Modify the `Lentroller::borrowAllowed` function to omit the `updateLendBorrowIndex` and `distributeBorrowLend` function calls as below
```diff
    function borrowAllowed(address lToken, address borrower, uint256 borrowAmount)
        external
        override
        returns (uint256)
    {
        // Pausing is a very serious situation - we revert to sound the alarms
        require(!borrowGuardianPaused[lToken], "borrow is paused");


        if (!markets[lToken].isListed) {
            return uint256(Error.MARKET_NOT_LISTED);
        }


        if (!markets[lToken].accountMembership[borrower]) {
            // only lTokens may call borrowAllowed if borrower not in market
            require(msg.sender == lToken, "sender must be lToken");


            // attempt to add borrower to the market
            Error err = addToMarketInternal(LToken(msg.sender), borrower);
            if (err != Error.NO_ERROR) {
                return uint256(err);
            }


            // it should be impossible to break the important invariant
            assert(markets[lToken].accountMembership[borrower]);
        }


        if (oracle.getUnderlyingPrice(LToken(lToken)) == 0) {
            return uint256(Error.PRICE_ERROR);
        }


        uint256 borrowCap = borrowCaps[lToken];
        // Borrow cap of 0 corresponds to unlimited borrowing
        if (borrowCap != 0) {
            uint256 totalBorrows = LToken(lToken).totalBorrows();
            uint256 nextTotalBorrows = add_(totalBorrows, borrowAmount);
            require(nextTotalBorrows < borrowCap, "market borrow cap reached");
        }


        (Error err,, uint256 shortfall) =
            getHypotheticalAccountLiquidityInternal(borrower, LToken(lToken), 0, borrowAmount);
        if (err != Error.NO_ERROR) {
            return uint256(err);
        }


        if (shortfall > 0) {
            return uint256(Error.INSUFFICIENT_LIQUIDITY);
        }


        // Keep the flywheel moving
-        Exp memory borrowIndex = Exp({mantissa: LToken(lToken).borrowIndex()});
-        updateLendBorrowIndex(lToken, borrowIndex);
-        distributeBorrowerLend(lToken, borrower, borrowIndex);


        return uint256(Error.NO_ERROR);
    }
```

3. Modify the the `Lendtroller::updateLendBorrowIndex` function to use `block.timestamp` instead of `block.number`. This way all users will be duly rewarded for borrowing assets from the protocol. 
