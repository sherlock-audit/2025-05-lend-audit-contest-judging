Skinny Charcoal Chinchilla

High

# Outdated exchange rate cause CoreRouter to under transfer to redeemers, leaving interest stuck in the contract.

### Summary

When users supply token and redeem back via `CoreRouter.sol::redeem()`, they get `(_amount * exchangeRateBefore) / 1e18` of lTokens added to their balance. The `exchangeRateBefore` is fetched from the `Ltoken.sol::exchangeRateStored()`, which represent the increase in value of the Ltoken due to interest from borrows. The problem is, `exchangeRateBefore` is as the name suggests the exchange rate of the Ltoken to the underlying token before the redeem happen (i.e outdated). In reality, the redeemed amount is calculated and given to the CoreRouter using the current (i.e up-to-date) exchange rate in `Ltoken.sol::redeemFresh()`. This means, the user wont get his rightful interest for his supplied assets and the interest will be stuck in the router.

When the router redeem from the Ltoken, [LErc20.sol::redeem()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LErc20.sol#L64) -> [Ltoken.sol::redeemInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L454) -> [Ltoken.sol::redeemFresh()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L457), interest is accrued via [accrueInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L324) before redemption.

```solidity 
    function redeemInternal(uint256 redeemTokens) internal nonReentrant { 
@>      accrueInterest();
        redeemFresh(payable(msg.sender), redeemTokens, 0);
    }
```
The amount transfered to the Router from the Ltoken is calculated as: 

```solidity 
    function redeemFresh(address payable redeemer, uint256 redeemTokensIn, uint256 redeemAmountIn) internal {
        require(redeemTokensIn == 0 || redeemAmountIn == 0, "one of redeemTokensIn or redeemAmountIn must be zero");
        Exp memory exchangeRate = Exp({mantissa: exchangeRateStoredInternal()});
        uint256 redeemTokens;
        uint256 redeemAmount;
        if (redeemTokensIn > 0) {
            redeemTokens = redeemTokensIn;
@>          redeemAmount = mul_ScalarTruncate(exchangeRate, redeemTokensIn);
        } else {

            redeemTokens = div_(redeemAmountIn, exchangeRate);
            redeemAmount = redeemAmountIn;
        }
        uint256 allowed = lendtroller.redeemAllowed(address(this), redeemer, redeemTokens);
        if (allowed != 0) {
            revert RedeemLendtrollerRejection(allowed);
        }
        if (accrualBlockNumber != getBlockNumber()) {
            revert RedeemFreshnessCheck();
        }
        if (getCashPrior() < redeemAmount) {
            revert RedeemTransferOutNotPossible();
        }
        totalSupply = totalSupply - redeemTokens;
        accountTokens[redeemer] = accountTokens[redeemer] - redeemTokens;

@>      doTransferOut(redeemer, redeemAmount);

        emit Transfer(redeemer, address(this), redeemTokens);
        emit Redeem(redeemer, redeemAmount, redeemTokens);
        lendtroller.redeemVerify(address(this), redeemer, redeemAmount, redeemTokens);
    }
```
At this point, exchangeRate is the up-to-date value because of the call to accrueInterest(). The router will transfer less than this amount to the user because it uses the outdated exchange rate to calculate the transfer amount.


### Root Cause

In [CoreRouter.sol::redeem()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L100) the amount to redeem to the user is calculated as: 

```solidity 
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        require(_amount > 0, "Zero redeem amount");
        require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
        require(collateral >= borrowed, "Insufficient liquidity");

        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
@>      uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;

        require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed"); 
        IERC20(_token).transfer(msg.sender, expectedUnderlying);
        lendStorage.distributeSupplierLend(_lToken, msg.sender);
        uint256 newInvestment = lendStorage.totalInvestment(msg.sender, _lToken) - _amount;
        lendStorage.updateTotalInvestment(msg.sender, _lToken, newInvestment);
        if (newInvestment == 0) {
            lendStorage.removeUserSuppliedAsset(msg.sender, _lToken);
        }
        emit RedeemSuccess(msg.sender, _lToken, expectedUnderlying, _amount);
        return 0;
    }
```

The protcol explicitily uses the exchange rate before redeeming, which is wrong. 

### Internal Pre-conditions

none

### External Pre-conditions

1. There must be other suppliers and borrowers 
2. Interest must be accrued. The more interest accrued since last call to accruedInterst(), the more the redeemr lose, the more tokens get stuck in the router. 


### Attack Path

none

### Impact


The router transfer the redeemer less than it receives. The redeemer lose interest. This interest is stuck in the router.

### PoC

Paste this test into src/test/TestBorrowing.t.sol. This is a test showing the redeemer, who supplied at the same block of a borrow, loses his interest when redeeming. 

```solidity 
    function test_redeemIndex() public {
        uint256 amount = 1e18;
        vm.startPrank(deployer);
        (address token, address lToken) = _supply(amount);
        uint256 maxBorrow = (amount * 70) / 100;
        uint256 initialTokenBalance = IERC20(token).balanceOf(deployer);
        coreRouter.borrow(maxBorrow, token);
        assertEq(
            IERC20(token).balanceOf(deployer) - initialTokenBalance,
            maxBorrow,
            "Should receive correct amount of borrowed tokens"
        );
        vm.stopPrank(); 

        address supplier = makeAddr("user");
        ERC20Mock(token).mint(supplier, 100e18);
        vm.startPrank(supplier);
        IERC20(token).approve(address(coreRouter), 100e18);
        coreRouter.supply(100e18, token);

        vm.roll(72000); //skip 10 day
        coreRouter.redeem(5e11, payable(lToken));
        //not increased
        assertEq(IERC20(token).balanceOf(supplier), 100e18);
    }
```


### Mitigation

Calculate the redeem amount using the up-to-date exchange rate. 

```diff
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        require(_amount > 0, "Zero redeem amount");
        require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
        require(collateral >= borrowed, "Insufficient liquidity");

-       uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
-       uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;

        require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");

+       uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
+       uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;

        IERC20(_token).transfer(msg.sender, expectedUnderlying);
        lendStorage.distributeSupplierLend(_lToken, msg.sender);
        uint256 newInvestment = lendStorage.totalInvestment(msg.sender, _lToken) - _amount;
        lendStorage.updateTotalInvestment(msg.sender, _lToken, newInvestment);
        if (newInvestment == 0) {
            lendStorage.removeUserSuppliedAsset(msg.sender, _lToken);
        }
        emit RedeemSuccess(msg.sender, _lToken, expectedUnderlying, _amount);
        return 0;
    }
```