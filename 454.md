Winning Cinnabar Gorilla

Medium

# In the case of bad debt in  LToken, CoreRouter users who redeem earlier can avoid loss and the other users suffer more loss

### Summary

The users who redeem earlier can fully redeem despite their proportion of total supply and redeemable amount from `LToken`, leading to the other users cannot fully redeem due to insufficient liquidity in `LToken`.

### Root Cause

When a user redeems, protocol calculates the expected underlying tokens based on the redeem amount specified by the user.

[CoreRouter.sol#L117-L118](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L117-L118):
```solidity
        // Calculate expected underlying tokens
        uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
```

The underlying tokens are redeemed from `LToken`.

[CoreRouter.sol#L120-L121](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L120-L121):
```solidity
        // Perform redeem
        require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");
```

The mechanism is unfair to the last user who redeem, as they may not be able to fully redeem as the users who redeem earlier.

Consider the following scenarios:
1.  Alice and Bob each supplies 1500 DAI to `CoreRouter`, and the tokens are deposited into `LToken`;
2. A borrower borrows 1000 DAI directly from `LToken`, then there is 2000 DAI left;
3. Bob calls to fully redeem his investment, 1500 DAI are redeemed from `LToken` to `CoreRouter` then to Bob;
4. Alice calls to fully redeem her investment, however, because there is only 500 DAI left in `LToken`, the transaction will fail;
5. Alice can only redeem 500 DAI;

While this works fine under normal conditions, in the case of bad debt, i.e. the borrower's collateral token price dumps leading to non-profitable liquidation, then the users who redeem earlier can avoid the loss and the last users suffer more loss than expected.

### Internal Pre-conditions

NA

### External Pre-conditions

Bad debt occurs in `LToken`.

### Attack Path

NA

### Impact

Users suffer more loss than expected.

### PoC

Please run `forge test --mt testPOC_UnfairRedeem`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import "forge-std/Test.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import {ILayerZeroEndpointV2} from "@layerzerolabs/lz-evm-protocol-v2/contracts/interfaces/ILayerZeroEndpointV2.sol";

import {Lend} from "../src/Governance/Lend.sol";
import {Lendtroller, LendtrollerInterface} from "../src/Lendtroller.sol";
import {LErc20Immutable, LToken} from "../src/LErc20Immutable.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel, InterestRateModel} from "../src/WhitePaperInterestRateModel.sol";
import {LendtrollerErrorReporter, TokenErrorReporter} from "../src/ErrorReporter.sol";

import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {CrossChainRouter, Origin} from "../src/LayerZero/CrossChainRouter.sol";

contract POC is Test {
    using SafeERC20 for IERC20;

    uint32 constant chainAEid = 30101; // Ethereum
    uint32 constant chainBEid = 30184; // Base

    address owner;
    address mockDestLToken;
    address mockPeer;

    IERC20 btc;
    IERC20 dai;
    IERC20 weth;

    LErc20Immutable lTokenBTC;
    LErc20Immutable lTokenDAI;
    LErc20Immutable lTokenWETH;

    Lend lend;

    ILayerZeroEndpointV2 endpoint;

    Lendtroller lendtroller;
    SimplePriceOracle priceOracle;
    WhitePaperInterestRateModel interestRateModel;

    LendStorage lendStorage;
    CoreRouter coreRouter;
    CrossChainRouter crossChainRouter;

    function setUp() public {
        vm.createSelectFork("https://eth.drpc.org");

        owner = makeAddr("Owner");
        mockDestLToken = makeAddr("MockDestLToken");
        mockPeer = makeAddr("ChainBPeer");
        endpoint = ILayerZeroEndpointV2(0x1a44076050125825900e736c501f859c50fE728c);

        btc = IERC20(0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599);
        dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
        weth = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

        vm.startPrank(owner);

        // Deploy Lendtroller
        lendtroller = new Lendtroller();
        // Deploy Lend
        lend = new Lend(address(lendtroller));
        // Deploy PriceOracle
        priceOracle = new SimplePriceOracle();
        // Deploy InterestRateModel
        interestRateModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);
        // Deploy lTokens
        lTokenBTC = new LErc20Immutable(
            address(btc),
            lendtroller,
            interestRateModel,
            1e8, // 1:1
            "Lending BTC",
            "lBTC",
            18,
            payable(owner)
        );
        lTokenDAI = new LErc20Immutable(
            address(dai),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending DAI",
            "lDAI",
            18,
            payable(owner)
        );
        lTokenWETH = new LErc20Immutable(
            address(weth),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending WETH",
            "lWETH",
            18,
            payable(owner)
        );

        // Deploy LendStorage
        lendStorage = new LendStorage(
            address(lendtroller),
            address(priceOracle),
            chainAEid
        );
        // Deploy CoreRouter
        coreRouter = new CoreRouter(
            address(lendStorage),
            address(priceOracle),
            address(lendtroller)
        );
        // Deploy CrossChainRouter
        crossChainRouter = new CrossChainRouter(
            address(endpoint),
            owner,
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            chainAEid
        );

        // Configurations
        {
            priceOracle.setUnderlyingPrice(lTokenBTC, 100000e18);
            priceOracle.setUnderlyingPrice(lTokenDAI, 1e18);
            priceOracle.setUnderlyingPrice(lTokenWETH, 2500e18);

            lendtroller._setPriceOracle(priceOracle);
            lendtroller.setLendStorage(address(lendStorage));
            lendtroller._supportMarket(lTokenBTC);
            lendtroller._supportMarket(lTokenDAI);
            lendtroller._supportMarket(lTokenWETH);
            lendtroller.setLendToken(address(lend));
            lendtroller._setCollateralFactor(lTokenBTC, 0.9e18);
            lendtroller._setCollateralFactor(lTokenDAI, 0.9e18);
            lendtroller._setCollateralFactor(lTokenWETH, 0.9e18);

            LToken[] memory lTokens = new LToken[](3);
            lTokens[0] = lTokenBTC;
            lTokens[1] = lTokenDAI;
            lTokens[2] = lTokenWETH;
            uint256[] memory supplySpeeds = new uint256[](3);
            supplySpeeds[0] = 1e18;
            supplySpeeds[1] = 1e18;
            supplySpeeds[2] = 1e18;
            uint256[] memory borrowSpeeds = new uint256[](3);
            borrowSpeeds[0] = 2e18;
            borrowSpeeds[1] = 2e18;
            borrowSpeeds[2] = 2e18;
            lendtroller._setLendSpeeds(lTokens, supplySpeeds, borrowSpeeds);

            lendStorage.addSupportedTokens(address(btc), address(lTokenBTC));
            lendStorage.addSupportedTokens(address(dai), address(lTokenDAI));
            lendStorage.addSupportedTokens(address(weth), address(lTokenWETH));
            lendStorage.addUnderlyingToDestlToken(address(btc), address(mockDestLToken), chainBEid);
            lendStorage.addUnderlyingToDestlToken(address(dai), address(mockDestLToken), chainBEid);
            lendStorage.addUnderlyingToDestlToken(address(weth), address(mockDestLToken), chainBEid);
            lendStorage.setAuthorizedContract(address(coreRouter), true);
            lendStorage.setAuthorizedContract(address(crossChainRouter), true);

            crossChainRouter.setPeer(chainBEid, bytes32(uint256(uint160(mockPeer))));

            coreRouter.setCrossChainRouter(address(crossChainRouter));

            deal(address(crossChainRouter), 1 ether);
        }

        vm.stopPrank();

        {
            vm.label(address(btc), "BTC");
            vm.label(address(dai), "DAI");
            vm.label(address(weth), "WETH");
            vm.label(address(lTokenBTC), "lToken BTC");
            vm.label(address(lTokenDAI), "lToken DAI");
            vm.label(address(lTokenWETH), "lToken WETH");
            vm.label(address(endpoint), "Endpoint");
            vm.label(address(lendtroller), "Lendtroller");
            vm.label(address(lend), "Lend");
            vm.label(address(priceOracle), "PriceOracle");
            vm.label(address(lendStorage), "LendStorage");
            vm.label(address(coreRouter), "CoreRouter");
            vm.label(address(crossChainRouter), "CrossChainRouter");
        }
    }

    function testPOC_UnfairRedeem() public {
        address alice = makeAddr("Alice");
        deal(address(dai), alice, 1500e18);
        
        // Alice supplies 1500 Dai
        {
            vm.startPrank(alice);
            dai.safeApprove(address(coreRouter), 1500e18);
            coreRouter.supply(1500e18, address(dai));
            vm.stopPrank();
        }

        address bob = makeAddr("Bob");
        deal(address(dai), bob, 1500e18);
        
        // Bob supplies 1500 Dai
        {
            vm.startPrank(bob);
            dai.safeApprove(address(coreRouter), 1500e18);
            coreRouter.supply(1500e18, address(dai));
            vm.stopPrank();
        }

        {
            address borrower = makeAddr("Borrower");
            deal(address(weth), borrower, 1 ether);

            vm.startPrank(borrower);
            
            address[] memory lTokens = new address[](1);
            lTokens[0] = address(lTokenWETH);
            lendtroller.enterMarkets(lTokens);

            weth.approve(address(lTokenWETH), 1 ether);
            lTokenWETH.mint(1 ether);
            lTokenDAI.borrow(1000e18);

            vm.stopPrank();
        }

        uint256 aliceInvestment = lendStorage.totalInvestment(alice, address(lTokenDAI));
        uint256 bobInvestment = lendStorage.totalInvestment(bob, address(lTokenDAI));

        // Bob can fully redeem
        vm.prank(bob);
        coreRouter.redeem(bobInvestment, payable(address(lTokenDAI)));
        assertEq(dai.balanceOf(bob), 1500e18);

        vm.startPrank(alice);

        // Alice cannot fully redeem
        vm.expectRevert(abi.encodeWithSelector(TokenErrorReporter.RedeemTransferOutNotPossible.selector));
        coreRouter.redeem(aliceInvestment, payable(address(lTokenDAI)));

        // She can only redeem 500 DAI
        coreRouter.redeem(500e18, payable(address(lTokenDAI)));
        assertEq(dai.balanceOf(alice), 500e18);

        vm.stopPrank();
    }
}
```

### Mitigation

It is recommended to allow users to redeem based on their proportion of total supply and the redeemable amount.