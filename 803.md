Skinny Charcoal Chinchilla

High

# Cross-chain liquidations of low-decimal tokens are almost always possible.

### Summary

When a user's cross-chain position gets liquidated, the validity of the liquidation (i.e borrowed > collateral) is checked on the soure chain where the collateral exists via `CrossChainRouter.sol::_checkLiquidationValid()`. The function has a flaw, where it treat the repaying amount as a new borrow, allowing liquidations even with sufficient collateral. 

In [CrossChainRouter.sol::_checkLiquidationValid()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L431), the validity is checked as:

```solidity 
    function _checkLiquidationValid(LZPayload memory payload) private view returns (bool) {
        (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
@>          payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount 
        );
        return borrowed > collateral;
    }
```

If this returns true, the liquidation proceeds as this is the only check of the borrowed amount and collateral. We can see `payload.destlToken` and `payload.amount` is inputed into [LendStorage::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L385). This function treat the `payload.amount` as a new borrow, when its actually the repay amount in term of Ltoken, calculated from `LendTroller.sol::liquidateCalculateSeizeTokens()`.

```solidity 
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
            uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
            vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()}); 
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
            vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }

        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);
            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));
            totalBorrow += borrowWithInterest(account, address(asset));
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.sumBorrowPlusEffects = 
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }
@>      if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
                vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()});
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects = 
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }
            if (borrowAmount > 0) {
@>              vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }
        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```

This causes the user's borrow to be more than actual. This is not a very big issues with 18 decimals token because their respective Ltoken would be in 9 decimals. (By default, 1e18 of a token == 5e9 Ltoken, exchangeRate of 18 decimal tokens is 2e26. See [Deploy.s.sol](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/script/Deploy.s.sol#L126)). For stablecoins like USDC and USDT, which has 6 decimals, their Ltoken would be in 9 decimals too. (exchangeRate is 2e14). 

In [LendTroller.sol::liquidateCalculateSeizeTokens()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/Lendtroller.sol#L852), we can see it return how much collateral of Ltoken to seize based on the repay amount. 

```solidity 
    function liquidateCalculateSeizeTokens(address lTokenBorrowed, address lTokenCollateral, uint256 actualRepayAmount) //@note liquidateCalculateSeizeTokens
        external
        view
        override
        returns (uint256, uint256)
    {
        uint256 priceBorrowedMantissa = oracle.getUnderlyingPrice(LToken(lTokenBorrowed));

        uint256 priceCollateralMantissa = oracle.getUnderlyingPrice(LToken(lTokenCollateral));

        if (priceBorrowedMantissa == 0 || priceCollateralMantissa == 0) {
            return (uint256(Error.PRICE_ERROR), 0);
        }
        uint256 exchangeRateMantissa = LToken(lTokenCollateral).exchangeRateStored(); // Note: reverts on error 
        uint256 seizeTokens;
        Exp memory numerator;
        Exp memory denominator;
        Exp memory ratio;
        numerator = mul_(Exp({mantissa: liquidationIncentiveMantissa}), Exp({mantissa: priceBorrowedMantissa}));
        denominator = mul_(Exp({mantissa: priceCollateralMantissa}), Exp({mantissa: exchangeRateMantissa}));
        ratio = div_(numerator, denominator);
        seizeTokens = mul_ScalarTruncate(ratio, actualRepayAmount); 
        return (uint256(Error.NO_ERROR), seizeTokens);
    }
```


### Root Cause

```solidity 
    function _checkLiquidationValid(LZPayload memory payload) private view returns (bool) {
        (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
@>          payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount 
        );
        return borrowed > collateral;
    }
```

There is no need to pass in `payload.destlToken` and `payload.amount` into the function. Remove it.

### Internal Pre-conditions

none

### External Pre-conditions

1. User must borrow a 6 decimal or low decimal token.

### Attack Path

Assumsing the default exchange rate and no incentive for easier example.

1. Alice borrows 10000e6 of USDC with collateral of 20000e6 USDC
2. The attacker try to liquidate alice with repay amount as 2e6. 
3. The function returns 1e10 (2e6 * 1e18 / 2e14). 
4. This amount is treated as a borrow making alice's borrow 2e10. Allowing liquidation.
5. Alcie lose all her collateral.

NOTE: the liquidation would revert if there is not enough collateral to deduct from storage, but the attacker can simply calculate and craft how much to pass in as `repayAmount`.


### Impact

6 decimals borrowers will be liquidated unfairly. 

### PoC

_No response_

### Mitigation

Dont pass in the amount nor the destlToken.

```diff
    function _checkLiquidationValid(LZPayload memory payload) private view returns (bool) {
        (uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(
-          payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount 
+          payload.sender, address(0), 0, 0
        );
        return borrowed > collateral;
    }
```