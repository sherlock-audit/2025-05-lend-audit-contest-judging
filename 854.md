Skinny Charcoal Chinchilla

Medium

# User's have less borrowing power due to using outdated collateral values.

### Summary

hen users do a same-chain borrow via [CoreRouter.sol::borrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L145), the borrowing token is made sure to be up-to-date why the collateral tokens of the user is not. This gives users less borrowing power as the amount user can borrow is compared between the up-to-date borrowing value and outdated collateral value.

```solidity 
    function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");
        address _lToken = lendStorage.underlyingTolToken(_token);

@>      LTokenInterface(_lToken).accrueInterest();
        (uint256 borrowed, uint256 collateral) = 
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);
        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);
        uint256 borrowAmount = currentBorrow.borrowIndex != 0 
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0; 
        require(collateral >= borrowAmount, "Insufficient collateral");
        ...
    }
```

The [LendStorage.sol::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L385) calculates and return the USD value of both the total borrows( previous borrows + current borrow) and total collateral. However since only the borrowing token is updated via [Ltoken.sol::accrueInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L324), the comparison does not reflect the real value of the collateral. [Ltoken.sol::exchangeRateStored()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L281-L307) returns the last updated exchange rate.

```solidity 
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
            uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
@>          vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()}); 
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
@>          vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);
            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));
            totalBorrow += borrowWithInterest(account, address(asset));
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
@>          vars.sumBorrowPlusEffects = 
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
                vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()}); 
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects = 
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }
            if (borrowAmount > 0) {
@>              vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }
        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```


### Root Cause

The comparison is done using values of different times, causing the user to gain less borrowing power.

```solidity 
    function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");
        address _lToken = lendStorage.underlyingTolToken(_token);

@>      LTokenInterface(_lToken).accrueInterest();
        (uint256 borrowed, uint256 collateral) = 
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);
        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);
        uint256 borrowAmount = currentBorrow.borrowIndex != 0 
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0; 
        require(collateral >= borrowAmount, "Insufficient collateral");
        ...
    }
```

### Internal Pre-conditions

none

### External Pre-conditions

none

### Attack Path

none

### Impact

The user get less borrowing power, their outdated collateral value is compared against the up-to-date borrowing value.


### PoC

none

### Mitigation

Update both the borrows and collaterals of the user before any calculations / comparisons.

```diff
    function borrow(uint256 _amount, address _token) external {

+       address[] memory suppliedAssets = userSuppliedAssets[msg.sender].values();
+       address[] memory borrowedAssets = userBorrowedAssets[msg.sender].values();

+       for (uint256 i = 0; i < suppliedAssets.length; i++) {
+           LTokenInterface(suppliedAssets[i]).accrueInterest();
        }

+       for (uint256 i = 0; i < borrowedAssets.length; i++) {
+           LTokenInterface(borrowedAssets[i]).accrueInterest();
        }

        require(_amount != 0, "Zero borrow amount");
        address _lToken = lendStorage.underlyingTolToken(_token);

-       LTokenInterface(_lToken).accrueInterest();
        (uint256 borrowed, uint256 collateral) = 
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);
        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);
        uint256 borrowAmount = currentBorrow.borrowIndex != 0 
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0; 
        require(collateral >= borrowAmount, "Insufficient collateral");
        ...
    }

```