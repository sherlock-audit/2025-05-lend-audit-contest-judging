Scrawny Red Sloth

Medium

# `borrowCrossChain()` fails to scale `borrow_principle` resulting in cross-chain collateral leftover after full repayment

### Summary

During `borrowCrossChain()`, when updating the `userCrossChainCollaterals[index].principle`, the existing principle is first scaled by the current borrow index before adding `payload.amount`, however the `payload.amount` is added directly to `userBorrows[index].principle` before first scaling this principle. As such, this yields two structs with mismatched principles on different chains resulting in left over cross-chain collateral even after the user has performed full repayment.

### Root Cause

When a user perform a borrow, the `userCrossChainCollaterals[index].principle` is [updated as follows](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L630-L639) on the destination chain:
```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        ---snip---
        if (found) {
            // @audit existing principle is scaled first by currentBorrowIndex
>>          uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            // @audit Then set correctly
>>          userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            lendStorage.updateCrossChainCollateral(
                payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
        }
    }
```
On, source chain however, the [borrow principle is updated directly](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L708-L715) as follows:
```solidity
    function _handleValidBorrowRequest(LZPayload memory payload, uint32 srcEid) private { //@audit-ok
        ---snip---

        if (found) {
            // @audit Retrieved here
>>          LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
            // @audit Principle updated directly without scaling
>>          userBorrows[index].principle = userBorrows[index].principle + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // @audit Updated in storage
>>          lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        }
        ---snip---
    }
```

Now, during the first borrow, these two structs will record he same `principle` since during this time, the borrower has no stored borrow index and as such, there is no scaling to be done. As such, this issue is masked.

However, if the user performs a first borrow, then time elapses such that interest is accrued before he does a subsequent borrow, then these structs end up storing dirrent values as `principle` as one is scaled and the other is not.

Now, the issue comes when this user perform a full repay of their borrow. During this time, the user will specify the full borrow principle and [the following will be done](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L556-L561):
```solidity
    function _handleDestRepayMessage(LZPayload memory payload, uint32 srcEid) private {
        ---snip---

        if (payload.amount >= currentBorrow) {
            // @audit Full repayment will remove the borrow struct
>>          lendStorage.removeCrossChainBorrow(payload.sender, payload.srcToken, index);
            if (userBorrows.length == 1) {
                lendStorage.removeUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));
            }
        }
    }
```
However, since this amount does not match the principle in collateral struct, [only update will be done](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L523-L527) on it:
```solidity
    function _updateRepaymentState(
        //...
    ) private {
        ---snip---

        if (repayAmountFinal == borrowedAmount) {
            //..
        } else {
            // @audit Principle is Just decremented then stored
>>          userCrossChainCollaterals[index].principle = borrowedAmount - repayAmountFinal;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;
            lendStorage.updateCrossChainCollateral(borrower, _token, index, userCrossChainCollaterals[index]);
        }
        ---snip---
    }
```
This way, a user winds up with his borrow repayed but the protocol still records a cross chain collateral based on an already non-existent borrow. This causes inconsistent system state.

### Internal Pre-conditions

1. User has previously executed at least one successful cross-chain borrow
2. Enough time has passed since the first borrow such that interest has accrued (`borrowIndex` has increased)
3. User initiates a subsequent cross-chain borrow operation

### External Pre-conditions

1. User later attempts full repayment of their borrow position


### Attack Path

1. User borrows amount `X` and both chains record same principle (`X`)
2. Time passes and  `borrowIndex` increases
3. User borrows amount `Y` where Source Chain adds `Y` directly → `X + Y` while Dest Chain scales `X` first → `(X * index_ratio) + Y`.
4. Repays `X + Y` (source chain’s amount) where Source Chain debt is cleared → deleted while in Dest Chain, partial collateral reduction → leftover

### Impact

When users repay their full borrow amount, the mismatch between scaled and unscaled principles across chains results in leftover collateral that remains locked in the protocol despite the debt being fully cleared. This creates an inconsistent state where the protocol incorrectly maintains collateral records for non-existent debts.


### PoC

1. Add the following test in `test/testBorrowingCrossChain.t.sol`:
```solidity
    function test_borrowCrossChain_fails_to_scale_borrow_principle_causing_xchain_collateral_leftover
    (
        uint256 amountToSupply, 
        uint256 amountToBorrow
    )
        public
    {
        amountToSupply = bound(amountToSupply, 1e18, 1e30);

        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
        (address tokenB, address lTokenB) = _supplyB(amountToSupply * 2);

        // Fund Router A and B with ETH for LayerZero fees
        vm.deal(address(routerA), 1 ether);
        vm.deal(address(routerB), 1 ether);

        // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
        uint256 maxBorrow = (lendStorageA.getMaxBorrowAmount(deployer, lTokenA) * 0.9e18) / 1e18;

        uint256 boundedBorrow = bound(amountToBorrow, 0.1e18, maxBorrow) / 2;

        // boundedBorrow = boundedBorrow / 2;

        vm.startPrank(deployer);

        // FIRST BORROW
        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);

        // Get the destination chain borrow details using the test contract address
        LendStorage.Borrow[] memory userBorrows = lendStorageA.getCrossChainBorrows(deployer, tokenA);
        LendStorage.Borrow[] memory userCollaterals = lendStorageB.getCrossChainCollaterals(deployer, tokenB);

        // SHOULD MATCH:
        // This is because, during first borrow, the user has no stored borrow index on chain A.
        // Therefore, the principle is scaled by the same index as the collateral yielding egual values.
        assertEq(userBorrows[0].principle, userCollaterals[0].principle, "Borrow principle should match");

        // Advance some blocks to accrue interest
        vm.warp(block.timestamp + 1000);
        vm.roll(block.number + 1000);

        // SECOND BORROW
        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);

        // Get the borrow and collateral details using the test contract address
        LendStorage.Borrow[] memory newUserBorrows = lendStorageA.getCrossChainBorrows(deployer, tokenA);
        LendStorage.Borrow[] memory newUserCollaterals = lendStorageB.getCrossChainCollaterals(deployer, tokenB);

        // DOES NOT MATCH:
        // This is because, during second borrow, the user has a stored borrow index on chain A.
        // Therefore, the collateral principle is scaled by the diff between current index and stored borrow index 
        // whereas borrow principle is left unchanged yielding different values.
        assertNotEq(newUserBorrows[0].principle, newUserCollaterals[0].principle, "Borrow principle should NOT match");

        // FULL REPAYMENT
        uint256 fullBorrowAmount = newUserBorrows[0].principle;

        ERC20Mock(tokenB).mint(deployer, fullBorrowAmount);
        IERC20(tokenB).approve(address(coreRouterB), fullBorrowAmount);

        // user repays full borrow
        routerB.repayCrossChainBorrow(deployer, fullBorrowAmount, lTokenB, 31337);

        // Check  borrow details:
        // we expect the user to have 0 borrows on chain A now
        LendStorage.Borrow[] memory currentUserBorrows = lendStorageA.getCrossChainBorrows(deployer, tokenA); 
        assertEq(currentUserBorrows.length, 0, "Should have 0 borrows");

        // check user collateral details:
        // we expect the user to have 0 collaterals on chain B now. However, this is not the case
        LendStorage.Borrow[] memory currentUserCollaterals = lendStorageB.getCrossChainCollaterals(deployer, tokenB);  
        assertNotEq(currentUserCollaterals.length, 0, "Should still have collaterals");

        vm.stopPrank();
    }
```

### Mitigation

On the source chain, scale the existing principle before adding new amount:
```diff
    if (found) {
        LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
-       userBorrows[index].principle = userBorrows[index].principle + payload.amount;
+       uint256 scaledPrinciple = (userBorrows[index].principle * payload.borrowIndex) / userBorrows[index].borrowIndex;
+       userBorrows[index].principle = scaledPrinciple + payload.amount;
        userBorrows[index].borrowIndex = payload.borrowIndex;
        lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
    }
```