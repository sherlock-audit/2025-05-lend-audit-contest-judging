Big Sable Whale

High

# incorrect usage of seizeTokens as repay tokens in _handleLiquidationSuccess function

### Summary

when a borrower cross borrows tokens of chainB using borrowCrossChain function in chain A.Then a liquidator can call liquidateCrossChain function in chainB.which will do sufficient checks to send a msg to chain A via layerZero,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L172-L191
```solidity
    function liquidateCrossChain(
        address borrower,
        uint256 repayAmount,
        uint32 srcEid,
        address lTokenToSeize,
        address borrowedAsset
    ) external {
        LendStorage.LiquidationParams memory params = LendStorage.LiquidationParams({
            borrower: borrower,
            repayAmount: repayAmount,
            srcEid: srcEid,
            lTokenToSeize: lTokenToSeize, // Collateral lToken from the user's position to seize
            borrowedAsset: borrowedAsset,
            storedBorrowIndex: 0,
            borrowPrinciple: 0,
            borrowedlToken: address(0)
        });


        _validateAndPrepareLiquidation(params);
        _executeLiquidation(params);
```
and let's see call flow from  _executeLiquidation function,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L235-L242
```solidity
    function _executeLiquidation(LendStorage.LiquidationParams memory params) private {
        // First part: Validate and prepare liquidation parameters
        uint256 maxLiquidation = _prepareLiquidationValues(params);


        require(params.repayAmount <= maxLiquidation, "Exceeds max liquidation");


        // Secon part: Validate collateral and execute liquidation
        _executeLiquidationCore(params);
```
then 
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L264-L285
```solidity
    function _executeLiquidationCore(LendStorage.LiquidationParams memory params) private {
        // Calculate seize tokens
        address borrowedlToken = lendStorage.underlyingTolToken(params.borrowedAsset);


        (uint256 amountSeizeError, uint256 seizeTokens) = LendtrollerInterfaceV2(lendtroller)
            .liquidateCalculateSeizeTokens(borrowedlToken, params.lTokenToSeize, params.repayAmount);


        require(amountSeizeError == 0, "Seize calculation failed");


        // Send message to Chain A to execute the seize
        _send(
            params.srcEid,
            seizeTokens,
            params.storedBorrowIndex,
            0,
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid), // Convert to Chain A version before sending
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
    }
```
here if we see, seizeTokens are tokens to be seized from borrower but this is not repay amount.
now payload.amount = seizeTokens.
now this triggers _handleLiquidationExecute function in chain A,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L312-L366
```solidity
    function _handleLiquidationExecute(LZPayload memory payload, uint32 srcEid) private {
        // Execute the seize of collateral
        uint256 protocolSeizeShare = mul_(payload.amount, Exp({mantissa: lendStorage.PROTOCOL_SEIZE_SHARE_MANTISSA()}));


        require(protocolSeizeShare < payload.amount, "Invalid protocol share");


        uint256 liquidatorShare = payload.amount - protocolSeizeShare;


        // Update protocol rewards
        lendStorage.updateProtocolReward(
            payload.destlToken, lendStorage.protocolReward(payload.destlToken) + protocolSeizeShare
        );


        // Distribute LEND rewards
        lendStorage.distributeSupplierLend(payload.destlToken, payload.sender); // borrower
        lendStorage.distributeSupplierLend(payload.destlToken, payload.liquidator); // liquidator


        // Update total investment for borrower
        lendStorage.updateTotalInvestment(
            payload.sender,
            payload.destlToken,
            lendStorage.totalInvestment(payload.sender, payload.destlToken) - payload.amount
        );


        // Update total investment for liquidator
        lendStorage.updateTotalInvestment(
            payload.liquidator,
            payload.destlToken,
            lendStorage.totalInvestment(payload.liquidator, payload.destlToken) + liquidatorShare
        );


        // Clear user supplied asset if total investment is 0
        if (lendStorage.totalInvestment(payload.sender, payload.destlToken) == 0) {
            lendStorage.removeUserSuppliedAsset(payload.sender, payload.destlToken);
        }


        emit LiquidateBorrow(
            payload.liquidator, // liquidator
            payload.srcToken, // borrowed token
            payload.sender, // borrower
            payload.destlToken // collateral token
        );


        _send(
            srcEid,
            payload.amount,
            0,
            0,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.LiquidationSuccess
        );
    }
```
here also payload.amount = seizeTokens.
this will trigger _handleLiquidationSuccess function in chain B,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443-L470
```solidity
    function _handleLiquidationSuccess(LZPayload memory payload) private {
        // Find the borrow position on Chain B to get the correct srcEid
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);


        // Find the specific collateral record
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            currentEid, // srcEid is current chain
            0, // We don't know destEid yet, but we can match on other fields
            payload.destlToken,
            payload.srcToken
        );


        require(found, "Borrow position not found");


        LendStorage.Borrow[] memory userCollaterals = lendStorage.getCrossChainCollaterals(payload.sender, underlying);
        uint32 srcEid = uint32(userCollaterals[index].srcEid);


        // Now that we know the borrow position and srcEid, we can repay the borrow using the escrowed tokens
        // repayCrossChainBorrowInternal will handle updating state and distributing rewards.
        repayCrossChainBorrowInternal(
            payload.sender, // The borrower
            payload.liquidator, // The liquidator (repayer)
            payload.amount, // Amount to repay
            payload.destlToken, // lToken representing the borrowed asset on this chain
            srcEid // The chain where the collateral (and borrow reference) is tracked
        );
```
here it is giving payload.amount = seizeTokens as input to repayCrossChainBorrowInternal function, now let's see repayCrossChainBorrowInternal function,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L368-L395
```solidity
    function repayCrossChainBorrowInternal(
        address borrower,
        address repayer,
        uint256 _amount,
        address _lToken,
        uint32 _srcEid
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest();


        // Get borrow details and validate
        (uint256 borrowedAmount, uint256 index, LendStorage.Borrow memory borrowPosition) =
            _getBorrowDetails(borrower, _token, _lToken, _srcEid);


        // Calculate and validate repay amount
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
        require(repayAmountFinal <= borrowedAmount, "Repay amount exceeds borrow");


        // Handle token transfers and repayment
        _handleRepayment(borrower, repayer, _lToken, repayAmountFinal);


        // Update state
        _updateRepaymentState(
            borrower, _token, _lToken, borrowPosition, repayAmountFinal, borrowedAmount, index, _srcEid
        );


        emit RepaySuccess(borrower, _token, repayAmountFinal);
    }
```
here we can see we are using seizeTokens as _amount, but _amount should be repay tokens.this may cause the below check to pass eventhough actually it should fail due to using more repaytokens than borrowedAmount.
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L383-L384
```solidity
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
        require(repayAmountFinal <= borrowedAmount, "Repay amount exceeds borrow");
```
and afterwards we are using same seizeTokens as tokens to transfer as repay tokens, which will cause transfer of wrong amount of tokens as repay tokens. and also repayment status will also be updated wrongly.

### Root Cause

using payload.amount instead of repay tokens in below lines of code,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L467
```solidity
            payload.amount, // Amount to repay
```

### Internal Pre-conditions

none

### External Pre-conditions

none 

### Attack Path

liquidator calls liquidateCrossChain function 

### Impact

using seizeTokens in place of repay tokens causes wrong transfer to repay tokens from liquidator to this contract.and also  _updateRepaymentState will update wrong no of repay tokens.

### PoC

_No response_

### Mitigation

use actual repay tokens  instead of (payload.amount = seizeTokens)  in below lines of code,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L467
```solidity
            payload.amount, // Amount to repay
```