Fierce Eggplant Dalmatian

High

# Integer Underflow in `repayBorrowInternal` function Leads to Transaction Revert and DOS Attack Vector

## Title
Integer Underflow in `repayBorrowInternal` function Leads to Transaction Revert and DOS Attack Vector

## Summary
The `repayBorrowInternal()` function fails to validate that the repayment amount doesn't exceed the borrowed amount, leading to arithmetic underflow in the `updateBorrowBalance` call. This causes transaction reverts, prevents legitimate debt repayments, and creates potential denial-of-service attack vectors.

## Root Cause
The vulnerability stems from insufficient input validation in the repayment logic in [`repayBorrowInternal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L459-L504) function. When users specify a repayment amount greater than their outstanding debt (common when users want to ensure full repayment but are uncertain of exact debt amount), the function attempts to calculate `borrowedAmount - repayAmountFinal`, resulting in integer underflow since `repayAmountFinal > borrowedAmount`.
```solidity
>>      uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount; 
        // Transfer tokens from the liquidator to the contract
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal); 

        _approveToken(_token, _lToken, repayAmountFinal);

        lendStorage.distributeBorrowerLend(_lToken, borrower);

        // Repay borrowed tokens
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");

        // Update same-chain borrow balances //! what happens if it not same chain?
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
>>              borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex() 
            );
        }
```

## Internal Pre-conditions
User calls `repayBorrow()` with `_amount > borrowedAmount`.

## External Pre-conditions
None

## Attack Path
1. User owes 100 `USDC` (`borrowedAmount = 100e6`).
2. User calls `repayBorrow(105e6, lUSDC)` to ensure full repayment.
3. Contract sets `repayAmountFinal = 105e6`.
4. Since `repayAmountFinal > borrowedAmount`
5. `updateBorrowBalance()` function is called
```solidity
borrowedAmount - repayAmountFinal = 100e6 - 105e6 â†’ Underflow!
```
6. Transaction reverts without repaying any debt.

## Impact
Users won't be able to repay their debts even with sufficient funds.

## POC
In `test/TestBorrowing.t.sol`, add the following test
```solidity
    function test_repay_fails_when_repayAmountFinal_exceeds_borrowedAmount_due_to_underflow(uint256 amount) public {
        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
        amount = bound(amount, 1e18, 1e30);
        vm.startPrank(deployer);

        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        // mint tokens
        ERC20Mock(token).mint(deployer, 2 * amount);

        IERC20(token).approve(address(coreRouter), amount);

        // supply tokens as collateral
        coreRouter.supply(amount, token);

        // Calculate maximum allowed borrow (70% of collateral to leave some safety margin)
        uint256 maxBorrow = (amount * 70) / 100;

        // Get initial balances
        uint256 initialTokenBalance = IERC20(token).balanceOf(deployer);

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, lToken, maxBorrow);

        // Borrow tokens
        coreRouter.borrow(maxBorrow, token);

        // Verify balances after borrowing
        assertEq(
            IERC20(token).balanceOf(deployer) - initialTokenBalance,
            maxBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        // Verify borrow balance is tracked correctly
        assertEq(
            lendStorage.borrowWithInterestSame(deployer, lToken),
            maxBorrow,
            "Borrow balance should be tracked correctly"
        );

        // Repay borrow

        // approve contract to pull tokens
        IERC20(token).approve(address(coreRouter), maxBorrow + 1);

        // expect revert with underflow
        vm.expectRevert(abi.encodeWithSignature("Panic(uint256)", 0x11));
        coreRouter.repayBorrow(maxBorrow + 1, lToken);

        vm.stopPrank();
    }
```

## Mitigation
Cap repayment amount to borrowed amount
```diff
-    uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
+    uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : (_amount > borrowedAmount ? borrowedAmount : _amount);
```
