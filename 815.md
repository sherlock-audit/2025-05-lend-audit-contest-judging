Skinny Charcoal Chinchilla

High

# Wrong interest calculation causes unfair liquidations.

### Summary


When a user liquidate someone via, `CoreRouter.sol::liquidateBorrow()`, the function first get the up-to-date borrowed amount(original borrowed amount + interest) and collateral amount via `LendStorage.sol::getHypotheticalAccountLiquidityCollateral()`. These amount are passed into `CoreRouter.sol::liquidateBorrowInternal()` which calls `liquidateBorrowAllowedInternal()`. This function check if the liquidation is valid or not by making sure the up-to-date borrowed amount is more than collateral. However, the function add interest to the up-to-date borrowed amount again, making it larger than the actual amount. This will make users unfairly liquidated even when they have enough collateral to cover for borrow + interest. 

Before calling `LendStorage.sol::getHypotheticalAccountLiquidityCollateral()`, [accrueInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L324) is called on the borrowed token to make sure interest is up-to-date. 

```solidity 
    function liquidateBorrow(address borrower, uint256 repayAmount, address lTokenCollateral, address borrowedAsset)
        external
    {
        address borrowedlToken = lendStorage.underlyingTolToken(borrowedAsset);

        LTokenInterface(borrowedlToken).accrueInterest(); 
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(borrower, LToken(payable(borrowedlToken)), 0, 0);

        liquidateBorrowInternal(
            msg.sender, borrower, repayAmount, lTokenCollateral, payable(borrowedlToken), collateral, borrowed 
        );
    }
```

The [LendStorage.sol::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L385) then returns the up-to-date borrow + interest amount and collateral. 

```solidity 
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        ...
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);
            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));
@>          totalBorrow += borrowWithInterest(account, address(asset));
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.sumBorrowPlusEffects = 
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }
        ...
    }

    function borrowWithInterestSame(address borrower, address _lToken) public view returns (uint256) {
        uint256 borrowIndex = borrowBalance[borrower][_lToken].borrowIndex;
        uint256 borrowBalanceSameChain = borrowIndex != 0
            ? (borrowBalance[borrower][_lToken].amount * uint256(LTokenInterface(_lToken).borrowIndex())) / borrowIndex
            : 0;
        return borrowBalanceSameChain;
    }
```

### Root Cause

In the [liquidateBorrowAllowedInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L329), this up-to-date borrowed + interest amount is treated as the original borrow amount and interest is added to it. This amount is then compared to the collateral which is incorrect. 

```solidity 
    function liquidateBorrowAllowedInternal(
        address payable lTokenBorrowed,
        address borrower,
        uint256 repayAmount,
        uint256 collateral,
        uint256 borrowed
    ) internal view returns (uint256) {
        LendStorage.BorrowMarketState memory borrowBalance = lendStorage.getBorrowBalance(borrower, lTokenBorrowed); 
        if (LendtrollerInterfaceV2(lendtroller).isDeprecated(LToken(lTokenBorrowed))) {
            require(borrowBalance.amount >= repayAmount, "Repay > total borrow");
        } else {
            uint256 borrowedAmount;

@>          borrowedAmount = 
                (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex;
            require(borrowedAmount > collateral, "Insufficient shortfall"); 

            uint256 maxClose = mul_ScalarTruncate(
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
            );
            require(repayAmount <= maxClose, "Too much repay");
        }
        return 0;
    }
```


### Internal Pre-conditions

none

### External Pre-conditions

A user must borrow and interest must accrue.

### Attack Path

1. Alice borrow 90e18 of a token with 100e18 of collateral. 
2. Interest accrue for a year at an interest rate of 10% APY. 
3. Alice owe the protocol 99e18 tokens (90e18 + 9e18).
4. A malicious user try to liquidate alice since liquidators are incentivized to liquidate whenever possible. 
5. 99e18 is increased again by 10%, resulting in 108.9e18 borrowed amount. 
6. The liquidation goes through even though alice only owe 99.9e18 with a collateral of 100e18.


### Impact

Users with enough collateral will be liquidated if interest hasnt been accrued for a long time. 

### PoC

none

### Mitigation

Dont calculate the interest again. The `borrowed` amount is up-to-date and correct, compare with that.

```diff
    function liquidateBorrowAllowedInternal(
        address payable lTokenBorrowed,
        address borrower,
        uint256 repayAmount,
        uint256 collateral,
        uint256 borrowed
    ) internal view returns (uint256) {
        LendStorage.BorrowMarketState memory borrowBalance = lendStorage.getBorrowBalance(borrower, lTokenBorrowed); 
        if (LendtrollerInterfaceV2(lendtroller).isDeprecated(LToken(lTokenBorrowed))) {
            require(borrowBalance.amount >= repayAmount, "Repay > total borrow");
        } else {
-           uint256 borrowedAmount;

-           borrowedAmount = 
-               (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex;
-           require(borrowedAmount > collateral, "Insufficient shortfall"); 
+           require(borrowed > collateral, "Insufficient shortfall");

            uint256 maxClose = mul_ScalarTruncate(
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
            );
            require(repayAmount <= maxClose, "Too much repay");
        }
        return 0;
    }
```