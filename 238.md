Fun Shadow Parakeet

Medium

# Cross-chain borrowed assets will be lost for the users using the account abstraction wallet

### Summary

Account abstraction (AA) wallets generate different addresses on different chains. `borrowCrossChain` packs `msg.sender` into the LayerZero payload as `payload.sender`. On the destination chain, the loan is sent to an address that cannot be controlled by the original sender.
As a result, the user carries the debt on chain A but never receives the borrowed asset on chain B, since the funds are sent to an unreachable address.

### Root Cause

`borrowCrossChain` uses [`msg.sender`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L148) (chain A address) as the borrower field in the `payload`. The destination chain blindly trusts this field and mints the debt to that address. For an account abstraction wallet, the address on chain A is not the same as the address on chain B.

### Internal Pre-conditions

None.

### External Pre-conditions

The user is using an account abstraction wallet.

### Attack Path

Any account abstraction wallet user can unintentionally trigger this issue:
1. Alice supplies `1000` USDC‑A on chain A.
2. Bob (AA wallet) calls `borrowCrossChain` on chain A to borrow `500` USDC‑B for chain B.
3. Bob never receives the `500` USDC‑B but still owes the debt on chain A.

### Impact

The borrowed asset is sent to an unreachable address on chain B, resulting in a loss of user funds.


### PoC
Account abstraction wallets have seen rapid adoption, with over 6.4 million users and more than $100 billion in assets.
With such a large user base, the likelihood of AA users interacting with cross-chain borrowing via borrowCrossChain is high, making the associated risks significant and worth addressing.

The following scenario and steps illustrate this issue:

1. Bob Supply `1000` USDC-A on Chain A
2. Bob calls `borrowCrossChain` to borrow `500` USDC-B.
3. `borrowCrossChain` uses `msg.sender` (Bob’s address on Chain A) as `payload.sender`, which is included in the LayerZero message.

```solidity
    _send(
        _destEid,
        _amount,
        0, // Initial borrowIndex, will be set on dest chain
        collateral,
@>      msg.sender,
        destLToken,
        address(0), // liquidator
        _borrowToken,
        ContractType.BorrowCrossChain
    );
```

4. On Chain B, `_handleBorrowCrossChainRequest` is triggered. The contract uses `payload.sender` (Bob’s Chain A address) as the recipient of the borrowed tokens and LEND rewards, updating internal records accordingly:

```solidity
@>      CoreRouter(coreRouter).borrowForCrossChain(payload.sender, payload.amount, payload.destlToken, destUnderlying);

        /**
         * @dev If existing cross-chain collateral, update it. Otherwise, add new collateral.
         */
        if (found) {
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            lendStorage.updateCrossChainCollateral(
@>              payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
        } else {
            lendStorage.addCrossChainCollateral(
@>              payload.sender,
                destUnderlying,
                LendStorage.Borrow({
                    srcEid: srcEid,
                    destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
        }

        // Track borrowed asset
@>      lendStorage.addUserBorrowedAsset(payload.sender, payload.destlToken);

        // Distribute LEND rewards on destination chain
@>      lendStorage.distributeBorrowerLend(payload.destlToken, payload.sender);

        // Send confirmation back to source chain with updated borrow details
        _send(
            srcEid,
            payload.amount,
            currentBorrowIndex,
            payload.collateral,
@>          payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.ValidBorrowRequest
        );
```

Here, Bob’s Chain A address is not valid on Chain B (typical with AA wallets). The `500` USDC-B are transferred to an address that Bob cannot access.

Finally, the message is sent back to Chain A via `_send`, including `payload.sender` in the payload.


5. Meanwhile, on Chain A, `_handleValidBorrowRequest` is called and updates the borrow record, marking Bob as owing `500` USDC-A worth of debt:

```solidity
        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
            userBorrows[index].principle = userBorrows[index].principle + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
@>          lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
            // Add new borrow record
            lendStorage.addCrossChainBorrow(
@>              payload.sender,
                payload.srcToken,
                LendStorage.Borrow(
                    srcEid, currentEid, payload.amount, payload.borrowIndex, payload.destlToken, payload.srcToken
                )
            );
        }

@>      lendStorage.addUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));
```

As a result, Bob does not receive the borrowed `500` USDC-B on Chain B, yet a debt of `500` USDC-A is still recorded under his address on Chain A.

> **Note**
>
> This issue is similar to and inspired by the valid issues listed below:
> - https://solodit.cyfrin.io/issues/m-03-all-reallocate-cross-chain-token-and-rewards-will-be-lost-for-the-users-using-the-account-abstraction-wallet-code4rena-nudgexyz-nudgexyz-git
> - https://solodit.cyfrin.io/issues/m-02-all-bridged-funds-will-be-lost-for-the-users-using-the-account-abstraction-wallet-code4rena-ondo-finance-ondo-finance-git

### Mitigation

Consider passing an explicit recipient address on the destination chain as a parameter, instead of relying on `msg.sender`.