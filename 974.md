Scrawny Red Sloth

Medium

# Failure in `redeem()` and `repayBorrow()` for token blacklisted users

## Summary
User who get blacklisted after supplying collateral or taking borrows experience redeem and repayment failures.


## Root cause
During [redeem](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L124), the tokens i.e `expectedUnderlying` is sent directly to `msg.sender`:
```solidity
        // Calculate expected underlying tokens
        uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;

        // Transfer underlying tokens to the user
>>      IERC20(_token).transfer(msg.sender, expectedUnderlying);
```
[`repayBorrow()`]() also enforces that the borrower must make repayment directly from their account and does not allow another account to do this on their behalf:
```solidity
    function repayBorrow(uint256 _amount, address _lToken) public {
        //@audit msg.sender is both borrower and repayer
>>      repayBorrowInternal(msg.sender, msg.sender, _amount, _lToken, true);
    }
```


Now, the protocol intends to incoorporate the following tokens:
> Whitelisted only (e.g BTC, ETH, USDC, DAI, USDT). ERC20 standard

As seen, `USDC/ USDT` are among the listed tokens. However, these tokens have `blacklisting` functionality which prevents addresses from sending. receiving tokens.

A scenario therefore will exist where a user supplies collateral as `USDC` setting them an investment in the protocol. Then when they eventually attempt to redeem these tokens back, if for whatever reason they happen to be blacklisted by this time, their redemption attempt will fail.


## Internal Pre-conditions
1. Alice have USDC supplied as collateral
2. Bob has an active borrow

## External Pre-conditions
1. Alice and Bob gets balcklisted
2. Alice attempts to redeem her funds
3. Bob attempts to repay his borrow

## Attack path
1.User supplies USDC as collateral or takes a borrow (USDC)
2. User gets blacklisted after
3. User calls `redeem()` to redeem their supplied tokens OR `repayBorrow()` to pay their debt
4. Redeem and repay fails

## Impact
1. Redeem
Since the redeem functionality does not allow for provision of a `to` address, the attempt to diretly transfer these tokens to the caller will fail for token blacklisted users. As such, these user funds remain stuck in protocol.

Even if these users choose to borrow other tokens based on their stuck collateral, there is a `maxBorrow` threshold and therefore, they wont receive an equivalent value for their deposited funds which still translates to loss.

2. Repay
Users remain unable to repay their debts. As such, if prices flactuate such that they become insolvent, they stand to loose their collateral.

## Poc
1. Modify `ERC20Mock.sol` as follows:
```diff
    contract ERC20Mock is ERC20 {
+       mapping(address => bool) public isBlacklisted;

        constructor() ERC20("ERC20Mock", "E20M") {}

        function mint(address account, uint256 amount) external {
            _mint(account, amount);
        }

        function burn(address account, uint256 amount) external {
            _burn(account, amount);
        }

        // Add these functions
+       function setBlacklist(address user, bool status) external {
+           isBlacklisted[user] = status;
+       }

+       function _beforeTokenTransfer(
+           address from,
+           address to,
+           uint256 amount
+       ) internal override {
+           require(!isBlacklisted[from], "USDC: sender blacklisted");
+           require(!isBlacklisted[to], "USDC: recipient blacklisted");
+           super._beforeTokenTransfer(from, to, amount);
+       }
    }
```
2. Add the following test in `test/TestRedeeming.t.sol`:
```solidity
    function test_redeem_fails_for_token_balcklisted_users(uint256 amount) public {
        vm.assume(amount > 1e18 && amount < 1e36);
        vm.startPrank(deployer);

        // First supply tokens
        (address token, address lToken) = _supply(amount);

        // Get initial balances
        uint256 initialUnderlyingBalance = IERC20(token).balanceOf(deployer);
        uint256 initialLTokenBalance = lendStorage.totalInvestment(deployer, lToken);

        // deployer gets blacklisted by USDC
        ERC20Mock(token).setBlacklist(deployer, true);

        // should have been blacklisted
        assertEq(ERC20Mock(token).isBlacklisted(deployer), true);

        // Expect revert due to blacklisting
        vm.expectRevert(bytes("USDC: recipient blacklisted"));

        coreRouter.redeem(initialLTokenBalance, payable(lToken));

        uint256 finalUnderlyingBalance = IERC20(token).balanceOf(deployer);
        uint256 finalLTokenBalance = lendStorage.totalInvestment(deployer, lToken);

        // assert no change
        assertEq(finalUnderlyingBalance, initialUnderlyingBalance);
        assertEq(finalLTokenBalance, initialLTokenBalance);

        vm.stopPrank();
    }
```
3. Add the following test in `test/TestBorrowingCrossChain.t.sol`:
```solidity
    function test_repayBorrow_failure_for_token_blacklisted_users(uint256 amountToSupply, uint256 amountToBorrow) public {
        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
        amountToSupply = bound(amountToSupply, 1e18, 1e30);

        // Fund Router A with ETH for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        // First supply tokens as collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(amountToSupply);

        // Calculate maximum allowed borrow (70% of collateral to leave some safety margin)
        uint256 maxBorrow = (amountToSupply * 70) / 100;

        // Get initial balances
        uint256 initialTokenBalance = IERC20(tokenA).balanceOf(deployer);

        vm.startPrank(deployer);

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, lTokenA, maxBorrow);

        // Borrow tokens
        coreRouterA.borrow(maxBorrow, tokenA);

        // Verify balances after borrowing
        assertEq(
            IERC20(tokenA).balanceOf(deployer) - initialTokenBalance,
            maxBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        // Verify borrow balance is tracked correctly
        assertEq(
            lendStorageA.borrowWithInterestSame(deployer, lTokenA),
            maxBorrow,
            "Borrow balance should be tracked correctly"
        );

        // Check any crosschain borrow details
        LendStorage.Borrow[] memory userBorrows = lendStorageA.getCrossChainBorrows(deployer, tokenA);
        assertEq(userBorrows.length, 0, "User should have 0 cross chain borrows");

        // check borrows in this chain
        LendStorage.BorrowMarketState memory borrowBalance = lendStorageA.getBorrowBalance(deployer, lTokenA);
        assertEq(borrowBalance.amount, maxBorrow, "User borrow balance should match");

        // From here, user can only repay their debt themselves in the chain they took the borrow

        // mint user more tokens
        uint256 amount = maxBorrow;
        ERC20Mock(tokenA).mint(deployer, amount);

        // deployer gets blacklisted by USDC
        ERC20Mock(tokenA).setBlacklist(deployer, true);

        // should have been blacklisted
        assertEq(ERC20Mock(tokenA).isBlacklisted(deployer), true);


        IERC20(tokenA).approve(address(coreRouterA), amount);

        // Expect revert due to blacklisting
        vm.expectRevert(bytes("USDC: sender blacklisted"));
        coreRouterA.repayBorrow(maxBorrow, lTokenA);

        // borrow should remain unchanged
        LendStorage.BorrowMarketState memory currentBorrowBalance = lendStorageA.getBorrowBalance(deployer, lTokenA);
        assertEq(currentBorrowBalance.amount, maxBorrow, "User debt should remain unchanged");

        vm.stopPrank();
    }
```

## Mitigation
1. Add a `to` parameter in `redeem()` function:
```diff
-   function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
+   function redeem(uint256 _amount, address payable _lToken, address to) external returns (uint256) {
        ---snip---
        // Transfer underlying tokens to the user
-       IERC20(_token).transfer(msg.sender, expectedUnderlying);
+       IERC20(_token).transfer(to, expectedUnderlying);
    }
```
2. Modify `repayBorrow()` as follows:
```diff
-   function repayBorrow(uint256 _amount, address _lToken) public {
+   function repayBorrow(address _borrower, uint256 _amount, address _lToken) public {
-       repayBorrowInternal(msg.sender, msg.sender, _amount, _lToken, true);
+       repayBorrowInternal(_borrower, msg.sender, _amount, _lToken, true);
    }
```