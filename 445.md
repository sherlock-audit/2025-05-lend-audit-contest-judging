Sweet Sandstone Squid

Medium

# Use of wrong exchange rate will cause loss of value for users of the protocol

### Summary

In the `CoreRouter` contract's token supply and redemption functions, the protocol uses `exchangeRateStored` to calculate token amounts. The issue is that this exchange rate does not account for accrued interest, while the underlying lToken `mint` and `redeem` operations use the updated exchange rate. This discrepancy causes loss of user funds as the difference in value between the two exchange rates accumulate in the lToken contracts over time.

### Root Cause

The `CoreRouter` calculates how many lTokens to credit to the user based on an outdated exchange rate, while the actual `lToken` contract uses a freshly updated exchange rate (which is higher due to accrued interest). As a result, the `CoreRouter` credits fewer lTokens to the user in `supply` than were actually minted, and fewer underlying tokens than they should get in `redeem`
The issue is seen in In [CoreRouter::supply](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L61) and in [CoreRouter::redeem](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L100)

### Internal Pre-conditions

NIL

### External Pre-conditions

1. The `exchangeRateStored` needs to lag behind the `exchangeRateCurrent`

### Attack Path

NIL

### Impact

Users loss a portion of their supplied and redeemed funds, depended on how large the difference is between the two exchange rated. The loss compounds over time, resulting in a portion of user funds being unredeemable from the lToken contract

### PoC

Add the below test function in `TestSupplying.t.sol`
```solidity
function testValueLossDueToExchangeRateDiscrepancy() public {
        uint256 userSupplyAmount = 1000e18;
        uint256 deployerSupplyAmount = 100e18;
        address user = makeAddr("user");
        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        //user supplies tokens and borrows from the router
        vm.startPrank(user);
        ERC20Mock(token).mint(user, userSupplyAmount);
        IERC20(token).approve(address(coreRouter), userSupplyAmount);

        coreRouter.supply(userSupplyAmount, token);

        uint256 borrowAmount = userSupplyAmount * 40/100;
        coreRouter.borrow(borrowAmount, token);
        vm.stopPrank();

        //simulate blocks being added and interest accruing
        vm.roll(block.number + 600000);

        uint256 exchangeRateStored = LTokenInterface(lToken).exchangeRateStored();
        uint256 exchangeRateCurrent = LTokenInterface(lToken).exchangeRateCurrent();
        assertGt(exchangeRateCurrent, exchangeRateStored);

        console2.log("Exchange rate stored: ", exchangeRateStored);
        console2.log("Exchange rate current: ", exchangeRateCurrent);

        //deployer supplies tokens but gets lesser than expected supply tokens
        vm.startPrank(deployer);
        ERC20Mock(token).mint(deployer, deployerSupplyAmount);
        IERC20(token).approve(address(coreRouter), deployerSupplyAmount);
        assertEq(IERC20(token).balanceOf(deployer), deployerSupplyAmount);

        coreRouter.supply(deployerSupplyAmount, token);
        assertEq(IERC20(token).balanceOf(deployer), 0);
        vm.stopPrank();

        uint256 expectedDeployerSupplyTokens = (deployerSupplyAmount * 1e18) / exchangeRateCurrent;
        uint256 actualDeployerSupplyTokens = lendStorage.totalInvestment(deployer, lToken);

        //deployer immediately redeems tokens but gets lesser than they supplied
        vm.startPrank(deployer);
        uint256 amountToRedeem = lendStorage.totalInvestment(deployer, lToken);
        coreRouter.redeem(amountToRedeem, payable(lToken));
        assertLt(IERC20(token).balanceOf(deployer), deployerSupplyAmount);
        vm.stopPrank();
    }
```

### Mitigation

Use the current exchange rate instead i.e `exchangeRateCurrent`