Ancient Amethyst Pig

High

# Malicious borrower will bypass cross-chain collateral accounting, impacting protocol liquidity and risk management

### Summary

The `borrowWithInterest` function’s collateral branch uses the condition:
```solidity
if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) { … }
```

Because for real cross-chain entries `srcEid != destEid`, this check never passes and **no collateral records are ever counted**. As a result, the protocol under-reports users’ outstanding cross-chain debt, miscalculates account liquidity, and may allow unsafe over-borrowing or prevent proper liquidation.

### Root Cause

```solidity
    function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;


        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];


        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
        // Only one mapping should be populated:
        if (borrows.length > 0) {
            for (uint256 i = 0; i < borrows.length; i++) {
                if (borrows[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
                }
            }
        } else {
            for (uint256 i = 0; i < collaterals.length; i++) {
                // Only include a cross-chain collateral borrow if it originated locally.
                @> if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
                }
            }
        }
        return borrowedAmount;
    }
```

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L497
In `LendStorage.sol:497`, a mistaken extra check on `srcEid` in the collateral loop: the code requires both `destEid == currentEid` and `srcEid == currentEid`. In cross-chain flows, collateral records on the destination chain carry `srcEid = remoteChainId`, so they never satisfy `srcEid == currentEid`.


### Internal Pre-conditions

1. `crossChainBorrows[borrower][token]` is empty.
2. `crossChainCollaterals[borrower][token]` contains at least one entry.

### External Pre-conditions

N/A

### Attack Path

1. Borrow on Chain A: User borrows funds, populating `crossChainBorrows` there.
2. Protocol miscalculates the user’s `borrowedAmount` as zero on Chain B.
3. Liquidity checks (`getHypotheticalAccountLiquidityCollateral`) treat the user as having no debt, allowing them to Over-borrow on Chain B.

### Impact

- **Under-reporting debt**: `borrowWithInterest` returns `0` instead of the real amount.
- **Liquidity miscalculation**: `sumBorrowPlusEffects` omits cross-chain debt, inflating the user’s borrowing capacity and collateral buffer.
- **Unsafe borrows/withdrawals**: Users can borrow far beyond safe limits.

### PoC

_No response_

### Mitigation

Replace the collateral-branch check with:
```diff
for (uint256 i = 0; i < collaterals.length; i++) {
    // Only include a cross-chain collateral borrow if it originated locally.
-  if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
+  if (collaterals[i].destEid == currentEid) {
      borrowedAmount += (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
    }
}
```