Big Sable Whale

High

# malicious liquidator can get collateral tokens of borrower in chainA even if he doesn't pay repay tokens in chainB

### Summary

when a borrower cross borrows tokens of chainB giving collateral in chain A.Then a liquidator can call liquidateCrossChain function in chainB.which will do sufficient checks to send a msg to chain A via layerZero,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L264-L285
```solidity
    function _executeLiquidationCore(LendStorage.LiquidationParams memory params) private {
        // Calculate seize tokens
        address borrowedlToken = lendStorage.underlyingTolToken(params.borrowedAsset);


        (uint256 amountSeizeError, uint256 seizeTokens) = LendtrollerInterfaceV2(lendtroller)
            .liquidateCalculateSeizeTokens(borrowedlToken, params.lTokenToSeize, params.repayAmount);


        require(amountSeizeError == 0, "Seize calculation failed");


        // Send message to Chain A to execute the seize
        _send(
            params.srcEid,
            seizeTokens,
            params.storedBorrowIndex,
            0,
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid), // Convert to Chain A version before sending
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
    }
```
and layerZero calls _lzreceive function of CrossChainRoute in  chainA, 
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L772-L776
```solidity
        } else if (cType == ContractType.CrossChainLiquidationExecute) {
            if (_checkLiquidationValid(payload)) {
                _handleLiquidationExecute(payload, srcEid);
            } else {
                _sendLiquidationFailure(payload, srcEid);
```
this will check if this liquidation call is valid, using _checkLiquidationValid function.And if it is valid then _handleLiquidationExecute function will execute,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L312-L366
```solidity
    function _handleLiquidationExecute(LZPayload memory payload, uint32 srcEid) private {
        // Execute the seize of collateral
        uint256 protocolSeizeShare = mul_(payload.amount, Exp({mantissa: lendStorage.PROTOCOL_SEIZE_SHARE_MANTISSA()}));


        require(protocolSeizeShare < payload.amount, "Invalid protocol share");


        uint256 liquidatorShare = payload.amount - protocolSeizeShare;


        // Update protocol rewards
        lendStorage.updateProtocolReward(
            payload.destlToken, lendStorage.protocolReward(payload.destlToken) + protocolSeizeShare
        );


        // Distribute LEND rewards
        lendStorage.distributeSupplierLend(payload.destlToken, payload.sender); // borrower
        lendStorage.distributeSupplierLend(payload.destlToken, payload.liquidator); // liquidator


        // Update total investment for borrower
        lendStorage.updateTotalInvestment(
            payload.sender,
            payload.destlToken,
            lendStorage.totalInvestment(payload.sender, payload.destlToken) - payload.amount
        );


        // Update total investment for liquidator
        lendStorage.updateTotalInvestment(
            payload.liquidator,
            payload.destlToken,
            lendStorage.totalInvestment(payload.liquidator, payload.destlToken) + liquidatorShare
        );


        // Clear user supplied asset if total investment is 0
        if (lendStorage.totalInvestment(payload.sender, payload.destlToken) == 0) {
            lendStorage.removeUserSuppliedAsset(payload.sender, payload.destlToken);
        }


        emit LiquidateBorrow(
            payload.liquidator, // liquidator
            payload.srcToken, // borrowed token
            payload.sender, // borrower
            payload.destlToken // collateral token
        );


        _send(
            srcEid,
            payload.amount,
            0,
            0,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.LiquidationSuccess
        );
    }
```
this function makes some state changes like updateTotalInvestment of borrower and liquidator, changing protocolRewards etc and sends a msg to chainB, which will trigger below lines in CrossChainRouter contract in chain B,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L778-L779
```solidity
        } else if (cType == ContractType.LiquidationSuccess) {
            _handleLiquidationSuccess(payload);
```
and _handleLiquidationSuccess function calls repayCrossChainBorrowInternal function internally,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L464-L470
```solidity
        repayCrossChainBorrowInternal(
            payload.sender, // The borrower
            payload.liquidator, // The liquidator (repayer)
            payload.amount, // Amount to repay
            payload.destlToken, // lToken representing the borrowed asset on this chain
            srcEid // The chain where the collateral (and borrow reference) is tracked
        );
```
and it will call _handleRepayment function internally,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L387
```solidity
        _handleRepayment(borrower, repayer, _lToken, repayAmountFinal);
```
and let's see it's call flow,
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L423-L426
```solidity
    function _handleRepayment(address _borrower, address repayer, address _lToken, uint256 repayAmountFinal) private {
        // Execute the repayment
        CoreRouter(coreRouter).repayCrossChainLiquidation(_borrower, repayer, repayAmountFinal, _lToken);
    }
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L216-L220
```solidity
    function repayCrossChainLiquidation(address _borrower, address _liquidator, uint256 _amount, address _lToken)
        external
    {
        require(msg.sender == crossChainRouter, "Access Denied");
        repayBorrowInternal(_borrower, _liquidator, _amount, _lToken, false);
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L459-L483
```solidity
    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);


        LTokenInterface(_lToken).accrueInterest();


        uint256 borrowedAmount;


        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }


        require(borrowedAmount > 0, "Borrowed amount is 0");


        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;


        // Transfer tokens from the liquidator to the contract
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
```
here if liquidator doesn't approve repay tokens to this contract or if he doesn't have enough tokens then this check will fail 
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L483
```solidity
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
```
so this call will revert.
so if this call reverts then liquidator was not needed to pay repay tokens in chainB but he will take collateral seize tokens of borrower in chainA. And above mentioned state changes were happened  in chianA even if liquidation call is failed.

### Root Cause

The cross-chain liquidation process is not atomic across chains. When a liquidation is initiated, the protocol first seizes the borrower's collateral on Chain A and transfers it to the liquidator. However, if the subsequent repayment transaction on Chain B fails or reverts  due to insufficient funds or a revert in the repayment logic), the liquidator has already received the seized collateral on Chain A without actually repaying the debt on Chain B. This lack of atomicity between the two chains allows the liquidator to potentially receive collateral without fulfilling their repayment obligation, leading to protocol loss and unfair liquidation outcomes and change of state related to this liquidation in chain A will out completion of liquidation process. This may also happens when repayment failed due to some other reason even if liquidator is not malicious

### Internal Pre-conditions

none 

### External Pre-conditions

none 

### Attack Path

malicious liquidator calls  liquidateCrossChain function in chainB and recieves seized collateral tokens in chainA  but didn't pay repay tokens in chainB after that.

### Impact

loss of borrower's collateral funds and improper state updates regarding totalInvestment etc.

### PoC

_No response_

### Mitigation

When a liquidation is initiated, move the collateral to an escrow contract or mark it as "pending" on Chain A.
Only release the collateral to the liquidator after successful repayment is confirmed on Chain B.