Boxy Brunette Goblin

High

# Incorrect LEND tokens distribution will not let some users to receive the LEND tokens they should get.

### Summary

The missing reset of accrued LEND token amount at `CoreRouter.claimLend()` cause incorrect LEND token distribution.
This will let some users get more LEND tokens they should, and let some users get less than they should.
In some cases, some LEND tokens are locked in `CoreRouter`.

### Root Cause

In `CoreRouter->claimLend()`, the contract tries to send the accrued LEND tokens to the users.
And in `grantLendInternal()`, it sends the LEND tokens to the user via `IERC20->safeTransfer()`.
Here it's missing the reset of the accrued amount after send.

The LEND tokens the user earned are going to be added to the accrued amount on and on.
On `claimLend()`, it should withdraw only the amount the user has earned from the last claim.
But it withdraws the accrued amount which is stacked over the entire period and it's much bigger than the user should get.

```solidity
CoreRouter.sol->claimLend()
for (uint256 j = 0; j < holders.length;) {
    uint256 accrued = lendStorage.lendAccrued(holders[j]);
    if (accrued > 0) {
        grantLendInternal(holders[j], accrued);
        // @audit: missing reset of the accrued amount
    }
    unchecked {
        ++j;
    }
}
```

```solidity
function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
    address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
    uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

    if (amount > 0 && amount <= lendBalance) {
        // @audit: send accrued amount to the user, the amount should be the LEND tokens the user earned from last claim
        IERC20(lendAddress).safeTransfer(user, amount); 
        return 0;
    }
    return amount;
}
```

#LoC
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L399-L407


### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. user1 supply 500 tokens.
2. user2 supply 1000 tokens.
3. After some time (e.g. 1 day), admin calls `claimLend([user1, user2], lToken ...)`;
4. Let's assume user1 got 10 LEND tokens. 
    user2 will get 20 LEND token as he's total investment is 2 times than user1.
5. Again, after some time (e.g. 1 day), admin calls `claimLend([user1, user2], lToken ...)`;
6. user1 will get 20 LEND tokens. 
    It's because the accrued amount of User1 is 20.
    user2 will get nothing because there are only 10 LEND tokens left in CoreRouter, and it's not enough to be sent to user2.
    The accrued amount of User2 is 40.

So,
user1 got 30 LEND tokens. ( supposed to get 20 )
user2 got 20 LEND tokens. ( supposed to get 40 )
10 LEND tokens are locked in CoreRouter.

### Impact

Some users (the first users) get more LEND tokens than they should because they receive the accrued amount over the entire period.
Some users (the other users) get less LEND tokens than they should because the first users took all. There is not enough balance in CoreRouter.
Sometimes some LEND tokens are locked in CoreRouter.

### PoC

Please add the following test function to TestLendAccrual.t.sol.
It proves that the LEND balance of the users and the amount accrued are different.

```solidity
function test_multiple_users_accrue_over_time() public {
    // Supply different amounts with two users
    uint256 user1Supply = 500e18;
    uint256 user2Supply = 1000e18;
    (, address lToken) = _supply(user1Supply, user1);
    _supply(user2Supply, user2);

    // Advance time
    vm.warp(block.timestamp + 1 days);
    vm.roll(block.number + 1000);

    // Claim for both users
    address[] memory holders = new address[](2);
    holders[0] = user1;
    holders[1] = user2;
    LToken[] memory lTokenArray = new LToken[](1);
    lTokenArray[0] = LToken(lToken);

    vm.prank(user1);
    coreRouter.claimLend(holders, lTokenArray, false, true);

    
    // Get accrued amounts
    uint256 user1Accrued = lendStorage.lendAccrued(user1);
    uint256 user2Accrued = lendStorage.lendAccrued(user2);
    
    // Verify user1 accrued proportionally more (roughly 2x)
    assertApproxEqRel(
        user1Accrued * 2,
        user2Accrued,
        0.01e18, // 1% tolerance
        "Users should accrue proportionally to their supply"
    );

    // Advance time again
    vm.warp(block.timestamp + 1 days);
    vm.roll(block.number + 1000);
    
    coreRouter.claimLend(holders, lTokenArray, false, true);

    // For now user1Accrued is the total amount of LEND that user1 has accrued
    user1Accrued = lendStorage.lendAccrued(user1);
    user2Accrued = lendStorage.lendAccrued(user2);

    console2.log("user1Accrued", user1Accrued);
    console2.log("user2Accrued", user2Accrued);

    address lendToken = lendtroller.getLendAddress();
    uint256 user1Balance = ERC20Mock(lendToken).balanceOf(user1);
    uint256 user2Balance = ERC20Mock(lendToken).balanceOf(user2);

    assertEq(user1Accrued, user1Balance, "User1 should have the same accrued amount");
    assertEq(user2Accrued, user2Balance, "User2 should have the same accrued amount");
}
```

### Mitigation

```solidity
for (uint256 j = 0; j < holders.length;) {
    uint256 accrued = lendStorage.lendAccrued(holders[j]);
    if (accrued > 0) {
        grantLendInternal(holders[j], accrued);
        lendStorage.updateLendAccrued(holders[j], 0);
    }
    unchecked {
        ++j;
    }
}
```