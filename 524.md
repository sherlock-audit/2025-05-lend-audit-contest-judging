Ancient Corduroy Aphid

High

# Missing accrual on destination chain will underreport borrow value for protocol as core functions skip proper debt collection

### Summary

The failure to accrue interest on cross-chain borrowed assets on the destination chain will cause underreporting of total debt for the protocol, as core accounting functions like _handleBorrowCrossChainRequest and _checkLiquidationValid will miss interest accrued on crossChainCollaterals, leading to incorrect liquidity, borrow limits, and liquidation behavior.

### Root Cause

In [LendStorage.sol:borrowWithInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L478-L504), the following condition is used to collect cross-chain borrow positions stored in crossChainCollaterals:
```solidity
if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid)
```
However, crossChainCollaterals is only ever populated on the destination chain, and these records always have srcEid != currentEid. As a result, this condition always fails, and no debt is collected from that mapping.

### Internal Pre-conditions

1. A user initiates a cross-chain borrow from source chain A to destination chain B.

2. borrowWithInterest() is called on chain B (destination), where only crossChainCollaterals is populated.

3. The faulty condition prevents reading any borrow positions.



### External Pre-conditions

1. The borrowed asset appreciates in value or accrues interest via accrueInterest() on destination chain.

2. The oracle price for the borrowed asset remains stable or increases.

### Attack Path

1. User initiates a cross-chain borrow from Chain A to Chain B.

2. _handleBorrowCrossChainRequest() or _checkLiquidationValid() is called on Chain B.

3. borrowWithInterest() fails to recognize any outstanding debt from crossChainCollaterals due to incorrect condition.

4. User appears to have more available liquidity than they truly do, bypassing limits or avoiding liquidation.

### Impact

- The protocol underestimates the user's borrow balance, allowing:

     - Over-borrowing beyond collateral limits (breaking assumptions of LTV caps)

     - Avoidance of liquidation despite being underwater

     - Accounting discrepancies in TVL/debt metrics

- Protocol may suffer under-collateralized losses.

### PoC

To run the TestBorrowingCrossChain test file, we first need to modify the setUp function:
```solidity
function setUp() public override(LayerZeroTest) {
        super.setUp();

        deployer = makeAddr("deployer");
        vm.deal(deployer, 1000 ether);

        endpointA = new EndpointV2(CHAIN_A_ID, address(this)); //set eid in endpointA
        endpointB = new EndpointV2(CHAIN_B_ID, address(this)); //set eid in endpointB

        // Deploy protocol on Chain A using the endpoint we just created
        vm.chainId(CHAIN_A_ID);     //set currentID in routerA
        Deploy deployA = new Deploy();
        (
            address priceOracleAddressA,
            address lendtrollerAddressA,
            address interestRateModelAddressA,
            address[] memory lTokenAddressesA,
            address payable routerAddressA,
            address payable coreRouterAddressA,
            address lendStorageAddressA,
            , //address _layerZeroEndpoint
            address[] memory _supportedTokensA
        ) = deployA.run(address(endpointA)); // Pass the endpoint address to Deploy.run

        // Store Chain A values
        routerA = CrossChainRouterMock(payable(routerAddressA));
        lendStorageA = LendStorage(lendStorageAddressA);
        coreRouterA = CoreRouter(coreRouterAddressA);
        lendtrollerA = Lendtroller(lendtrollerAddressA);
        interestRateModelA = InterestRateModel(interestRateModelAddressA);
        priceOracleA = SimplePriceOracle(priceOracleAddressA);
        lTokensA = lTokenAddressesA;
        supportedTokensA = _supportedTokensA;

        // Deploy protocol on Chain B
        vm.chainId(CHAIN_B_ID); //set currentID in routerB
        Deploy deployB = new Deploy();
        (
            address priceOracleAddressB,
            address lendtrollerAddressB,
            address interestRateModelAddressB,
            address[] memory lTokenAddressesB,
            address payable routerAddressB,
            address payable coreRouterAddressB,
            address lendStorageAddressB,
            , // address _layerZeroEndpoint
            address[] memory _supportedTokensB
        ) = deployB.run(address(endpointB));

        // Store Chain B values
        routerB = CrossChainRouterMock(payable(routerAddressB));
        lendStorageB = LendStorage(lendStorageAddressB);
        coreRouterB = CoreRouter(coreRouterAddressB);
        lendtrollerB = Lendtroller(lendtrollerAddressB);
        interestRateModelB = InterestRateModel(interestRateModelAddressB);
        priceOracleB = SimplePriceOracle(priceOracleAddressB);
        lTokensB = lTokenAddressesB;
        supportedTokensB = _supportedTokensB;

        // Now set up cross-chain mappings as the owner
        vm.startPrank(routerA.owner());
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            lendStorageA.addUnderlyingToDestUnderlying(supportedTokensA[i], supportedTokensB[i], CHAIN_B_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageA.addUnderlyingToDestlToken(supportedTokensA[i], lTokensB[i], CHAIN_B_ID);
        }
        vm.stopPrank();

        vm.startPrank(routerB.owner());
        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            lendStorageB.addUnderlyingToDestUnderlying(supportedTokensB[i], supportedTokensA[i], CHAIN_A_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageB.addUnderlyingToDestlToken(supportedTokensB[i], lTokensA[i], CHAIN_A_ID);
        }
        vm.stopPrank();

        // Set up initial prices for supported tokens on both chains

        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            priceOracleA.setDirectPrice(supportedTokensA[i], 1e18);
        }

        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            priceOracleB.setDirectPrice(supportedTokensB[i], 1e18);
        }

        vm.label(address(routerA), "Router A");
        vm.label(address(routerB), "Router B");
        // After deploying routers, set up their pair contracts
        routerA = CrossChainRouterMock(payable(routerAddressA));
        routerB = CrossChainRouterMock(payable(routerAddressB));

        // Set up pair relationships
        routerA.setPairContract(payable(address(routerB)));
        routerB.setPairContract(payable(address(routerA)));
    }
```
Then, modify the getOrCreateAnvilConfig function in the HelperConfig file located in the script folder as follows:
```diff
--     uint32 eid = 31337;
++  uint32 eid = uint32(block.chainid);
```

After that, add this test function to TestBorrowingCrossChain.t.sol file:
```solidity
    function testRegression_BorrowWithInterest_MissingDebtFromCrossChainCollaterals() public {

        address user1 = makeAddr("user1");

        // Fund Router A with ETH for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        // First supply tokens as collateral on Chain A
        (address tokenA,address lTokenA) = _supplyA(1e20);

        // Then supply tokens as borrowable on Chain B
        // @note - Has to be enough tokens to cover the borrow on the destination chain...
        _supplyB(2e20);

        // Verify token mappings
        address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);
        address destLToken = lendStorageA.underlyingToDestlToken(tokenA, CHAIN_B_ID);

        require(destToken != address(0), "Token mapping not set up correctly");
        require(destLToken != address(0), "LToken mapping not set up correctly");

        vm.startPrank(deployer);

        // Call borrowCrossChain with token address
        vm.chainId(CHAIN_A_ID);
        routerA.borrowCrossChain(6e18, tokenA, CHAIN_B_ID);

        vm.stopPrank();

        (uint256 totalBorrowed,) =
            lendStorageB.getHypotheticalAccountLiquidityCollateral(deployer, LToken(payable(destLToken)), 0, 0);

        uint256 totalBorrowedWithInterest  = lendStorageB.borrowWithInterest(deployer, destLToken);

        console2.log("Total borrowed on Chain B:", totalBorrowed);
        console2.log("Total borrowed with interest on Chain B:", totalBorrowedWithInterest);
    }
```
Then, you can see this result:
```bash
Ran 1 test for test/TestBorrowingCrossChain.t.sol:TestBorrowingCrossChain
[PASS] testRegression_BorrowWithInterest_MissingDebtFromCrossChainCollaterals() (gas: 1564591)
Logs:
  Total borrowed on Chain B: 0
  Total borrowed with interest on Chain B: 0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.71ms (1.43ms CPU time)

Ran 1 test suite in 14.56ms (9.71ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Change the condition in borrowWithInterest() to:
```solidity
if (collaterals[i].destEid == currentEid)
```
This will correctly collect borrow entries stored in crossChainCollaterals on the destination chain.