Curved Walnut Cyborg

High

# supply() records wrong amount in totalInvestment

### Summary

supply() uses old exchangeRate so it writes a greater amount to totalInvestment than what was actually minted

### Root Cause

In CoreRouter users supply tokens and the contract records the amount supplied to totalInvestment. However it uses wrong exchangeRate and because of this it writes a larger amount.
```solidity
    function supply(uint256 _amount, address _token) external {
        address _lToken = lendStorage.underlyingTolToken(_token);

        require(_lToken != address(0), "Unsupported Token");

        require(_amount > 0, "Zero supply amount");

        // Transfer tokens from the user to the contract
        IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);

        _approveToken(_token, _lToken, _amount);

        // Get exchange rate before mint
@>      uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        // Mint lTokens
        require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

        // Calculate actual minted tokens using exchangeRate from before mint
@>      uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;

        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        lendStorage.distributeSupplierLend(_lToken, msg.sender);

        // Update total investment using calculated mintTokens
        lendStorage.updateTotalInvestment(
@>          msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
        );

        emit SupplySuccess(msg.sender, _lToken, _amount, mintTokens);
    }
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L61-L92

`exchangeRateBefore` used is likely going to be less than `exchangeRate` used in lToken.mint since the `borrowIndex` of the lToken is not updated prior. `exchangeRate` can only grow so `mintTokens` will be a greater amount than what was actually minted in lToken. This leads to protocol recording more `totalInvestment` for users than they are owed.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. User A supplies 1000*1e18 DAI
2. User B supplies 1e18 ETH and borrows 500*1e18 DAI
3. 5 blocks pass, some interest is accrued but not yet written
4. User C supplies 800*1e18 DAI

Amount of lToken minted for coreRouter is going to be less than what was stored in `totalInvestment` for user C because the `exchangeRateStored` used didn't include that interest from step 3. If user C was to instantly redeem all of his investment of DAI he would get a little more than 800*1e18 DAI, stealing from user A.

### Impact

Attackers can steal from the protocol and users as slightly inflated value is often written to totalInvestment (also leads to protocol insolvency even with normal user behavior)

### PoC

_No response_

### Mitigation

For example, call accrueInterest() on the supplied lToken prior to calling exchangeRateStored() or just use exchangeRateCurrent() instead
```solidity
    function supply(uint256 _amount, address _token) external {
            ...
        // Get exchange rate before mint
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateCurrent();
            ...
    }
```