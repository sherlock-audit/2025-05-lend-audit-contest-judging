Beautiful Hazelnut Ape

High

# Failure to Call accrueInterest() in redeem Function

### Summary

The [redeem](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L100) function neglects to invoke accrueInterest(), resulting in outdated interest rates 

### Root Cause

```solidity

//Failure to Call LTokenInterface(_lToken).accrueInterest();
// Check liquidity
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
        require(collateral >= borrowed, "Insufficient liquidity");
```

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L385
```solidity
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
 ..............

// Second loop: Calculate borrow value from borrowed assets
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);

            // Get borrow balance for this asset
            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));

            // Add cross-chain borrows if any
            totalBorrow += borrowWithInterest(account, address(asset));


```

Missing `LTokenInterface(_lToken).accrueInterest()`: 

`Redeem` function directly uses stale interest instead of real-time values , leading to:
1.Underestimated liabilities (unaccounted accrued interest).
2.Inflated collateral ratios.

### Internal Pre-conditions

1.Attacker borrows assets 
2.Attacker  redeeming collateral

### External Pre-conditions

1.Attacker borrows assets 
2.Attacker  redeeming collateral

### Attack Path

1.Attacker borrows assets 
2. Waits for interest accumulation without triggering updates.
3.Exploiting outdated interest rates for front-running redeeming collateral


### Impact

1.Underestimated liabilities (unaccounted accrued interest).
2.Exploiting outdated interest rates for front-running liquidations and redeeming collateral

### PoC

PATH:
test/poc.t.sol

case 1:
`function test_accrueInterest:`
console.log("sumBorrow",sumBorrow);//70000449486301355000 [7e19]
console.log("sumCollateral",sumCollateral);//75000000000000000000 [7.5e19]

case 2:
`function test_without_accrueInterest:`
console.log("sumBorrow_without",sumBorrow);//70000000000000000000 [7e19]
console.log("sumCollateral_without",sumCollateral);//75000000000000000000 [7.5e19]

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";
import "forge-std/console.sol";

contract TestLiquidations is LayerZeroTest {
    // State variables
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokensA;
    address[] public supportedTokensB;
    bool public isTestnet;
    address public deployer;
    address public liquidator;

    // Chain A (Source)
    CrossChainRouterMock public routerA;
    LendStorage public lendStorageA;
    CoreRouter public coreRouterA;
    Lendtroller public lendtrollerA;
    InterestRateModel public interestRateModelA;
    SimplePriceOracle public priceOracleA;
    address[] public lTokensA;

    // Chain B (Destination)
    CrossChainRouterMock public routerB;
    LendStorage public lendStorageB;
    CoreRouter public coreRouterB;
    Lendtroller public lendtrollerB;
    InterestRateModel public interestRateModelB;
    SimplePriceOracle public priceOracleB;
    address[] public lTokensB;

    uint32 constant CHAIN_A_ID = 1;
    uint32 constant CHAIN_B_ID = 2;

    EndpointV2 public endpointA;
    EndpointV2 public endpointB;

    // Events
    event LiquidateBorrow(
        address indexed liquidator, address indexed lToken, address indexed borrower, address lTokenCollateral
    );

    function setUp() public override(LayerZeroTest) {
        super.setUp();

        deployer = makeAddr("deployer");
        liquidator = makeAddr("liquidator");
        vm.deal(deployer, 1000 ether);
        vm.deal(liquidator, 1000 ether);

        // Deploy protocol on Chain A using the endpoint we just created
        Deploy deployA = new Deploy();
        (
            address priceOracleAddressA,
            address lendtrollerAddressA,
            address interestRateModelAddressA,
            address[] memory lTokenAddressesA,
            address payable routerAddressA,
            address payable coreRouterAddressA,
            address lendStorageAddressA,
            , //address _layerZeroEndpoint
            address[] memory _supportedTokensA
        ) = deployA.run(address(endpointA)); // Pass the endpoint address to Deploy.run

        // Store Chain A values
        routerA = CrossChainRouterMock(payable(routerAddressA));
        vm.label(address(routerA), "Router A");
        lendStorageA = LendStorage(lendStorageAddressA);
        vm.label(address(lendStorageA), "LendStorage A");
        coreRouterA = CoreRouter(coreRouterAddressA);
        vm.label(address(coreRouterA), "CoreRouter A");
        lendtrollerA = Lendtroller(lendtrollerAddressA);
        vm.label(address(lendtrollerA), "Lendtroller A");
        interestRateModelA = InterestRateModel(interestRateModelAddressA);
        vm.label(address(interestRateModelA), "InterestRateModel A");
        priceOracleA = SimplePriceOracle(priceOracleAddressA);
        vm.label(address(priceOracleA), "PriceOracle A");
        lTokensA = lTokenAddressesA;
        supportedTokensA = _supportedTokensA;

        // Deploy protocol on Chain B
        Deploy deployB = new Deploy();
        (
            address priceOracleAddressB,
            address lendtrollerAddressB,
            address interestRateModelAddressB,
            address[] memory lTokenAddressesB,
            address payable routerAddressB,
            address payable coreRouterAddressB,
            address lendStorageAddressB,
            , // address _layerZeroEndpoint
            address[] memory _supportedTokensB
        ) = deployB.run(address(endpointB));

        // Store Chain B values
        routerB = CrossChainRouterMock(payable(routerAddressB));
        vm.label(address(routerB), "Router B");
        lendStorageB = LendStorage(lendStorageAddressB);
        vm.label(address(lendStorageB), "LendStorage B");
        coreRouterB = CoreRouter(coreRouterAddressB);
        vm.label(address(coreRouterB), "CoreRouter B");
        lendtrollerB = Lendtroller(lendtrollerAddressB);
        vm.label(address(lendtrollerB), "Lendtroller B");
        interestRateModelB = InterestRateModel(interestRateModelAddressB);
        vm.label(address(interestRateModelB), "InterestRateModel B");
        priceOracleB = SimplePriceOracle(priceOracleAddressB);
        vm.label(address(priceOracleB), "PriceOracle B");
        lTokensB = lTokenAddressesB;
        supportedTokensB = _supportedTokensB;

        // Set up cross-chain mappings
        vm.startPrank(routerA.owner());
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            lendStorageA.addUnderlyingToDestUnderlying(supportedTokensA[i], supportedTokensB[i], CHAIN_B_ID);
            lendStorageA.addUnderlyingToDestlToken(supportedTokensA[i], lTokensB[i], CHAIN_B_ID);
            lendStorageA.setChainAssetMap(supportedTokensA[i], block.chainid, supportedTokensB[i]);
            lendStorageA.setChainLTokenMap(lTokensA[i], block.chainid, lTokensB[i]);
            lendStorageA.setChainLTokenMap(lTokensB[i], block.chainid, lTokensA[i]);
            console2.log("Mapped l token: ", lTokensA[i], "to", lTokensB[i]);
            console2.log("Mapping assets with chain: ", block.chainid);
        }
        vm.stopPrank();

        vm.startPrank(routerB.owner());
        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            lendStorageB.addUnderlyingToDestUnderlying(supportedTokensB[i], supportedTokensA[i], CHAIN_A_ID);
            lendStorageB.addUnderlyingToDestlToken(supportedTokensB[i], lTokensA[i], CHAIN_A_ID);
            lendStorageB.setChainAssetMap(supportedTokensB[i], block.chainid, supportedTokensA[i]);
            lendStorageB.setChainLTokenMap(lTokensB[i], block.chainid, lTokensA[i]);
            lendStorageB.setChainLTokenMap(lTokensA[i], block.chainid, lTokensB[i]);
            console2.log("Mapped l token: ", lTokensB[i], "to", lTokensA[i]);
            console2.log("Mapping assets with chain: ", block.chainid);
        }
        vm.stopPrank();

        // Set up initial token prices
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            priceOracleA.setDirectPrice(supportedTokensA[i], 1e18);
        }
        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            priceOracleB.setDirectPrice(supportedTokensB[i], 1e18);
        }

        // Set up router pairs
        routerA.setPairContract(payable(address(routerB)));
        routerB.setPairContract(payable(address(routerA)));

        vm.label(address(routerA), "Router A");
        vm.label(address(routerB), "Router B");
    }

    // Helper functions
    function _supplyA(address user, uint256 amount, uint256 tokenIndex)
        internal
        returns (address token, address lToken)
    {
        vm.deal(address(routerA), 1 ether);
        token = supportedTokensA[tokenIndex];
        lToken = lendStorageA.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }

    function _supplyB(address user, uint256 amount, uint256 tokenIndex)
        internal
        returns (address token, address lToken)
    {
        vm.deal(address(routerB), 1 ether);
        token = supportedTokensB[tokenIndex];
        lToken = lendStorageB.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterB), amount);
        coreRouterB.supply(amount, token);
        vm.stopPrank();
    }

    function _setupBorrowAndPriceDropScenario(
        uint256 supplyAmount,
        uint256 borrowAmount,
        uint256 newPrice,
        bool returnA
    ) internal returns (address tokenA, address tokenB, address lToken) {
        // Supply token0 as collateral on Chain A
        address lTokenA;
        (tokenA, lTokenA) = _supplyA(deployer, supplyAmount, 0);

        // Supply token1 as liquidity on Chain A from random wallet
        address lTokenB;
        (tokenB, lTokenB) = _supplyA(address(1), supplyAmount, 1);

        vm.prank(deployer);
        coreRouterA.borrow(borrowAmount, tokenB);

        // Simulate price drop of collateral (tokenA) only on first chain
        priceOracleA.setDirectPrice(tokenA, newPrice);
        // Simulate price drop of collateral (tokenA) only on second chain
        priceOracleB.setDirectPrice(
            lendStorageB.lTokenToUnderlying(lendStorageB.crossChainLTokenMap(lTokenA, block.chainid)), newPrice
        );

        return (tokenA, tokenB, returnA ? lTokenA : lTokenB);
    }

    function _setupBorrowCrossChainAndPriceDropScenario(uint256 supplyAmount, uint256 borrowAmount, uint256 newPrice)
        internal
        returns (address tokenA, address lTokenA)
    {
        // Supply collateral on Chain A
        (tokenA, lTokenA) = _supplyA(deployer, supplyAmount, 0);

        // Supply liquidity on Chain B for borrowing
        _supplyB(liquidator, supplyAmount * 2, 0);

        vm.deal(address(routerA), 1 ether); // For LayerZero fees

        // Borrow cross-chain from Chain A to Chain B
        vm.startPrank(deployer);
        routerA.borrowCrossChain(borrowAmount, tokenA, CHAIN_B_ID);
        vm.stopPrank();

        // Simulate price drop of collateral (tokenA) only on first chain
        priceOracleA.setDirectPrice(tokenA, newPrice);
        // Simulate price drop of collateral (tokenA) only on second chain
        priceOracleB.setDirectPrice(
            lendStorageB.lTokenToUnderlying(lendStorageB.crossChainLTokenMap(lTokenA, block.chainid)), newPrice
        );
    }



    // test_accrueInterest
    function test_accrueInterest(uint256 supplyAmount, uint256 borrowAmount, uint256 newPrice) public {
         supplyAmount =  100e18;
        borrowAmount = 70e18; 
        newPrice =2e16; 

        // Supply token0 as collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount, 0);

        // Supply token1 as liquidity on Chain A from random wallet
        (address tokenB, address lTokenB) = _supplyA(address(1), supplyAmount, 1);

        vm.prank(deployer);
        coreRouterA.borrow(borrowAmount, tokenB);
        address _lToken = lendStorageA.underlyingTolToken(tokenB);


       uint256 height = vm.getBlockNumber();
        vm.roll(height+100);
        LTokenInterface(_lToken).accrueInterest();
        // Check account liquidity after borrow
        (uint256 sumBorrow, uint256 sumCollateral) =
            lendStorageA.getHypotheticalAccountLiquidityCollateral(deployer, LToken(lTokenB), 0, 0);

        console.log("sumBorrow",sumBorrow);//70000449486301355000 [7e19
        console.log("sumCollateral",sumCollateral);//75000000000000000000 [7.5e19]
   
    }


    //test_without_accrueInterest
    function test_without_accrueInterest(uint256 supplyAmount, uint256 borrowAmount, uint256 newPrice) public {
        
        supplyAmount =  100e18;
        borrowAmount = 70e18; 
        newPrice =2e16; 
        // Supply token0 as collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount, 0);

        // Supply token1 as liquidity on Chain A from random wallet
        (address tokenB, address lTokenB) = _supplyA(address(1), supplyAmount, 1);

        vm.prank(deployer);
        coreRouterA.borrow(borrowAmount, tokenB);
        address _lToken = lendStorageA.underlyingTolToken(tokenB);

        // Check account liquidity after borrow
        uint256 height = vm.getBlockNumber();
        vm.roll(height+100);
        (uint256 sumBorrow, uint256 sumCollateral) =
            lendStorageA.getHypotheticalAccountLiquidityCollateral(deployer, LToken(lTokenB), 0, 0);

        console.log("sumBorrow_without",sumBorrow);//70000000000000000000 [7e19]
        console.log("sumCollateral_without",sumCollateral);//75000000000000000000 [7.5e19]
   
    }
}

```





### Mitigation

```solidity
function redeem(uint256 _amount, address _lToken) external {  
    LTokenInterface(_lToken).accrueInterest(); // Add this line  
    uint256 exchangeRate = LTokenInterface(_lToken).exchangeRateCurrent();  
    // Proceed with redemption...  
}  
```