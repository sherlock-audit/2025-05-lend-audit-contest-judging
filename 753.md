Fun Shadow Parakeet

Medium

# Out-of-order LayerZero messages may lead to incorrect state updates

### Summary

LayerZero does not guarantee that messages will be executed in the order they were sent. The `BorrowCrossChain`, `repayCrossChainBorrow`, and `liquidateCrossChain` functions rely on LayerZero for cross-chain messaging but do not track message order. As a result, if two or more related transactions arrive out of order on the destination chain, a later message can overwrite the state updated by an earlier one.

### Root Cause

LayerZero may encounter message delays or failures due to factors such as network congestion, delays in DVN verification, or the asynchronous nature of `lzReceive` execution by the executor. These behaviors are acknowledged in the official LayerZero documentation [`Message Ordering`](https://docs.layerzero.network/v2/developers/evm/oapp/message-design-patterns#message-ordering) and [`Error Codes & Handling`](https://docs.layerzero.network/v2/developers/evm/troubleshooting/error-messages). If a sequence of cross-chain messages is dispatched from the source chain, they may arrive and execute out of order on the destination chain. Without an explicit ordering mechanism, later messages can be overwritten by earlier ones, leading to inconsistent protocol state and mismatched accounting.

This issue affects several key cross-chain operations:
- `BorrowCrossChain` (initiated from Chain A): A message is sent to Chain B to record the borrow and [update](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L637-L639) collateral. A follow-up confirmation is then sent back to Chain A to finalize the state [update](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L710-L715).
- `RepayCrossChainBorrow` (initiated from Chain B): A message is sent to Chain A to [update](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L563-L567) the borrower’s debt records.
- `LiquidateCrossChain` (initiated from Chain B): A message is first sent to Chain A to [update](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L320-L341) the borrower’s debt, rewards, and other related states. Then, Chain A sends a message back to Chain B to [update](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L524-L526) the collateral. Finally, a confirmation message may be sent again to Chain A to [update](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L563-L567) and finalize the liquidation state. This multi-step process increases complexity and makes the system more prone to message ordering issues.


Take `borrowCrossChain` as an example. Consider the following scenario:

1. 	A user initiates two cross-chain borrow transactions from the source chain (Chain A) to the destination chain (Chain B): TransactionA and TransactionB (expected order: A → B).

Each `BorrowCrossChain` call on Chain A sends a cross-chain message to Chain B:

```solidity
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );

```

2. On Chain B, each cross-chain message triggers `_handleBorrowCrossChainRequest`, which updates the collateral record via `lendStorage.updateCrossChainCollateral`:


```solidity
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

@>          lendStorage.updateCrossChainCollateral(
                payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
```


3. Due to message delay, TansactionB is executed before TransactionA on Chain B (actual order: B -> A). TransactionB runs first, and when TransactionA arrives later, it recalculates based on the data already updated by TransactionB and then overwrites the state using `updateCrossChainCollateral`:

```solidity
    function updateCrossChainCollateral(address user, address underlying, uint256 index, Borrow memory newCollateral)
        external
        onlyAuthorized
    {
        Borrow storage collateral = crossChainCollaterals[user][underlying][index];
        collateral.srcEid = newCollateral.srcEid;
        collateral.destEid = newCollateral.destEid;
        collateral.principle = newCollateral.principle;
        collateral.borrowIndex = newCollateral.borrowIndex;
        collateral.borrowedlToken = newCollateral.borrowedlToken;
        collateral.srcToken = newCollateral.srcToken;
    }
```


4. Furthermore, a confirmation message is sent back to Chain A using `_send`:

```solidity
        // Send confirmation back to source chain with updated borrow details
        _send(
            srcEid,
            payload.amount,
            currentBorrowIndex,
            payload.collateral,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.ValidBorrowRequest
        );
```

Chain A then processes `_handleValidBorrowRequest`, which updates local borrow state again:

```solidity
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
            userBorrows[index].principle = userBorrows[index].principle + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
@>          lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
```

Notably, all subsequent calculations rely on the previously incorrect state. This leads to inconsistent borrow records between Chain A and Chain B and may result in further incorrect borrow calculations.

`repayCrossChainBorrow` and `liquidateCrossChain` follow the same pattern and are similarly vulnerable.

### Internal Pre-conditions

None

### External Pre-conditions

LayerZero experiences latency, message delays, or other delivery failures that result in out-of-order execution.


### Attack Path

Multiple cross-chain transactions are sent using LayerZero. Due to out-of-order execution, the destination chain ends up with a different state than expected on the source chain, resulting in mismatched records between the two.

### Impact

Internal accounting becomes inconsistent between the source and destination chains. Users’ borrow or repay states may be recorded incorrectly, which can affect collateral calculations, repayment accuracy, and liquidation logic. This creates a risk of user fund loss or unfair liquidation, and undermines the protocol’s ability to rely on cross-chain state integrity.

### PoC

This PoC demonstrates how out-of-order cross-chain messages can corrupt user borrow records and lead to a loss of `principal`:

1. The user supplies `1000` TokenA as collateral on Chain A.
2. The user performs two cross-chain borrows in sequence: TransactionA (`200` TokenA) followed by TransactionB (`100` TokenA), Under normal conditions, TransactionA executes when the borrowIndex is `1.0`, and TransactionB executes later when the borrowIndex is `1.5` (The user likely anticipates this index increase and borrows accordingly).
3. However, due to LayerZero message delays, TransactionB is executed before TransactionA.
4. As a result, the final borrow state on Chain B is inaccurate: the user’s `principal` is reduced, and the more recent state from TransactionB is overwritten by the earlier TransactionA.

Below is a runnable PoC. The following setup is required to run it:

First, expose `_handleBorrowCrossChainRequest` by adding the following to `test/mocks/CrossChainRouterMock.sol`:

```solidity
    // Make internal function callable for testing
    function handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) public {
        _handleBorrowCrossChainRequest(payload, srcEid);
    }
```

Then, add the test cases `test_cross_chain_borrow_multiple_abnormal` and `test_cross_chain_borrow_multiple_normal` to `test/TestLiquidations.t.sol`:
- `test_cross_chain_borrow_multiple_abnormal` simulates the out-of-order execution scenario described above.
- `test_cross_chain_borrow_multiple_normal` represents the normal ordered case.

The two can be directly compared.

```solidity

    // Abnormal Case
    function test_cross_chain_borrow_multiple_abnormal() public {

        // 1. Supply collateral on Chain A
        uint256 supplyAmount = 1000e18;

        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount, 0);
        address destlToken = lendStorageA.underlyingToDestlToken(tokenA, CHAIN_B_ID);

        // Ignore coreRouter logic
        vm.mockCall(
            address(coreRouterB),
            abi.encodeWithSelector(CoreRouter.borrowForCrossChain.selector),
            ""
        );
        // 2. Mock borrowIndex = 1.0
        setMockBorrowIndex(1e18, destlToken);
        // 3. TransactionB: First cross-chain borrow of 100 TokenA (note the reversed order — TransactionB executes first)
        CrossChainRouterMock.LZPayload memory payload = CrossChainRouterMock.LZPayload({
            amount: 100e18,
            borrowIndex: 0,
            collateral: supplyAmount,
            sender: msg.sender,
            destlToken: destlToken,
            liquidator: address(0),
            srcToken: tokenA,
            contractType: uint8(CrossChainRouterMock.ContractType.BorrowCrossChain)
        });
        routerB.handleBorrowCrossChainRequest(payload, CHAIN_A_ID);

        // 4. Update borrowIndex = 1.5
        setMockBorrowIndex(1.5e18, destlToken);
        // 5. TransactionA: Second cross-chain borrow of 200 TokenA
        payload = CrossChainRouterMock.LZPayload({
            amount: 200e18,
            borrowIndex: 0,
            collateral: supplyAmount,
            sender: msg.sender,
            destlToken: destlToken,
            liquidator: address(0),
            srcToken: tokenA,
            contractType: uint8(CrossChainRouterMock.ContractType.BorrowCrossChain)
        });

        // 6. Final state should have principle = 100e18 and borrowIndex = 1e18
        address destUnderlying = lendStorageB.lTokenToUnderlying(destlToken);
        LendStorage.Borrow[] memory userCrossChainCollaterals =
            lendStorageB.getCrossChainCollaterals(msg.sender, destUnderlying);

        assertEq(userCrossChainCollaterals[0].principle, 100e18, "Principal should be 100e18");
        assertEq(userCrossChainCollaterals[0].borrowIndex, 1e18, "Borrow index should be 1e18");
    }


    // Normal Case (Expected Execution Order)
    function test_cross_chain_borrow_multiple_normal() public {
        // 1. Supply collateral on Chain A
        uint256 supplyAmount = 1000e18;

        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount, 0);
        address destlToken = lendStorageA.underlyingToDestlToken(tokenA, CHAIN_B_ID);

        // Ignore coreRouter logic
        vm.mockCall(
            address(coreRouterB),
            abi.encodeWithSelector(CoreRouter.borrowForCrossChain.selector),
            ""
        );
        // 2. Mock borrowIndex = 1.0
        setMockBorrowIndex(1e18, destlToken);
        // 3. TransactionA: First cross-chain borrow of 200 TokenA
        CrossChainRouterMock.LZPayload memory payload = CrossChainRouterMock.LZPayload({
            amount: 200e18,
            borrowIndex: 0,
            collateral: supplyAmount,
            sender: msg.sender,
            destlToken: destlToken,
            liquidator: address(0),
            srcToken: tokenA,
            contractType: uint8(CrossChainRouterMock.ContractType.BorrowCrossChain)
        });
        routerB.handleBorrowCrossChainRequest(payload, CHAIN_A_ID);

        // 4. Update borrowIndex = 1.5
        setMockBorrowIndex(1.5e18, destlToken);
        // 5. TransactionB: Second cross-chain borrow of 100 TokenA
        payload = CrossChainRouterMock.LZPayload({
            amount: 100e18,
            borrowIndex: 0,
            collateral: supplyAmount,
            sender: msg.sender,
            destlToken: destlToken,
            liquidator: address(0),
            srcToken: tokenA,
            contractType: uint8(CrossChainRouterMock.ContractType.BorrowCrossChain)
        });

        // 6. Final state should have principle = 200e18 and borrowIndex = 1e18
        address destUnderlying = lendStorageB.lTokenToUnderlying(destlToken);
        LendStorage.Borrow[] memory userCrossChainCollaterals =
            lendStorageB.getCrossChainCollaterals(payload.sender, destUnderlying);

        assertEq(userCrossChainCollaterals[0].principle, 200e18, "Principal should be 200e18");
        assertEq(userCrossChainCollaterals[0].borrowIndex, 1e18, "Borrow index should be 1e18");
    }

    // Mock borrowIndex
    function setMockBorrowIndex(uint256 _borrowIndex, address _destlToken) internal {
        vm.mockCall(
            address(_destlToken),
            abi.encodeWithSelector(LTokenInterface(_destlToken).borrowIndex.selector),
            abi.encode(_borrowIndex)
        );
    }
```

Abnormal Case run:

```shell
forge test --mt test_cross_chain_borrow_multiple_abnormal  -vv
```

Output:

<img width="823" alt="Image" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/4e715f10-40ba-4537-b801-c9a7dc73966a" />


Normal Case run:

```shell
forge test --mt test_cross_chain_borrow_multiple_normal -vv
```

Output:

<img width="823" alt="Image" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/24b6256c-d905-42a5-be5e-fc73307e4698" />


In the abnormal case, the user’s `principal` is recorded as `100e18`, whereas in the normal case it should be `200e18`.
This results in understated borrow `principal`, causing accounting discrepancies and potential loss of user funds.


### Mitigation

Consider adding a mapping to track received nonces and enabling LayerZero’s ordered execution feature by including `addExecutorOrderedExecutionOption` in `_lzSend`, as recommended in [the official documentation](https://docs.layerzero.network/v2/developers/evm/oapp/message-design-patterns#code-example-2).