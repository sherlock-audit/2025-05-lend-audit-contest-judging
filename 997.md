Sweet Sandstone Squid

Medium

# Not adding liquidated tokens to liquidator `userSuppliedAssets` will cause unfair liquidations for users

### Summary

In `CoreRouter::liquidateSeizeUpdate` the function increases the liquidator's token balance through `updateTotalInvestment` but fails to register these tokens in the `userSuppliedAssets` mapping. If these tokens are not supplied by the liquidator, it will cause collateral from liquidations to be effectively invisible during liquidity calculations, leading to unfair liquidations of users who should be solvent.
```solidity
function liquidateSeizeUpdate(
    address sender,
    address borrower,
    address lTokenCollateral,
    address borrowedlToken,
    uint256 repayAmount
) internal {
    // ... calculation of seize tokens ...
    
    // Update total investment
    lendStorage.updateTotalInvestment(
        borrower, lTokenCollateral, lendStorage.totalInvestment(borrower, lTokenCollateral) - seizeTokens
    );
    lendStorage.updateTotalInvestment(
        sender,
        lTokenCollateral,
        lendStorage.totalInvestment(sender, lTokenCollateral) + (seizeTokens - currentReward)
    );
     // Missing critical line:
    // lendStorage.addUserSuppliedAsset(sender, lTokenCollateral);
}
```
As seen above, the function updates the `totalInvestment` mapping to record the seized tokens for the liquidator, but fails to call `addUserSuppliedAsset` to register the `lToken` in the liquidator's supplied assets list. This is an issue because in `LendStorage::getHypotheticalAccountLiquidityCollateral`, collateral is only calculated from assets listed in the `userSuppliedAssets` mapping:
```solidity
function getHypotheticalAccountLiquidityCollateral(...) public view returns (uint256, uint256) {
    // ...
    address[] memory suppliedAssets = userSuppliedAssets[account].values();
    
    // First loop: Calculate collateral value from supplied assets
    for (uint256 i = 0; i < suppliedAssets.length;) {
        LToken asset = LToken(suppliedAssets[i]);
        uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];
        // ... collateral calculation ... 
    }
    // ...
}
```
Since the seized tokens aren't added to `userSuppliedAssets`, they aren't included in collateral calculations, despite being properly tracked in `totalInvestment`.

### Root Cause

In [liquidateSeizeUpdate](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L278), the liquidated token address is not added to the liquidator's `userSuppliedAssets`

### Internal Pre-conditions

NIL

### External Pre-conditions

1. The liquidator should not have supplied the asset previously
2. The liquidator should have a borrow position

### Attack Path

- User A supplies 100 DAI and borrows 70 USDC (70% collateral factor)
- User B supplies 100 USDC and borrows 40 DAI (40% collateral factor)
- DAI price drops, making User B's position liquidatable
- User A liquidates User B, repaying 20 DAI and receiving ~22 USDC (with liquidation incentive)
- User A now owns 22 USDC from the liquidation, but these tokens are not registered in userSuppliedAssets
- USDC price drops, and despite User A having actual collateral of 100 DAI + 22 USDC, only the 100 DAI is counted
- User A is incorrectly marked as liquidatable and loses funds in a liquidation, despite having sufficient actual collateral

### Impact

It will lead to unfair liquidations as a liquidator who acquires significant collateral through liquidations may themselves be liquidated, despite having sufficient actual collateral.

### PoC
Add the below test function to `TestSupplying.t.sol`
```solidity
struct UserDetails{
            address userAddress;
            address assetToken;
            address lToken;
            uint256 collateralFactorForAssetToken;
        }
    
    function testUsersGetUnfairlyLiquidated() public {
        uint256 supplyAmount = 100e18;

        UserDetails memory user1Details = UserDetails({
            userAddress: makeAddr("user1"),
            assetToken: supportedTokens[0],
            lToken: lendStorage.underlyingTolToken(supportedTokens[0]),
            collateralFactorForAssetToken: lendtroller.getCollateralFactorMantissa(lendStorage.underlyingTolToken(supportedTokens[0]))
        });

        UserDetails memory user2Details = UserDetails({ 
            userAddress: makeAddr("user2"),
            assetToken: supportedTokens[1],
            lToken: lendStorage.underlyingTolToken(supportedTokens[1]),
            collateralFactorForAssetToken: lendtroller.getCollateralFactorMantissa(lendStorage.underlyingTolToken(supportedTokens[1]))
        });

        //deployer supplies both tokens to ensure enough liquidity
        vm.startPrank(deployer);
        ERC20Mock(user1Details.assetToken).mint(deployer, supplyAmount);
        IERC20(user1Details.assetToken).approve(address(coreRouter), supplyAmount);
        ERC20Mock(user2Details.assetToken).mint(deployer, supplyAmount);
        IERC20(user2Details.assetToken).approve(address(coreRouter), supplyAmount);

        coreRouter.supply(supplyAmount, user1Details.assetToken);
        coreRouter.supply(supplyAmount, user2Details.assetToken);
        vm.stopPrank();
        
        //user1 supplies token1 and borrows token2
        vm.startPrank(user1Details.userAddress);
        ERC20Mock(user1Details.assetToken).mint(user1Details.userAddress, supplyAmount);
        IERC20(user1Details.assetToken).approve(address(coreRouter), supplyAmount);

        coreRouter.supply(supplyAmount, user1Details.assetToken);

        uint256 borrowAmount1 = supplyAmount * user1Details.collateralFactorForAssetToken / 1e18; //user1 borrows max allowed amount i.e 75%
        coreRouter.borrow(borrowAmount1, user2Details.assetToken);
        vm.stopPrank();

        //user2 supplies token2 and borrows token1
        vm.startPrank(user2Details.userAddress);
        ERC20Mock(user2Details.assetToken).mint(user2Details.userAddress, supplyAmount);
        IERC20(user2Details.assetToken).approve(address(coreRouter), supplyAmount);

        coreRouter.supply(supplyAmount, user2Details.assetToken);

        uint256 borrowAmount2 = supplyAmount * user2Details.collateralFactorForAssetToken / 2e18; //user2 borrows 50%
        coreRouter.borrow(borrowAmount2, user1Details.assetToken);
        vm.stopPrank();

        //token1 price falls and user1 position becomes liquidateable
        priceOracle.setDirectPrice(user1Details.assetToken, 7e17);

        uint256 user2TotalCollateralValueBeforeLiquidatingUser1 = (lendStorage.getTotalSupplied(user2Details.userAddress, user1Details.lToken) * priceOracle.getUnderlyingPrice(LToken(user1Details.lToken)) / 1e18) + (lendStorage.getTotalSupplied(user2Details.userAddress, user2Details.lToken) * priceOracle.getUnderlyingPrice(LToken(user2Details.lToken))/ 1e18);

        //user2 liquidates user1
        vm.startPrank(user2Details.userAddress);
        ERC20Mock(user2Details.assetToken).mint(user2Details.userAddress, lendStorage.getMaxLiquidationRepayAmount(user1Details.userAddress, user2Details.lToken, true));
        IERC20(user2Details.assetToken).approve(address(coreRouter), lendStorage.getMaxLiquidationRepayAmount(user1Details.userAddress, user2Details.lToken, true));

        coreRouter.liquidateBorrow(
            user1Details.userAddress, lendStorage.getMaxLiquidationRepayAmount(user1Details.userAddress, user2Details.lToken, true), user1Details.lToken, user2Details.assetToken
        );
        vm.stopPrank();

        //user2 expected collateral values and remaining borrow amounts
        uint256 user2TotalCollateralValueAfterLiquidatingUser1 = (
            lendStorage.getTotalSupplied(user2Details.userAddress, user1Details.lToken) * priceOracle.getUnderlyingPrice(LToken(user1Details.lToken)) / 1e18
        ) + (lendStorage.getTotalSupplied(user2Details.userAddress, user2Details.lToken) * priceOracle.getUnderlyingPrice(LToken(user2Details.lToken)) / 1e18);

        assertGt(user2TotalCollateralValueAfterLiquidatingUser1, user2TotalCollateralValueBeforeLiquidatingUser1);

        uint256 user2ExpectedEffectiveCollateralLimit =
            user2TotalCollateralValueAfterLiquidatingUser1 * user2Details.collateralFactorForAssetToken / 1e18;

        (uint256 actualBorrowsAccounted, uint256 user2ActualEffectiveCollateralLimit) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(user2Details.userAddress, LToken(address(0)), 0, 0);

        uint256 user2ExpectedRemainingMaxBorrow = user2ExpectedEffectiveCollateralLimit - actualBorrowsAccounted;
        uint256 user2ActualRemainingMaxBorrow = user2ActualEffectiveCollateralLimit - actualBorrowsAccounted;
        assertGt(user2ExpectedEffectiveCollateralLimit, user2ActualEffectiveCollateralLimit);
        assertGt(user2ExpectedRemainingMaxBorrow, user2ActualRemainingMaxBorrow);

        console2.log("User2 expected effective collateral limit: ", user2ExpectedEffectiveCollateralLimit);
        console2.log("User2 actual effective collateral limit: ", user2ActualEffectiveCollateralLimit);
        console2.log("User2 expected remaining borrow amount: ", user2ExpectedRemainingMaxBorrow);
        console2.log("User2 actual remaining borrow amount: ", user2ActualRemainingMaxBorrow);
        
        //price moves again and user2 position should not be liquidateable based on their totalsupplied value but it still is liquidateble
        priceOracle.setDirectPrice(user2Details.assetToken, 2e17);

        //deployer liquidates user2 even as they have enough collateral
        vm.startPrank(deployer);
            ERC20Mock(user1Details.assetToken).mint(deployer, 1e18);
            IERC20(user1Details.assetToken).approve(address(coreRouter), 1e18);

            coreRouter.liquidateBorrow(
                user2Details.userAddress, 1e18, user2Details.lToken, user1Details.assetToken
            );
        vm.stopPrank();
    }
```

### Mitigation

_No response_