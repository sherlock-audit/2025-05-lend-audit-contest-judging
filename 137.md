Winning Cinnabar Gorilla

High

# Incorrect accounting of TotalInvestment

### Summary

When user supplies, `TotalInvestment` can be wrongly updated as outdated exchange rate is used for calculation.

### Root Cause

When user supplies tokens to `CoreRouter`, the exchange rate for calculating `mintTokens` is retrieved by calling `LToken::exchangeRateStored()`.

[CoreRouter.sol#L73-L80](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L73-L80):
```solidity
        // Get exchange rate before mint
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        // Mint lTokens
        require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

        // Calculate actual minted tokens using exchangeRate from before mint
        uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;
```

The problem is that `exchangeRateBefore` returned can be outdated as `exchangeRateStored()` does not accrue interest before calculating the exchange rate.

[LToken.sol#L276-L283](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L276-L283):
```solidity
    /**
     * @notice Calculates the exchange rate from the underlying to the LToken
     * @dev This function does not accrue interest before calculating the exchange rate
     * @return Calculated exchange rate scaled by 1e18
     */
    function exchangeRateStored() public view override returns (uint256) {
        return exchangeRateStoredInternal();
    }
```

`mintTokens` is used to update `totalInvestment`, as a result, the actual minted LToken amount can be less than `totalInvestment`, leading to incorrect accounting.

```solidity
        // Update total investment using calculated mintTokens
        lendStorage.updateTotalInvestment(
            msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
        );
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

NA

### Impact

One of the impacts is that user cannot claim Lend tokens. This is because user's accrued Lend amount is calculated by `totalInvestment`, however, because the actual Lend token claimed from `Lendtroller` is less than `lendAccrued`, no Lend tokens will be transferred to the user.

[LendStorage.sol#L326-L334](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L326-L334):
```solidity
        // Calculate the appropriate account balance and delta based on supply or borrow
@>      uint256 supplierTokens = totalInvestment[supplier][lToken];

        // Calculate LEND accrued: lTokenAmount * accruedPerLToken
        uint256 supplierDelta = mul_(supplierTokens, deltaIndex);

        // Update the accrued LEND for the account
        uint256 supplierAccrued = add_(lendAccrued[supplier], supplierDelta);
@>      lendAccrued[supplier] = supplierAccrued;
```

[CoreRouter.sol#L420-L423](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L420-L423):
```solidity
@>      if (amount > 0 && amount <= lendBalance) {
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
```

### PoC

Please run `forge test --mt testPOC_UserCannotClaimLend`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import "forge-std/Test.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import {ILayerZeroEndpointV2} from "@layerzerolabs/lz-evm-protocol-v2/contracts/interfaces/ILayerZeroEndpointV2.sol";

import {Lend} from "../src/Governance/Lend.sol";
import {Lendtroller, LendtrollerInterface} from "../src/Lendtroller.sol";
import {LErc20Immutable, LToken} from "../src/LErc20Immutable.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel, InterestRateModel} from "../src/WhitePaperInterestRateModel.sol";

import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {CrossChainRouter, Origin} from "../src/LayerZero/CrossChainRouter.sol";

contract POC is Test {
    using SafeERC20 for IERC20;

    uint32 constant chainAEid = 30101; // Ethereum
    uint32 constant chainBEid = 30184; // Base

    address owner;
    address mockDestLToken;
    address mockPeer;

    IERC20 usdt;
    IERC20 dai;
    LErc20Immutable lTokenUSDT;
    LErc20Immutable lTokenDAI;
    Lend lend;

    ILayerZeroEndpointV2 endpoint;

    Lendtroller lendtroller;
    SimplePriceOracle priceOracle;
    WhitePaperInterestRateModel interestRateModel;

    LendStorage lendStorage;
    CoreRouter coreRouter;
    CrossChainRouter crossChainRouter;

    function setUp() public {
        vm.createSelectFork("https://eth.drpc.org");

        owner = makeAddr("Owner");
        mockDestLToken = makeAddr("MockDestLToken");
        mockPeer = makeAddr("ChainBPeer");
        endpoint = ILayerZeroEndpointV2(
            0x1a44076050125825900e736c501f859c50fE728c
        );

        usdt = IERC20(0xdAC17F958D2ee523a2206206994597C13D831ec7);
        dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);

        vm.startPrank(owner);

        // Deploy Lendtroller
        lendtroller = new Lendtroller();
        // Deploy Lend
        lend = new Lend(address(lendtroller));
        // Deploy PriceOracle
        priceOracle = new SimplePriceOracle();
        // Deploy InterestRateModel
        interestRateModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);
        // Deploy lTokens
        lTokenUSDT = new LErc20Immutable(
            address(usdt),
            lendtroller,
            interestRateModel,
            1e6, // 1:1
            "Lending USDT",
            "lUSDT",
            18,
            payable(owner)
        );
        lTokenDAI = new LErc20Immutable(
            address(dai),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending DAI",
            "lDAI",
            18,
            payable(owner)
        );

        // Deploy LendStorage
        lendStorage = new LendStorage(
            address(lendtroller),
            address(priceOracle),
            chainAEid
        );
        // Deploy CoreRouter
        coreRouter = new CoreRouter(
            address(lendStorage),
            address(priceOracle),
            address(lendtroller)
        );
        // Deploy CrossChainRouter
        crossChainRouter = new CrossChainRouter(
            address(endpoint),
            owner,
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            chainAEid
        );

        // Configurations
        {
            priceOracle.setUnderlyingPrice(lTokenUSDT, 1e18);
            priceOracle.setUnderlyingPrice(lTokenDAI, 1e18);

            lendtroller._setPriceOracle(priceOracle);
            lendtroller.setLendStorage(address(lendStorage));
            lendtroller._supportMarket(lTokenUSDT);
            lendtroller._supportMarket(lTokenDAI);
            lendtroller.setLendToken(address(lend));
            lendtroller._setCollateralFactor(lTokenUSDT, 0.9e18);
            lendtroller._setCollateralFactor(lTokenDAI, 0.9e18);

            LToken[] memory lTokens = new LToken[](2);
            lTokens[0] = lTokenUSDT;
            lTokens[1] = lTokenDAI;
            uint256[] memory supplySpeeds = new uint256[](2);
            supplySpeeds[0] = 1e18;
            supplySpeeds[1] = 1e18;
            uint256[] memory borrowSpeeds = new uint256[](2);
            borrowSpeeds[0] = 2e18;
            borrowSpeeds[1] = 2e18;
            lendtroller._setLendSpeeds(lTokens, supplySpeeds, borrowSpeeds);

            lendStorage.addSupportedTokens(address(usdt), address(lTokenUSDT));
            lendStorage.addSupportedTokens(address(dai), address(lTokenDAI));
            lendStorage.addUnderlyingToDestlToken(
                address(usdt),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(dai),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.setAuthorizedContract(address(coreRouter), true);
            lendStorage.setAuthorizedContract(address(crossChainRouter), true);

            crossChainRouter.setPeer(
                chainBEid,
                bytes32(uint256(uint160(mockPeer)))
            );

            coreRouter.setCrossChainRouter(address(crossChainRouter));

            deal(address(crossChainRouter), 1 ether);
        }

        vm.stopPrank();

        {
            vm.label(address(usdt), "USDT");
            vm.label(address(dai), "DAI");
            vm.label(address(lTokenUSDT), "lToken USDT");
            vm.label(address(lTokenDAI), "lToken DAI");
            vm.label(address(endpoint), "Endpoint");
            vm.label(address(lendtroller), "Lendtroller");
            vm.label(address(lend), "Lend");
            vm.label(address(priceOracle), "PriceOracle");
            vm.label(address(lendStorage), "LendStorage");
            vm.label(address(coreRouter), "CoreRouter");
            vm.label(address(crossChainRouter), "CrossChainRouter");
        }
    }

    function testPOC_UserCannotClaimLend() public {
        {
            address supplier = makeAddr("Supplier");
            address borrower = makeAddr("Borrower");

            deal(address(dai), supplier, 10000e18);
            deal(address(dai), borrower, 10000e18);

            vm.startPrank(supplier);
            dai.safeApprove(address(lTokenDAI), 10000e18);
            lTokenDAI.mint(10000e18);
            vm.stopPrank();

            vm.startPrank(borrower);
            address[] memory lTokens = new address[](2);
            dai.safeApprove(address(lTokenDAI), 10000e18);
            // It does not make much sense to supply and borrow the same token, but this is only for test
            lTokenDAI.mint(10000e18);
            lTokenDAI.borrow(8000e18);
            vm.stopPrank();

            vm.roll(block.number + 100000);
        }

        // Alice supply
        address alice = makeAddr("Alice");
        deal(address(dai), alice, 1000e18);
        {
            vm.startPrank(alice);
            dai.safeApprove(address(coreRouter), 1000e18);
            coreRouter.supply(1000e18, address(dai));
            vm.stopPrank();
        }

        uint256 investment = lendStorage.totalInvestment(
            alice,
            address(lTokenDAI)
        );
        uint256 actualMint = lTokenDAI.balanceOf(address(coreRouter));

        // The acutal minted LToken balance is less than totalInvestment
        assertEq(investment, 1000e18);
        assertApproxEqAbs(actualMint, 997.7e18, 0.03e18);

        // Accrue Lend
        vm.roll(block.number + 1000);

        // Claim Lend
        address[] memory holders = new address[](1);
        holders[0] = alice;
        LToken[] memory lTokens = new LToken[](1);
        lTokens[0] = lTokenUSDT;

        coreRouter.claimLend(holders, lTokens, false, true);

        // Lend cannot be claimed and stuck in the contract
        assertEq(lend.balanceOf(alice), 0);
        assertApproxEqAbs(
            lend.balanceOf(address(coreRouter)),
            47.5e18,
            0.02e18
        );
    }
}

```

### Mitigation

Call `LToken::exchangeRateCurrent()` to get the latest exchange rate.

```diff
-       uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
+       uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateCurrent();
```