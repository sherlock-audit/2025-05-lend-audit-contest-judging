Magnificent Holographic Camel

Medium

# Max liquidation amount is less than actual in cross chain liquidation

### Summary

Incorrectly repay amount validation can cause the max liquidation amount less than actual

### Root Cause

The function [`liquidateCrossChain()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L172) validates max close twice. The first one is handled in [function `_validateAndPrepareLiquidation()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L197-L233), the second one is handled in function [`_executeLiquidation()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L235C14-L239).

With the first one, the max close is fetched by calling `lendStorage.getMaxLiquidationRepayAmount(params.borrower,params.borrowedlToken,false)`. This call returns the max repay for the borrow amount without accruing interest. This is because the borrow amount returned from [`borrowWithInterest()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L478C14-L503) is the borrow amount without accruing interest. Even though the borrow is calculated as `(collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex` but indeed at this execution step, interest has not yet accrued in `LToken` by the call `LToken::accrueInterest()`

With the second validation, the max liquidation amount is calculated in the [function `_prepareLiquidationValues()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L245-L262), which does accrue debt interest. Hence, this one is the correct one.

```solidity
    function _validateAndPrepareLiquidation(LendStorage.LiquidationParams memory params) private view {
        require(params.borrower != msg.sender, "Liquidator cannot be borrower");
        require(params.repayAmount > 0, "Repay amount cannot be zero");

        // Get the lToken for the borrowed asset on this chain
        params.borrowedlToken = lendStorage.underlyingTolToken(params.borrowedAsset);
        require(params.borrowedlToken != address(0), "Invalid borrowed asset");

        // Important: Use underlying token addresses consistently
        address borrowedUnderlying = lendStorage.lTokenToUnderlying(params.borrowedlToken);

        // Verify the borrow position exists and get details
        LendStorage.Borrow[] memory userCrossChainCollaterals =
            lendStorage.getCrossChainCollaterals(params.borrower, borrowedUnderlying);
        bool found = false;

        for (uint256 i = 0; i < userCrossChainCollaterals.length;) {
            if (userCrossChainCollaterals[i].srcEid == params.srcEid) {
                found = true;
                params.storedBorrowIndex = userCrossChainCollaterals[i].borrowIndex;
                params.borrowPrinciple = userCrossChainCollaterals[i].principle;
                break;
            }
            unchecked {
                ++i;
            }
        }
        require(found, "No matching borrow position");

// <<<<<<<<< lack of the logics to call to LToken to accrue interest
        // Validate liquidation amount against close factor
@>        uint256 maxLiquidationAmount = lendStorage.getMaxLiquidationRepayAmount(
            params.borrower,
            params.borrowedlToken,
            false // cross-chain liquidation
        );
@>        require(params.repayAmount <= maxLiquidationAmount, "Exceeds max liquidation");  // <<<<<<<<<<<<<< first max amount check
    }

    function _executeLiquidation(LendStorage.LiquidationParams memory params) private {
        // First part: Validate and prepare liquidation parameters
        uint256 maxLiquidation = _prepareLiquidationValues(params);

@>        require(params.repayAmount <= maxLiquidation, "Exceeds max liquidation"); // <<<<<<<<<<<<<<<< second max amount check

        // Secon part: Validate collateral and execute liquidation
        _executeLiquidationCore(params);
    }

    function _prepareLiquidationValues(LendStorage.LiquidationParams memory params)
        private
        returns (uint256 maxLiquidation)
    {
        // Accrue interest
@>        LTokenInterface(params.borrowedlToken).accrueInterest();
        uint256 currentBorrowIndex = LTokenInterface(params.borrowedlToken).borrowIndex();

        // Calculate current borrow value with accrued interest
        uint256 currentBorrow = (params.borrowPrinciple * currentBorrowIndex) / params.storedBorrowIndex;

        // Verify repay amount is within limits
        maxLiquidation = mul_ScalarTruncate(
            Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), currentBorrow
        );

        return maxLiquidation;
    }
```

In case, the repay amount is the maximum amount from the second validation, then it may not pass the first validation. To succeed liquidation, the repay amount may need to decrease to pass the first validation (the incorrect validation) => The max liquidation amount can be less than actual

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

1. A cross chain borrow is executed, with borrow amount = 1000
2. After some time, the interest accrued and the actual borrow amount is 1020. But the states onchain is not yet updated (i.e `LToken::accrueInterest()` has not yet been called).
3. Assume max close factor = 50%, then it is expected that liquidator can repay up to 510. 
The liquidator tries to liquidate the cross chain borrow with repay amount = 1020 * 50% = 510, but it will be failed. 
He can only succeed to repay 1000* 50 % = 500

### Impact

- Max liquidation amount can be less than actual, causing the seized amount less than actual -> advantages for borrowers
- Liquidation works incorrectly

### PoC

_No response_

### Mitigation

Consider:
- Add the call `LToken(params.borrowedlToken).accrueInterest()` before calling `lendStorage.getMaxLiquidationRepayAmount()`
- Remove the second validation