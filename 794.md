Skinny Charcoal Chinchilla

High

# Partial cross-chain repay credits user with more debt

### Summary

After a cross-chain repay, the `crossChainCollaterals` on the Destination Chain, and `crossChainBorrow` on the Source Chain(where we have the collateral), mappings are updated on both chains to reflect the repay amount. On the destination chain, `CoreRouter.sol::repayBorrowInternal()` is called to take repayin funds from the borrower and to repay the Ltoken. For partial cross-chain repays, `LendStorage.sol::updateBorrowBalance()` is called, which adds the leftover borrow amount to the user's `borrowBalance`. This is wrong, as after the partial repayment, user will have the correct `crossChainCollaterals` and `crossChainBorrow` on both chain AND have the leftover amount in `borrowBalance`, as if he executed a same chain borrow on Chain B. 

### Root Cause

When handling token transfers of cross-chain repayments in [CoreRouter.sol::repayBorrowInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L459), [LendStorage.sol::updateBorrowBalance()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L241) is called no matter if its a same chain or cross chain repay. 

```solidity 
    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest();
        uint256 borrowedAmount;
        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken); 
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }
        require(borrowedAmount > 0, "Borrowed amount is 0");
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
        _approveToken(_token, _lToken, repayAmountFinal);
        lendStorage.distributeBorrowerLend(_lToken, borrower);
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
@>          lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }
```

### Internal Pre-conditions

none

### External Pre-conditions


User must partially repay a cross-chain borrow. 

### Attack Path

none

### Impact

Partial cross-chain repayments add leftover amount to the user's borrowBalance as if he had done a same-chain borrow on the Destination Chain. 


### PoC

Paste this test into src/test/TestBorrowingCrossChain.t.sol. 
This is a test showing a cross-chain borrow and partial repayment.

```solidity 
    function test_partial_crosschain_repay(uint256 amountToSupply, uint256 amountToBorrow) public {
        amountToSupply = bound(amountToSupply, 1e18, 1e30);
        vm.deal(address(routerA), 1 ether);
        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
        (address tokenB, address lTokenB) = _supplyB(amountToSupply * 2);
        uint256 maxBorrow = (lendStorageA.getMaxBorrowAmount(deployer, lTokenA) * 0.9e18) / 1e18;
        uint256 boundedBorrow = bound(amountToBorrow, 0.1e18, maxBorrow);
        address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);
        require(destToken != address(0), "Token mapping not set up correctly");
        uint256 initialTokenBalance = IERC20(destToken).balanceOf(deployer);
        vm.startPrank(deployer);
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, tokenA, boundedBorrow);
        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);
        assertEq(
            IERC20(destToken).balanceOf(deployer) - initialTokenBalance,
            boundedBorrow,
            "Should receive correct amount of borrowed tokens"
        );
        IERC20(tokenB).approve(address(coreRouterB), type(uint256).max);
        //NOTE: 31337 is chainId of Chain A
        routerB.repayCrossChainBorrow(deployer, boundedBorrow / 2, lTokenB, 31337);
        assert(lendStorageB.getBorrowBalance(deployer, lTokenB).amount > 0);
        vm.stopPrank();
    }
```


### Mitigation

Only call updateBorrowBalance() if its a same-chain repayment. 

```diff
    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest();
        uint256 borrowedAmount;
        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken); 
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }
        require(borrowedAmount > 0, "Borrowed amount is 0");
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
        _approveToken(_token, _lToken, repayAmountFinal);
        lendStorage.distributeBorrowerLend(_lToken, borrower);
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");
+       if (isSameChain) {
            if (repayAmountFinal == borrowedAmount) {
                lendStorage.removeBorrowBalance(borrower, _lToken);
                lendStorage.removeUserBorrowedAsset(borrower, _lToken);
            } else {
                lendStorage.updateBorrowBalance(
                    borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
                );
            }
+       }
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }

```
