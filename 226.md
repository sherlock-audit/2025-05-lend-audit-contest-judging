Creamy Pine Cow

High

# Borrowing twice the allowed amount is possible across both chains

### Summary

Cross chain borrows do not accurately account for the user's used collateral on the source chain - which allows borrowing up to twice the amount they should be able to, by using already used up collateral values.

### Root Cause

Users initiate cross-chain borrows on a chain A by calling [CrossChainRouter::borrowCrossChain()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113-L154), during this call a payload meant for the destination chain B is sent - this payload contains the value of the entire collateral on the source chain:

```solidity
    function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
        ...

        // Get current collateral amount for the LayerZero message
        // This will be used on dest chain to check if sufficient
        (, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
    }
```

This collateral value is used when the message is received on the [destination chain B](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L622):

```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        ...

        // Get existing borrow amount
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );

        // Verify the collateral from source chain is sufficient for total borrowed amount
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");

       ...
    }
```

The recipient chain router verifies that this collateral value does not exceed the `totalBorrowed` amount for the user on that `chain B`, but the problem lies in passing the entire collateral value of the user from the source chain to this destination chain.

On the source chain, **same** chain borrows are processed with [CoreRouter::borrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L145-L190):

```solidity
    function borrow(uint256 _amount, address _token) external {
        ...

        // Update records
        if (currentBorrow.borrowIndex != 0) {
            uint256 _newPrinciple =
                (currentBorrow.amount * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex;

            lendStorage.updateBorrowBalance(
                msg.sender, _lToken, _newPrinciple + _amount, LTokenInterface(_lToken).borrowIndex()
            );
        } else {
            lendStorage.updateBorrowBalance(msg.sender, _lToken, _amount, LTokenInterface(_lToken).borrowIndex());
        }

        lendStorage.addUserBorrowedAsset(msg.sender, _lToken);

        // Emit BorrowSuccess event
        emit BorrowSuccess(msg.sender, _lToken, lendStorage.getBorrowBalance(msg.sender, _lToken).amount);
    }

```

While both the `CoreRouter` and `CrossChainRouter` share the same `LendStorage` contract, [LendStorage::updateBorrowBalance()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L241-L248) is used exclusively by the `CoreRouter` to maintain same chain borrow accounting. From the snippet above, no cross-chain messages are sent - which means that other chains are not aware of individual same chain borrows i.e a chain B does not have any information about chain A's same-chain borrows.

Due to this limitation, the collateral amount value on the source chain is sent along with the rest of the payload, but this fails to subtract the total borrows (same-chain and initiated cross-chain borrows) on the source chain before forwarding, hence on receiving this borrow request - the destination chain B is not aware of used same-chain borrows nor previously initiated cross-chain borrows to other chains on the source chain A - and does not factor this part on executing the borrow.

Thus, if the user has maxed their CR on the source chain (same-chain and other cross-chain borrows) - cross-chain borrows can still be carried out as the users collateral amount value is sent and the destination chain has no way of knowing that the said collateral is used up on the source chain.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

For simplicity, assume:

- 1 BTC (~$100k) can be used to borrow a max of $80k USDC after all the collateral ratio and pricing is taken into account.

#### Scenario
1. Attacker supplies 1 BTC as collateral on `chain A`.
2. Attacker borrows the max amount allowed $80k USDC on `chain A`. This could entirely be same-chain borrows, or mixed with cross-chain borrows initiated from `chain A` to other `chains X, Y, Z`
3. Attacker calls [CrossChainRouter::borrowCrossChain()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113-L154).
4. The contract sends $80k as the collateral value in the destination chain's payload as that's the worth of the users collateral after all the oracle and CR calculation.
5. Destination `chain B` receives the borrow request and executes:

```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
       ...

        // Verify the collateral from source chain is sufficient for total borrowed amount
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");

        ...
    }
```

6. The condition passes as the user has not initiated any same-chain nor cross-chain borrows from this `chain B` and thus the `totalBorrowed` on the destination `chain B` would be zero.

From the above, we can see that due to passing the entire collateral value without subtracting the same-chain borrows or initiated cross-chain borrows - the destination chain executes without knowing that the user has used up to their max borrow on the source chain.

The attacker can even take it a step further by borrowing across all allowed chains:

1. Supplying 1 BTC putting a collateral worth of $80k USDC on chain A.
2. Borrowing the entire allowed $80k on chain A.
3. Initiating multiple cross-chain borrows to all other allowed chains from A. This works as the attacker has not initiated any borrows on other chains.

With this the user can borrow $80k worth of assets on each allowed chain, which is way over what he should be able to. E.g if 5 other chains are allowed, that is `$80k * 5 = $400k (cross-chain) + $80k (same-chain) = $480k`  worth of assets on $80k collateral value.

### Impact

Double/Over borrowing which puts the entire protocol at risk. Loss of funds.

### PoC

_No response_

### Mitigation

Subtract the total borrows (same-chain + initiated cross-chain borrows to other chains X, Y, Z) from the collateral value before sending the payload:

```solidity
    function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
        ...

        // Get current collateral amount for the LayerZero message
        // This will be used on dest chain to check if sufficient
        (uint256 totalBorrows, uint256 collateral) =         // @audit-fix: get same-chain and other chain borrows initiated from current chain
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        collateral = collateral - totalBorrows;                    // @audit-fix: send the actual available collateral on this chain

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
    }
```

> Note: `LendStorage::getHypotheticalAccountLiquidityCollateral()` will always return the amount of total borrows i.e same-chain and initiated cross-chain borrows from chain A to other chains X, Y, Z - and is therefore safe to use.