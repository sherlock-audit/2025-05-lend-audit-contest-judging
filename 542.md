Winning Cinnabar Gorilla

High

# User won't be able to repay cross-chain borrow

### Summary

User won't be able to repay cross-chain borrow due to that the borrowing record cannot be found.

### Root Cause

When a user repays cross-chain borrow by calling `CrossChainRouter::repayCrossChainBorrow()`, `CoreRouter::repayCrossChainLiquidation()` is triggered to handle the repayment.

[CrossChainRouter.sol#L423-L426](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L423-L426):
```solidity
    function _handleRepayment(address _borrower, address repayer, address _lToken, uint256 repayAmountFinal) private {
        // Execute the repayment
        CoreRouter(coreRouter).repayCrossChainLiquidation(_borrower, repayer, repayAmountFinal, _lToken);
    }
```

In `repayCrossChainLiquidation()`, `repayBorrowInternal()` is in turn triggered and this function checks the borrowed amount to repay.

[CoreRouter.sol#L472-L476](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L472-L476):
```solidity
        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
@>          borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }
```

The borrowed amount is calculated in `LendStorage::borrowWithInterest()`, please note as the user repays on the destination chain of the cross-chain borrow, therefore `crossChainCollaterals` is looped through to find the borrowing record.

[LendStorage.sol#L478-L503](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L478-L503):
```solidity
    function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;

        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
@>      Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
        // Only one mapping should be populated:
        if (borrows.length > 0) {
            for (uint256 i = 0; i < borrows.length; i++) {
                if (borrows[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
                }
            }
        } else {
@>          for (uint256 i = 0; i < collaterals.length; i++) {
@>              // Only include a cross-chain collateral borrow if it originated locally.
@>              if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
@>                  borrowedAmount +=
@>                      (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
@>              }
            }
        }
        return borrowedAmount;
```

As can be seen, it requires that both `destEid` and `srcEid` should be equal to `currentEid`, this is wrong because when a cross-chain borrow was processed, `destEid` was updated to `currentEid` but `srcEid` is the eid of the source chain.

[CrossChainRouter.sol#L641-L652](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L641-L652):
```solidity
            lendStorage.addCrossChainCollateral(
                payload.sender,
                destUnderlying,
                LendStorage.Borrow({
@>                  srcEid: srcEid,
@>                  destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
```

As a result, `borrowedAmount` would always be **0** and the transaction will revert.

[CoreRouter.sol#L478](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L478):
```solidity
        require(borrowedAmount > 0, "Borrowed amount is 0");
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

NA

### Impact

User cannot repay their cross-chain debt, and they will be eventually liquidated.

### PoC

Please run `forge test --mt testPOC_CannotRepayCrossChainBorrow`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import "forge-std/Test.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

import {ILayerZeroEndpointV2} from "@layerzerolabs/lz-evm-protocol-v2/contracts/interfaces/ILayerZeroEndpointV2.sol";

import {Lend} from "../src/Governance/Lend.sol";
import {Lendtroller, LendtrollerInterface} from "../src/Lendtroller.sol";
import {LErc20Immutable, LToken} from "../src/LErc20Immutable.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel, InterestRateModel} from "../src/WhitePaperInterestRateModel.sol";

import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {CrossChainRouter, Origin} from "../src/LayerZero/CrossChainRouter.sol";

contract POC is Test {
    using SafeERC20 for IERC20;

    uint32 constant chainAEid = 30101; // Ethereum
    uint32 constant chainBEid = 30184; // Base

    address owner;
    address mockDestLToken;
    address mockPeer;

    IERC20 btc;
    IERC20 dai;
    IERC20 weth;

    LErc20Immutable lTokenBTC;
    LErc20Immutable lTokenDAI;
    LErc20Immutable lTokenWETH;

    Lend lend;

    ILayerZeroEndpointV2 endpoint;

    Lendtroller lendtroller;
    SimplePriceOracle priceOracle;
    WhitePaperInterestRateModel interestRateModel;

    LendStorage lendStorage;
    CoreRouter coreRouter;
    CrossChainRouter crossChainRouter;

    function setUp() public {
        vm.createSelectFork("https://eth.drpc.org");

        owner = makeAddr("Owner");
        mockDestLToken = makeAddr("MockDestLToken");
        mockPeer = makeAddr("ChainBPeer");
        endpoint = ILayerZeroEndpointV2(
            0x1a44076050125825900e736c501f859c50fE728c
        );

        btc = IERC20(0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599);
        dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
        weth = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

        vm.startPrank(owner);

        // Deploy Lendtroller
        lendtroller = new Lendtroller();
        // Deploy Lend
        lend = new Lend(address(lendtroller));
        // Deploy PriceOracle
        priceOracle = new SimplePriceOracle();
        // Deploy InterestRateModel
        interestRateModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);
        // Deploy lTokens
        lTokenBTC = new LErc20Immutable(
            address(btc),
            lendtroller,
            interestRateModel,
            1e8, // 1:1
            "Lending BTC",
            "lBTC",
            18,
            payable(owner)
        );
        lTokenDAI = new LErc20Immutable(
            address(dai),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending DAI",
            "lDAI",
            18,
            payable(owner)
        );
        lTokenWETH = new LErc20Immutable(
            address(weth),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending WETH",
            "lWETH",
            18,
            payable(owner)
        );

        // Deploy LendStorage
        lendStorage = new LendStorage(
            address(lendtroller),
            address(priceOracle),
            chainAEid
        );
        // Deploy CoreRouter
        coreRouter = new CoreRouter(
            address(lendStorage),
            address(priceOracle),
            address(lendtroller)
        );
        // Deploy CrossChainRouter
        crossChainRouter = new CrossChainRouter(
            address(endpoint),
            owner,
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            chainAEid
        );

        // Configurations
        {
            priceOracle.setUnderlyingPrice(lTokenBTC, 100000e18);
            priceOracle.setUnderlyingPrice(lTokenDAI, 1e18);
            priceOracle.setUnderlyingPrice(lTokenWETH, 2500e18);

            lendtroller._setPriceOracle(priceOracle);
            lendtroller.setLendStorage(address(lendStorage));
            lendtroller._supportMarket(lTokenBTC);
            lendtroller._supportMarket(lTokenDAI);
            lendtroller._supportMarket(lTokenWETH);
            lendtroller.setLendToken(address(lend));
            lendtroller._setCollateralFactor(lTokenBTC, 0.8e18);
            lendtroller._setCollateralFactor(lTokenDAI, 0.8e18);
            lendtroller._setCollateralFactor(lTokenWETH, 0.8e18);
            lendtroller._setCloseFactor(0.5e18);
            lendtroller._setLiquidationIncentive(1.08e18);

            LToken[] memory lTokens = new LToken[](3);
            lTokens[0] = lTokenBTC;
            lTokens[1] = lTokenDAI;
            lTokens[2] = lTokenWETH;
            uint256[] memory supplySpeeds = new uint256[](3);
            supplySpeeds[0] = 1e18;
            supplySpeeds[1] = 1e18;
            supplySpeeds[2] = 1e18;
            uint256[] memory borrowSpeeds = new uint256[](3);
            borrowSpeeds[0] = 2e18;
            borrowSpeeds[1] = 2e18;
            borrowSpeeds[2] = 2e18;
            lendtroller._setLendSpeeds(lTokens, supplySpeeds, borrowSpeeds);

            lendStorage.addSupportedTokens(address(btc), address(lTokenBTC));
            lendStorage.addSupportedTokens(address(dai), address(lTokenDAI));
            lendStorage.addSupportedTokens(address(weth), address(lTokenWETH));
            lendStorage.addUnderlyingToDestlToken(
                address(btc),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(dai),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(weth),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.setAuthorizedContract(address(coreRouter), true);
            lendStorage.setAuthorizedContract(address(crossChainRouter), true);

            crossChainRouter.setPeer(
                chainBEid,
                bytes32(uint256(uint160(mockPeer)))
            );

            coreRouter.setCrossChainRouter(address(crossChainRouter));

            deal(address(crossChainRouter), 1 ether);
        }

        vm.stopPrank();

        {
            vm.label(address(btc), "BTC");
            vm.label(address(dai), "DAI");
            vm.label(address(weth), "WETH");
            vm.label(address(lTokenBTC), "lToken BTC");
            vm.label(address(lTokenDAI), "lToken DAI");
            vm.label(address(lTokenWETH), "lToken WETH");
            vm.label(address(endpoint), "Endpoint");
            vm.label(address(lendtroller), "Lendtroller");
            vm.label(address(lend), "Lend");
            vm.label(address(priceOracle), "PriceOracle");
            vm.label(address(lendStorage), "LendStorage");
            vm.label(address(coreRouter), "CoreRouter");
            vm.label(address(crossChainRouter), "CrossChainRouter");
        }
    }

    function testPOC_CannotRepayCrossChainBorrow() public {
        // Supply liqudity
        {
            address supplier = makeAddr("Supplier");
            deal(address(dai), supplier, 2000e18);

            vm.startPrank(supplier);
            dai.safeApprove(address(coreRouter), 2000e18);
            coreRouter.supply(2000e18, address(dai));
            vm.stopPrank();
        }

        address alice = makeAddr("Alice");

        // Mock lazyzero data to trigger _handleBorrowCrossChainRequest()
        {
            Origin memory _origin = Origin({
                srcEid: chainBEid,
                sender: bytes32(uint256(uint160(address(mockPeer)))),
                nonce: 1
            });

            bytes memory _payload = abi.encode(
                1000e18, // amount
                0, // borrowIndex
                1000e18, // collateral
                alice, // sender
                address(lTokenDAI), // destLToken
                address(0), // liquidator
                makeAddr("DAI on Base"), // srcToken
                uint8(CrossChainRouter.ContractType.BorrowCrossChain) // contractType
            );

            vm.prank(address(endpoint));
            crossChainRouter.lzReceive(
                _origin, 
                bytes32(0), 
                _payload, 
                makeAddr("Executor"), 
                ""
            );
        }

        // Reapy cross-chain borrow
        deal(address(dai), alice, 1000e18);

        vm.startPrank(alice);
        dai.safeApprove(address(coreRouter), 1000e18);
        vm.expectRevert(bytes("Borrowed amount is 0"));
        crossChainRouter.repayCrossChainBorrow(alice, 1000e18, address(lTokenDAI), chainBEid);
        vm.stopPrank();
    }
}

```

### Mitigation

It is recommended to implement a dedicated function for calculating cross-chain borrowed amount with interest.

```diff
        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
-           borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
+           borrowedAmount = lendStorage.borrowWithInterestForRepayCrossChainBorrow(borrower, _lToken);
        }
``` 

```solidity
    function borrowWithInterestForRepayCrossChainBorrow(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;

        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

        for (uint256 i = 0; i < collaterals.length; i++) {
            // Only include a cross-chain collateral borrow if it originated locally.
            if (collaterals[i].destEid == currentEid) {
                borrowedAmount +=
                    (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
            }
        }

        return borrowedAmount;
    }
```