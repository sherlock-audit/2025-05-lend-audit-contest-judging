Dapper Champagne Spider

Medium

# Hardcoded gas limit insufficient cross-chain execution on some L2 chains

### Summary

The protocol sets a hardcoded gas limit of 1,000,000 for LayerZero cross-chain operations, which may be insufficient on Arbitrum where dynamic gas estimation includes additional L1 calldata costs. This can cause transaction failures and DoS of cross-chain borrowing operations on Arbitrum.

### Root Cause

In [`CrossChainRouter._send()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L818), the gas limit is hardcoded at 1,000,000:

```solidity
bytes memory options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(1_000_000, 0);
```

On Arbitrum, transaction gas costs include both L2 execution costs and L1 calldata posting costs. The L1 component is calculated dynamically based on:

1. Transaction size after Brotli compression
2. Current L1 base fee 
3. L2 base fee ratio

Reference: https://docs.arbitrum.io/build-decentralized-apps/how-to-estimate-gas

This means actual gas consumption can exceed 1,000,000 limit because the base (computational) cost could be as high as ~650k gas, for example, for `_handleBorrowCrossChainRequest` execution before L1 costs are factored in.

### Internal Pre-conditions

None

### External Pre-conditions

1. Protocol deployed on Arbitrum 
2. High L1 base fees relative to L2 base fees
3. Large transaction payloads that don't compress well

### Attack Path

1. User initiates cross-chain borrow with large payload on Arbitrum
2. L1 calldata costs push total gas requirement above 1,000,000
3. LayerZero executor runs out of gas during `lzReceive` execution
4. Transaction fails and user's cross-chain operation is not processed
5. User funds may be locked until manual intervention

### Impact

1. Legitimate borrowing attempts fail systematically
2. Operations work on some chains but fail on others  
3. Failed transactions may require manual recovery
4. Core cross-chain functionality becomes unusable during high L1 fee periods


### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {CrossChainRouter} from "../src/LayerZero/CrossChainRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel} from "../src/WhitePaperInterestRateModel.sol";
import {LErc20Immutable} from "../src/LErc20Immutable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Origin} from "@layerzerolabs/lz-evm-oapp-v2/contracts/oapp/OApp.sol";
import "@layerzerolabs/lz-evm-protocol-v2/contracts/EndpointV2.sol";
import {SimpleMessageLib} from "@layerzerolabs/lz-evm-protocol-v2/contracts/messagelib/SimpleMessageLib.sol";
import {TreasuryMock} from "@layerzerolabs/lz-evm-protocol-v2/test/mocks/TreasuryMock.sol";

/**
 * @title TestSingleLzReceiveGas
 * @notice Measures gas consumption of single _lzReceive call for BorrowCrossChain message
 * @dev Uses actual CrossChainRouter contract, measures only destination chain processing
 */
contract TestSingleLzReceiveGas is Test {
    address public user;
    
    CrossChainRouter public router;
    LendStorage public lendStorage;
    CoreRouter public coreRouter;
    Lendtroller public lendtroller;
    SimplePriceOracle public priceOracle;
    WhitePaperInterestRateModel public interestRateModel;
    
    ERC20Mock public mockToken;
    LErc20Immutable public lToken;
    
    EndpointV2 public endpoint;
    SimpleMessageLib public simpleMessageLib;
    
    uint32 constant CURRENT_CHAIN_ID = 2; // Destination chain
    uint32 constant SOURCE_CHAIN_ID = 1;  // Source chain (where collateral exists)
    
    function setUp() public {
        user = makeAddr("user");
        vm.deal(user, 100 ether);

        // Deploy mock token
        mockToken = new ERC20Mock();
        
        // Create LayerZero endpoint and configure it properly
        endpoint = new EndpointV2(CURRENT_CHAIN_ID, address(this));
        
        // Deploy and configure SimpleMessageLib to avoid send library errors
        TreasuryMock treasuryMock = new TreasuryMock();
        simpleMessageLib = new SimpleMessageLib(address(endpoint), address(treasuryMock));
        
        // Register the message library
        endpoint.registerLibrary(address(simpleMessageLib));
        
        // Set as default send and receive library for the source chain
        endpoint.setDefaultSendLibrary(SOURCE_CHAIN_ID, address(simpleMessageLib));
        endpoint.setDefaultReceiveLibrary(SOURCE_CHAIN_ID, address(simpleMessageLib), 0);
        
        // Also set the receive library for the current chain (destination)
        endpoint.setDefaultReceiveLibrary(CURRENT_CHAIN_ID, address(simpleMessageLib), 0);
        
        // Deploy core contracts directly
        priceOracle = new SimplePriceOracle();
        
        // Deploy lendtroller
        lendtroller = new Lendtroller();
        
        // Deploy interest rate model
        uint256 baseRate = 0.1e18; // 10%
        uint256 multiplier = 0.05e18; // 5%
        interestRateModel = new WhitePaperInterestRateModel(baseRate, multiplier);
        
        // Deploy LendStorage
        lendStorage = new LendStorage(address(lendtroller), address(priceOracle), CURRENT_CHAIN_ID);
        
        // Deploy CoreRouter
        coreRouter = new CoreRouter(address(lendStorage), address(priceOracle), address(lendtroller));
        
        // Deploy the REAL CrossChainRouter (not mock)
        router = new CrossChainRouter(
            address(endpoint),
            address(this), // owner
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            CURRENT_CHAIN_ID
        );
        
        // Deploy lToken
        lToken = new LErc20Immutable(
            address(mockToken),
            lendtroller,
            interestRateModel,
            1e18, // initial exchange rate
            "Lend Mock Token",
            "lMOCK",
            8, // decimals
            payable(address(this)) // admin
        );
        
        // Configure the system
        _configureLendingProtocol();
        
        // Set up peer for cross-chain messaging
        _setupPeer();
        
        // Provide liquidity
        _setupLiquidity();
    }

    function _configureLendingProtocol() internal {
        // Set authorized contracts
        lendStorage.setAuthorizedContract(address(coreRouter), true);
        lendStorage.setAuthorizedContract(address(router), true);
        
        // Configure lendtroller FIRST
        lendtroller.setLendStorage(address(lendStorage));
        lendtroller._setPriceOracle(priceOracle);
        lendtroller._setLiquidationIncentive(1.08e18); // 8% liquidation incentive
        lendtroller._setCloseFactor(0.5e18); // 50% close factor
        
        // Set token price BEFORE supporting market and setting collateral factor
        priceOracle.setDirectPrice(address(mockToken), 1e18); // $1 per token
        
        // Support market in lendtroller BEFORE setting collateral factor
        lendtroller._supportMarket(lToken);
        
        // Verify the lToken underlying price is set correctly
        uint256 tokenPrice = priceOracle.getUnderlyingPrice(lToken);
        require(tokenPrice > 0, "Token price not set correctly");
        
        // Now set collateral factor - this should work since market is supported and price is set
        uint256 collateralFactorMantissa = 0.75e18; // 75% collateral factor
        lendtroller._setCollateralFactor(lToken, collateralFactorMantissa);
        
        // Verify the collateral factor was set correctly
        uint256 actualCollateralFactor = lendtroller.getCollateralFactorMantissa(address(lToken));
        require(actualCollateralFactor == collateralFactorMantissa, "Collateral factor not set correctly");
        
        // Add token to LendStorage
        lendStorage.addSupportedTokens(address(mockToken), address(lToken));
        
        // Connect CoreRouter to CrossChainRouter
        coreRouter.setCrossChainRouter(address(router));
        
        // Fund the CrossChainRouter with ETH for LayerZero fees
        vm.deal(address(router), 1 ether);
    }

    function _setupPeer() internal {
        // Set peer for source chain so messages are accepted
        bytes32 peerAddress = bytes32(uint256(uint160(address(0x123)))); // Mock source router address
        router.setPeer(SOURCE_CHAIN_ID, peerAddress);
    }

    function _setupLiquidity() internal {
        // Create a separate liquidity provider
        address liquidityProvider = makeAddr("liquidityProvider");
        uint256 liquidityAmount = 1000e18; // Large liquidity pool
        
        // Mint tokens and provide liquidity via liquidityProvider
        vm.startPrank(liquidityProvider);
        mockToken.mint(liquidityProvider, liquidityAmount);
        IERC20(mockToken).approve(address(coreRouter), liquidityAmount);
        coreRouter.supply(liquidityAmount, address(mockToken));
        vm.stopPrank();
        
        // Give CoreRouter some collateral by having it supply tokens
        uint256 coreRouterCollateral = 100e18;
        mockToken.mint(address(coreRouter), coreRouterCollateral);
        
        vm.startPrank(address(coreRouter));
        IERC20(mockToken).approve(address(coreRouter), coreRouterCollateral);
        coreRouter.supply(coreRouterCollateral, address(mockToken));
        
        // The CoreRouter itself needs to be in the market to borrow
        address[] memory lTokens = new address[](1);
        lTokens[0] = address(lToken);
        lendtroller.enterMarkets(lTokens);
        vm.stopPrank();
    }

    /**
     * @notice Test gas consumption of _lzReceive for BorrowCrossChain message
     * @dev Measures gas for processing a single incoming LayerZero message
     */
    function testGas_LzReceive_BorrowCrossChain_SingleTx() public {
        uint256 borrowAmount = 1e16; // Much smaller amount: 0.01 token
        uint256 collateralValue = 10e18; // Still large collateral from source chain
        
        address mockSourceToken = makeAddr("sourceToken");
        
        // Create LayerZero payload for BorrowCrossChain
        bytes memory payload = abi.encode(
            borrowAmount,        // amount to borrow
            0,                   // borrowIndex (initial)
            collateralValue,     // collateral from source chain
            user,                // borrower
            address(lToken),     // destination lToken
            address(0),          // liquidator (not applicable)
            mockSourceToken,     // source token address
            uint8(0)             // contractType: BorrowCrossChain
        );
        
        // Create LayerZero Origin struct
        Origin memory origin = Origin({
            srcEid: SOURCE_CHAIN_ID,
            sender: bytes32(uint256(uint160(address(0x123)))), // Must match the peer address
            nonce: uint64(1) // Use nonce 1 instead of 0
        });
        
        bytes32 guid = bytes32(uint256(1));

        // Encode the complete function call data
        bytes memory fullTxData = abi.encodeWithSelector(
            endpoint.lzReceive.selector,
            origin,
            address(router),     // receiver (the OApp)
            guid,                // guid
            payload,             // message
            bytes("")            // extraData
        );
        
        console2.log("COMPLETE RAW TRANSACTION DATA TO SEND:");
        console2.logBytes(fullTxData);
        
        bytes32 payloadHash = keccak256(abi.encodePacked(guid, payload));
        
        vm.prank(address(simpleMessageLib)); // Verify call should also come from receive library
        endpoint.verify(origin, address(router), payloadHash);
        
        // Send the raw transaction data using low-level call
        vm.prank(address(simpleMessageLib)); // Endpoint expects call from receive library
        uint256 gasStart = gasleft();
        
        (bool success, bytes memory returnData) = address(endpoint).call(fullTxData);
        
        uint256 gasUsed = gasStart - gasleft();
        
        require(success, "Raw transaction call failed");
        
        console2.log("Gas used:", gasUsed);
        
        // Verify gas measurement is reasonable
        assertGt(gasUsed, 10000, "Gas should be > 10k for real processing");
        assertLt(gasUsed, 1000000, "Gas should be < 1M for efficient processing");
    }
}
```

Output:
```text
Ran 1 test for test/TestSingleLzReceiveGas.t.sol:TestSingleLzReceiveGas
[PASS] testGas_LzReceive_BorrowCrossChain_SingleTx() (gas: 670809)
Logs:
  COMPLETE RAW TRANSACTION DATA TO SEND:
  0x0c0c389e000000000000000000000000000000000000000000000000000000000000000100000000000000000000000000000000000000000000000000000000000001230000000000000000000000000000000000000000000000000000000000000001000000000000000000000000d6bbde9174b1cdaa358d2cf4d57d1a9f7178fbff000000000000000000000000000000000000000000000000000000000000000100000000000000000000000000000000000000000000000000000000000000e000000000000000000000000000000000000000000000000000000000000002000000000000000000000000000000000000000000000000000000000000000100000000000000000000000000000000000000000000000000002386f26fc1000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000008ac7230489e800000000000000000000000000006ca6d1e2d5347bfab1d91e883f1915560e09129d00000000000000000000000015cf58144ef33af1e14b5208015d11f9143e27b90000000000000000000000000000000000000000000000000000000000000000000000000000000000000000c283f6085a391df8e3f78ddc49c6b4701612283800000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
  Gas used: 647331
```

**Note**: The coded PoC shows only the L2 component of gas estimation (i.e., computational costs). To obtain the full transaction gas limit on Arbitrum chains, the calldata costs of posting it on L1 (Ethereum) must also be factored in. See details here https://docs.arbitrum.io/build-decentralized-apps/how-to-estimate-gas


### Mitigation

Implement dynamic gas limit calculation that accounts for L1 costs on Arbitrum, or use a higher static limit (e.g., 2,000,000) for all chains, or allow configuration per destination chain based on their specific gas mechanics. 