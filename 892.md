Acrobatic Fleece Trout

High

# The Cross-Chain borrowing doesn't work for users with account abstraction wallets

## Summary

The users who use Account Abstraction wallets have a different address across different chains for the same account. A user with an account abstraction wallet calls `borrowCrossChain` on the source chain. The implementation uses `msg.sender` as the address to receive tokens on the destination chain. The protocol assumes identical wallet addresses across source and destination chains, but this is not true in case of account abstraction wallets and the user will lose his/her borrowed funds.

## Root Cause

The user calls the [`CrossChainRouter::borrowCrossChain`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113C5-L154C6) function to initiate a borrow on source chain (Chain A):

```solidity

    function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
        require(msg.sender != address(0), "Invalid sender");
        require(_amount != 0, "Zero borrow amount");
        require(address(this).balance > 0, "Out of money");

        // Get source lToken for collateral
        address _lToken = lendStorage.underlyingTolToken(_borrowToken);
        require(_lToken != address(0), "Unsupported source token");

        // Get the destination chain's version of the token
        address destLToken = lendStorage.underlyingToDestlToken(_borrowToken, _destEid);
        require(destLToken != address(0), "Unsupported destination token");

        // Accrue interest on source token (collateral token) on source chain
        LTokenInterface(_lToken).accrueInterest();

        // Add collateral tracking on source chain
        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        if (!isMarketEntered(msg.sender, _lToken)) {
            enterMarkets(_lToken);
        }

        // Get current collateral amount for the LayerZero message
        // This will be used on dest chain to check if sufficient
        (, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
    }

```

The function builds a LayerZero message payload (sets the `msg.sender` as sender address) and calls `_send` function with `ContractType.BorrowCrossChain`:

```solidity

    function _send(
        uint32 _dstEid,
        uint256 _amount,
        uint256 _borrowIndex,
        uint256 _collateral,
        address _sender,
        address _destlToken,
        address _liquidator,
        address _srcToken,
        ContractType ctype
    ) internal {
        bytes memory payload =
            abi.encode(_amount, _borrowIndex, _collateral, _sender, _destlToken, _liquidator, _srcToken, ctype);

        bytes memory options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(1_000_000, 0);

@>      _lzSend(_dstEid, payload, options, MessagingFee(address(this).balance, 0), payable(address(this)));
    }

```

This function sends the message via `LayerZero` and dispatches the message to the destination chain (Chain B). Then the function `_lzReceive` is called automatically by LayerZeroâ€™s receiver adapter on the destination chain:

```solidity

function _lzReceive(
        Origin calldata _origin,
        bytes32, /*_guid*/
        bytes calldata _payload,
        address, /*_executor*/
        bytes calldata /*_extraData*/
    ) internal override {
        LZPayload memory payload;

        // Decode individual fields from payload
        (
            payload.amount,
            payload.borrowIndex,
            payload.collateral,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            payload.contractType
        ) = abi.decode(_payload, (uint256, uint256, uint256, address, address, address, address, uint8));

        uint32 srcEid = _origin.srcEid;
        ContractType cType = ContractType(payload.contractType);
        // Handle different message types
        if (cType == ContractType.BorrowCrossChain) {
@>          _handleBorrowCrossChainRequest(payload, srcEid);
        } else if (cType == ContractType.ValidBorrowRequest) {
            _handleValidBorrowRequest(payload, srcEid);
        } else if (cType == ContractType.DestRepay) {
            _handleDestRepayMessage(payload, srcEid);
        } else if (cType == ContractType.CrossChainLiquidationExecute) {
            if (_checkLiquidationValid(payload)) {
                _handleLiquidationExecute(payload, srcEid);
            } else {
                _sendLiquidationFailure(payload, srcEid);
            }
        } else if (cType == ContractType.LiquidationSuccess) {
            _handleLiquidationSuccess(payload);
        } else if (cType == ContractType.LiquidationFailure) {
            _handleLiquidationFailure(payload);
        } else {
            revert("Invalid contract type");
        }
    }


```

The function also calls the `_handleBorrowCrossChainRequest` function that calls the `CoreRouter.borrowForCrossChain` function to mint/transfer the borrowed tokens.

```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        // Accrue interest on borrowed token on destination chain
        LTokenInterface(payload.destlToken).accrueInterest();

        // Get current borrow index from destination lToken
        uint256 currentBorrowIndex = LTokenInterface(payload.destlToken).borrowIndex();

        // Important: Use the underlying token address
        address destUnderlying = lendStorage.lTokenToUnderlying(payload.destlToken);

        // Check if user has any existing borrows on this chain
        bool found = false;
        uint256 index;

        LendStorage.Borrow[] memory userCrossChainCollaterals =
            lendStorage.getCrossChainCollaterals(payload.sender, destUnderlying);

        /**
         * Filter collaterals for the given srcEid. Prevents borrowing from
         * multiple collateral sources.
         */
        for (uint256 i = 0; i < userCrossChainCollaterals.length;) {
            if (
                userCrossChainCollaterals[i].srcEid == srcEid
                    && userCrossChainCollaterals[i].srcToken == payload.srcToken
            ) {
                index = i;
                found = true;
                break;
            }
            unchecked {
                ++i;
            }
        }

        // Get existing borrow amount
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );

        // Verify the collateral from source chain is sufficient for total borrowed amount
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");

        // Execute the borrow on destination chain
@>      CoreRouter(coreRouter).borrowForCrossChain(payload.sender, payload.amount, payload.destlToken, destUnderlying);

        .....

}

```

The problem is that the `borrower` parameter is set to be `payload.sender` that is the caller of the `borrowCrossChain` function on the source chain (Chain A). And if the user uses account abstraction wallet this address will not be the address owned by the caller and the caller will lose his/her borrowed amount:

```solidity

    function borrowForCrossChain(address _borrower, uint256 _amount, address _destlToken, address _destUnderlying)
        external
    {
        require(crossChainRouter != address(0), "CrossChainRouter not set");

        require(msg.sender == crossChainRouter, "Access Denied");

        require(LErc20Interface(_destlToken).borrow(_amount) == 0, "Borrow failed");

@>      IERC20(_destUnderlying).transfer(_borrower, _amount);
    }

```

## Impact

The user who uses account abstraction wallet is unable to receive the borrowed funds, as they are sent to a non-recoverable address on the destination chain.

## Mitigation

Use an explicit recipient address parameter in `borrowCrossChain` function and verify its ownership off-chain or through signature proof. Alternatively, require a cross-chain identity registry or alias mapping system to ensure address consistency.
