Fun Shadow Parakeet

Medium

# `CoreRouter::supply` uses a stale `exchangeRateStored` which causes internal accounting mismatches

### Summary

`CoreRouter::supply` reads the `lToken` exchange rate before calling `LErc20::mint`. But inside `mint`, the protocol first updates internal state by calling `accrueInterest`, which usually increases the exchange rate. So when the router uses the old rate to calculate `mintTokens`, it overestimates how many tokens the user should get. This leads to a mismatch between `lendStorage` records and the actual number of `lTokens` minted.

### Root Cause

Inside [`LErc20::mint`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LErc20.sol#L53-L56), the function first calls `accrueInterest` before performing the actual minting via `mintFresh`:

```solidity
    function mintInternal(uint256 mintAmount) internal nonReentrant {
@>      accrueInterest();
        // mintFresh emits the actual Mint event if successful and logs on errors, so we don't need to
@>      mintFresh(msg.sender, mintAmount);
    }
```

[`accrueInterest`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L368-L371) updates key state variables such as `totalBorrows` and `totalReserves`, which normally increases the exchange rate used in `mintFresh`:

```solidity
        accrualBlockNumber = currentBlockNumber;
        borrowIndex = borrowIndexNew;
        totalBorrows = totalBorrowsNew;
        totalReserves = totalReservesNew;
```

Then in [`mintFresh`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L408), it calculates the exchange rate as follows:

```solidity
        Exp memory exchangeRate = Exp({mantissa: exchangeRateStoredInternal()});
```

[`exchangeRateStoredInternal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L305) uses the updated variables to compute the new exchange rate:

```solidity
    function exchangeRateStoredInternal() internal view virtual returns (uint256) {
        uint256 _totalSupply = totalSupply;
        if (_totalSupply == 0) {
            /*
             * If there are no tokens minted:
             *  exchangeRate = initialExchangeRate
             */
            return initialExchangeRateMantissa;
        } else {
            /*
             * Otherwise:
             *  exchangeRate = (totalCash + totalBorrows - totalReserves) / totalSupply
             */
            uint256 totalCash = getCashPrior();
            uint256 cashPlusBorrowsMinusReserves = totalCash + totalBorrows - totalReserves;
@>          uint256 exchangeRate = cashPlusBorrowsMinusReserves * expScale / _totalSupply;

            return exchangeRate;
        }
    }
```
This `determines` the actual number of lTokens minted inside the mint function.

However, in [`CoreRouter::supply`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L74-L89), the contract reads the exchange rate before calling `mint`, and uses that stale rate to calculate the user’s `lToken` balance:

```solidity
@>      uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        // Mint lTokens
@>       require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

        // Calculate actual minted tokens using exchangeRate from before mint
        uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;

        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        lendStorage.distributeSupplierLend(_lToken, msg.sender);

        // Update total investment using calculated mintTokens
@>      lendStorage.updateTotalInvestment(
            msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
        );
```
Because `exchangeRateBefore` is normally lower, mintTokens is overstated. As a result, the user’s balance in `lendStorage` exceeds the actual `lTokens` held by the router, leading to a mismatch between `lendStorage` and the `lToken` contract.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

No special setup is needed. Any `CoreRouter::supply` call between interest-accruing blocks can cause the mismatch. A whale can repeatedly supply assets to amplify the impact.

### Impact

Unsynchronized accounting across contracts may overstate user assets and potentially lead to protocol losses.

### PoC

Consider the following scenario:
1. A user calls `CoreRouter::supply` with `1000 TokenA`.
2. Inside `supply`, it reads `exchangeRateStored = 1.0` and calculates `mintTokens = (1000 * 1e18 * 1e18) / 1e18 = 1000e18`.
3. However, `LToken::mint` internally calls `accrueInterest`, which updates the exchange rate to `1.1` before minting, The actual mint becomes: `mintTokens = (1000 * 1e18 * 1e18) / 1.1e18 = 909e18`.
4. As a result, although only `~909e18` are actually minted by `lToken`, `lendStorage` still records `1000e18` for the user.
This leads to an accounting mismatch between `lendStorage` and `lToken`, and effectively causes a protocol-level loss of `~90 lTokens`.

### Mitigation

Consider recording the actual number of `lTokens` minted by measuring the balance difference before and after minting, instead of relying on a potentially outdated exchange rate.