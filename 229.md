Creamy Pine Cow

High

# Cross-chain liquidations will always revert as the accounted collateral info will never be found during liquidation

### Summary

Legitimate cross-chain liquidations can never be performed as the cross-chain collateral would never found due to wrongly passed search parameters.

### Root Cause

If users make cross-chain borrows from a chain A to a chain B, liquidations on those borrows are triggered by calling [CrossChainRouter::liquidateCrossChain()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L172-L192) on the borrow execution chain B.

A few messages are shared back and forth between chains A and B. During this exchange, [CrossChainRouter::_handleLiquidationSuccess()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443-L471) is called on chain B (i.e the borrow execution chain):

```solidity
    function _handleLiquidationSuccess(LZPayload memory payload) private {
        // Find the borrow position on Chain B to get the correct srcEid
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);

        // Find the specific collateral record
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            currentEid, // srcEid is current chain                            // @audit: wrong chain parameter
            0, // We don't know destEid yet, but we can match on other fields  // @audit: misleading comment and wrong chain parameter
            payload.destlToken,
            payload.srcToken
        );

        require(found, "Borrow position not found");

        ...
    }
```

The problem lies in the chain ID values passed to the function [LendStorage::findCrossChainCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L683-L705)

```solidity
    function findCrossChainCollateral(
        address user,
        address underlying,
        uint256 srcEid,
        uint256 destEid,
        address borrowedlToken,
        address srcToken
    ) public view returns (bool, uint256) {
        Borrow[] memory userCollaterals = crossChainCollaterals[user][underlying];

        for (uint256 i = 0; i < userCollaterals.length;) {
            if (
                userCollaterals[i].srcEid == srcEid && userCollaterals[i].destEid == destEid
                    && userCollaterals[i].borrowedlToken == borrowedlToken && userCollaterals[i].srcToken == srcToken
            ) {
                return (true, i);
            }
            unchecked {
                ++i;
            }
        }
        return (false, 0);
    }
```

The function call wrongly passes `currentEid (current chain id)` as the source chain, and also wrongfully assumes that `destEid (destination chain id)` is not known and thus passes zero while misleading with the comment that other parameters can be still matched - this is false as the above code snippet shows that all parameters must be matched. `Chain B` is where this is executed, and during the cross-chain borrow accounting, here is how the values are populated on `chain B` itself:

```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        ...

        /**
         * @dev If existing cross-chain collateral, update it. Otherwise, add new collateral.
         */
        if (found) {
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            lendStorage.updateCrossChainCollateral(
                payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
        } else {
            lendStorage.addCrossChainCollateral(
                payload.sender,
                destUnderlying,
                LendStorage.Borrow({
                    srcEid: srcEid,
                    destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
        }

        ...
    }
```

During creation both of those values are known, as the `srcEid` is the cross-chain borrow initiator - `chain A`, and the `destEid` is the current cross-chain borrow executing `chain B`.

So by passing the current executing `chain B` as `srcEid` instead of `destEid`, and also wrongfully passing `destEid` as zero - this ensures that the recorded cross-chain collateral on `chain B` will never be found, and will therefore always return false due to the stringent comparisons done by `LendStorage::findCrossChainCollateral()`. This causes the `require` statement to always revert:

```solidity
    function _handleLiquidationSuccess(LZPayload memory payload) private {
        // Find the borrow position on Chain B to get the correct srcEid
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);

        // Find the specific collateral record
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            currentEid, // srcEid is current chain                            // @audit: wrong chain parameter
            0, // We don't know destEid yet, but we can match on other fields  // @audit: misleading comment and wrong chain parameter
            payload.destlToken,
            payload.srcToken
        );

        require(found, "Borrow position not found");    // @audit: will always fail

        ...
    }
```

Therefore cross-chain liquidations can never be completed, thus harming the health of the entire protocol and breaking an invariant that should be held.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. User Bob deposits collateral on `chain A`, and performs a cross-chain borrow from `chain A` to `chain B`.
2. Bob's position becomes open for liquidation.
3. Liquidator Alex attempts to perform a liquidation on Bob's cross-chain position on `chain B`.
4. Liquidation always fails halfway because Bob's cross-chain collateral accounting on `chain B` will never be found at that stage.

### Impact

Entire protocol health is at risk as cross-chain positions cannot ever be liquidated.

### PoC

_No response_

### Mitigation

Pass the current executing chain id as the `destEid` since it is where the main borrow was issued and stored. Also add the source chain id from the payload like it was done in other functions:

```solidity
    function _handleLiquidationSuccess(LZPayload memory payload, uint32 srcEid) private {  // @audit-fix: pass the source chain ID
        // Find the borrow position on Chain B to get the correct srcEid
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);

        // Find the specific collateral record
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            srcEid,                                        // @audit-fix: chain that sent the LZ payload
            currentEid,                               // @audit-fix: the current chain is the destination chain id, as it was set during the initial cross-chain accounting
            payload.destlToken,
            payload.srcToken
        );

        require(found, "Borrow position not found");

        ...
    }
```