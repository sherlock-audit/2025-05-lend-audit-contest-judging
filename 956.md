Fierce Eggplant Dalmatian

High

# Double scaling of borrowed amount via `borrowIndex`

## Title
Double scaling of borrowed amount via `borrowIndex`

## Summary
In both the `borrow` and `liquidateBorrowAllowedInternal` functions, the borrowed amount is incorrectly scaled a second time using `borrowIndex`, even though it has already been adjusted during the call to `getHypotheticalAccountLiquidityCollateral`. This leads to overestimation of the borrow amount, which could cause users being denied valid borrow requests and premature liquidations based on inflated shortfall assumptions.

## Root Cause
In [`borrow`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L145-L190) function, first scaling is done in a call to [`getHypotheticalAccountLiquidityCollateral`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L385-L467) function. This scaling is done in (same-chain borrow)[`borrowWithInterestSame`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L509-L515) and (cross-chain borrow) [`borrowWithInterest`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L478-L504)
same-chain borrow
```solidity
        uint256 borrowBalanceSameChain = borrowIndex != 0
            ? (borrowBalance[borrower][_lToken].amount * uint256(LTokenInterface(_lToken).borrowIndex())) / borrowIndex
            : 0;
```
cross-chain borrow
```solidity
        borrowedAmount +=
            (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
```

The return scaled borrowed value is then scaled again in the `borrow` function as follows
```solidity
        uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex) 
            : 0;
```
This same issue is also observed in [`liquidateBorrowAllowedInternal`]() function where it is first scaled in the call to [`getHypotheticalAccountLiquidityCollateral`](), which returns the scaled `borrowed` value which is passed to `liquidateBorrowAllowedInternal` as a parameter. This `borrowed` parameter is then scaled again.
```solidity
    function liquidateBorrowAllowedInternal(
        address payable lTokenBorrowed,
        address borrower,
        uint256 repayAmount,
        uint256 collateral,
>>      uint256 borrowed
    ) internal view returns (uint256) {
        //...SNIP...

            // For same-chain liquidations, calculate borrowed amount using the borrowBalance's index
>>          borrowedAmount =
                (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex; 
        //...SNIP...
```
As seen, initial liquidity check returns properly scaled debt. Liquidation validation needlessly re-applies scaling

## Internal Pre-Conditions
1. Existing borrow positions with accrued interest
2. Non-zero time elapsed since last index update
3. Protocol calls either:
   - `borrow()` function for new loans
   - `liquidateBorrowAllowedInternal()` for liquidations

## External Pre-Conditions
None

## Attack Path
1. User calls `getMaxBorrowAmount` function to determine the maximum amount they can borrow of a specific asset
2. User then calls the `borrow` function
3. The call fails since `borrowed` is greater than `collateral` due to double scaling

## Impact
The borrower's debt is overestimated therefore users with healthy collateral ratios may appear undercollateralized and this will results in blocked borrows even when they have enough collateral or users being wrongly liquidated.

## POC
Add the following `test in test/TestBorrowing.t.sol`;
```solidity
    function test_double_scaling_in_borrow_calculation_prevents_maxBorrowAmount(uint256 amount) public {
        vm.assume(amount > 1e18 && amount < 1e36);
        vm.startPrank(deployer);

        // Supply collateral
        (address token, address lToken) = _supply(amount);

        uint256 borrowAmount = (amount * 40) / 100; // 40% of collateral 

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, lToken, borrowAmount);

        // user makes first borrow
        coreRouter.borrow(borrowAmount, token);

        // Verify the borrow succeeded
        assertEq(
            lendStorage.borrowWithInterestSame(deployer, lToken),
            borrowAmount
        );

        // Advance some blocks to accrue interest
        vm.warp(block.timestamp + 1000);
        vm.roll(block.number + 1000);

        // get maxBorrow now
        uint256 maxBorrow = lendStorage.getMaxBorrowAmount(deployer, lToken);

        // attempt this borrow: expect revert 
        vm.expectRevert(bytes("Insufficient collateral"));
        coreRouter.borrow(maxBorrow, token);

        vm.stopPrank();
    }
```

## Mitigation
Avoid re-scaling the `borrowed` amount in both locations. Instead, use it directly