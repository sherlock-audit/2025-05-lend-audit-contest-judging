Sour Cloth Ladybug

High

# `CrossChainRouter` contract will quickly drain its ETH balance due to flaw in LayerZero native gas payment mechanism

### Summary

The [`CrossChainRouter._send()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L804) function attempts to use the entire current ETH balance of the contract (`address(this).balance`) as the `nativeFee` for every single LayerZero message. This will lead to excessive fee payments, rapid depletion of contract ETH, and likely transaction failures or denial of service for cross-chain operations.

### Root Cause

-   **`CrossChainRouter.sol`**: The [`_send()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L804) function, specifically the line where [`MessagingFee`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L820) is constructed:
    `_lzSend(_dstEid, payload, options, MessagingFee(address(this).balance, 0), payable(address(this)));`
-   **`CrossChainRouter.sol`**: The [`_payNative(uint256 _nativeFee)`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L794) override which is passed `address(this).balance` as `_nativeFee` from [`_lzSend`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L804)'s `MessagingFee` argument and attempts to use it, after a balance check.

Here are the steps that lead to the issue:
1.  The internal [`_send()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L804) function is called to dispatch a LayerZero message.
2.  The [`MessagingFee`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L820) is constructed with `msgValue = address(this).balance`.
3.  `_lzSend` is called. The LayerZero `OApp`'s [`_payNative(uint256 _nativeFee)`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L794) (which is overridden in this contract) will be invoked.
4.  `CrossChainRouter._payNative` checks if `address(this).balance < _nativeFee`. Since `_nativeFee` *is* `address(this).balance` from the `MessagingFee` struct, this check becomes `address(this).balance < address(this).balance`, which is false. Thus, it attempts to use the entire contract balance as the fee.
   

### Internal Pre-conditions

-   The `CrossChainRouter` contract must have some ETH balance to attempt to send any LayerZero message.
-   Any function that calls the internal `_send()` function (e.g., `borrowCrossChain`, handlers in `_lzReceive` that send acknowledgments or further messages) will trigger this behavior.


### External Pre-conditions

-   LayerZero endpoint (`endpoint`) is operational.


### Attack Path

This is less of an attack path by a malicious user and more of a fundamental operational flaw as 
any cross-chain function invocation (e.g., `borrowCrossChain`, `repayCrossChainBorrow` which calls `_updateRepaymentState` -> `_send`, or handlers in `_lzReceive` like `_handleLiquidationExecute` -> `_send`) can lead to excessive fee payments.

If the contract has more ETH than the actual LayerZero cost, it pays a massively inflated fee, draining its ETH.
On the other hand, if `address(this).balance` (while being used as the fee) is still less than what LayerZero *actually* required for the specific message (e.g., if the contract has very little ETH), the LayerZero send operation might fail or the message could get stuck. The `OApp._lzSend` itself might also revert if the `msgValue` provided in `MessagingFee` is insufficient for its internal logic before even reaching the endpoint.


### Impact


The contract will quickly drain its ETH balance paying for LayerZero messages, making cross-chain functionality unsustainable without constant, large ETH top-ups.
In addition, once the ETH balance is depleted or insufficient for even the (overstated) fee attempt, all cross-chain message sending will halt, breaking all cross-chain functionalities (borrows, repays, liquidations).

### PoC

- First, in order to demonstrate the issue, we'll need to modify the [`CrossChainRouterMock`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/test/mocks/CrossChainRouterMock.sol) contract to simulate the real [`CrossChainRouter`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol) contract by adding the `_payNative` function as below:

```diff
diff --git a/Lend-V2/test/mocks/CrossChainRouterMock.sol b/Lend-V2/test/mocks/CrossChainRouterMock.sol
index e1efb3a..21c50c2 100644
--- a/Lend-V2/test/mocks/CrossChainRouterMock.sol
+++ b/Lend-V2/test/mocks/CrossChainRouterMock.sol
@@ -68,6 +68,10 @@ contract CrossChainRouterMock is Ownable, ExponentialNoError {
     event RepaySuccess(address repayBorrowPayer, address lToken, uint256 repayBorrowAccountBorrows);
     event BorrowSuccess(address indexed borrower, address indexed token, uint256 accountBorrow);
 
+    event PayNativeAmountTransferred(address indexed sender, uint256 nativeFee);
+
+    error NotEnoughNative(uint256 msgValue);
+
     /**
      * @notice Constructor initializes the contract with required addresses
      * @param _lendStorage LendStorage contract address
@@ -815,7 +819,7 @@ contract CrossChainRouterMock is Ownable, ExponentialNoError {
         uint32 _dstChainId,
         bytes memory _payload,
         bytes memory, // options
-        MessagingFee memory, // messagingFee
+        MessagingFee memory _fee, // messagingFee
         address payable // refundAddress
     ) internal {
         // Create mock Origin struct
@@ -825,6 +829,9 @@ contract CrossChainRouterMock is Ownable, ExponentialNoError {
             nonce: 0
         });
 
+        uint256 messageValue = _payNative(_fee.nativeFee);
+        emit PayNativeAmountTransferred(msg.sender, messageValue);
+
         // Call _lzReceive directly on pair contract
         pairContract._lzReceive(
             origin,
@@ -834,4 +841,18 @@ contract CrossChainRouterMock is Ownable, ExponentialNoError {
             bytes("") // extraData
         );
     }
+
+    /**
+     * @dev Internal function to pay the native fee associated with the message.
+     * @param _nativeFee The native fee to be paid.
+     * @return nativeFee The amount of native currency paid.
+     *
+     * @dev Overridden to use contract balance instead of msg.value.
+     */
+    function _payNative(uint256 _nativeFee) internal virtual returns (uint256 nativeFee) {
+        if (address(this).balance < _nativeFee) {
+            revert NotEnoughNative(address(this).balance);
+        }
+        return _nativeFee;
+    }
 }

```
As depicted in the diff, we also add a new event `PayNativeAmountTransferred` and modify the mock [`_lzSend`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/test/mocks/CrossChainRouterMock.sol#L814) function to log the native fee received, exactly as being done in the original `_lzSend` function implemented on the contract [`OAppSender`](https://github.com/LayerZero-Labs/LayerZero-v2/blob/88428755be6caa71cb1d2926141d73c8989296b5/packages/layerzero-v2/evm/oapp/contracts/oapp/OAppSender.sol#L82) by LayerZero.

- Second, the following diff can be applied to file [`TestBorrowingCrossChain.t.sol`](`https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/test/TestBorrowingCrossChain.t.sol`)

```diff
diff --git a/Lend-V2/test/TestBorrowingCrossChain.t.sol b/Lend-V2/test/TestBorrowingCrossChain.t.sol
index 95694e2..5776c97 100644
--- a/Lend-V2/test/TestBorrowingCrossChain.t.sol
+++ b/Lend-V2/test/TestBorrowingCrossChain.t.sol
@@ -55,6 +55,7 @@ contract TestBorrowingCrossChain is LayerZeroTest {
 
     // Events to test
     event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);
+    event PayNativeAmountTransferred(address indexed sender, uint256 nativeFee);
 
     function setUp() public override(LayerZeroTest) {
         super.setUp();
@@ -235,6 +236,50 @@ contract TestBorrowingCrossChain is LayerZeroTest {
         vm.stopPrank();
     }
 
+    function test_that_cross_chain_borrowing_send_full_balance(uint256 amountToSupply, uint256 amountToBorrow) public {
+        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
+        amountToSupply = bound(amountToSupply, 1e18, 1e30);
+
+        // Fund Router A with ETH for LayerZero fees
+        vm.deal(address(routerA), 1 ether);
+
+        // First supply tokens as collateral on Chain A
+        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
+
+        // Then supply tokens as borrowable on Chain B
+        // @note - Has to be enough tokens to cover the borrow on the destination chain...
+        _supplyB(amountToSupply * 2);
+
+        // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
+        uint256 maxBorrow = (lendStorageA.getMaxBorrowAmount(deployer, lTokenA) * 0.9e18) / 1e18;
+
+        uint256 boundedBorrow = bound(amountToBorrow, 0.1e18, maxBorrow);
+
+        // Verify token mappings
+        address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);
+
+        require(destToken != address(0), "Token mapping not set up correctly");
+
+        // Get initial balances
+        uint256 initialTokenBalance = IERC20(destToken).balanceOf(deployer);
+
+        vm.startPrank(deployer);
+
+        // Expect PayNativeAmountTransferred event with the whole routerA's balance being transferred as nativeFee
+        vm.expectEmit(true, true, true, true);
+        emit PayNativeAmountTransferred(address(routerA), address(routerA).balance);
+        // Call borrowCrossChain with token address
+        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);
+
+        // Verify the borrow was successful
+        assertEq(
+            IERC20(destToken).balanceOf(deployer) - initialTokenBalance,
+            boundedBorrow,
+            "Should receive correct amount of borrowed tokens"
+        );
+
+        vm.stopPrank();
+    }
     function test_that_a_user_can_only_borrow_up_to_their_allowed_capacity(
         uint256 amountToSupply,
         uint256 amountToBorrow

```
- Finally, the test case can be run using the followng command `forge test --mt test_that_cross_chain_borrowing_send_full_balance -vvvv`, which should output:

```text
Ran 1 test for test/TestBorrowingCrossChain.t.sol:TestBorrowingCrossChain
[PASS] test_that_cross_chain_borrowing_send_full_balance(uint256,uint256) (runs: 1003, Î¼: 1560685, ~: 1560837)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 792.46ms (785.82ms CPU time)

Ran 1 test suite in 792.95ms (792.46ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)

```
Note that each diff can be applied using the command `git apply file.diff` in order to reproduce the test case.


### Mitigation

Do not use `address(this).balance` as the `msgValue` for `MessagingFee`. Instead, use LayerZero's [`_quote`](https://github.com/LayerZero-Labs/LayerZero-v2/blob/88428755be6caa71cb1d2926141d73c8989296b5/packages/layerzero-v2/evm/oapp/contracts/oapp/OAppSender.sol#L47) function to get an accurate estimate of the required native gas for the specific message (`_dstEid`, `_message`, `options`, `_payInLzToken`).
```solidity
// Example in _send()

________snip______

MessagingParams memory params = MessagingParams({
    dstEid: destEid,
    receiver: peerRouterB_bytes32,
    message: messagePayload,
    options: options,
    payInLzToken: false
});

// Call quote()
MessagingFee memory feeData = _quote(params, address(this));

________snip______
```

In addition, the `_payNative()` override should simply use the `_nativeFee` argument passed to it (which will be `feeData.nativeFee` if `_send` is corrected) and check the contract balance against *that specific amount*. 

The primary fix is in `_send` passing the correctly estimated `MessagingFee` to `_lzSend`.
