Fierce Eggplant Dalmatian

High

# Unscaled principle handling in `_handleValidBorrowRequest` leads to residual Cross-Chain collateral during full repayment

## Title
Unscaled principle handling in `_handleValidBorrowRequest` leads to residual Cross-Chain collateral during full repayment

## Summary
The `_handleValidBorrowRequest` function fails to properly scale borrow principles when updating existing cross-chain borrow positions, resulting in accounting discrepancies that leave residual collateral on the source chain even after full repayment. This vulnerability stems from incorrect debt accounting during cross-chain borrow position updates.

## Root Cause
The [`_handleBorrowCrossChainRequest`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L581-L673) handles the borrow request on the destination chain. It checks if the user has a borrowing position that uses the same collateral. If cross-chain collateral is found, the existing principle is adjusted and the new borrow amount is added to it and updated. Otherwise, add new collateral
```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        // Accrue interest on borrowed token on destination chain
        LTokenInterface(payload.destlToken).accrueInterest();

        // Get current borrow index from destination lToken
        uint256 currentBorrowIndex = LTokenInterface(payload.destlToken).borrowIndex();

        // Important: Use the underlying token address
        address destUnderlying = lendStorage.lTokenToUnderlying(payload.destlToken);

        // Check if user has any existing borrows on this chain
        bool found = false;
        uint256 index;

        LendStorage.Borrow[] memory userCrossChainCollaterals =
            lendStorage.getCrossChainCollaterals(payload.sender, destUnderlying);

        /**
         * Filter collaterals for the given srcEid. Prevents borrowing from
         * multiple collateral sources.
         */ 
        for (uint256 i = 0; i < userCrossChainCollaterals.length;) {
            if (
                userCrossChainCollaterals[i].srcEid == srcEid
                    && userCrossChainCollaterals[i].srcToken == payload.srcToken
            ) {
                index = i;
                found = true;
                break;
            }
            unchecked {
                ++i;
            }
        }

        // Get existing borrow amount
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );

        // Verify the collateral from source chain is sufficient for total borrowed amount
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");

        // Execute the borrow on destination chain
        CoreRouter(coreRouter).borrowForCrossChain(payload.sender, payload.amount, payload.destlToken, destUnderlying);

        /**
         * @dev If existing cross-chain collateral, update it. Otherwise, add new collateral.
         */
        if (found) {
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

>>          userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            lendStorage.updateCrossChainCollateral(
                payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
        } else {
            lendStorage.addCrossChainCollateral(
                payload.sender,
                destUnderlying,
                LendStorage.Borrow({
                    srcEid: srcEid,
                    destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
        }
        // Track borrowed asset
        lendStorage.addUserBorrowedAsset(payload.sender, payload.destlToken);
        //? adds the destination token(the token being borrowed) to the user's borrowed assets on chain B

        // Distribute LEND rewards on destination chain
        lendStorage.distributeBorrowerLend(payload.destlToken, payload.sender); 

        // Send confirmation back to source chain with updated borrow details
        _send(
            srcEid,
            payload.amount,
            currentBorrowIndex,
            payload.collateral,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.ValidBorrowRequest
        );
    }
```
A confirmation is send back to source chain with updated borrow details

The [`_handleValidBorrowRequest`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L703-L731) handles the valid borrow request on the source chain. It checks for specific borrow record, if the record is found it updates the `principle` and `borrowIndex` as follows;
```solidity
    function _handleValidBorrowRequest(LZPayload memory payload, uint32 srcEid) private {
        // Find the specific borrow record using the new helper
        (bool found, uint256 index) =
            lendStorage.findCrossChainBorrow(payload.sender, payload.srcToken, srcEid, currentEid, payload.destlToken);

        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
>>          userBorrows[index].principle = userBorrows[index].principle + payload.amount; 
>>          userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
            // Add new borrow record
            lendStorage.addCrossChainBorrow(
                payload.sender,
                payload.srcToken,
                LendStorage.Borrow(
                    srcEid, currentEid, payload.amount, payload.borrowIndex, payload.destlToken, payload.srcToken
                )
            );
        }

        lendStorage.addUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));

        // Emit BorrowSuccess event
        emit BorrowSuccess(payload.sender, payload.srcToken, payload.amount);
    }
```
As seen, the above function adds the new borrow amount directly to the stored principle. It fails to account for interest on the existing position


## Internal Pre-conditions
1. Existing cross-chain borrow position with accrued interest
2. New borrow request initiated on destination chain

## External Pre-conditions
None

## Attack Path
1. User borrows 100 DAI (index 1.0e18) on Chain B
2. 20% interest accumulates (new index 1.2e18)
3. User borrows 50 DAI more
4. Protocol records 170 DAI ((100*1.2/1.0) + 50) on Chain B
5. On chain A, protocol records 150 DAI (100+50)

## Impact
The unscaled update creates permanent desynchronization between actual debt position on destination chain and recorded debt position on source chain. This will result in cross-chain collateral leftover during full repayment

## POC
Add the following test in `test/testBorrowingCrossChain.t.sol`
```solidity
    function test_borrowCrossChain_allows_collateral_leftover_due_to_incorrect_principle_scaling(uint256 amountToSupply, uint256 amountToBorrow)public {
        amountToSupply = bound(amountToSupply, 1e18, 1e30);

        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
        (address tokenB, address lTokenB) = _supplyB(amountToSupply * 2);

        // Fund Router A and B with ETH for LayerZero fees
        vm.deal(address(routerA), 1 ether);
        vm.deal(address(routerB), 1 ether);

        // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
        uint256 maxBorrow = (lendStorageA.getMaxBorrowAmount(deployer, lTokenA) * 0.9e18) / 1e18;

        uint256 boundedBorrow = bound(amountToBorrow, 0.1e18, maxBorrow) / 2;

        // boundedBorrow = boundedBorrow / 2;

        vm.startPrank(deployer);

        // FIRST BORROW
        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);

        // Get the destination chain borrow details using the test contract address
        LendStorage.Borrow[] memory userBorrows = lendStorageA.getCrossChainBorrows(deployer, tokenA);
        LendStorage.Borrow[] memory userCollaterals = lendStorageB.getCrossChainCollaterals(deployer, tokenB);

        // SHOULD MATCH:
        // This is because, during first borrow, the user has no stored borrow index on chain A.
        // Therefore, the principle is scaled by the same index as the collateral yielding egual values.
        assertEq(userBorrows[0].principle, userCollaterals[0].principle, "Borrow principle should match");

        // Advance some blocks to accrue interest
        vm.warp(block.timestamp + 1000);
        vm.roll(block.number + 1000);

        // SECOND BORROW
        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);

        // Get the borrow and collateral details using the test contract address
        LendStorage.Borrow[] memory newUserBorrows = lendStorageA.getCrossChainBorrows(deployer, tokenA);
        LendStorage.Borrow[] memory newUserCollaterals = lendStorageB.getCrossChainCollaterals(deployer, tokenB);

        // DOES NOT MATCH:
        // This is because, during second borrow, the user has a stored borrow index on chain A.
        // Therefore, the collateral principle is scaled by the diff between current index and stored borrow index 
        // whereas borrow principle is left unchanged yielding different values.
        assertNotEq(newUserBorrows[0].principle, newUserCollaterals[0].principle, "Borrow principle should NOT match");

        // FULL REPAYMENT
        uint256 fullBorrowAmount = newUserBorrows[0].principle;

        ERC20Mock(tokenB).mint(deployer, fullBorrowAmount);
        IERC20(tokenB).approve(address(coreRouterB), fullBorrowAmount);

        // user repays full borrow
        routerB.repayCrossChainBorrow(deployer, fullBorrowAmount, lTokenB, 31337);

        // Check  borrow details:
        // we expect the user to have 0 borrows on chain A now
        LendStorage.Borrow[] memory currentUserBorrows = lendStorageA.getCrossChainBorrows(deployer, tokenA); 
        assertEq(currentUserBorrows.length, 0, "Should have 0 borrows");

        // check user collateral details:
        // we expect the user to have 0 collaterals on chain B now. However, this is not the case
        LendStorage.Borrow[] memory currentUserCollaterals = lendStorageB.getCrossChainCollaterals(deployer, tokenB);  
        assertNotEq(currentUserCollaterals.length, 0, "Should still have collaterals");

        vm.stopPrank();
    }
```

## Mitigation
On the source chain, scale the existing principle before adding new amount
```diff
    if (found) {
        LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
-       userBorrows[index].principle = userBorrows[index].principle + payload.amount;
+       uint256 newPrinciple = (userBorrows[index].principle * payload.borrowIndex) / userBorrows[index].borrowIndex;
+       userBorrows[index].principle = newPrinciple + payload.amount;
        userBorrows[index].borrowIndex = payload.borrowIndex;
        lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
    }
```
