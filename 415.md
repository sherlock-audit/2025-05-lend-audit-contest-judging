Magnificent Holographic Camel

High

# Incorrectly calculation of total borrow amount in borrow and liquidation

### Summary

Using mismatched borrow index can cause the borrow amount to be incorrectly calculated

### Root Cause

The function [`CoreRouter.borrow()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L145) allows user to borrow asset using the supplied collateral. User's liquidity state is retrieved by calling to LendStorage `(uint256 borrowed, uint256 collateral) = lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount)`, in which the returned value `borrowed` is the [total borrow amount across multiple markets, plus the borrow amount `_amount`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L421-L463). The total borrow amount is then accrued checking collateral sufficiency `((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)`

Here, `borrowed` is the total borrow across multiple markets and it should not be scaled with the borrow index `LTokenInterface(_lToken).borrowIndex()` which is the updated borrow index from borrow asset's LToken `_lToken`. So, the value `borrowAmount` is imprecise to be used to check collateral sufficiency. As a result, the collateral sufficiency validation is unexpected


```solidity
    function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");

        address _lToken = lendStorage.underlyingTolToken(_token);

        LTokenInterface(_lToken).accrueInterest();

@>        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);

@>        uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;

@>        require(collateral >= borrowAmount, "Insufficient collateral");

        // Enter the Compound market
        enterMarkets(_lToken);

        // Borrow tokens
        require(LErc20Interface(_lToken).borrow(_amount) == 0, "Borrow failed");

        // Transfer borrowed tokens to the user
        IERC20(_token).transfer(msg.sender, _amount);

        lendStorage.distributeBorrowerLend(_lToken, msg.sender);

        // Update records
        if (currentBorrow.borrowIndex != 0) {
            uint256 _newPrinciple =
                (currentBorrow.amount * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex;

            lendStorage.updateBorrowBalance(
                msg.sender, _lToken, _newPrinciple + _amount, LTokenInterface(_lToken).borrowIndex()
            );
        } else {
            lendStorage.updateBorrowBalance(msg.sender, _lToken, _amount, LTokenInterface(_lToken).borrowIndex());
        }

        lendStorage.addUserBorrowedAsset(msg.sender, _lToken);

        // Emit BorrowSuccess event
        emit BorrowSuccess(msg.sender, _lToken, lendStorage.getBorrowBalance(msg.sender, _lToken).amount);
    }
``` 
##

Note that the problem also happens with the function [`liquidateBorrow()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L230-L243)
```solidity
    function liquidateBorrow(address borrower, uint256 repayAmount, address lTokenCollateral, address borrowedAsset)
        external
    {
        // The lToken of the borrowed asset
        address borrowedlToken = lendStorage.underlyingTolToken(borrowedAsset);

        LTokenInterface(borrowedlToken).accrueInterest();

@>        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(borrower, LToken(payable(borrowedlToken)), 0, 0);

@>        liquidateBorrowInternal(
            msg.sender, borrower, repayAmount, lTokenCollateral, payable(borrowedlToken), collateral, borrowed
        );
    }

    function liquidateBorrowInternal(
        address liquidator,
        address borrower,
        uint256 repayAmount,
        address lTokenCollateral,
        address payable borrowedlToken,
        uint256 collateral,
        uint256 borrowed
    ) internal {
        require(
@>            liquidateBorrowAllowedInternal(borrowedlToken, borrower, repayAmount, collateral, borrowed) == 0,
            "Borrow not allowed"
        );
        require(borrower != liquidator, "Liquidator cannot be borrower");
        require(repayAmount > 0, "Repay amount not zero");

        repayBorrowInternal(borrower, liquidator, repayAmount, borrowedlToken, true);

        // Liquidation logic for same chain
        liquidateSeizeUpdate(msg.sender, borrower, lTokenCollateral, borrowedlToken, repayAmount);
    }

    function liquidateBorrowAllowedInternal(
        address payable lTokenBorrowed,
        address borrower,
        uint256 repayAmount,
        uint256 collateral,
        uint256 borrowed
    ) internal view returns (uint256) {
        // Determine borrowIndex and borrowAmount based on chain type
        LendStorage.BorrowMarketState memory borrowBalance = lendStorage.getBorrowBalance(borrower, lTokenBorrowed);

        // Allow accounts to be liquidated if the market is deprecated
        if (LendtrollerInterfaceV2(lendtroller).isDeprecated(LToken(lTokenBorrowed))) {
            require(borrowBalance.amount >= repayAmount, "Repay > total borrow");
        } else {
            // The borrower must have shortfall in order to be liquidatable
            uint256 borrowedAmount;

            // For same-chain liquidations, calculate borrowed amount using the borrowBalance's index
@>            borrowedAmount =
                (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex;

            require(borrowedAmount > collateral, "Insufficient shortfall");

            /* The liquidator may not repay more than what is allowed by the closeFactor */
            uint256 maxClose = mul_ScalarTruncate(
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
            );

            require(repayAmount <= maxClose, "Too much repay");
        }

        return 0;
    }
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

1. Assume there are 2 markets for [USDC, USDT]
2. Alice borrows 600 USDC, resulting borrow balance for LUSDC `{amount: 600, borrowIndex: idx_USDC}`
3. Alice then borrows 400 USDT, resulting borrow balance LUSDT `{amount: 400, borrowIndex: idx_USDT}`
4. After some time, Alice borrows 500 USDT. Here, the `borrowed` value includes borrow from both USDC and USDT above, plus 500. It will then be scaled with only the new USDT borrow index. Given that `idx_USDC_new / idx_USDC < idx_USDT_new / idx_USDT` then the result `borrowAmount` is higher than expected and vice versa
==> The collateral sufficiency check is not precise

### Impact

- Collateral sufficiency check is imprecise, impacting user's borrowing operations. Moreover, when the calculated `borrowAmount` is less than actual, the borrower is exposed to liquidation
- Liquidation validation can be incorrect causing harms for liquidators and borrowers. This also breaks core functionality of the protocol

### PoC

_No response_

### Mitigation

Consider accrue interest for all debt positions in the function `getHypotheticalAccountLiquidityCollateral()` and then just use the returned value `borrowed` to compare with `collateral`