Lone Champagne Hornet

High

# A supplier will receive incorrect LEND rewards due to reward index update after minting

### Summary

The incorrect ordering of `distributeSupplierLend()` after minting in `supply()` will cause an inflated LEND reward for the supplier, as the LEND index is updated using the newly minted lTokens, allowing the supplier to receive a portion of rewards they didn't earn yet.

### Root Cause

- In `CoreRouter.sol: 84`, the call to `distributeSupplierLend()` comes **after** the user’s lTokens are minted
https://github.com/sherlock-audit/2025-05-lend-audit-contest-sylvarithos/blob/551944cd87d138620b89c11674a92f1dcbe0efbe/Lend-V2/src/LayerZero/CoreRouter.sol#L84
```solidity
function supply(uint256 _amount, address _token) external {
    address _lToken = lendStorage.underlyingTolToken(_token);

    require(_lToken != address(0), "Unsupported Token");

    require(_amount > 0, "Zero supply amount");

    // Transfer tokens from the user to the contract
    IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);

    _approveToken(_token, _lToken, _amount);

    // Get exchange rate before mint
    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

    // Mint lTokens
    require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

    // Calculate actual minted tokens using exchangeRate from before mint
    uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;

    lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

84  lendStorage.distributeSupplierLend(_lToken, msg.sender);
```

- In `distributeSupplierLend()`, the reward is calculated using supplyIndex which is updated using the current `totalSupply()` of lTokens in the `Lendtroller.sol: 1286`
https://github.com/sherlock-audit/2025-05-lend-audit-contest-sylvarithos/blob/551944cd87d138620b89c11674a92f1dcbe0efbe/Lend-V2/src/LayerZero/LendStorage.sol#L305
```solidity
function distributeSupplierLend(address lToken, address supplier) external onlyAuthorized {
    // Trigger supply index update
305    LendtrollerInterfaceV2(lendtroller).triggerSupplyIndexUpdate(lToken);

    // Get the appropriate lend state based on whether it's for supply or borrow
    (uint224 supplyIndex,) = LendtrollerInterfaceV2(lendtroller).lendSupplyState(lToken);

    // Get the relevant indexes and accrued LEND for the account
    uint256 supplierIndex = lendSupplierIndex[lToken][supplier];

    lendSupplierIndex[lToken][supplier] = supplyIndex;

    // Update the account's index to the current index since we are distributing accrued LEND
    if (supplierIndex == 0 && supplyIndex >= LEND_INITIAL_INDEX) {
        // Covers the case where users borrowed tokens before the market's borrow state index was set.
        // Rewards the user with LEND accrued from the start of when borrower rewards were first
        // set for the market.
        supplierIndex = LEND_INITIAL_INDEX;
    }

    // Calculate change in the cumulative sum of the LEND per lToken accrued
    Double memory deltaIndex = Double({mantissa: sub_(supplyIndex, supplierIndex)});
```
https://github.com/sherlock-audit/2025-05-lend-audit-contest-sylvarithos/blob/551944cd87d138620b89c11674a92f1dcbe0efbe/Lend-V2/src/Lendtroller.sol#L1286
```solidity
function updateLendSupplyIndex(address lToken) internal {
    LendMarketState storage supplyState = lendSupplyState[lToken];
    uint256 supplySpeed = lendSupplySpeeds[lToken];
    uint32 blockNumber = safe32(getBlockNumber(), "block number exceeds 32 bits");
    uint256 deltaBlocks = sub_(uint256(blockNumber), uint256(supplyState.block));
    if (deltaBlocks > 0 && supplySpeed > 0) {
1284        uint256 supplyTokens = LToken(lToken).totalSupply();
        uint256 lendAccrued = mul_(deltaBlocks, supplySpeed);
1286        Double memory ratio = supplyTokens > 0 ? fraction(lendAccrued, supplyTokens) : Double({mantissa: 0});
        supplyState.index =
            safe224(add_(Double({mantissa: supplyState.index}), ratio).mantissa, "new index exceeds 224 bits");
        supplyState.block = blockNumber;
    } else if (deltaBlocks > 0) {
        supplyState.block = blockNumber;
    }
}
```
- Since the new supply already includes the **user’s just-minted tokens**, the reward calculation incorrectly includes this amount when computing accrued rewards for previous blocks

This allows users to **claim LEND rewards retroactively** for blocks they did not participate in.

### Internal Pre-conditions

1. The supplier calls `supply()` with a non-zero `_amount` of a supported token
2. The market’s `lendSupplySpeed` is non-zero
3. There has been at least 1 block since the last `updateLendSupplyIndex()` was triggered

### External Pre-conditions

1. LEND distribution has been enabled in the market via `lendSupplySpeeds[lToken] > 0`
2. Some time has passed (at least 1 block), and LEND rewards have accumulated but have not yet been distributed

### Attack Path

1. Supplier waits for multiple blocks to pass with unclaimed LEND rewards
2. Supplier calls `supply()` for the first time
3. Inside `supply()`:
    - lTokens are minted
    - `distributeSupplierLend()` is called after minting
    - `updateLendSupplyIndex()` runs using the **new totalSupply**, which includes the attacker
4. The attacker receives a share of rewards they were not eligible for (i.e., before they supplied)

### Impact

Suppliers suffer **reward dilution**.  
This creates Unfair LEND distributions

### PoC

_No response_

### Mitigation

Move the call to `distributeSupplierLend()` **before** `mint()` in the `supply()` function.  
This ensures the reward index is updated and the user is assigned an accurate index **before their balance is updated**.
```solidity
function supply(uint256 _amount, address _token) external {
    address _lToken = lendStorage.underlyingTolToken(_token);

    require(_lToken != address(0), "Unsupported Token");

    require(_amount > 0, "Zero supply amount");

    // Transfer tokens from the user to the contract
    IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);

    _approveToken(_token, _lToken, _amount);

+	lendStorage.distributeSupplierLend(_lToken, msg.sender);
    // Get exchange rate before mint
    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

    // Mint lTokens
    require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

    // Calculate actual minted tokens using exchangeRate from before mint
    uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;

    lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

-   lendStorage.distributeSupplierLend(_lToken, msg.sender);
```
