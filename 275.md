Ancient Corduroy Aphid

High

# CoreRouter will miscompute liquidity and allow invalid redeems or borrows for users as stale exchange rates are used without accruing interest across all relevant markets

### Summary

The missing accrual of interest across all user-supplied and borrowed assets in [CoreRouter.redeem()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L100-L138) and [CoreRouter.bollow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L145-L190) will cause an incorrect liquidity computation for users, as the function [getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L385-L467) will use stale exchangeRateStored() values for multiple lTokens. This may allow a user to bypass liquidity constraints or unjustly fail valid redemptions or borrows.



### Root Cause

In [CoreRouter.sol:110](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L110-L112), the call to getHypotheticalAccountLiquidityCollateral() is made without accruing interest on all relevant markets (userSuppliedAssets and userBorrowedAssets).
Each LToken.exchangeRateStored() used inside this function is stale unless accrueInterest() is explicitly called for that market.

```solidity
function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;

        // Calculate collateral value from supplied assets
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();

        // First loop: Calculate collateral value from supplied assets
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
            uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];

            // Get collateral factor and price for this asset
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
   @>         vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()});    //used stale exchangeRate

            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);

            // Add to collateral sum
            vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }

        // Second loop: Calculate borrow value from borrowed assets
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);

            // Get borrow balance for this asset
    @>        uint256 totalBorrow = borrowWithInterestSame(account, address(asset));

            // Add cross-chain borrows if any
    @>        totalBorrow += borrowWithInterest(account, address(asset));

            // Get price for borrowed asset
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

            // Add to borrow sum
            vars.sumBorrowPlusEffects =
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }

        // Handle effects of current action
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

            // Add effect of redeeming collateral
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
    @>            vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()});
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }

            // Add effect of new borrow
            if (borrowAmount > 0) {
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }

        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```
In the borrow function, the part is missing as well.
```solidity
LTokenInterface(_lToken).accrueInterest(); 

//should add a part to collect the accrual of interest across all user-supplied and borrowed assets

(uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);
```

### Internal Pre-conditions

1. A user must have supplied multiple assets (userSuppliedAssets) and/or borrowed from multiple markets (userBorrowedAssets)

2. The block must advance after interest has accrued, but before the call to redeem() or borrow()

### External Pre-conditions

1. The borrow rate must be non-zero
2. Time or block advancement must have occurred

### Attack Path

1. User supplies collateral across multiple assets, triggering tracked userSuppliedAssets

2. User borrows in at least one market to accrue interest over time

3. User waits several blocks for exchangeRateStored() values to become stale

4. User calls CoreRouter.redeem() which:

  -  Calls getHypotheticalAccountLiquidityCollateral() without accruing interest on each asset

  -  Miscalculates sumCollateral and sumBorrowPlusEffects

  -  Passes the collateral >= borrowed check improperly

5. Redeem proceeds even though the account no longer has sufficient liquidity at up-to-date rates

### Impact

The protocol suffers a potential loss of funds if redemptions are allowed despite actual liquidity shortfall.
The user may also unjustly be blocked from redemption if the stale values underestimate collateral value.
Both scenarios break solvency assumptions and create denial of service or insolvency risk.

### PoC

Add this test file to TestRedeeming.t.sol file:
```solidity
    function test_getHypotheticalAccountLiquidityCollateral_with_stale_exchangeRate() public {
        vm.startPrank(deployer);

        // Step 1: Supply tokens
        uint256 supplyAmount = 100e18;
        (address token, address lToken) = _supply(supplyAmount);

        // Step 2: Borrow tokens to create interest accrual
        coreRouter.borrow(50e18, token);

        // Step 3: Get initial liquidity
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(deployer, LToken(lToken), 0, 0);

        console2.log("Borrowed before interest accrual:", borrowed);
        console2.log("Collateral before interest accrual:", collateral);

        // Step 4: Advance time and block to accrue interest
        vm.warp(block.timestamp + 3 days);
        vm.roll(block.number + 2000);

        LTokenInterface(lToken).accrueInterest();

        // Step 5: Get liquidity after interest accrual
        (uint256 newBorrowed, uint256 newcollateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(deployer, LToken(lToken), 0, 0);

        console2.log("Borrowed after interest accrual:", newBorrowed);
        console2.log("Collateral after interest accrual:", newcollateral);
        vm.stopPrank();
    }
```
Then, you can see this result:
```bash
Ran 1 test for test/TestRedeeming.t.sol:TestRedeeming
[PASS] test_getHypotheticalAccountLiquidityCollateral_with_accrued_interest() (gas: 854031)
Logs:
  Borrowed before interest accrual: 50000000000000000000
  Collateral before interest accrual: 75000000000000000000
  Borrowed after interest accrual: 50005945585996800000
  Collateral after interest accrual: 75004459189497600000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.15ms (1.07ms CPU time)

Ran 1 test suite in 15.55ms (5.15ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```



### Mitigation

Before calling getHypotheticalAccountLiquidityCollateral(), explicitly accrue interest on all relevant lTokens:
```solidity
function accrueAllUserAssets(address user) internal {
    address[] memory supplied = lendStorage.getUserSuppliedAssets(user);
    address[] memory borrowed = lendStorage.getUserBorrowedAssets(user);

    for (uint256 i = 0; i < supplied.length; ++i) {
        LTokenInterface(supplied[i]).accrueInterest();
    }
    for (uint256 i = 0; i < borrowed.length; ++i) {
        LTokenInterface(borrowed[i]).accrueInterest();
    }
}
```
Call this function in redeem() and borrow() before performing liquidity checks.
