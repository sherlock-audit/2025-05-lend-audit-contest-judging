Skinny Charcoal Chinchilla

High

# Cross-chain liquidations take wrong amount from liquidator, allowing them to steal collateral of other users and making users even more insolvent.

### Summary

After a liquidation is deeemed sucessful, the `CrossChainRouter.sol::repayCrossChainBorrowInternal()` handles the liquidation on the Destination Chain. This function incorrectly handle cross-chain liquidations, causing the protocol to take the wrong amount from liquidator. 

The cross-chain liquidation flow goes like this: 

```solidity 
1. liquidateCrossChain() Chain B
    _validateAndPrepareLiquidation()
    _executeLiquidation()
        _prepareLiquidationValues()
        _executeLiquidationCore()
            _send()

2. _checkLiquidationValid() Chain A
    _handleLiquidationExecute();
        _send()

3. _handleLiquidationSuccess() Chain B
        repayCrossChainBorrowInternal()
            _handleRepayment()
                CoreRouter(coreRouter).repayCrossChainLiquidation();
                    repayBorrowInternal();
                        IERC20(_token).safeTransferFrom();
            _updateRepaymentState()
                _send()

4. _handleDestRepayMessage() Chain A
        updateCrossChainBorrow();
```

The repay amount of `CrossChainRouter.sol::repayCrossChainBorrowInternal()`, originates from [_executeLiquidationCore](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L264) where its calculated via [LendTroller.sol::liquidateCalculateSeizeTokens()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/Lendtroller.sol#L852). This function returns the collateral to seize in term of Ltoken, since collateral of users are stored in term of Ltoken in storage. 

```solidity 
    function liquidateCalculateSeizeTokens(address lTokenBorrowed, address lTokenCollateral, uint256 actualRepayAmount) //@note liquidateCalculateSeizeTokens
        external
        view
        override
        returns (uint256, uint256)
    {
        uint256 priceBorrowedMantissa = oracle.getUnderlyingPrice(LToken(lTokenBorrowed));
        uint256 priceCollateralMantissa = oracle.getUnderlyingPrice(LToken(lTokenCollateral));
        if (priceBorrowedMantissa == 0 || priceCollateralMantissa == 0) {
            return (uint256(Error.PRICE_ERROR), 0);
        }
        uint256 exchangeRateMantissa = LToken(lTokenCollateral).exchangeRateStored(); // Note: reverts on error 
        uint256 seizeTokens;
        Exp memory numerator;
        Exp memory denominator;
        Exp memory ratio;
        numerator = mul_(Exp({mantissa: liquidationIncentiveMantissa}), Exp({mantissa: priceBorrowedMantissa}));
        denominator = mul_(Exp({mantissa: priceCollateralMantissa}), Exp({mantissa: exchangeRateMantissa}));
        ratio = div_(numerator, denominator);
        seizeTokens = mul_ScalarTruncate(ratio, actualRepayAmount); 
        return (uint256(Error.NO_ERROR), seizeTokens);
    }
```

```solidity 
    function _executeLiquidationCore(LendStorage.LiquidationParams memory params) private {
        address borrowedlToken = lendStorage.underlyingTolToken(params.borrowedAsset);
        (uint256 amountSeizeError, uint256 seizeTokens) = LendtrollerInterfaceV2(lendtroller)
            .liquidateCalculateSeizeTokens(borrowedlToken, params.lTokenToSeize, params.repayAmount);
        require(amountSeizeError == 0, "Seize calculation failed");
        _send(
            params.srcEid,
@>          seizeTokens, 
            params.storedBorrowIndex, 
            0,
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid), 
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
    }
```
This amount is then sent to the Source Chain, where its used to update storage.
No change is done to the amount and its relayed back to the Destination Chain. But the collateral is deducted from the user and credited to the liquidator. 

```solidity 
    function _handleLiquidationExecute(LZPayload memory payload, uint32 srcEid) private {
        uint256 protocolSeizeShare = mul_(payload.amount, Exp({mantissa: lendStorage.PROTOCOL_SEIZE_SHARE_MANTISSA()}));
        require(protocolSeizeShare < payload.amount, "Invalid protocol share");
        uint256 liquidatorShare = payload.amount - protocolSeizeShare;
        lendStorage.updateProtocolReward(
            payload.destlToken, lendStorage.protocolReward(payload.destlToken) + protocolSeizeShare
        );
        lendStorage.distributeSupplierLend(payload.destlToken, payload.sender); 
        lendStorage.distributeSupplierLend(payload.destlToken, payload.liquidator); 
        lendStorage.updateTotalInvestment(
            payload.sender,
            payload.destlToken,
@>          lendStorage.totalInvestment(payload.sender, payload.destlToken) - payload.amount
        );
        lendStorage.updateTotalInvestment(
            payload.liquidator,
            payload.destlToken,
@>          lendStorage.totalInvestment(payload.liquidator, payload.destlToken) + liquidatorShare
        );
        if (lendStorage.totalInvestment(payload.sender, payload.destlToken) == 0) {
            lendStorage.removeUserSuppliedAsset(payload.sender, payload.destlToken);
        }
        emit LiquidateBorrow(
            payload.liquidator, 
            payload.srcToken, 
            payload.sender, 
            payload.destlToken 
        );
        _send(
            srcEid,
            payload.amount,
            0,
            0,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.LiquidationSuccess
        );
    }

```

On the destination chain, the [_handleLiquidationSuccess()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443) fails to convert the amount in terms of the underlying token. It passed the amount in term of Ltoken into subsequent functons. In [repayCrossChainBorrowInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L368), the only requirement is that it has to be less than the borrowed amount. Which it is since its in term of Ltoken and borrwed amount is in term of the token(e.g WETH).

```solidity 
    function _handleLiquidationSuccess(LZPayload memory payload) private {
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            currentEid,
            0,
            payload.destlToken,
            payload.srcToken
        );
        require(found, "Borrow position not found");
        LendStorage.Borrow[] memory userCollaterals = lendStorage.getCrossChainCollaterals(payload.sender, underlying);
        uint32 srcEid = uint32(userCollaterals[index].srcEid);
        repayCrossChainBorrowInternal(
            payload.sender, 
            payload.liquidator, 
@>          payload.amount, 
            payload.destlToken, 
            srcEid 
        );
    }

    function repayCrossChainBorrowInternal(
        address borrower,
        address repayer,
        uint256 _amount,
        address _lToken,
        uint32 _srcEid
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest(); 
        (uint256 borrowedAmount, uint256 index, LendStorage.Borrow memory borrowPosition) =
            _getBorrowDetails(borrower, _token, _lToken, _srcEid);
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
        require(repayAmountFinal <= borrowedAmount, "Repay amount exceeds borrow");
@>      _handleRepayment(borrower, repayer, _lToken, repayAmountFinal);
@>      _updateRepaymentState(
            borrower, _token, _lToken, borrowPosition, repayAmountFinal, borrowedAmount, index, _srcEid
        );
        emit RepaySuccess(borrower, _token, repayAmountFinal);
    }
```

`handleRepayment()` calls `CoreRouter.sol::repayCrossChainLiquidation()`, which calls [repayBorrowInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L459) where the transfer is done. This gives the liquidator `Ltoken amount` of Ltokens while only taking `Ltoken amount` of underlying tokens. The user getting liquidated gets is borrow position updated as if only `Ltoken amount` of the underlying tokens were repaid, while he lost `Ltoken amount` of his Ltoken collateral.

```solidity 
    function _handleRepayment(address _borrower, address repayer, address _lToken, uint256 repayAmountFinal) private {
        CoreRouter(coreRouter).repayCrossChainLiquidation(_borrower, repayer, repayAmountFinal, _lToken);
    }

    function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest();
        uint256 borrowedAmount;
        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken); 
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }
        require(borrowedAmount > 0, "Borrowed amount is 0");
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
@>      IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
        _approveToken(_token, _lToken, repayAmountFinal);
        lendStorage.distributeBorrowerLend(_lToken, borrower);
        ...
    }
```


### Root Cause

The function fails to convert the Ltoken amount to underlying amount, before passing it into functions that operate in term of underlying.

```solidity 
    function _handleLiquidationSuccess(LZPayload memory payload) private {
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            currentEid,
            0,
            payload.destlToken,
            payload.srcToken
        );
        require(found, "Borrow position not found");
        LendStorage.Borrow[] memory userCollaterals = lendStorage.getCrossChainCollaterals(payload.sender, underlying);
        uint32 srcEid = uint32(userCollaterals[index].srcEid);
        repayCrossChainBorrowInternal(
            payload.sender, 
            payload.liquidator, 
@>          payload.amount, 
            payload.destlToken, 
            srcEid 
        );
    }
```

### Internal Pre-conditions

None

### External Pre-conditions

1. A user must be liquidated

### Attack Path

1. User A is subject to liquidation with 5e11 of collateral in Ltoken
2. The attacker liquidates the user, the liquidateCalculateSeizeTokens() returns 2.5e11 of Ltoken. (assuming the max liquidation amount) 
3. The attacker gives 2.5e11 of underlying token to the protocol, and receives 2.5e11 of Ltoken. 
4. The user's borrow position is deducted by 2.5e11 while his collateral is halved. 
5. He is still subject to liquidation as borrowed amount is in term of underlying. 
6. Its as if the attacker traded 1:1 of underlying and Ltoken.

### Impact

Users positions stay unhealthy after liquidation. The liquidator can gain collateral from users while repaying basically dust. 

### PoC

_No response_

### Mitigation

Fix the function to handle cross-chain repayments. Convert the Ltoken amount to underlying amount before passing it into subsequent functions.

```diff
    function _handleLiquidationSuccess(LZPayload memory payload) private {
        address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
            currentEid,
            0,
            payload.destlToken,
            payload.srcToken
        );
        require(found, "Borrow position not found");
        LendStorage.Borrow[] memory userCollaterals = lendStorage.getCrossChainCollaterals(payload.sender, underlying);
        uint32 srcEid = uint32(userCollaterals[index].srcEid);

+       uint256 underlyingAmount = payload.amount * Ltoken(payload.destlToken).exchangeRateCurrent() / 1e18;

        repayCrossChainBorrowInternal(
            payload.sender, 
            payload.liquidator, 
-           payload.amount, 
+           underlyingAmount,
            payload.destlToken, 
            srcEid 
        );
    }
```