Lone Champagne Hornet

High

# Redeeming lTokens will underestimate borrow balances, allowing collateral withdrawal that violates protocol liquidity rules

### Summary

The use of outdated borrow indices in `getHypotheticalAccountLiquidityCollateral()` will cause an underestimation of a user's borrow value during `redeem()`, as the borrow interest is not accrued before calculation. As a result, users may redeem more collateral than they should be allowed, breaking liquidity and risking protocol insolvency.

### Root Cause

In `CoreRouter.sol: 110 redeem()` function, the liquidity check relies on `getHypotheticalAccountLiquidityCollateral()` to compute the user's borrow and collateral balances.
https://github.com/sherlock-audit/2025-05-lend-audit-contest-sylvarithos/blob/551944cd87d138620b89c11674a92f1dcbe0efbe/Lend-V2/src/LayerZero/CoreRouter.sol#L110
```solidity
function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
    // Redeem lTokens
    address _token = lendStorage.lTokenToUnderlying(_lToken);

    require(_amount > 0, "Zero redeem amount");

    // Check if user has enough balance before any calculations
    require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");

    // Check liquidity
110    (uint256 borrowed, uint256 collateral) =
        lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
    require(collateral >= borrowed, "Insufficient liquidity");

```

However, this check uses `borrowWithInterest()` and `borrowWithInterestSame()` internally, which depend on outdated `borrowIndex` values from lToken, **without first calling `accrueInterest()`** on the corresponding lToken.
https://github.com/sherlock-audit/2025-05-lend-audit-contest-sylvarithos/blob/551944cd87d138620b89c11674a92f1dcbe0efbe/Lend-V2/src/LayerZero/LendStorage.sol#L425
```solidity
function getHypotheticalAccountLiquidityCollateral(
    address account,
    LToken lTokenModify,
    uint256 redeemTokens,
    uint256 borrowAmount
) public view returns (uint256, uint256) {
    ...

    // Second loop: Calculate borrow value from borrowed assets
    for (uint256 i = 0; i < borrowedAssets.length;) {
        LToken asset = LToken(borrowedAssets[i]);

        // Get borrow balance for this asset
425       uint256 totalBorrow = borrowWithInterestSame(account, address(asset));

        // Add cross-chain borrows if any
428        totalBorrow += borrowWithInterest(account, address(asset));

        // Get price for borrowed asset
        vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
        vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

        // Add to borrow sum
        vars.sumBorrowPlusEffects =
            mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

        unchecked {
            ++i;
        }
    }
```

As a result, borrow balances are calculated with **stale indices**, significantly underestimating the user's current debt.

### Internal Pre-conditions

1. The user holds lTokens and has a non-zero borrow position
2. There has been time (blocks) elapsed since last interest accrual
3. The user calls `redeem()` to withdraw collateral

### External Pre-conditions

- Interest rate model must produce non-zero borrow rates
- Borrowed asset’s borrowIndex has diverged from storage since last `accrueInterest()` update

### Attack Path

1. A user borrows tokens and holds collateral
2. Time passes and interest accumulates, increasing borrowIndex
3. The user calls `redeem()`
4. The system checks liquidity using **stale borrowIndex**, underestimating the user’s debt
5. The user is allowed to redeem lTokens beyond safe levels
6. If interest is later accrued, protocol becomes undercollateralized

### Impact

- Users can redeem more collateral than they should, violating liquidity requirements
- The protocol becomes partially or fully **insolvent**
- Attackers can potentially drain assets via **over-redeem and exit**

###  Mitigation

Before computing liquidity in `redeem()`, call `accrueInterest()` on all borrowed assets to ensure borrow indices are up-to-date:
```solidity
function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
    // Redeem lTokens
    address _token = lendStorage.lTokenToUnderlying(_lToken);

    require(_amount > 0, "Zero redeem amount");

    // Check if user has enough balance before any calculations
    require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");

+	LTokenInterface(_lToken).accrueInterest();

    // Check liquidity
    (uint256 borrowed, uint256 collateral) =
        lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
    require(collateral >= borrowed, "Insufficient liquidity");
```