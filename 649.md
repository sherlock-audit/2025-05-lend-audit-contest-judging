Ambitious Shamrock Moth

High

# Liquidator will never transfer funds to protocol

### Summary

Liquidator will cause the protocol to never recoup borrowed funds from unhealthy positions as the liquidator can avoid repayment through any liquidation.

### Root Cause

When the liquidation is successfully processed on the collateral chain a message is sent to the debt chain and handled here:

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443-L445

The issue is on line 445, where the `payload.destlToken` is mapped to underlying, however the `payload.destlToken` will always be equal to the lToken that was seized on the collateral chain. This arises due to incorrect passing of this parameter in the messages sent between the `CrossChainRouter`s. Therefore `payload.destlToken` will not have a mapping entry recorded on the debt chain `lendStorage`, as it exists only on the collateral chain. The `underlying` variable will be set to `address(0)` and so no corresponding borrow position can be found. The transaction will revert on CrossChainRouter.sol:457. Moreover, it doesn't make sense to search using the `underlying` of the asset that was seized, it should search instead using the borrowed underlying asset.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. Liquidator calls `CrossChainRouter::liquidateCrossChain` on debt chain, which results in a message sent to the collateral chain with `destLToken` equal to the address of the lToken to seize on the collateral chain.
2. Layer zero endpoint calls `CrossChainRouter::receive` on the collateral chain. Assuming the liquidation is valid, this sends a message to the debt chain with `destlToken` being unchanged from the previous step.
3. Layer zero endpoint calls `CrossChainRouter::receive` on the debt chain, which reverts and so the funds are not paid by the liquidator.

### Impact

The protocol loses 100% of the cross-chain debt owed to it. The liquidator profits by taking the borrower's collateral.

### PoC

Add a new test file in the test folder with the below contents and run using: `forge test --match-test test_handle_liquidation_success_issue --match-contract HGranoTest -vvv`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {Deploy} from "../script/Deploy.s.sol";
import {HelperConfig} from "../script/HelperConfig.s.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMock.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../src/Lendtroller.sol";
import {InterestRateModel} from "../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../src/LTokenInterfaces.sol";
import {LToken} from "../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import {SendLibMock} from "@layerzerolabs/lz-evm-messagelib-v2/test/mocks/SendLibMock.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";

contract HGranoTest is LayerZeroTest {
    HelperConfig public helperConfig;
    address public layerZeroEndpoint;
    address[] public supportedTokensA;
    address[] public supportedTokensB;
    address[] public supportedTokensC;
    bool public isTestnet;
    address public deployer;

    // Chain A (Source)
    CrossChainRouterMock public routerA;
    LendStorage public lendStorageA;
    CoreRouter public coreRouterA;
    Lendtroller public lendtrollerA;
    InterestRateModel public interestRateModelA;
    SimplePriceOracle public priceOracleA;
    address[] public lTokensA;

    // Chain B (Destination)
    CrossChainRouterMock public routerB;
    LendStorage public lendStorageB;
    CoreRouter public coreRouterB;
    Lendtroller public lendtrollerB;
    InterestRateModel public interestRateModelB;
    SimplePriceOracle public priceOracleB;
    address[] public lTokensB;

    // I had to change these values to fix test failures as the mocks used chain ID as the Endpoint ID
    uint32 constant CHAIN_A_ID = 31337;
    uint32 constant CHAIN_B_ID = 31337;

    EndpointV2 public endpointA;
    EndpointV2 public endpointB;
    SimpleMessageLib public simpleMsgLibA;
    SimpleMessageLib public simpleMsgLibB;

    // Events to test
    event BorrowSuccess(address indexed borrower, address indexed lToken, uint256 accountBorrow);

    function setUp() public override(LayerZeroTest) {
        super.setUp();

        deployer = makeAddr("deployer");
        vm.deal(deployer, 1000 ether);

        // Deploy protocol on Chain A using the endpoint we just created
        Deploy deployA = new Deploy();
        (
            address priceOracleAddressA,
            address lendtrollerAddressA,
            address interestRateModelAddressA,
            address[] memory lTokenAddressesA,
            address payable routerAddressA,
            address payable coreRouterAddressA,
            address lendStorageAddressA,
            , //address _layerZeroEndpoint
            address[] memory _supportedTokensA
        ) = deployA.run(address(endpointA)); // Pass the endpoint address to Deploy.run

        // Store Chain A values
        routerA = CrossChainRouterMock(payable(routerAddressA));
        lendStorageA = LendStorage(lendStorageAddressA);
        coreRouterA = CoreRouter(coreRouterAddressA);
        lendtrollerA = Lendtroller(lendtrollerAddressA);
        interestRateModelA = InterestRateModel(interestRateModelAddressA);
        priceOracleA = SimplePriceOracle(priceOracleAddressA);
        lTokensA = lTokenAddressesA;
        supportedTokensA = _supportedTokensA;

        // Deploy protocol on Chain B
        Deploy deployB = new Deploy();
        (
            address priceOracleAddressB,
            address lendtrollerAddressB,
            address interestRateModelAddressB,
            address[] memory lTokenAddressesB,
            address payable routerAddressB,
            address payable coreRouterAddressB,
            address lendStorageAddressB,
            , // address _layerZeroEndpoint
            address[] memory _supportedTokensB
        ) = deployB.run(address(endpointB));

        // Store Chain B values
        routerB = CrossChainRouterMock(payable(routerAddressB));
        lendStorageB = LendStorage(lendStorageAddressB);
        coreRouterB = CoreRouter(coreRouterAddressB);
        lendtrollerB = Lendtroller(lendtrollerAddressB);
        interestRateModelB = InterestRateModel(interestRateModelAddressB);
        priceOracleB = SimplePriceOracle(priceOracleAddressB);
        lTokensB = lTokenAddressesB;
        supportedTokensB = _supportedTokensB;

        // Now set up cross-chain mappings as the owner
        vm.startPrank(routerA.owner());
        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            lendStorageA.addUnderlyingToDestUnderlying(supportedTokensA[i], supportedTokensB[i], CHAIN_B_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageA.addUnderlyingToDestlToken(supportedTokensA[i], lTokensB[i], CHAIN_B_ID);
            // Add mapping from lToken to destination lToken
            lendStorageA.setChainLTokenMap(lTokensA[i], CHAIN_B_ID, lTokensB[i]);
        }
        vm.stopPrank();

        vm.startPrank(routerB.owner());
        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            lendStorageB.addUnderlyingToDestUnderlying(supportedTokensB[i], supportedTokensA[i], CHAIN_A_ID);
            // Add mapping from underlying token to destination lToken
            lendStorageB.addUnderlyingToDestlToken(supportedTokensB[i], lTokensA[i], CHAIN_A_ID);
            // Add mapping from lToken to destination lToken
            lendStorageB.setChainLTokenMap(lTokensB[i], CHAIN_A_ID, lTokensA[i]);
        }
        vm.stopPrank();

        // Set up initial prices for supported tokens on both chains

        for (uint256 i = 0; i < supportedTokensA.length; i++) {
            priceOracleA.setDirectPrice(supportedTokensA[i], 1e18);
        }

        for (uint256 i = 0; i < supportedTokensB.length; i++) {
            priceOracleB.setDirectPrice(supportedTokensB[i], 1e18);
        }

        vm.label(address(routerA), "Router A");
        vm.label(address(routerB), "Router B");
        // After deploying routers, set up their pair contracts
        routerA = CrossChainRouterMock(payable(routerAddressA));
        routerB = CrossChainRouterMock(payable(routerAddressB));

        // Set up pair relationships
        routerA.setPairContract(payable(address(routerB)));
        routerB.setPairContract(payable(address(routerA)));
    }

    // Helper function to supply tokens before testing borrowing
    function _supplyA(uint i, address user, uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[i];
        lToken = lendStorageA.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }

    function _supplyB(uint i, address user, uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        token = supportedTokensB[i];
        lToken = lendStorageB.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterB), amount);
        coreRouterB.supply(amount, token);
        vm.stopPrank();
    }

    function test_handle_liquidation_success_issue() public {
        address borrower = makeAddr("borrower");
        address liquidator = makeAddr("liquidator");
        address supplier = makeAddr("supplier");
        uint256 amountToSupply = 100_000e18;

        vm.deal(address(routerA), 1 ether);

        (address token0A, address lToken0A) = _supplyA(0, borrower, amountToSupply);
        (address token1B, address lToken1B) = _supplyB(1, supplier, amountToSupply * 2);

        uint256 borrowedAmount = 67500e18;

        vm.prank(borrower);

        // Execute borrow
        routerA.borrowCrossChain(borrowedAmount, supportedTokensA[1], CHAIN_B_ID);

        uint256 repayAmount = borrowedAmount / 2;

        vm.prank(deployer);
        priceOracleA.setDirectPrice(token0A, 0.1e18);

        vm.prank(deployer);

        // Liquidate
        vm.startPrank(liquidator);
        deal(token1B, liquidator, 100_000e18);
        IERC20(token1B).approve(address(coreRouterB), type(uint256).max);
        routerB.liquidateCrossChain(
            borrower,
            repayAmount, // amount to repay
            31337,
            lTokensB[0], // collateral lToken (on Chain B)
            token1B // borrowed asset (Chain B version)
        );
    }
}
```

The test fails with the error message: `[Revert] Borrow position not found`.

### Mitigation

_No response_