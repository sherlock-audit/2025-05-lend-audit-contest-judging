Winning Zinc Squirrel

Medium

# CoreRouter Fails with USDT Due to Non-Standard transfer() Implementation

### Summary

As mentioned in the README, the contracts are intended to be deployed on chains including Ethereum and utilize USDT. However, the `CoreRouter` contract is incompatible with USDT, as the `redeem()`, `borrow()`, and `borrowForCrossChain()` functions will revert during execution.

### Root Cause
The `CoreRouter` contract uses OpenZeppelin's IERC20 interface, which defines the transfer() function as returning a bool:
```solidity
/**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);
```
However, [USDT's transfer() function does not return any value](https://vscode.blockscan.com/ethereum/0xdac17f958d2ee523a2206206994597c13d831ec7), differing from the IERC20 standard.
```solidity
 function transfer(address to, uint value) public;
```
As a result, when `CoreRouter` calls `IERC20.transfer()`, the absence of a returned bool causes a mismatch with the expected function signature. This discrepancy can trigger a revert due to ABI encoding expecting a boolean return value from the contract call.



### Impact
The [redeem()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L124), [borrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L170), and [borrowForCrossChain()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L204) functions will always fail and revert when interacting with USDT due to the incompatible `transfer()` implementation.


### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.23;

import "forge-std/Test.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract MockUSDT {
    function transfer(address to, uint256 value) public {}
}

contract MockERC20 {
    function transfer(address to, uint256 value) public returns (bool) {}
}

contract POC_USDTTransfer is Test {
    MockUSDT usdt;
    MockERC20 erc20;

    function setUp() public {
        usdt = new MockUSDT();
        erc20 = new MockERC20();
    }

    // This test will revert because USDT's transfer() doesn't return a bool
    function testUSDTTransfer_should_revert() public {
        IERC20(address(usdt)).transfer(msg.sender, 1000);
    }

    // This test will pass because MockERC20's transfer() conforms to IERC20
    function testERC20Transfer_should_not_revert() public {
        IERC20(address(erc20)).transfer(msg.sender, 1000);
    }
}

```
1. Create a test file named `POC_USDTTransfer.t.sol` in the [test folder](https://github.com/sherlock-audit/2025-05-lend-audit-contest/tree/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/test).
2. Paste the code into the file and run the tests.

### Mitigation

Use safeTransfer instead of transfer.