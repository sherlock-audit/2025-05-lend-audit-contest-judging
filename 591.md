Magnificent Holographic Camel

High

# Unable to liquidate cross chain borrow

### Summary

Incorrect implementation to fetch cross chain borrowed amount can cause cross chain liquidation unable to executed

### Root Cause

In the function [`CrossChainRouter::_validateAndPrepareLiquidation()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L197), the repay amount is checked with the max liquidation amount, with max liquidation amount fetched from `lendStorage.getMaxLiquidationRepayAmount(params.borrower,params.borrowedlToken,false)`. The function [`LendStorage::getMaxLiquidationRepayAmount()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L573C14-L573C42) calculates cross chain borrow amount by using the function [`LendStorage::borrowWithInterest()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L478). The cross chain liquidation is initiated from dest chain, then the function `borrowWithInterest()` will calculate borrow amount from the variable `crossChainCollaterals[borrower][_token]`
```solidity
    function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;

        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
        // Only one mapping should be populated:
        if (borrows.length > 0) {
            for (uint256 i = 0; i < borrows.length; i++) {
                if (borrows[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
                }
            }
        } else {
            for (uint256 i = 0; i < collaterals.length; i++) {
                // Only include a cross-chain collateral borrow if it originated locally.
@>                if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) { // <<<<< incorrect condition
                    borrowedAmount +=
                        (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
                }
            }
        }
        return borrowedAmount;
    }
```
However, the condition `if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid)` can cause the function unable to find the correct cross chain borrow information to sum. This is because the [function `_handleBorrowCrossChainRequest()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L581C14-L581C44) adds cross chain collateral with `srcEid: srcEid, destEid: currentEid`, with `srcEid` is eid of source chain, and `currentEid` is eid of the dest chain. As a result, the function `borrowWithInterest()` will return `borrowedAmount = 0` and the max close is calculated to be `0` which is totally incorrect
```solidity
        ...
        if (found) {
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            lendStorage.updateCrossChainCollateral(
                payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
        } else {
            lendStorage.addCrossChainCollateral(
                payload.sender,
                destUnderlying,
                LendStorage.Borrow({
@>                    srcEid: srcEid,
@>                    destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
        }
        ...
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

1. A cross chain borrow is executed
2. After some time, the debt position is under water
3. A cross chain liquidation is initiated but failed because repay amount must be > 0, but the max close is calculated to be 0

### Impact

- Unable to execute cross chain liquidation
- Core functionality is broken

### PoC

_No response_

### Mitigation

```diff
-    function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
+    function borrowWithInterest(address borrower, address _lToken, uint32 remoteEid) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;

        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
        // Only one mapping should be populated:
        if (borrows.length > 0) {
            for (uint256 i = 0; i < borrows.length; i++) {
                if (borrows[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
                }
            }
        } else {
            for (uint256 i = 0; i < collaterals.length; i++) {
                // Only include a cross-chain collateral borrow if it originated locally.
-                if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
+                if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == remoteEid) {
                    borrowedAmount +=
                        (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
                }
            }
        }
        return borrowedAmount;
    }
```