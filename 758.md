Tall Sepia Marmot

High

# The local borrowing/debt position gets reduced during cross-chain repayment even though it should not.

### Summary

-

### Root Cause

-

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

During cross-chain repayment, the `CrossChainRouter.repayCrossChainBorrow()` function will be executed, which will in turn execute the `repayBorrowInternal()` function.

`CrossChainRouter.repayCrossChainBorrow -> CrossChainRouter.repayCrossChainBorrowInternal -> CrossChainRouter._handleRepayment -> CoreRouter.repayCrossChainLiquidation -> CoreRouter.repayBorrowInternal`

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L216

```solidity
File: CoreRouter.sol
216:     function repayCrossChainLiquidation(address _borrower, address _liquidator, uint256 _amount, address _lToken)
217:         external
218:     {
219:         require(msg.sender == crossChainRouter, "Access Denied");
220:         repayBorrowInternal(_borrower, _liquidator, _amount, _lToken, false);
221:     }
```

Note that the `_isSameChain` is set to `false` when calling the `repayBorrowInternal()` function. This means that it is attempting to repay the cross-chain debt or borrowing amount.

```solidity
repayBorrowInternal(_borrower, _liquidator, _amount, _lToken, false);
```

However, the bug or issue is that at Line 493-500 of the `repayBorrowInternal()` function below, the code is attempting to update debt or borrowing amount (`borrowBalance[user][lToken]`) of the current/same chain, which is incorrect. 

Since it is a cross-chain repayment, only the debt of cross-chain debt or borrowing amount that is stored in the `crossChainBorrows[user][underlying][index]` or `crossChainCollaterals[user][underlying][index]` needs to be reduced, which will be performed near the end of the transaction in the `CrossChainRouter._updateRepaymentState()` function.

Thus, during a cross-chain repayment, there is no need to call the `removeBorrowBalance()` or `updateBorrowBalance` in Line 493 - Line 499. Otherwise, it will lead to an unexpected result or a revert due to underflow.

If the `repayAmountFinal` is larger than the `borrowBalance[user][lToken]` (which can occur if the cross-chain debt is larger than same-chain debt), it could lead to an underflow revert when attempting to reduce them, resulting in cross-chain repayment being DOS or blocked. 

Another negative impact of this issue is that the `borrowBalance[user][lToken]` will be reduced when it should not have been. This means that the user's debt get reduced or get cleared when it should not have been, which in turn, means that since there is fewer debt now, user can start borrow additional assets from the protocol, leading to bad debt and loss of assets for the protocol.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L459

```solidity
File: CoreRouter.sol
459:     function repayBorrowInternal(
460:         address borrower,
461:         address liquidator,
462:         uint256 _amount,
463:         address _lToken,
464:         bool _isSameChain
465:     ) internal {
466:         address _token = lendStorage.lTokenToUnderlying(_lToken);
467: 
468:         LTokenInterface(_lToken).accrueInterest();
469: 
470:         uint256 borrowedAmount;
471: 
472:         if (_isSameChain) {
473:             borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
474:         } else {
475:             borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
476:         }
477: 
478:         require(borrowedAmount > 0, "Borrowed amount is 0");
479: 
480:         uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
481: 
482:         // Transfer tokens from the liquidator to the contract
483:         IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
484: 
485:         _approveToken(_token, _lToken, repayAmountFinal);
486: 
487:         lendStorage.distributeBorrowerLend(_lToken, borrower);
488: 
489:         // Repay borrowed tokens
490:         require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");
491: 
492:         // Update same-chain borrow balances
493:         if (repayAmountFinal == borrowedAmount) {
494:             lendStorage.removeBorrowBalance(borrower, _lToken);
495:             lendStorage.removeUserBorrowedAsset(borrower, _lToken);
496:         } else {
497:             lendStorage.updateBorrowBalance(
498:                 borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
499:             );
500:         }
```

### Impact

Bad debt and loss of assets for the protocol. Cross-chain repayment can be DOS or blocked. 

### PoC

_No response_

### Mitigation

Skip updating the same-chain debt during cross-chain repayment.

```diff
function repayBorrowInternal(
    address borrower,
    address liquidator,
    uint256 _amount,
    address _lToken,
    bool _isSameChain
) internal {
    address _token = lendStorage.lTokenToUnderlying(_lToken);

    LTokenInterface(_lToken).accrueInterest();

    uint256 borrowedAmount;

    if (_isSameChain) {
        borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
    } else {
        borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
    }

..SNIP..
+   if (_isSameChain) {
        // Update same-chain borrow balances
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }
+  }
```