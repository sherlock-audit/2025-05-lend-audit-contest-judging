Magnificent Holographic Camel

High

# Cross chain borrow debt only records principle on source chain

### Summary

Cross chain borrow debt only records principle on source chain

### Root Cause

The function [`_handleValidBorrowRequest()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L703) is executed on source chain after the cross chain borrow request is executed on dest chain. It updates the cross chain borrow record if existed, else it creates a new cross chain borrow record. In case updating an existing borrow, the new borrow amount does not accrues interest, but only accounts for principle
```solidity
    function _handleValidBorrowRequest(LZPayload memory payload, uint32 srcEid) private {
        // Find the specific borrow record using the new helper
        (bool found, uint256 index) =
            lendStorage.findCrossChainBorrow(payload.sender, payload.srcToken, srcEid, currentEid, payload.destlToken);

        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
@>            userBorrows[index].principle = userBorrows[index].principle + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
            // Add new borrow record
            lendStorage.addCrossChainBorrow(
                payload.sender,
                payload.srcToken,
                LendStorage.Borrow(
                    srcEid, currentEid, payload.amount, payload.borrowIndex, payload.destlToken, payload.srcToken
                )
            );
        }

        lendStorage.addUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));

        // Emit BorrowSuccess event
        emit BorrowSuccess(payload.sender, payload.srcToken, payload.amount);
    }
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

1. Alice initiates a cross chain borrow from chain A->B , with borrow value $1000. 
The borrow succeeds and on chain A, cross chain borrow amount is recorded $1000
2. After some time, Alice continues to borrow cross chain from chain A->B, with borrow value $500
Here, on chain A, cross chain borrow amount is updated to be $1500 without accounting for the interest accrued for $1000 borrowed at step (1)

### Impact

- Debt recorded is without interest
- Core functionality broken

### PoC

_No response_

### Mitigation

```diff
        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
-            userBorrows[index].principle = userBorrows[index].principle + payload.amount;
+            userBorrows[index].principle = userBorrows[index].principle * payload.borrowIndex / userBorrows[index].borrowIndex + payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;
```