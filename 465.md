Massive Taffy Millipede

Medium

# Accrual delay on collateral redemption leads to lost interest locked in the router

### Summary

The `CoreRouter.redeem` function calculates the amount of underlying tokens to return to a user based on an exchange rate fetched *before* interest is accrued within the `LToken.redeem` call. This will cause a small loss of interest for redeeming users, as the `CoreRouter` receives the full amount (including newly accrued interest) from the `LToken` but forwards a smaller amount (based on the stale rate) to the user. The difference accumulates in the `CoreRouter` contract, becoming effectively locked.


### Root Cause

[CoreRouter.sol#L115-L124](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L115-L124)
- `exchangeRateBefore` is determined by calling `LTokenInterface(_lToken).exchangeRateStored()` *prior* to the interest accrual and is used to calculate `expectedUnderlying ` which is ultimately the amount transfered to the user:
    ```solidity
    // CoreRouter.sol
    // exchange rate between underlying and LToken is calculated first
    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
    // ...
    uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
    // ...
    // while interest accrues on this call
    require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed"); 
    // ...
    IERC20(_token).transfer(msg.sender, expectedUnderlying); // User receives amount based on stale rate
    ```
[LToken.sol#L455](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L455)
[LToken.sol#L482](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L482)
[LToken.sol#L538](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L538)
- In `LToken.sol`'s `redeemInternal` function (called by `redeem`), `accrueInterest()` is invoked and then `redeemFresh`  sends back the underlying to the router based on the updated exchange rate
    ```solidity
    // LToken.sol
    function redeemInternal(uint256 redeemTokens) internal nonReentrant {
        accrueInterest(); // Interest is updated, new exchange rate applies internally
        redeemFresh(payable(msg.sender), redeemTokens, 0); // msg.sender here is CoreRouter
    }

     function redeemFresh(address payable redeemer, uint256 redeemTokensIn, uint256 redeemAmountIn) internal {
        ...
        // gets the new exchange rate
        Exp memory exchangeRate = Exp({mantissa: exchangeRateStoredInternal()});
        ...
        // sends back to router
        doTransferOut(redeemer, redeemAmount);
    ```

So `redeemFresh` transfers underlying tokens to `CoreRouter` based on the updated exchange rate (post-`accrueInterest`), while `CoreRouter` transfers to the end-user based on `exchangeRateBefore`. The longer the period between last accrual and user withdrawal, the larger the difference will be.


### Internal Pre-conditions

1. A user (collateral provider) needs to call `CoreRouter.redeem` to initiate the withdrawal of their supplied collateral.
2. The `accrualBlockNumber` state variable within the target `LToken` contract must be less than the `currentBlockNumber` at the time `LToken.redeemInternal()` calls `accrueInterest()`. This ensures that `blockDelta > 0` and new interest is calculated and applied during the transaction.

### External Pre-conditions

none

### Attack Path

1. User calls `CoreRouter.redeem(redeemLTokenAmount, lTokenAddress)` to withdraw their collateral.
2. `CoreRouter` reads the current exchange rate: `exchangeRateBefore = LToken.exchangeRateStored()`.
3. `CoreRouter` calculates the amount of underlying tokens it expects to send to the user: `expectedUnderlying = (redeemLTokenAmount * exchangeRateBefore) / 1e18`.
4. `CoreRouter` calls `LToken.redeem(redeemLTokenAmount)`.
5. Inside the `LToken.redeem` call (specifically `redeemInternal`):
    a. `LToken.accrueInterest()` is executed. This updates the LToken's internal accounting (`totalBorrows`, `totalReserves`, `borrowIndex`), leading to a new,  higher current exchange rate.
    b. `LToken.redeemFresh()` calculates the amount of underlying tokens to transfer based on `redeemLTokenAmount` and this *newly updated* exchange rate. Let's call this `actualUnderlyingSentToRouter`.
    c. `LToken.redeemFresh()` transfers `actualUnderlyingSentToRouter` to the `CoreRouter` contract (which is `msg.sender` from `LToken`'s perspective).
6. Execution returns to `CoreRouter`.
7. `CoreRouter` transfers `expectedUnderlying` (calculated in step 3 using the stale `exchangeRateBefore`) to the original User.
8. The difference, `actualUnderlyingSentToRouter - expectedUnderlying`, representing the interest that accrued on the user's share *during this transaction's execution*, remains in the `CoreRouter` contract.

### Impact

The redeeming user suffers a loss of the interest that accrued on their portion of the collateral between the last global interest accrual event for the `LToken` and their own `redeem` transaction. While the amount per transaction might not be large, this leads to a consistent drain of value from users. The `CoreRouter` contract will accumulate these residual amounts of underlying tokens. **Crucially, as there is no function in the router to withdraw arbitrary tokens, this accumulated interest becomes effectively locked and inaccessible within the `CoreRouter` contract.**

### PoC

Paste the following test in the `Lend-V2/test/TestRedeeming.t.sol` contract and run with `forge test --mt testInterestAccrualOnRedeem -vvvv` :
```solidity
    // helper function
    function _supplyPerUser(uint256 amount, address user) internal returns (address token, address lToken) {
        token = supportedTokens[0];
        lToken = lendStorage.underlyingTolToken(token);

        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouter), amount);
        coreRouter.supply(amount, token);
    }

    // main test
    function testInterestAccrualOnRedeem() public {
        uint256 supplyAmount = 1000e18;
        uint256 borrowAmount = 500e18;
        address alice = makeAddr("alice");
        address bob = makeAddr("bob");

        // Setup: Alice supplies collateral, Bob borrows to generate interest
        vm.startPrank(alice);
        (address token, address lToken) = _supplyPerUser(supplyAmount, alice);
        vm.stopPrank();

        vm.startPrank(bob);
        _supplyPerUser(supplyAmount, bob); // Bob also supplies as collateral
        coreRouter.borrow(borrowAmount, token); // Bob borrows to create interest accrual
        vm.stopPrank();

        // Fast forward time to allow interest to accumulate
        vm.warp(block.timestamp + 365 days);
        vm.roll(block.number + 100);

        vm.startPrank(alice);
        // Get Alice's lToken balance
        uint256 aliceLTokenBalance = lendStorage.totalInvestment(alice, lToken);

        // Record exchange rate BEFORE redeem (stale rate)
        uint256 exchangeRateBefore = LTokenInterface(lToken).exchangeRateStored();

        // Calculate expected underlying based on stale rate
        uint256 expectedFromStaleRate = (aliceLTokenBalance * exchangeRateBefore) / 1e18;

        // Record CoreRouter's token balance before redeem
        uint256 routerBalanceBefore = IERC20(token).balanceOf(address(coreRouter));

        // Record Alice's token balance before redeem
        uint256 aliceBalanceBefore = IERC20(token).balanceOf(alice);

        // Perform redeem (this will trigger accrueInterest internally)
        coreRouter.redeem(aliceLTokenBalance, payable(lToken));

        // Record exchange rate AFTER redeem (fresh rate, post-accrual)
        uint256 exchangeRateAfter = LTokenInterface(lToken).exchangeRateStored();

        // Calculate what Alice should have received with fresh rate
        uint256 expectedFromFreshRate = (aliceLTokenBalance * exchangeRateAfter) / 1e18;

        // Record balances after redeem
        uint256 routerBalanceAfter = IERC20(token).balanceOf(address(coreRouter));
        uint256 aliceBalanceAfter = IERC20(token).balanceOf(alice);

        // Calculate actual amounts
        uint256 aliceReceived = aliceBalanceAfter - aliceBalanceBefore;
        uint256 routerGained = routerBalanceAfter - routerBalanceBefore;
        vm.stopPrank();

        // Verify the vulnerability exists
        assertGt(exchangeRateAfter, exchangeRateBefore, "Interest should have accrued");
        assertGt(expectedFromFreshRate, expectedFromStaleRate, "Fresh rate should yield more tokens");

        // Alice received amount based on stale rate
        assertEq(aliceReceived, expectedFromStaleRate, "Alice received amount based on stale exchange rate");

        // The difference between fresh and stale calculations remains in CoreRouter
        uint256 expectedDifference = expectedFromFreshRate - expectedFromStaleRate;
        assertGt(expectedDifference, 0, "There should be a positive difference");

        // CoreRouter should have gained this difference (lost interest)
        assertEq(routerGained, expectedDifference, "CoreRouter should have accumulated the lost interest");

        // Demonstrate that Alice lost interest that should have been hers
        assertLt(
            aliceReceived, expectedFromFreshRate, "Alice received less than she should have with current exchange rate"
        );
    }
```

### Mitigation
Add a call to `LToken.accrueInterest` in the beginning of the function so the exchange rate is up-to-date and use it to calculate the `expectedUnderlying`