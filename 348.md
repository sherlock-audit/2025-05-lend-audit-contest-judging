Beautiful Gingerbread Beetle

High

# Incorrect Borrow Balance Update on Cross-Chain Repayment May Lead to Protocol Insolvency

### Summary

The function `repayCrossChainBorrow` incorrectly updates the same-chain borrow balance for a user when repaying a cross-chain loan. In scenarios where a user holds both cross-chain and same-chain debt for the same `lToken`, the function erroneously assumes the repayment applies to both. This results in the protocol reducing the same-chain borrow amount, even though no repayment was made for that portion. Consequently, the borrower's debt position is inaccurately recorded, leading to potential under-collateralization.

### Root Cause

The [`repayCrossChainBorrow`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L161) function calls [`repayCrossChainBorrowInternal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L368C5-L396C1) in `CrossChainRouter`. This function further calls [`_handleRepayment`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L387C9-L387C25), which then invokes [`repayCrossChainLiquidation`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L423C5-L426C6) in `CoreRouter`. This in turn leads to a call to [`repayBorrowInternal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L220).

The core issue lies within the `repayBorrowInternal` function. Although it accepts an `_isSameChain` flag to distinguish between cross-chain and same-chain repayments, the function proceeds to update the same-chain borrow balance [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L492) without verifying the request was actually from the same chain. 

```solidity 
function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);

        LTokenInterface(_lToken).accrueInterest();

        uint256 borrowedAmount;

        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }

        require(borrowedAmount > 0, "Borrowed amount is 0");

        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;

        // Transfer tokens from the liquidator to the contract
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);

        _approveToken(_token, _lToken, repayAmountFinal);

        lendStorage.distributeBorrowerLend(_lToken, borrower);

        // Repay borrowed tokens
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");

        // Update same-chain borrow balances
       // @audit - Same chain borrow balances are updated when the debt is repaid using cross chain repay function
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }

        // Emit RepaySuccess event
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }
```

### Internal Pre-conditions

User holds both cross-chain and same-chain debt for the same `lToken`



### Impact

If a borrower has both cross-chain and same-chain loans for the same lToken, this bug can zero out the same-chain loan without actual repayment, potentially causing the protocol to go underwater.



### PoC

Steps to run PoC - 

1. Paste the following test in `Lend-V2/test/TestBorrowingCrossChain.t.sol`
2. Run the following command  - `forge test --mt test_cross_chain_repay -vvvv`


```solidity 
function test_cross_chain_repay() public {
        // Fund Router A with ETH for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        uint256 amountToSupply = 1e30; // Large amount for testing

        // First supply tokens as collateral on Chain A
        (address tokenA, address ltokenA) = _supplyA(amountToSupply);

        // Then supply tokens as borrowable on Chain B
        // @note - Has to be enough tokens to cover the borrow on the destination chain...
        (address tokenB, address ltokenB) = _supplyB(amountToSupply * 2);

          // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
        uint256 maxBorrow = (lendStorageA.getMaxBorrowAmount(deployer,ltokenA) * 0.9e18) / 1e18;

         // Verify token mappings
        address destToken = lendStorageA.underlyingToDestUnderlying(tokenA, CHAIN_B_ID);

        require(destToken != address(0), "Token mapping not set up correctly");

        // Get initial balances
        uint256 initialTokenBalance = IERC20(destToken).balanceOf(deployer);

        vm.startPrank(deployer);

        // Expect BorrowSuccess event
        vm.expectEmit(true, true, true, true);
        emit BorrowSuccess(deployer, tokenA,maxBorrow );

        // Call borrowCrossChain with token address
        routerA.borrowCrossChain(maxBorrow, tokenA, CHAIN_B_ID);

        // Verify the borrow was successful
        assertEq(
            IERC20(destToken).balanceOf(deployer) - initialTokenBalance,
            maxBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        // User now also borows same lToken on same chain
        // User borrows same token on Chain B
        uint256 borrowAmount = 10e18; // 10 tokens to borrow
        coreRouterB.borrow(
            borrowAmount,
            tokenB
        );

        IERC20(tokenB).approve(address(coreRouterB), maxBorrow);

       (uint256 borrowAmountBefore,) = lendStorageB.borrowBalance(deployer, ltokenB);

       // Repay cross-chain borrow
        routerB.repayCrossChainBorrow(
            address(deployer),
            maxBorrow,
            ltokenB,
            31337 // chainId for testing
        );

       (uint256 borrowAmountAfter,) = lendStorageB.borrowBalance(deployer, ltokenB);
       assertNotEq(borrowAmountBefore, borrowAmountAfter);
       // The borrow amount on the same chain is reduced to 0 even though the user did not repay amount
       // Balance update for chain should not affect the borrow amount while repaying cross chain loan
       assertEq(borrowAmountAfter, 0);
       
       vm.stopPrank();
    }
```

### Mitigation

```diff 
function repayBorrowInternal(
        address borrower,
        address liquidator,
        uint256 _amount,
        address _lToken,
        bool _isSameChain
    ) internal {
        address _token = lendStorage.lTokenToUnderlying(_lToken);

        LTokenInterface(_lToken).accrueInterest();

        uint256 borrowedAmount;

        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
            borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
        }

        require(borrowedAmount > 0, "Borrowed amount is 0");

        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;

        // Transfer tokens from the liquidator to the contract
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);

        _approveToken(_token, _lToken, repayAmountFinal);

        lendStorage.distributeBorrowerLend(_lToken, borrower);

        // Repay borrowed tokens
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");

        // @audit - Same chain borrow balances are updated when the debt is repaid using cross chain repay function
        // Update same-chain borrow balances
+    if(_isSameChain){
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeBorrowBalance(borrower, _lToken);
            lendStorage.removeUserBorrowedAsset(borrower, _lToken);
        } else {
            lendStorage.updateBorrowBalance(
                borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
            );
        }
+ }

        // Emit RepaySuccess event
        emit RepaySuccess(borrower, _lToken, repayAmountFinal);
    }


```