Beautiful Gingerbread Beetle

High

# Liquidator can claim collateral tokens without repaying borrowers loan

### Summary

During the cross-chain liquidation process, the protocol follows a `Chain B → Chain A → Chain B` pattern to ensure all state changes are properly synchronized between chains.

Cross-chain liquidation is initiated on the chain **where the debt exists** — for example, Chain B. On Chain B, the [`liquidateCrossChain`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L172C13-L172C33) function is called. It then sends a cross-chain message to Chain A to seize collateral via [this call](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L274).

Before sending the message to Chain A, the LToken passed is correctly converted to Chain A’s version of the LToken via this line:

```solidity
 lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid), // Convert to Chain A version before sending
```

[Reference](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L280)

Once the message is received on Chain A, the function [`_handleLiquidationExecute`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L774) is invoked. It successfully seizes the collateral, distributes rewards, and sends a confirmation message back to Chain B via `_send`.

However, **while sending this message back**, the contract mistakenly uses the Chain A version of the LToken (`payload.destlToken`) **without converting it back to Chain B’s LToken address**:

```solidity
_send(
    srcEid,
    payload.amount,
    0,
    0,
    payload.sender,
    payload.destlToken, // ❌ Still Chain A's LToken address
    payload.liquidator,
    payload.srcToken,
    ContractType.LiquidationSuccess
);
```

[Source](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L355C9-L355C14)

On Chain B, this message is received and processed by [`_handleLiquidationSuccess`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L779C13-L779C38). The function attempts to resolve the underlying token from the LToken like so:

```solidity
address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);
```

[Line of interest](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L445)

Because `payload.destlToken` is a Chain A address and not registered on Chain B, this returns `address(0)`. The next function call to `findCrossChainCollateral` fails to locate the collateral entry and **reverts**:

```solidity
(bool found, uint256 index) = lendStorage.findCrossChainCollateral(
    payload.sender,
    underlying,
    currentEid, // srcEid is current chain
    0, // We don't know destEid yet, but we can match on other fields
    payload.destlToken,
    payload.srcToken
);

require(found, "Borrow position not found");
```

---

### Root Cause

While sending a success liquidation message back, the contract mistakenly uses current chain version of the LToken (payload.destlToken) without converting it back to other chain's  LToken address [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L361) 



### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

-

### Impact

Since the message from ChainB -> ChainA succeeded and the liquidator gains user collateral [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L337) and then sends the crosschain message back to ChainB [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L355). 

Since it does not pass the correct lToken version on Chain B, the call reverts [here](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L457) since the [underlying token ](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L445) returned here will be `address(0)`

The liquidator here gains users' collateral without repaying the users' borrowed tokens. This completely breaks the cross-chain liquidation mechanism and protocol accounting as well 

Note - Even if someone tries to retry the layerzero received message on Chain B  the call would still fail

### PoC

1. Paste the following test in `Lend-V2/test/TestBorrowingCrossChain.t.sol`
2. Run the following command - `forge test --mt test_cross_chain_liquidation_after_price_drop -vvvv`

```solidity
  function test_cross_chain_liquidation_after_price_drop()
        public
    {
        // Bound inputs to reasonable values
        uint256 supplyAmount = 100e18;
        uint borrowAmount =  75e18; // Max 75% LTV
        uint256 newPrice = 2e16; // 2% of original price

        // Supply collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount,0);

        // Supply liquidity on Chain B for borrowing
        (address tokenB, address lTokenB) = _supplyB(liquidator,supplyAmount,0);

        vm.deal(address(routerA), 1 ether); // For LayerZero fees

        // Borrow cross-chain from Chain A to Chain B
        vm.startPrank(deployer);
        routerA.borrowCrossChain(borrowAmount, tokenA, CHAIN_B_ID);
        vm.stopPrank();

        vm.startPrank(lendStorageA.owner());
        lendStorageA.addSupportedTokens(tokenA, lTokenA);
        lendStorageB.addSupportedTokens(tokenB, lTokenB);
        vm.stopPrank();

        // Simulate price drop of collateral (tokenA) only on first chain
        priceOracleA.setDirectPrice(tokenA, newPrice);
        // Simulate price drop of collateral (tokenA) only on second chain
        priceOracleB.setDirectPrice(
            lendStorageB.lTokenToUnderlying(lendStorageB.crossChainLTokenMap(lTokenA, block.chainid)), newPrice
        );

        // Set up liquidator with borrowed asset on Chain A
        vm.deal(liquidator, 1 ether);
        vm.startPrank(liquidator);

        // We need the Chain A version of the tokens for liquidation on Router A
        ERC20Mock(tokenA).mint(liquidator, borrowAmount);
        IERC20(tokenA).approve(address(routerA), borrowAmount);

        // Repay 50% of the borrow
        uint256 repayAmount = borrowAmount / 200;

        ERC20Mock(tokenB).mint(liquidator, 1e30);

        // approve router b to spend repay amount
        IERC20(tokenB).approve(address(coreRouterB), type(uint256).max);

        vm.expectRevert("Borrow position not found");
        // Call liquidateBorrow on Router B (where the borrow exists)
        routerB.liquidateCrossChain(
            deployer, // borrower
            repayAmount, // amount to repay
            31337, // chain where the collateral exists
            lTokenB, // collateral lToken (on Chain B)
            tokenB // borrowed asset (Chain B version)
        );
        vm.stopPrank();
    }

```

### Mitigation

Before sending the confirmation message back to Chain B, convert the LToken back to the version that is registered on Chain B (the original source chain). This ensures consistency and prevents address mismatches.

