Creamy Pine Cow

High

# Cross chain principle borrows are not properly accounted for on the source chain

### Summary

Cross-chain borrows originating from a source chain A to a destination chain B are not properly updated on the source chain leading to differing principle values on both chains if an existing borrow already exists - this in turn affects the uniformity of reported borrow and collateral ratios across chains.

### Root Cause

Cross-chain borrows are initiated using [CrossChainRouter::borrowCrossChain()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L113-L154) on the source chain. When the payload is received on the destination chain's `CrossChainRouter`, logic is handled by [CrossChainRouter::_handleBorrowCrossChainRequest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L581-L673). Within this function, the collateral on the source chain which was sent with the payload is ensured to be sufficient for the `totalBorrows (previous borrows + the new one)` and the borrow principle and index are properly accounted for:

```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        ...

        // Get existing borrow amount
        (uint256 totalBorrowed,) = lendStorage.getHypotheticalAccountLiquidityCollateral(
            payload.sender, LToken(payable(payload.destlToken)), 0, payload.amount
        );

        // Verify the collateral from source chain is sufficient for total borrowed amount
        require(payload.collateral >= totalBorrowed, "Insufficient collateral");

        // Execute the borrow on destination chain
        CoreRouter(coreRouter).borrowForCrossChain(payload.sender, payload.amount, payload.destlToken, destUnderlying);

        /**
         * @dev If existing cross-chain collateral, update it. Otherwise, add new collateral.
         */
        if (found) {
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            lendStorage.updateCrossChainCollateral(
                payload.sender, destUnderlying, index, userCrossChainCollaterals[index]
            );
        } else {
            lendStorage.addCrossChainCollateral(
                payload.sender,
                destUnderlying,
                LendStorage.Borrow({
                    srcEid: srcEid,
                    destEid: currentEid,
                    principle: payload.amount,
                    borrowIndex: currentBorrowIndex,
                    borrowedlToken: payload.destlToken,
                    srcToken: payload.srcToken
                })
            );
        }

        ...

        // Send confirmation back to source chain with updated borrow details
        _send(
            srcEid,
            payload.amount,
            currentBorrowIndex,
            payload.collateral,
            payload.sender,
            payload.destlToken,
            payload.liquidator,
            payload.srcToken,
            ContractType.ValidBorrowRequest
        );
    }
```

Another payload is sent back to the source chain as confirmation of a valid borrow request - this request contains the current borrow index of the borrowed token from the destination chain. On receiving this, accounting is done on the source chain with [CrossChainRouter::_handleValidBorrowRequest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L703-L731):

```solidity
    function _handleValidBorrowRequest(LZPayload memory payload, uint32 srcEid) private {
        // Find the specific borrow record using the new helper
        (bool found, uint256 index) =
            lendStorage.findCrossChainBorrow(payload.sender, payload.srcToken, srcEid, currentEid, payload.destlToken);

        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
            userBorrows[index].principle = userBorrows[index].principle + payload.amount;    // @audit: existing borrow principle is not updated
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
            // Add new borrow record
            lendStorage.addCrossChainBorrow(
                payload.sender,
                payload.srcToken,
                LendStorage.Borrow(
                    srcEid, currentEid, payload.amount, payload.borrowIndex, payload.destlToken, payload.srcToken
                )
            );
        }

        lendStorage.addUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));

        // Emit BorrowSuccess event
        emit BorrowSuccess(payload.sender, payload.srcToken, payload.amount);
    }
```

The problem lies in how the principle is updated if an existing borrow is found:

```solidity
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
            userBorrows[index].principle = userBorrows[index].principle + payload.amount;    // @audit: existing borrow principle is not updated
            userBorrows[index].borrowIndex = payload.borrowIndex;
```

The newly borrowed amount is simply added to the existing borrow, without considering accumulated principle debt due to increasing `borrowIndex`, while on the destination chain this is considered:

```solidity
    function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
        ...

        /**
         * @dev If existing cross-chain collateral, update it. Otherwise, add new collateral.
         */
        if (found) {
            uint256 newPrincipleWithAmount = (userCrossChainCollaterals[index].principle * currentBorrowIndex)
                / userCrossChainCollaterals[index].borrowIndex;

            userCrossChainCollaterals[index].principle = newPrincipleWithAmount + payload.amount;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;

            ...
```

Thus leading to different principle values on source and destination chain if the borrow index has increased (which it always will over time).

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

For simplicity, we'll assume the following.

1. BTC is $100k USDC.
2. On supplying 1 BTC, after all collateral ratio and oracle pricing calculations - a max of $80k can be borrowed.

#### Scenario
1. User deposits 1 BTC on chain A.
2. User borrows $10k USDC on chain B with a borrow index of 1e18.
3. Borrow index on chain B changes to 1.1e18.
4. User borrows another $10k USDC on chain B, and on chain B - his principle accounting is done as follows:

```text
        new_principle = (previous_principle * new_borrow_index / previous_borrow_index) + newly_borrowed_amount
                i.e = ($10_000 USDC * 1.1e18 / 1e18) + $10_000 = $21_000 USDC
```

5. Chain A receives valid borrow request payload with the new borrow index and updates as such:

```text
        new_principle = previous_principle + newly_borrowed_amount
                i.e = ($10_000 USDC + $10_000 USDC) = $20_000 USDC
```

Accounting mismatch between chains.

### Impact

Affects the accuracy and uniformity of functions like [LendStorage::borrowWithInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L478) across chains - as this function is used within multiple functions e.g:

[LendStorage::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L428): used for collateral and borrow ratio calculation. This mismatch opens up a window for under-estimation of cross-chain borrow amounts, letting an attacker leverage borrowing over his collateral value due to basically no interest from the source chain and slowly grief the protocol.

### PoC

_No response_

### Mitigation

Account for changed borrow index:

```solidity
    function _handleValidBorrowRequest(LZPayload memory payload, uint32 srcEid) private {
        // Find the specific borrow record using the new helper
        (bool found, uint256 index) =
            lendStorage.findCrossChainBorrow(payload.sender, payload.srcToken, srcEid, currentEid, payload.destlToken);

        if (found) {
            // Update existing borrow
            LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
            userBorrows[index].principle = userBorrows[index].principle * payload.borrowIndex / userBorrows[index].borrowIndex // @audit-fix: accumulate principle interest
            userBorrows[index].principle = userBorrows[index].principle + payload.amount;    
            userBorrows[index].borrowIndex = payload.borrowIndex;

            // Update in storage
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        } else {
       ...
    }
```