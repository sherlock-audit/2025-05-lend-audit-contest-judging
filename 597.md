Winning Cinnabar Gorilla

High

# User borrowed asset can be wrongly removed when the user repay cross chain borrow

### Summary

User borrowed asset can be wrongly removed when the user repay cross chain borrow.

### Root Cause

When a user repays cross chain borrow, the repayment status will be updated.

[CrossChainRouter.sol#L389-L392](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L389-L392):
```solidity
        // Update state
        _updateRepaymentState(
            borrower, _token, _lToken, borrowPosition, repayAmountFinal, borrowedAmount, index, _srcEid
        );
```

In `_updateRepaymentState()`, if `repayAmountFinal` equals `borrowedAmount` and there is only 1 cross chain borrow, then the user's borrowed asset is removed.

[CrossChainRouter.sol#L518-L522](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L518-L522):
```solidity
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeCrossChainCollateral(borrower, _token, index);
            if (userCrossChainCollaterals.length == 1) {
@>              lendStorage.removeUserBorrowedAsset(borrower, _lToken);
            }
```

[LendStorage.sol#L186-L188](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L186-L188):
```solidity
    function removeUserBorrowedAsset(address user, address lTokenAddress) external onlyAuthorized {
        userBorrowedAssets[user].remove(lTokenAddress);
    }
```

This is problematic because `userBorrowedAssets[user]` does not store cross-chain borrow info but the user's local borrowed asset, removing this can lead to incorrect accounting of user's local borrow status.

The same issue exists in `CrossChainRouter::_handleDestRepayMessage()`.

[CrossChainRouter.sol#L557-L561](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L557-L561):
```solidity
            // Full repayment
            lendStorage.removeCrossChainBorrow(payload.sender, payload.srcToken, index);
            if (userBorrows.length == 1) {
@>              lendStorage.removeUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));
            }
```

### Internal Pre-conditions

User has local borrow position.

### External Pre-conditions

NA

### Attack Path

NA

### Impact

`userBorrowedAssets[user]` stores a user's local borrowed asset, and is used for calculating user's borrowed amount in `LendStorage::getHypotheticalAccountLiquidityCollateral()`, as the borrowed asset info is wrongly removed, user does not have to repay their debt, results in the loss of protocol suppliers.

### PoC

We should firstly fix some other issues in the codebase.

1. To get the correct cross-chain `borrowedAmount` so that cross-chain repayment can be successfully executed;

[CoreRouter.sol#L472-L476](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L472-L476):
```diff
        if (_isSameChain) {
            borrowedAmount = lendStorage.borrowWithInterestSame(borrower, _lToken);
        } else {
-           borrowedAmount = lendStorage.borrowWithInterest(borrower, _lToken);
+           borrowedAmount = lendStorage.borrowWithInterestForRepayCrossChainBorrow(borrower, _lToken);
        }
```

Add the function below in `LendStorage`:

```solidity
    function borrowWithInterestForRepayCrossChainBorrow(address borrower, address _lToken) public view returns (uint256) {
        address _token = lTokenToUnderlying[_lToken];
        uint256 borrowedAmount;

        Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

        for (uint256 i = 0; i < collaterals.length; i++) {
            // Only include a cross-chain collateral borrow if it originated locally.
            if (collaterals[i].destEid == currentEid) {
                borrowedAmount +=
                    (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
            }
        }

        return borrowedAmount;
    }
```

2. We should not remove user local borrow balance when repay cross-chain borrow;
[CoreRouter.sol#L492-L500](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L492-L500):
```diff
+       if (_sameChain) {
            // Update same-chain borrow balances
            if (repayAmountFinal == borrowedAmount) {
                lendStorage.removeBorrowBalance(borrower, _lToken);
                lendStorage.removeUserBorrowedAsset(borrower, _lToken);
            } else {
                lendStorage.updateBorrowBalance(
                    borrower, _lToken, borrowedAmount - repayAmountFinal, LTokenInterface(_lToken).borrowIndex()
                );
            }
+       }
```

3. Now we run `forge test testPOC_UserBorrowedAssetIsWronglyRemoved`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import "forge-std/Test.sol";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

import {ILayerZeroEndpointV2} from "@layerzerolabs/lz-evm-protocol-v2/contracts/interfaces/ILayerZeroEndpointV2.sol";

import {Lend} from "../src/Governance/Lend.sol";
import {Lendtroller, LendtrollerInterface} from "../src/Lendtroller.sol";
import {LErc20Immutable, LToken} from "../src/LErc20Immutable.sol";
import {SimplePriceOracle} from "../src/SimplePriceOracle.sol";
import {WhitePaperInterestRateModel, InterestRateModel} from "../src/WhitePaperInterestRateModel.sol";

import {LendStorage} from "../src/LayerZero/LendStorage.sol";
import {CoreRouter} from "../src/LayerZero/CoreRouter.sol";
import {CrossChainRouter, Origin} from "../src/LayerZero/CrossChainRouter.sol";

contract POC is Test {
    using SafeERC20 for IERC20;

    uint32 constant chainAEid = 30101; // Ethereum
    uint32 constant chainBEid = 30184; // Base

    address owner;
    address mockDestLToken;
    address mockPeer;

    IERC20 btc;
    IERC20 dai;
    IERC20 weth;

    LErc20Immutable lTokenBTC;
    LErc20Immutable lTokenDAI;
    LErc20Immutable lTokenWETH;

    Lend lend;

    ILayerZeroEndpointV2 endpoint;

    Lendtroller lendtroller;
    SimplePriceOracle priceOracle;
    WhitePaperInterestRateModel interestRateModel;

    LendStorage lendStorage;
    CoreRouter coreRouter;
    CrossChainRouter crossChainRouter;

    function setUp() public {
        vm.createSelectFork("https://eth.drpc.org");

        owner = makeAddr("Owner");
        mockDestLToken = makeAddr("MockDestLToken");
        mockPeer = makeAddr("ChainBPeer");
        endpoint = ILayerZeroEndpointV2(
            0x1a44076050125825900e736c501f859c50fE728c
        );

        btc = IERC20(0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599);
        dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
        weth = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

        vm.startPrank(owner);

        // Deploy Lendtroller
        lendtroller = new Lendtroller();
        // Deploy Lend
        lend = new Lend(address(lendtroller));
        // Deploy PriceOracle
        priceOracle = new SimplePriceOracle();
        // Deploy InterestRateModel
        interestRateModel = new WhitePaperInterestRateModel(0.1e18, 0.05e18);
        // Deploy lTokens
        lTokenBTC = new LErc20Immutable(
            address(btc),
            lendtroller,
            interestRateModel,
            1e8, // 1:1
            "Lending BTC",
            "lBTC",
            18,
            payable(owner)
        );
        lTokenDAI = new LErc20Immutable(
            address(dai),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending DAI",
            "lDAI",
            18,
            payable(owner)
        );
        lTokenWETH = new LErc20Immutable(
            address(weth),
            lendtroller,
            interestRateModel,
            1e18, // 1:1
            "Lending WETH",
            "lWETH",
            18,
            payable(owner)
        );

        // Deploy LendStorage
        lendStorage = new LendStorage(
            address(lendtroller),
            address(priceOracle),
            chainAEid
        );
        // Deploy CoreRouter
        coreRouter = new CoreRouter(
            address(lendStorage),
            address(priceOracle),
            address(lendtroller)
        );
        // Deploy CrossChainRouter
        crossChainRouter = new CrossChainRouter(
            address(endpoint),
            owner,
            address(lendStorage),
            address(priceOracle),
            address(lendtroller),
            payable(address(coreRouter)),
            chainAEid
        );

        // Configurations
        {
            priceOracle.setUnderlyingPrice(lTokenBTC, 100000e18);
            priceOracle.setUnderlyingPrice(lTokenDAI, 1e18);
            priceOracle.setUnderlyingPrice(lTokenWETH, 2500e18);

            lendtroller._setPriceOracle(priceOracle);
            lendtroller.setLendStorage(address(lendStorage));
            lendtroller._supportMarket(lTokenBTC);
            lendtroller._supportMarket(lTokenDAI);
            lendtroller._supportMarket(lTokenWETH);
            lendtroller.setLendToken(address(lend));
            lendtroller._setCollateralFactor(lTokenBTC, 0.8e18);
            lendtroller._setCollateralFactor(lTokenDAI, 0.8e18);
            lendtroller._setCollateralFactor(lTokenWETH, 0.8e18);
            lendtroller._setCloseFactor(0.5e18);
            lendtroller._setLiquidationIncentive(1.08e18);

            LToken[] memory lTokens = new LToken[](3);
            lTokens[0] = lTokenBTC;
            lTokens[1] = lTokenDAI;
            lTokens[2] = lTokenWETH;
            uint256[] memory supplySpeeds = new uint256[](3);
            supplySpeeds[0] = 1e18;
            supplySpeeds[1] = 1e18;
            supplySpeeds[2] = 1e18;
            uint256[] memory borrowSpeeds = new uint256[](3);
            borrowSpeeds[0] = 2e18;
            borrowSpeeds[1] = 2e18;
            borrowSpeeds[2] = 2e18;
            lendtroller._setLendSpeeds(lTokens, supplySpeeds, borrowSpeeds);

            lendStorage.addSupportedTokens(address(btc), address(lTokenBTC));
            lendStorage.addSupportedTokens(address(dai), address(lTokenDAI));
            lendStorage.addSupportedTokens(address(weth), address(lTokenWETH));
            lendStorage.addUnderlyingToDestlToken(
                address(btc),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(dai),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.addUnderlyingToDestlToken(
                address(weth),
                address(mockDestLToken),
                chainBEid
            );
            lendStorage.setAuthorizedContract(address(coreRouter), true);
            lendStorage.setAuthorizedContract(address(crossChainRouter), true);

            crossChainRouter.setPeer(
                chainBEid,
                bytes32(uint256(uint160(mockPeer)))
            );

            coreRouter.setCrossChainRouter(address(crossChainRouter));

            deal(address(crossChainRouter), 1 ether);
        }

        vm.stopPrank();

        {
            vm.label(address(btc), "BTC");
            vm.label(address(dai), "DAI");
            vm.label(address(weth), "WETH");
            vm.label(address(lTokenBTC), "lToken BTC");
            vm.label(address(lTokenDAI), "lToken DAI");
            vm.label(address(lTokenWETH), "lToken WETH");
            vm.label(address(endpoint), "Endpoint");
            vm.label(address(lendtroller), "Lendtroller");
            vm.label(address(lend), "Lend");
            vm.label(address(priceOracle), "PriceOracle");
            vm.label(address(lendStorage), "LendStorage");
            vm.label(address(coreRouter), "CoreRouter");
            vm.label(address(crossChainRouter), "CrossChainRouter");
        }
    }

    function testPOC_UserBorrowedAssetIsWronglyRemoved() public {
        // Supply liqudity
        {
            address supplier = makeAddr("Supplier");
            deal(address(dai), supplier, 10000e18);

            vm.startPrank(supplier);
            dai.safeApprove(address(coreRouter), 10000e18);
            coreRouter.supply(10000e18, address(dai));
            vm.stopPrank();
        }

        address alice = makeAddr("Alice");

        // Supply WETH and borrow DAI
        {
            deal(address(weth), alice, 1 ether);

            vm.startPrank(alice);

            // Supply
            weth.safeApprove(address(coreRouter), 1 ether);
            coreRouter.supply(1 ether, address(weth));

            // Borrow
            coreRouter.borrow(500e18, address(dai));

            vm.stopPrank();
        }

        // Cross-chain borrow
        // Mock LayerZero data to trigger _handleBorrowCrossChainRequest()
        {
            Origin memory _origin = Origin({
                srcEid: chainBEid,
                sender: bytes32(uint256(uint160(address(mockPeer)))),
                nonce: 1
            });

            bytes memory _payload = abi.encode(
                1000e18, // amount
                0, // borrowIndex
                2000e18, // collateral
                alice, // sender
                address(lTokenDAI), // destLToken
                address(0), // liquidator
                makeAddr("DAI on Base"), // srcToken
                uint8(CrossChainRouter.ContractType.BorrowCrossChain) // contractType
            );

            vm.prank(address(endpoint));
            crossChainRouter.lzReceive(
                _origin, 
                bytes32(0), 
                _payload, 
                makeAddr("Executor"), 
                ""
            );
        }

        deal(address(dai), alice, 1000e18);

        // Reapy cross-chain borrow
        vm.startPrank(alice);
        dai.safeApprove(address(coreRouter), 1000e18);
        crossChainRouter.repayCrossChainBorrow(alice, 1000e18, address(lTokenDAI), chainBEid);
        vm.stopPrank();

        {
            // User still has 500 DAI local borrow debt but the borrowed asset info is removed

            LendStorage.BorrowMarketState memory state = lendStorage.getBorrowBalance(alice, address(lTokenDAI));
            assertEq(state.amount, 500e18);

            address[] memory borrowedAsset = lendStorage.getUserBorrowedAssets(alice);
            assertEq(borrowedAsset.length, 0);

            // This essentially means user's local debt is removed without being repaid
            (uint256 borrowed, ) = lendStorage.getHypotheticalAccountLiquidityCollateral(alice, lTokenDAI, 0, 0);
            assertEq(borrowed, 0);
        }
    }
}

```

### Mitigation

Do not remove user local borrowed asset info.

```diff
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeCrossChainCollateral(borrower, _token, index);
-           if (userCrossChainCollaterals.length == 1) {
-               lendStorage.removeUserBorrowedAsset(borrower, _lToken);
-           }
```

```diff
            // Full repayment
            lendStorage.removeCrossChainBorrow(payload.sender, payload.srcToken, index);
-           if (userBorrows.length == 1) {
-               lendStorage.removeUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));
-           }
```