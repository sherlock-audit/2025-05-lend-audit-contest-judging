Energetic Chocolate Weasel

High

# Cross-chain liquidations will fail as  `CrossChainRouter::_handleLiquidationExecute` sends wrong payload to destination chain

### Summary
`CrossChainRouter::_handleLiquidationExecute` sends the wrong payload to destination chain, causing cross-chain liquidations to fail. This causes user's liquidatable position to be unable to be liquidated, resulting in impacted protocol health and increased protocol insolvency risk.

### Details
Overview of the example provided below \
Step A. User supplies `ChainA_WETH` collateral to chainA and cross-chain borrows `ChainB_USDC` tokens on chainB \
Step B. `ChainA_WETH` price drops such that user's cross-chain borrow position is liquidatable \
Step C. Liquidator liquidates user's cross-chain borrow position using `ChainB_USDC` tokens on chainB and seizing `ChainA_WETH` collateral on chainA in return. However, this fails.

Simplified call traces and the relevant parameter values for the steps are provided below.

*Step A*

1. ChainA: User calls `CoreRouter::supply(amountCollateral, ChainA_WETH)` to supply `ChainA_WETH` collateral
2. ChainA: User calls `CrossChainRouter::borrowCrossChain(amountBorrow, ChainA_USDC, ChainB)` to cross-chain borrow `ChainB_USDC` tokens on chainB backed by the `ChainA_WETH` collateral on chainA
3. User's debt is recorded as follows:

```javascript
ChainA: LendStorage::crossChainBorrows[user][ChainA_USDC] (excerpt)
        srcEid = ChainB_Eid
        destEid = ChainA_Eid
        borrowedlToken = ChainB_lUSDC
        srcToken = ChainA_USDC

ChainB: LendStorage::crossChainCollaterals[user][ChainB_USDC] (excerpt)
        srcEid = ChainA_Eid
        destEid = ChainB_Eid
        borrowedlToken = ChainB_lUSDC
        srcToken = ChainA_USDC
```

*Step B*

1. `ChainA_WETH` price drops
2. User's `ChainA_WETH` collateral value on chainA drops
3. User's borrow position is liquidatable

*Step C*

1. ChainB: Liquidator calls `CrossChainRouter::liquidateCrossChain` with the following parameters, i.e. liquidator provides `ChainB_USDC_repayAmount` amount of `ChainB_USDC` tokens on chainB to liquidate the user's cross-chain borrow position and seize `ChainA_lWETH_toSeize` amount of `ChainA_WETH` collateral on chainA in return.

```javascript
CrossChainRouter::liquidateCrossChain({
    borrower: user,
    repayAmount: ChainB_USDC_repayAmount,
    srcEid: ChainA_Eid,
    lTokenToSeize: ChainA_lWETH_toSeize,
    borrowedAsset: ChainB_USDC
})
```
2. ChainB: `CrossChainRouter::_validateAndPrepareLiquidation`
3. ChainB: `CrossChainRouter::_executeLiquidation`
4. ChainB: `CrossChainRouter::_prepareLiquidationValues`
5. ChainB: `CrossChainRouter::_executeLiquidationCore`
6. ChainB: `CrossChainRouter::_send`

```javascript
(excerpt)_send({
            _dstEid: ChainA_Eid,
            _amount: ChainA_lWETH_seizeAmount
            _borrowIndex: storedBorrowIndex // <@ not used. should use this slot to cache ChainA_USDC (explained below)
            _collateral: 0,         // <@ should use this slot to send the ChainB_USDC_repayAmount (explained below)
            _sender: user,
            _destlToken: ChainA_lWETH,
            _liquidator: liquidator,
            _srcToken: ChainB_USDC,
            ctype: CrossChainLiquidationExecute
        })
```
7. ChainA: `CrossChainRouter::_lzReceive`
8. ChainA: `CrossChainRouter::_checkLiquidationValid` (returns true as user is liquidatable)
9. ChainA: `CrossChainRouter::_handleLiquidationExecute`
10. ChainA: `CrossChainRouter::_send`

```javascript
(excerpt)_send({
            _dstEid: ChainB_Eid,
            _amount: ChainA_lWETH_seizeAmount   // <@ should be ChainB_USDC_repayAmount
            _collateral: 0,         
            _sender: user,
            _destlToken: ChainA_lWETH,          // <@ should be ChainB_lUSDC
            _liquidator: liquidator,
            _srcToken: ChainB_USDC,             // <@ should be ChainA_USDC
            ctype: CrossChainLiquidationExecute
        })
```
`_amount` field should be `ChainB_USDC_repayAmount` instead of `ChainA_lWETH_seizeAmount` \
`_destlToken` field should be `ChainB_lUSDC` instead of `ChainA_lWETH`  \
`_srcToken` field should be `ChainA_USDC` instead of `ChainB_USDC` \
This causes issues in the following steps (explained below)

11. ChainB: `CrossChainRouter::_lzReceive`
12. ChainB: `CrossChainRouter::_handleLiquidationSuccess`

```javascript
// Find the borrow position on Chain B to get the correct srcEid
address underlying = lendStorage.lTokenToUnderlying(payload.destlToken);
//                 = lendStorage.lTokenToUnderlying(ChainA_lWETH);
//                 = ChainA_WETH (if "ChainA_lWETH to ChainA_WETH" mapping even exist on chainB)
//                   ^ this should be ChainB_USDC

// Find the specific collateral record
(bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,             // <@ ChainA_WETH
            currentEid, // srcEid is current chain                            // <@ should be srcEid = ChainA_Eid
            0, // We don't know destEid yet, but we can match on other fields // <@ no we do, destEid = currentEid (i.e. ChainB_Eid)
            payload.destlToken,     // <@ ChainA_lWETH
            payload.srcToken        // <@ ChainB_USDC
        );
LendStorage.Borrow[] memory userCollaterals = lendStorage.getCrossChainCollaterals(payload.sender, underlying);

// this code section will attempt to find:
// crossChainCollaterals[user][ChainA_WETH] with
//      borrowedlToken = ChainA_lWETH
//      srcToken = ChainB_USDC
//      srcEid = ChainB_Eid
//      destEid = 0
// but such a position does not exist
// this error is due to wrong payload in Step C10

// instead, it should find:
// crossChainCollaterals[user][ChainB_USDC] with
//      borrowedlToken = ChainB_lUSDC
//      srcToken = ChainA_USDC
//      srcEid = ChainA_Eid
//      destEid = ChainB_Eid
// i.e. the user's cross-chain debt position in Step A3

repayCrossChainBorrowInternal(
            payload.sender, // The borrower                                             // user
            payload.liquidator, // The liquidator (repayer)                             // liquidator
            payload.amount, // Amount to repay                                          // <@ ChainA_lWETH_seizeAmount
            payload.destlToken, // lToken representing the borrowed asset on this chain // <@ ChainA_lWETH
            srcEid // The chain where the collateral (and borrow reference) is tracked  // ChainA_Eid
        );
```
`CrossChainRouter::repayCrossChainBorrowInternal` is called with the wrong parameters \
`_amount` = `payload.amount` (`ChainA_lWETH_seizeAmount`) will repay the wrong amount of user's borrow principle (shown below) \
`_amount` field should be `ChainB_USDC_repayAmount` (refer Step C10) \
`_lToken` = `payload.destlToken` (`ChainA_lWETH`) will `transferFrom` the wrong token from the liquidator to perform liquidation (shown below) \
`_lToken` field should be `ChainB_lUSDC` (refer Step C10)

13. ChainB: `CrossChainRouter::repayCrossChainBorrowInternal`

```javascript
address _token = lendStorage.lTokenToUnderlying(_lToken);
//             = lendStorage.lTokenToUnderlying(ChainA_lWETH);
//             = ChainA_WETH (if "ChainA_lWETH to ChainA_WETH" mapping even exist on chainB)
//               ^ this should be ChainB_USDC

        // Get borrow details and validate
        (uint256 borrowedAmount, uint256 index, LendStorage.Borrow memory borrowPosition) =
            _getBorrowDetails(borrower, _token, _lToken, _srcEid);
//          _getBorrowDetails(user, ChainA_WETH, ChainA_lWETH, ChainA_Eid);
//          but such a position does not exist, instead it should find
//          _getBorrowDetails(user, ChainB_USDC, ChainB_lUSDC, ChainA_Eid);

        // Calculate and validate repay amount
        uint256 repayAmountFinal = _amount == type(uint256).max ? borrowedAmount : _amount;
//                               = _amount 
//                               = ChainA_lWETH_seizeAmount
//                                 ^ this is wrong, should repay ChainB_USDC_repayAmount

        // Handle token transfers and repayment
        _handleRepayment(borrower, repayer, _lToken, repayAmountFinal);
//      _handleRepayment(user, liquidator, ChainA_lWETH, ChainA_lWETH_seizeAmount);
//      (issue explained below)

        // Update state
        _updateRepaymentState(
            borrower, _token, _lToken, borrowPosition, repayAmountFinal, borrowedAmount, index, _srcEid
        );
//      _updateRepaymentState(user, ChainA_WETH, ChainA_lWETH, ?, ChainA_lWETH_seizeAmount, ?, ?, ChainA_Eid)
//      (issue explained below)
```

14. ChainB: `CrossChainRouter::_handleRepayment` 
15. ChainB: `CoreRouter::repayCrossChainLiquidation`
16. ChainB: `CoreRouter::repayBorrowInternal`

```javascript
        address _token = lendStorage.lTokenToUnderlying(_lToken);
//                     = lendStorage.lTokenToUnderlying(ChainA_lWETH);
//                     = ChainA_WETH (if "ChainA_lWETH to ChainA_WETH" mapping even exist on chainB)
//                       ^ this should be ChainB_USDC
.       
        // Transfer tokens from the liquidator to the contract
        IERC20(_token).safeTransferFrom(liquidator, address(this), repayAmountFinal);
//      Takes ChainA_lWETH_seizeAmount of ChainA_lWETH from liquidator
//      Should take ChainB_USDC_repayAmount of ChainB_USDC for the liquidation instead
.
        _approveToken(_token, _lToken, repayAmountFinal);
        // Repay borrowed tokens
        require(LErc20Interface(_lToken).repayBorrow(repayAmountFinal) == 0, "Repay failed");
//      Transfers ChainA_lWETH_seizeAmount of ChainA_WETH to ChainA_lWETH contract
//      Should transfer ChainB_USDC_repayAmount of ChainB_USDC to ChainB_lUSDC contract
```

Result: Wrong amount and wrong token is (attempted to be) taken from liquidator to liquidate the debt

17. ChainB: `CoreRouter::_updateRepaymentState`

```javascript
_updateRepaymentState({
        borrower: user,
        _token: ChainA_WETH,                        // <@ should be ChainB_USDC
        _lToken: ChainA_lWETH,                      // <@ should be ChainB_lUSDC
        borrowPosition: ?,
        repayAmountFinal: ChainA_lWETH_seizeAmount, // <@ should be ChainB_USDC_repayAmount
        borrowedAmount: ?,
        index: ?,
        _srcEid: ChainA_Eid
    })
```

```javascript
        uint256 currentBorrowIndex = LTokenInterface(_lToken).borrowIndex();
        //                         = LTokenInterface(ChainA_lWETH).borrowIndex();
        //                         wrong borrowIndex, should be LTokenInterface(ChainB_lUSDC).borrowIndex();

        LendStorage.Borrow[] memory userCrossChainCollaterals = lendStorage.getCrossChainCollaterals(borrower, _token);
        if (repayAmountFinal == borrowedAmount) {
            lendStorage.removeCrossChainCollateral(borrower, _token, index);
            if (userCrossChainCollaterals.length == 1) {
                lendStorage.removeUserBorrowedAsset(borrower, _lToken);
            }
        } else {
            userCrossChainCollaterals[index].principle = borrowedAmount - repayAmountFinal;
            userCrossChainCollaterals[index].borrowIndex = currentBorrowIndex;
            lendStorage.updateCrossChainCollateral(borrower, _token, index, userCrossChainCollaterals[index]);
        }
        // Updates crossChainCollaterals[user][ChainA_WETH][?]
        // Should update crossChainCollaterals[user][ChainB_USDC][index]
```
Result: Wrong `crossChainCollaterals` record is updated. \
Result: User's cross-chain debt position in Step A3 is not updated correctly during liquidation.

18. ChainB: `CrossChainRouter::_send`

```javascript
(excerpt)_send({
            _dstEid: ChainA_Eid,
            _amount: ChainA_lWETH_seizeAmount           // <@ should be ChainB_USDC_repayAmount
            _borrowIndex: ChainA_lWETH_borrowIndex      // <@ should be ChainB_lUSDC_borrowIndex
            _collateral: 0,         
            _sender: user,
            _destlToken: ChainA_lWETH,                  // <@ should be ChainB_lUSDC
            _liquidator: ChainA_WETH,                   // <@ should be liquidator
            _srcToken: borrowPosition.srcToken,         // <@ unknown value, should be ChainA_USDC
            ctype: CrossChainLiquidationExecute
        })
```
`_amount` field should be `ChainB_USDC_repayAmount` instead of `ChainA_lWETH_seizeAmount` \
`borrow` field should be `ChainB_lUSDC_borrowIndex` instead of `ChainA_lWETH_borrowIndex` \
`_destlToken` field should be `ChainB_lUSDC` instead of `ChainA_lWETH`  \
`liquidator` field should be `liquidator` instead of `ChainA_WETH` \
`_srcToken` field should be `ChainA_USDC` instead of `borrowPosition.srcToken` (unknown value) \
This causes issues in the following steps (explained below)

19. ChainA: `CrossChainRouter::_lzReceive`
20. ChainA: `CrossChainRouter::_handleDestRepayMessage`

```javascript
        // Find specific borrow using the new helper
        (bool found, uint256 index) =
            lendStorage.findCrossChainBorrow(payload.sender, payload.srcToken, srcEid, currentEid, payload.destlToken);
        // lendStorage.findCrossChainBorrow(user, ?, ChainB_Eid, ChainA_Eid, ChainA_lWETH);
        // but such a position does not exist, instead it should find
        // lendStorage.findCrossChainBorrow(user, ChainA_USDC, ChainB_Eid, ChainA_Eid, ChainB_lUSDC);
        LendStorage.Borrow[] memory userBorrows = lendStorage.getCrossChainBorrows(payload.sender, payload.srcToken);
        // lendStorage.getCrossChainBorrows(user, ?)
        // but such a position does not exist, instead it should find
        // lendStorage.getCrossChainBorrows(user, ChainA_USDC)

        // Calculate current borrow with interest
        uint256 currentBorrow = (userBorrows[index].principle * payload.borrowIndex) / userBorrows[index].borrowIndex;
        if (payload.amount >= currentBorrow) {
            // Full repayment
            lendStorage.removeCrossChainBorrow(payload.sender, payload.srcToken, index);
            if (userBorrows.length == 1) {
                lendStorage.removeUserBorrowedAsset(payload.sender, lendStorage.underlyingTolToken(payload.srcToken));
            }
        } else {
            // Partial repayment - update remaining borrow
            userBorrows[index].principle = currentBorrow - payload.amount;
            userBorrows[index].borrowIndex = payload.borrowIndex;
            lendStorage.updateCrossChainBorrow(payload.sender, payload.srcToken, index, userBorrows[index]);
        }
        // Updates crossChainBorrows[user][?][?]
        // Should update crossChainBorrows[user][ChainA_USDC][index]
```

Result: Wrong `crossChainBorrows` record is updated. \
Result: User's cross-chain debt position in Step A3 is not updated correctly during liquidation.

### Root Cause

In [`CrossChainRouter::_handleLiquidationExecute#L355-L365`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L355-L365), the wrong payload is sent to the destination chain.

[`CrossChainRouter::_handleLiquidationExecute#L355-L365`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L355-L365)

```javascript
        _send(
            srcEid,
@>          payload.amount,
            0,
            0,
            payload.sender,
@>          payload.destlToken,
            payload.liquidator,
@>          payload.srcToken,
            ContractType.LiquidationSuccess
        );
```

Additionally, in [`CrossChainRouter::_executeLiquidationCore#L274-L284`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L274-L284), the `repayAmount` (e.g. `ChainB_USDC_repayAmount`) and `borrowedAssetCrossChain` (e.g.`ChainA_USDC`) were not cached into the payload.

[`CrossChainRouter::_executeLiquidationCore#L274-L284`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L274-L284)

```javascript
        _send(
            params.srcEid,
            seizeTokens,
@>          params.storedBorrowIndex,
@>          0,
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid),
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
```

Moreover, in [`CrossChainRouter::__handleLiquidationSuccess#L448-L455`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L448-L455) `srcEid` and `destEid` field is set incorrectly, resulting in the function unable to find the correct `crossChainCollateral`.

[`CrossChainRouter::_handleLiquidationSuccess#L448-L455`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L448-L455)

```diff
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
@>          currentEid, // srcEid is current chain
@>          0, // We don't know destEid yet, but we can match on other fields
            payload.destlToken,
            payload.srcToken
        );
```

### Internal Pre-Conditions
`CrossChainRouter::_checkLiquidationValid` returns true (i.e. user's position is liquidatable)

### External Pre-Conditions
None

### Attack Path
(refer details section)

### Impact
Result:
1. Wrong amount and wrong token is taken from liquidator to liquidate the debt
2. Wrong `crossChainCollaterals` record is updated
3. Wrong `crossChainBorrows` record is updated
4. User's cross-chain debt position in Step A3 is not updated correctly during liquidation

Impact:
1. Cross-chain liquidations fail
2. User's liquidatable position cannot be liquidated
3. Protocol health is impacted and protocol insolvency risk increases

### PoC
(refer details section)

### Mitigation
In [`CrossChainRouter::_handleLiquidationExecute#L355-L365`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L355-L365), the payload should be as follows.

[`CrossChainRouter::_handleLiquidationExecute#L355-L365`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L355-L365)

```diff
+       // Retrieves the ChainA_borrowedAsset stored in payload.borrowIndex slot (e.g. ChainA_USDC)
+       address srcToken = uint160(uint256(payload.borrowIndex));
+       // Convert ChainA_borrowedAsset to ChainB_lToken (e.g. ChainA_USDC to ChainB_lUSDC)
+       address destlToken = lendStorage.underlyingToDestUnderlying(srcToken, srcEid);

        _send(
            srcEid,
-           payload.amount,
+           payload.collateral,
            0,
            0,
            payload.sender,
-           payload.destlToken,
+           destlToken
            payload.liquidator,
-           payload.srcToken,
+           srcToken,
            ContractType.LiquidationSuccess
        );
```

Additionally, in [`CrossChainRouter::_executeLiquidationCore#L274-L284`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L274-L284), the `repayAmount` (e.g. `ChainB_USDC_repayAmount`) and `borrowedAssetCrossChain` (e.g.`ChainA_USDC`) should be cached into the payload at `_collateral` and `_borrowIndex` field respectively. This is necessary so that the values can be used by `CrossChainRouter::_handleLiquidationExecute` to craft the correct payload to send to the destination chain.

[`CrossChainRouter::_executeLiquidationCore#L274-L284`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L274-L284)

```diff
+       // converts ChainB_borrowedAsset to ChainA_borrowedAsset (e.g. ChainB_USDC to ChainA_USDC)
+       address borrowedAssetCrossChain = lendStorage.crossChainAssetMap(params.borrowedAsset, params.srcEid);

        _send(
            params.srcEid,
            seizeTokens,
-           params.storedBorrowIndex,
+           uint256(uint160(borrowedAssetCrossChain)),
-           0,
+           params.repayAmount
            params.borrower,
            lendStorage.crossChainLTokenMap(params.lTokenToSeize, params.srcEid), // Convert to Chain A version before sending
            msg.sender,
            params.borrowedAsset,
            ContractType.CrossChainLiquidationExecute
        );
```

Moreover, the bug in [`CrossChainRouter::_handleLiquidationSuccess#L443-L455`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443-L455) and [`CrossChainRouter::_lzReceive#L778-L779`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L778-L779) should be fixed as below.

[`CrossChainRouter::_handleLiquidationSuccess#L443-L455`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L443-L455)

```diff
- function _handleLiquidationSuccess(LZPayload memory payload) private {
+ function _handleLiquidationSuccess(LZPayload memory payload, uint32 srcEid) private {
.
.
        (bool found, uint256 index) = lendStorage.findCrossChainCollateral(
            payload.sender,
            underlying,
-           currentEid, // srcEid is current chain
+           srcEid
-           0, // We don't know destEid yet, but we can match on other fields
+           currentEid
            payload.destlToken,
            payload.srcToken
        );
```

[`CrossChainRouter::_lzReceive#L778-L779`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CrossChainRouter.sol#L778-L779)
```diff
        } else if (cType == ContractType.LiquidationSuccess) {
-           _handleLiquidationSuccess(payload);
+           _handleLiquidationSuccess(payload, srcEid);
```