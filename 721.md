Old Mandarin Duck

High

# Incorrect `borrowedlToken` is used in `CoreRouter.liquidateBorrowAllowedInternal` function

### Summary

In `CoreRouter.liquidateBorrowAllowedInternal` function, incorrect `borrowedlToken` is used instead of `borrowed`.

### Root Cause

In [`CoreRouter.liquidateBorrowAllowedInternal`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L344-L350) function, `borrowed` reflects the current borrow index but `borrowedlToken` is calculated with borrow index incorrectly from `borrowed` again:

```solidity
    function liquidateBorrowAllowedInternal(
        address payable lTokenBorrowed,
        address borrower,
        uint256 repayAmount,
        uint256 collateral,
@>      uint256 borrowed
    ) internal view returns (uint256) {
        // Determine borrowIndex and borrowAmount based on chain type
        LendStorage.BorrowMarketState memory borrowBalance = lendStorage.getBorrowBalance(borrower, lTokenBorrowed);

        // Allow accounts to be liquidated if the market is deprecated
        if (LendtrollerInterfaceV2(lendtroller).isDeprecated(LToken(lTokenBorrowed))) {
            require(borrowBalance.amount >= repayAmount, "Repay > total borrow");
        } else {
            // The borrower must have shortfall in order to be liquidatable
@>          uint256 borrowedAmount;

            // For same-chain liquidations, calculate borrowed amount using the borrowBalance's index
@>          borrowedAmount =
                (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex;

@>          require(borrowedAmount > collateral, "Insufficient shortfall");

            /* The liquidator may not repay more than what is allowed by the closeFactor */
            uint256 maxClose = mul_ScalarTruncate(
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
            );

            require(repayAmount <= maxClose, "Too much repay");
        }

        return 0;
    }
```

In the above function, `borrowed` is from `liquidateBorrowInternal` and `liquidateBorrow` function sequentially:

```solidity
    function liquidateBorrow(address borrower, uint256 repayAmount, address lTokenCollateral, address borrowedAsset)
        external
    {
        // The lToken of the borrowed asset
        address borrowedlToken = lendStorage.underlyingTolToken(borrowedAsset);

        LTokenInterface(borrowedlToken).accrueInterest();

@>      (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(borrower, LToken(payable(borrowedlToken)), 0, 0);

        liquidateBorrowInternal(
@>          msg.sender, borrower, repayAmount, lTokenCollateral, payable(borrowedlToken), collateral, borrowed
        );
    }

    function liquidateBorrowInternal(
        address liquidator,
        address borrower,
        uint256 repayAmount,
        address lTokenCollateral,
        address payable borrowedlToken,
        uint256 collateral,
@>      uint256 borrowed
    ) internal {
        require(
@>          liquidateBorrowAllowedInternal(borrowedlToken, borrower, repayAmount, collateral, borrowed) == 0,
            "Borrow not allowed"
        );
        require(borrower != liquidator, "Liquidator cannot be borrower");
        require(repayAmount > 0, "Repay amount not zero");

        repayBorrowInternal(borrower, liquidator, repayAmount, borrowedlToken, true);

        // Liquidation logic for same chain
        liquidateSeizeUpdate(msg.sender, borrower, lTokenCollateral, borrowedlToken, repayAmount);
    }
```

Above functions show that `borrowed` reflects the current borrow index with `lendStorage.getHypotheticalAccountLiquidityCollateral` function call.

However, in `liquidateBorrowAllowedInternal` function, `borrowedAmount` is calculated again with current borrow index and `borrowed`.


### Internal Pre-conditions

No response

### External Pre-conditions

No response


### Attack Path

No response

### Impact

Incorrectly increased `borrowedAmount` is used in `require(borrowedAmount > collateral, "Insufficient shortfall");`, and this leads to incorrect handling when `borrowed <= collateral < borrowedAmount`. In this case, it should be reverted but not revert.

### PoC

No response

### Mitigation

Remove `borrowedAmount` and use `borrowed` instead of `borrowedAmount` in comparison:

```diff
--  uint256 borrowedAmount;

--  // For same-chain liquidations, calculate borrowed amount using the borrowBalance's index
--  borrowedAmount =
--      (borrowed * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) / borrowBalance.borrowIndex;

--  require(borrowedAmount > collateral, "Insufficient shortfall");
++  require(borrowed > collateral, "Insufficient shortfall");
```
