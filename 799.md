Sweet Gingham Whale

Medium

# Griefing attack on supply()::CoreRouter.sol

### Summary

A griefing vulnerability exists in `Supply::CoreRouter.sol` reward distribution mechanism (`distributeSupplierLend`). A malicious user can temporarily inflate the totalSupply of an lToken using a flash loan or flash mint to mint an underlying token (like DAI), then trigger a reward index update, effectively diluting the per-token rewards for all other suppliers. This leads to unfair distribution of rewards and reduced incentive for honest users, undermining the fairness and economic viability of the platform.

### Root Cause

The [`supply`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L61) function allows users to supply an underlying token to mint ltoken and also has a reward mechanism for token suppliers. 

```solidity

 function supply(uint256 _amount, address _token) external {
        address _lToken = lendStorage.underlyingTolToken(_token);

        require(_lToken != address(0), "Unsupported Token");

        require(_amount > 0, "Zero supply amount");

        // Transfer tokens from the user to the contract
        IERC20(_token).safeTransferFrom(msg.sender, address(this), _amount);

        _approveToken(_token, _lToken, _amount);

        // Get exchange rate before mint
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();

        // Mint lTokens
        require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");

        // Calculate actual minted tokens using exchangeRate from before mint
        uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;

        lendStorage.addUserSuppliedAsset(msg.sender, _lToken);

       >>> lendStorage.distributeSupplierLend(_lToken, msg.sender);

        // Update total investment using calculated mintTokens
        lendStorage.updateTotalInvestment(
            msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
        );

        emit SupplySuccess(msg.sender, _lToken, _amount, mintTokens);
    }
```
The issue lies in the use of [`distributeSupplierLend`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L303) to distribute rewards for the users.

```solidity
function distributeSupplierLend(address lToken, address supplier) external onlyAuthorized {
        // Trigger supply index update
       
  >>>LendtrollerInterfaceV2(lendtroller).triggerSupplyIndexUpdate(lToken);

        // Get the appropriate lend state based on whether it's for supply or borrow
        (uint224 supplyIndex,) = LendtrollerInterfaceV2(lendtroller).lendSupplyState(lToken);

        // Get the relevant indexes and accrued LEND for the account
        uint256 supplierIndex = lendSupplierIndex[lToken][supplier];

        lendSupplierIndex[lToken][supplier] = supplyIndex;

        // Update the account's index to the current index since we are distributing accrued LEND
        if (supplierIndex == 0 && supplyIndex >= LEND_INITIAL_INDEX) {
            // Covers the case where users borrowed tokens before the market's borrow state index was set.
            // Rewards the user with LEND accrued from the start of when borrower rewards were first
            // set for the market.
            supplierIndex = LEND_INITIAL_INDEX;
        }

        // Calculate change in the cumulative sum of the LEND per lToken accrued
        Double memory deltaIndex = Double({mantissa: sub_(supplyIndex, supplierIndex)});

        // Calculate the appropriate account balance and delta based on supply or borrow
        uint256 supplierTokens = totalInvestment[supplier][lToken];

        // Calculate LEND accrued: lTokenAmount * accruedPerLToken
        uint256 supplierDelta = mul_(supplierTokens, deltaIndex);

        // Update the accrued LEND for the account
        uint256 supplierAccrued = add_(lendAccrued[supplier], supplierDelta);
        lendAccrued[supplier] = supplierAccrued;
    }
```
[`distributeSupplierLend`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L303)  triggers the update of the supply index of the ltoken being minted by calling [`triggerSupplyIndexUpdate](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1262)`.

```solidity
function triggerSupplyIndexUpdate(address lToken) external {
        require(msg.sender == lendStorageAddress, "access denied");
  >>>      updateLendSupplyIndex(lToken);
    }
``` 
[`updateLendSupplyIndex(lToken)`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/Lendtroller.sol#L1278)

```soliidty
function updateLendSupplyIndex(address lToken) internal {
        LendMarketState storage supplyState = lendSupplyState[lToken];
        uint256 supplySpeed = lendSupplySpeeds[lToken];
        uint32 blockNumber = safe32(getBlockNumber(), "block number exceeds 32 bits");
        uint256 deltaBlocks = sub_(uint256(blockNumber), uint256(supplyState.block));
        if (deltaBlocks > 0 && supplySpeed > 0) {
 >>>          uint256 supplyTokens = LToken(lToken).totalSupply();
            uint256 lendAccrued = mul_(deltaBlocks, supplySpeed);
            Double memory ratio = supplyTokens > 0 ? fraction(lendAccrued, supplyTokens) : Double({mantissa: 0});
            supplyState.index =
                safe224(add_(Double({mantissa: supplyState.index}), ratio).mantissa, "new index exceeds 224 bits");
            supplyState.block = blockNumber;
        } else if (deltaBlocks > 0) {
            supplyState.block = blockNumber;
        }
    }
```
The reward calculation in `updateLendSupplyIndex` depends on:  **ratio = lendAccrued / totalSupply**.  [`distributeSupplierLend`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L303) calculates 
 `deltaIndex = supplyIndex - supplierIndex (difference between the previous and current index)` which gives the value of `ratio` calculated above.  
 If an attacker temporarily inflates totalSupply via a flash-minted deposit, the ratio becomes extremely small, slowing down the growth of supplyIndex. Since rewards are proportional to index growth, other users very few rewards depending on how inflated the totalSupply is.



### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path


**Flash Mint DAI**:

The attacker borrows a large amount of DAI (or another flash-mintable token) in a single transaction.

**Deposit and Mint lTokens**:

- The attacker calls supply() with the flash-minted DAI, artificially inflating LToken(lToken).totalSupply().

**Manipulate supplyIndex**:

- When updateLendSupplyIndex() is triggered, the inflated totalSupply makes ratio very small.
- The supplyIndex barely increases, reducing rewards for all suppliers.

**Withdraw and Repay Flash Loan**:

- The attacker redeems their lTokens, withdraws the DAI, and repays the flash loan.

- The protocol is left with a distorted supplyIndex, and legitimate users receive minimal rewards.

### Impact


**Griefing Attack**: The attacker  disrupts reward fairness.

**Reduced Rewards for Legitimate Users**: Suppliers earn fewer rewards due to manipulated supplyIndex.

### PoC

N/A

### Mitigation

Revert if totalSupply changes beyond a threshold in a single block.
Require deposits to remain locked for a few blocks before affecting reward calculations.