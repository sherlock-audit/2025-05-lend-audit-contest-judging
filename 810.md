Skinny Charcoal Chinchilla

High

# Usage of outdated values causes unfair same-chain liquidations.

### Summary

When a user's getting liquidated, his total borrwed USD value and total collateral USD values are compared to ensure he can be rightfully liquidated. However the comparison uses up-to-date borrowing USD value and outdated collateral USD value, allowing unfair liquidations.

In [CoreRouter.sol::liquidateSeizeUpdate()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L278), the collateral value to seize in terms of Ltoken is calculated using [Lendtroller.sol::liquidateCalculateSeizeTokens()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/Lendtroller.sol#L852). The Compound protocol updates the colalteral value before the call to this function. Here, the protocol only make sure the borrowed token value is up-to-date while using outdated collateral value to calculate the collateral to seize.

```solidity 

    function liquidateSeizeUpdate(
        address sender,
        address borrower,
        address lTokenCollateral,
        address borrowedlToken,
        uint256 repayAmount
    ) internal {
@>      (uint256 amountSeizeError, uint256 seizeTokens) = LendtrollerInterfaceV2(lendtroller)
            .liquidateCalculateSeizeTokens(borrowedlToken, lTokenCollateral, repayAmount);
        require(amountSeizeError == 0, "Failed to calculate");
        require(lendStorage.totalInvestment(borrower, lTokenCollateral) >= seizeTokens, "Insufficient collateral"); 
        uint256 currentReward = mul_(seizeTokens, Exp({mantissa: lendStorage.PROTOCOL_SEIZE_SHARE_MANTISSA()}));
```

Because of this the `seizeTokens` returned from the function will be larger than in actuality, since it calculates using the outdated exchange rate.


```solidity 
     
    function liquidateCalculateSeizeTokens(address lTokenBorrowed, address lTokenCollateral, uint256 actualRepayAmount) 
        external
        view
        override
        returns (uint256, uint256)
    {
        uint256 priceBorrowedMantissa = oracle.getUnderlyingPrice(LToken(lTokenBorrowed));
        uint256 priceCollateralMantissa = oracle.getUnderlyingPrice(LToken(lTokenCollateral));
        if (priceBorrowedMantissa == 0 || priceCollateralMantissa == 0) {
            return (uint256(Error.PRICE_ERROR), 0);
        }
@>      uint256 exchangeRateMantissa = LToken(lTokenCollateral).exchangeRateStored(); 
        uint256 seizeTokens;
        Exp memory numerator;
        Exp memory denominator;
        Exp memory ratio;
        numerator = mul_(Exp({mantissa: liquidationIncentiveMantissa}), Exp({mantissa: priceBorrowedMantissa}));
        denominator = mul_(Exp({mantissa: priceCollateralMantissa}), Exp({mantissa: exchangeRateMantissa}));
        ratio = div_(numerator, denominator);
        seizeTokens = mul_ScalarTruncate(ratio, actualRepayAmount); 
        return (uint256(Error.NO_ERROR), seizeTokens);
    }
```

The borrowed token's value is updated in [liquidateBorrow()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L230), the collateral token's exchange rate is not updated anywhere.

```solidity 
    function liquidateBorrow(address borrower, uint256 repayAmount, address lTokenCollateral, address borrowedAsset)
        external
    {
        address borrowedlToken = lendStorage.underlyingTolToken(borrowedAsset);
@>      LTokenInterface(borrowedlToken).accrueInterest();
        (uint256 borrowed, uint256 collateral) =
            lendStorage.getHypotheticalAccountLiquidityCollateral(borrower, LToken(payable(borrowedlToken)), 0, 0);
        liquidateBorrowInternal(
            msg.sender, borrower, repayAmount, lTokenCollateral, payable(borrowedlToken), collateral, borrowed 
        );
    }

    function liquidateBorrowInternal(
        address liquidator,
        address borrower,
        uint256 repayAmount,
        address lTokenCollateral,
        address payable borrowedlToken,
        uint256 collateral,
        uint256 borrowed
    ) internal {
        require(
            liquidateBorrowAllowedInternal(borrowedlToken, borrower, repayAmount, collateral, borrowed) == 0,
            "Borrow not allowed"
        );
        require(borrower != liquidator, "Liquidator cannot be borrower");
        require(repayAmount > 0, "Repay amount not zero");
        repayBorrowInternal(borrower, liquidator, repayAmount, borrowedlToken, true);
        liquidateSeizeUpdate(msg.sender, borrower, lTokenCollateral, borrowedlToken, repayAmount);
    }    
```


### Root Cause

[Ltoken.sol::accrueInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L324), is NOT called on the collateral token. only on the borrowed token.


### Internal Pre-conditions

none

### External Pre-conditions

none

### Attack Path

none

### Impact

Users will be unfairly liquidated as their borrowed value is up-to-date, while their collateral value is not.

### PoC

none

### Mitigation

Make sure the collateral token is up-to-date as well.

```diff
    function liquidateBorrowInternal(
        address liquidator,
        address borrower,
        uint256 repayAmount,
        address lTokenCollateral,
        address payable borrowedlToken,
        uint256 collateral,
        uint256 borrowed
    ) internal {
        require(
            liquidateBorrowAllowedInternal(borrowedlToken, borrower, repayAmount, collateral, borrowed) == 0,
            "Borrow not allowed"
        );
        require(borrower != liquidator, "Liquidator cannot be borrower");
        require(repayAmount > 0, "Repay amount not zero");

+       LTokenInterface(lTokenCollateral).accureInterst();

        repayBorrowInternal(borrower, liquidator, repayAmount, borrowedlToken, true);
        liquidateSeizeUpdate(msg.sender, borrower, lTokenCollateral, borrowedlToken, repayAmount);
    }    
```
