Tall Sepia Marmot

High

# Cross-Chain borrow/debt is never taken into account in `borrowWithInterest()` function

### Summary

-

### Root Cause

-

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

In Line 497, there is a condition (`collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid`) to filter the `crossChainCollaterals` items to be taken into account when computing the `borrowedAmount`. However, this condition will never be `True` under any circumstances. In the entire codebase, there is no code that sets both `crossChainBorrows.destEid` and `crossChainBorrows.srcEid` to be the exact value of `currentEid`.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L497


```solidity
File: LendStorage.sol
469:     /**
470:      * @notice Helper function to calculate borrow with interest.
471:      * @dev Returns the sum of all cross-chain borrows with interest in underlying tokens.
472:      * For example, a return value of 1e6 for the USDC lToken, would be 1 USDC.
473:      * Loops through crossChainBorrows and crossChainCollaterals, as only 1 is populated for each borrow,
474:      * on each chain.
475:      * For example if a cross chain borrow was initiated on chain A, crossChainBorrows will be populated on chain A,
476:      * and crossChainCollaterals will be populated on chain B. The other will be empty.
477:      */
478:     function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
479:         address _token = lTokenToUnderlying[_lToken];
480:         uint256 borrowedAmount;
481: 
482:         Borrow[] memory borrows = crossChainBorrows[borrower][_token];
483:         Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];
484: 
485:         require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
486:         // Only one mapping should be populated:
487:         if (borrows.length > 0) {
488:             for (uint256 i = 0; i < borrows.length; i++) {
489:                 if (borrows[i].srcEid == currentEid) {
490:                     borrowedAmount +=
491:                         (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
492:                 }
493:             }
494:         } else {
495:             for (uint256 i = 0; i < collaterals.length; i++) {
496:                 // Only include a cross-chain collateral borrow if it originated locally.
497:                 if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
498:                     borrowedAmount +=
499:                         (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
500:                 }
501:             }
502:         }
503:         return borrowedAmount;
504:     }
```

The only place where the `crossChainBorrows.destEid` and `crossChainBorrows.srcEid` is initialized is when handling the cross-chain borrowing request (See `_handleBorrowCrossChainRequest()` below) in the Destination Chain (e.g.,  Chain B). The `srcEid` will be Chain A where the cross-chain borrowing request is initialized, while `destEid` will be the destination chain (currentEid = Chain B).

Thus, `crossChainBorrows.destEid` and `crossChainBorrows.srcEid` will never be set to the exact same value under any circumstances. As a result, cross-chain borrow/debt is never taken into account within the `borrowWithInterest` function.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L581

```solidity
File: CrossChainRouter.sol
581:     function _handleBorrowCrossChainRequest(LZPayload memory payload, uint32 srcEid) private {
..SNIP..
640:         } else {
641:             lendStorage.addCrossChainCollateral(
642:                 payload.sender,
643:                 destUnderlying,
644:                 LendStorage.Borrow({
645:                     srcEid: srcEid, // @audit-info Chain A
646:                     destEid: currentEid, // @audit-info Chain B
647:                     principle: payload.amount,
648:                     borrowIndex: currentBorrowIndex,
649:                     borrowedlToken: payload.destlToken,
650:                     srcToken: payload.srcToken
651:                 })
652:             );
```

The `borrowWithInterest()` function is used in many key features (e.g., repayment, reward distribution, borrowing, liquidation, redemption) of the protocols. Thus, many of the calculations within the key feature will be incorrect, which might lead to a loss of assets. For instance, users might be unfairly liquidated or not liquidated at all, users able to borrow more than expected, users obtain more or less rewards than expected etc.

### Impact

Many of the calculations within the key feature will be incorrect, which might lead to a loss of assets. For instance, users might be unfairly liquidated or not liquidated at all, users able to borrow more than expected, users obtain more or less rewards than expected etc.

### PoC

_No response_

### Mitigation

_No response_