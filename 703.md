Tall Sepia Marmot

High

# Incorrect `totalInvestment` value due to the wrong exchange rate being used

### Summary

-

### Root Cause

-

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

**Instance 1 - `CoreRouter.supply`**

Line 74 stores the exchange rate before the mint in the `exchangeRateBefore` variable to be used later at Line 80 below to compute the number of lToken minted.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L80

```solidity
File: CoreRouter.sol
61:     function supply(uint256 _amount, address _token) external {
..SNIP..
74:         uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
75: 
76:         // Mint lTokens
77:         require(LErc20Interface(_lToken).mint(_amount) == 0, "Mint failed");
78: 
79:         // Calculate actual minted tokens using exchangeRate from before mint
80:         uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;
81: 
82:         lendStorage.addUserSuppliedAsset(msg.sender, _lToken);
83: 
84:         lendStorage.distributeSupplierLend(_lToken, msg.sender);
85: 
86:         // Update total investment using calculated mintTokens
87:         lendStorage.updateTotalInvestment(
88:             msg.sender, _lToken, lendStorage.totalInvestment(msg.sender, _lToken) + mintTokens
89:         );
```

The `mint()` is called at Line 77, it will call `mintInternal()` followed by `accrueInterest()` before performing the actual minting.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L384

```solidity
File: LToken.sol
379:     /**
380:      * @notice Sender supplies assets into the market and receives lTokens in exchange
381:      * @dev Accrues interest whether or not the operation succeeds, unless reverted
382:      * @param mintAmount The amount of the underlying asset to supply
383:      */
384:     function mintInternal(uint256 mintAmount) internal nonReentrant {
385:         accrueInterest();
386:         // mintFresh emits the actual Mint event if successful and logs on errors, so we don't need to
387:         mintFresh(msg.sender, mintAmount);
388:     }
```

However, the issue is that the `accrueInterest()` will update the `totalBorrows` and `totalReserves` variables, which are used within the `exchangeRateStored()` function to compute the exchange rate. Thus, after `accrueInterest()` is executed, the exchange rate will be updated or changed.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L324

```solidity
File: LToken.sol
324:     function accrueInterest() public virtual override returns (uint256) {
..SNIP..
367:         /* We write the previously calculated values into storage */
368:         accrualBlockNumber = currentBlockNumber;
369:         borrowIndex = borrowIndexNew;
370:         totalBorrows = totalBorrowsNew;
371:         totalReserves = totalReservesNew;
..SNIP..
377:     }
```

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L290

```solidity
File: LToken.sol
290:     function exchangeRateStoredInternal() internal view virtual returns (uint256) {
..SNIP..
303:             uint256 totalCash = getCashPrior();
304:             uint256 cashPlusBorrowsMinusReserves = totalCash + totalBorrows - totalReserves;
305:             uint256 exchangeRate = cashPlusBorrowsMinusReserves * expScale / _totalSupply;
306: 
307:             return exchangeRate;
308:         }
309:     }
```

As a result, the exchange rate used within the `mint()` operation is different from the exchange rate stored in the `exchangeRateBefore` variable.

The `supply()` function uses the old exchange rate (`exchangeRateBefore`) to compute the amount of tokens minted.

```solidity
uint256 mintTokens = (_amount * 1e18) / exchangeRateBefore;
```

Thus, when the `lendStorage.updateTotalInvestment` function within the `CoreRouter.supply()` function is executed to update the user's total investment, the wrong `mintTokens` value will be set.

**Instance 2 - `CoreRouter.redeem()`**

The exact same issue also occurs within the `CoreRouter.redeem()` function.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LToken.sol#L454

```solidity
    /**
     * @notice Sender redeems lTokens in exchange for the underlying asset
     * @dev Accrues interest whether or not the operation succeeds, unless reverted
     * @param redeemTokens The number of lTokens to redeem into underlying
     */
    function redeemInternal(uint256 redeemTokens) internal nonReentrant {
        accrueInterest();
        // redeemFresh emits redeem-specific logs on errors, so we don't need to
        redeemFresh(payable(msg.sender), redeemTokens, 0);
    }
```

As a result, the `expectedUnderlying` computed in Line 118 below will be incorrect, leading to the wrong update of the user's total investment.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L118

```solidity
File: CoreRouter.sol
100:     function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
101:         // Redeem lTokens
102:         address _token = lendStorage.lTokenToUnderlying(_lToken);
103: 
104:         require(_amount > 0, "Zero redeem amount");
105: 
106:         // Check if user has enough balance before any calculations
107:         require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");
108: 
109:         // Check liquidity
110:         (uint256 borrowed, uint256 collateral) =
111:             lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);
112:         require(collateral >= borrowed, "Insufficient liquidity");
113: 
114:         // Get exchange rate before redeem
115:         uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
116: 
117:         // Calculate expected underlying tokens
118:         uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
```

### Impact

This issue resulted in the incorrect `totalInvestment` value for the affected user. The `totalInvestment` value is used in many parts of the protocol. For instance, `totalInvestment` is used to determine the LEND rewards to which a user is entitled. 

- Thus, an incorrect `totalInvestment` will lead to more or less rewards being issued to the user, which in either case leads to a loss for the protocol or user. 
- In addition, the `totalInvestment` value is also used within the key `getHypotheticalAccountLiquidityCollateral()` function, which is often used to determine an account's collateral. Thus, an inaccurate value will cause the user's collateral value to be inflated or deflated. If inflated, user can over-borrow, leading to loss of assets for the protocol. If deflated, users can be liquidated prematurely, leading to loss of assets for the users.

### PoC

_No response_

### Mitigation

Use the user's before-and-after balance of lToken to compute the amount of lToken minted or received by the user. This is the most accurate approach.