Scrawny Red Sloth

Medium

# Incorrect `maxClose` calculation in `liquidateBorrowAllowedInternal()` prevents max liquidation

## Summary
In `liquidateBorrowAllowedInternal()`, the `maxClose` value is calculated based on `borrowBalance.amount` without prior scaling based on the current borrow index. This therefore provides incorrect constraint on the max amount that a liqudator can repay for a particular borrower and as such, liquidattors attempting to liquidate max will face reverts.

## Root cause
In [`_prepareLiquidationValues()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CrossChainRouter.sol#L235-L262) for cross-chain liquidations, the `maxLiquidation` is calculated as follows:
```solidity
    function _executeLiquidation(LendStorage.LiquidationParams memory params) private {
        // First part: Validate and prepare liquidation parameters
>>      uint256 maxLiquidation = _prepareLiquidationValues(params);

>>      require(params.repayAmount <= maxLiquidation, "Exceeds max liquidation");

        // Secon part: Validate collateral and execute liquidation
        _executeLiquidationCore(params);
    }

    function _prepareLiquidationValues(LendStorage.LiquidationParams memory params)
        private
        returns (uint256 maxLiquidation)
    {
        // Accrue interest
        LTokenInterface(params.borrowedlToken).accrueInterest();
        uint256 currentBorrowIndex = LTokenInterface(params.borrowedlToken).borrowIndex();

        // @audit scaling done here
>>      uint256 currentBorrow = (params.borrowPrinciple * currentBorrowIndex) / params.storedBorrowIndex;

        // @audit scaled amount is used get max amount
        maxLiquidation = mul_ScalarTruncate(
>>          Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), currentBorrow
        );

        return maxLiquidation;
    }
```
As seen above, before performing `params.repayAmount` validation, the `maxLiquidation` is calculated by first accruing interest then scaling the `params.borrowPrinciple` based on the new index to yield `currentBorrow` which is then used to calculate the max amount.

The same is done in [`getMaxLiquidationRepayAmount()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/LendStorage.sol#L573-L591) as follows:
```solidity
    function getMaxLiquidationRepayAmount(address borrower, address lToken, bool isSameChain)
        external
        view
        returns (uint256)
    {
        // Get the current borrow balance including interest
        uint256 currentBorrow = 0;

        // @audit scaling done here
>>      currentBorrow += isSameChain ? borrowWithInterestSame(borrower, lToken) : borrowWithInterest(borrower, lToken);

        // Get close factor from lendtroller (typically 0.5 or 50%)
        uint256 closeFactorMantissa = LendtrollerInterfaceV2(lendtroller).closeFactorMantissa();

            // @audit scaled amount used to get max amount
>>      uint256 maxRepay = (currentBorrow * closeFactorMantissa) / 1e18;

        return maxRepay;
    }
```

However, for same chain liquidations, this is handled incorrectly in [`liquidateBorrowAllowedInternal()`](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L353-L357):
```solidity
        // @audit borrow retrieved here
>>      LendStorage.BorrowMarketState memory borrowBalance = lendStorage.getBorrowBalance(borrower, lTokenBorrowed);

        // Allow accounts to be liquidated if the market is deprecated
        if (LendtrollerInterfaceV2(lendtroller).isDeprecated(LToken(lTokenBorrowed))) {
            //..
        } else {
            --snip--
            // @audit calculated based on unscaled amount
            uint256 maxClose = mul_ScalarTruncate(
>>              Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
            );

            // @audit False check here
            require(repayAmount <= maxClose, "Too much repay");
        }
```
As seen, the function retrieves the borrow details i.e `borrowBalance` and proceeds to calculate the `maxClose` based on the `borrowBalance.amount` without scaling it.

## Internal Pre-conditions
1. The protocol must have active borrow positions
2. The borrow positions must have accrued some interest since the last index update

## External Pre-conditions
1. Prices fall such that the borrower must be eligible for liquidation

## Attack path
1. A borrower takes out a loan when borrow index is `X`
2. Over time, interest accrues and borrow index increases to `Y` (Y > X)
3. Borrower becomes undercollateralized
4. Liquidator attempts to liquidate the position
5. System calculates maxClose based on original borrow amount instead of scaled amount

## Impact
The protocol remains flawed by preventing liquidators from closing as much debt as they should be allowed to (if `unscaled amount < scaled amount`)


# Poc
1. Modify `test/TestLiquidations.t.sol` as follows:
```diff
    // Add these new imports
+   import "../src/LayerZero/interaces/LendtrollerInterfaceV2.sol";
+   import "../src/ExponentialNoError.sol";

-   contract TestLiquidations is LayerZeroTest {
+   contract TestLiquidations is LayerZeroTest, ExponentialNoError {
```
2. Add the following test in the same file:
```solidity
    function test_incorrect_maxClose_calculation_in_liquidateBorrowAllowedInternal_prevents_max_liquidation
    (
        uint256 supplyAmount, 
        uint256 borrowAmount, 
        uint256 newPrice
    ) public {
        // Bound inputs to reasonable values
        supplyAmount = bound(supplyAmount, 100e18, 1000e18);
        borrowAmount = bound(borrowAmount, 50e18, supplyAmount * 60 / 100); // Max 60% LTV
        newPrice = bound(newPrice, 1e16, 5e16); // 1-5% of original price

        // Supply token0 as collateral on Chain A
        (address tokenA, address lTokenA) = _supplyA(deployer, supplyAmount, 0);

        // Supply token1 as liquidity on Chain A from random wallet
        (address tokenB, address lTokenB) = _supplyA(address(1), supplyAmount, 1);

        vm.prank(deployer);
        coreRouterA.borrow(borrowAmount, tokenB);

        // Advance some blocks to accrue interest
        vm.warp(block.timestamp + 1000);
        vm.roll(block.number + 1000);

        // Simulate price drop of collateral (tokenA) only on first chain
        priceOracleA.setDirectPrice(tokenA, newPrice);
        // Simulate price drop of collateral (tokenA) only on second chain
        priceOracleB.setDirectPrice(
            lendStorageB.lTokenToUnderlying(lendStorageB.crossChainLTokenMap(lTokenA, block.chainid)), newPrice
        );

        // ACCRUE INTRERST
        LToken(lTokenB).accrueInterest();

        // get max repayable amount
        uint256 maxRepayAmount = lendStorageA.getMaxLiquidationRepayAmount(deployer, lTokenB, true);

        // retrieve borrow details
        LendStorage.BorrowMarketState memory borrowBalance = lendStorageA.getBorrowBalance(deployer, lTokenB);

        // calculate max amount as done in liquidateBorrowAllowedInternal()
        uint256 maxClose = mul_ScalarTruncate(
            Exp({mantissa: LendtrollerInterfaceV2(address(lendtrollerA)).closeFactorMantissa()}), borrowBalance.amount
        );

        // assert maxClose < maxRepayAmount: This means that liquidators attempting to close max as calculated by 
        // getMaxLiquidationRepayAmount() may face a revert due to internal flawed constraint
        assertLt(maxClose, maxRepayAmount);


        // Attempt liquidation
        vm.startPrank(liquidator);
        ERC20Mock(tokenB).mint(liquidator, maxRepayAmount);
        IERC20(tokenB).approve(address(coreRouterA), maxRepayAmount);

        // expect revert when liquidator attempts to repay maxRepayAmount
        vm.expectRevert(bytes("Too much repay"));
        coreRouterA.liquidateBorrow(deployer, maxRepayAmount, lTokenA, tokenB);

        vm.stopPrank();
    }
```


## Mitigation
Modify as follows:
```diff
        else {
            --snip--
+           currentBorrow = 
+               (borrowBalance.amount * uint256(LTokenInterface(lTokenBorrowed).borrowIndex())) /
+                borrowBalance.borrowIndex;
            uint256 maxClose = mul_ScalarTruncate(
-               Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), borrowBalance.amount
+               Exp({mantissa: LendtrollerInterfaceV2(lendtroller).closeFactorMantissa()}), currentBorrow
            );

            require(repayAmount <= maxClose, "Too much repay");
        }
```