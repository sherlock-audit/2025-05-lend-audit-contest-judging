Cheerful Taupe Ladybug

High

# Incorrect Cross-Chain Borrow Calculation Allows Excessive Borrowing and Redemption

### Summary

An incorrect check inside the [LendStorage.borrowWithInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L478) function allows cross-chain borrowers to borrow additional assets and/or redeem collateral beyond their permissible limits. .

### Root Cause

The `LendStorage.borrowWithInterest()` function is responsible for calculating the total amount a user has borrowed, including interest, for a specific token. When dealing with `crossChainCollaterals` (i.e., collateral provided on the current chain to support a borrow on another chain, or collateral provided on another chain for a borrow on the current chain), the function attempts to sum up these borrowed amounts.

The critical flaw lies in the `else` block, which processes the `crossChainCollaterals` array:
```solidity
function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
    address _token = lTokenToUnderlying[_lToken];
    uint256 borrowedAmount;

    Borrow[] memory borrows = crossChainBorrows[borrower][_token];
    Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

    require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
    // Only one mapping should be populated:
    if (borrows.length > 0) {
        for (uint256 i = 0; i < borrows.length; i++) {
            if (borrows[i].srcEid == currentEid) {
                borrowedAmount +=
                    (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
            }
        }
    } else {
        for (uint256 i = 0; i < collaterals.length; i++) {
            // Only include a cross-chain collateral borrow if it originated locally.
@>          if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
                borrowedAmount +=
                    (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
            }
        }
    }
    return borrowedAmount;
}
```
The condition `if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid)` checks if the destination endpoint ID (`destEid`) of the collateral record and the source endpoint ID (`srcEid`) are both the current chain ID (`currentEid`).

For collateral supporting a cross-chain borrow (e.g., collateral on Chain B for a borrow taken on Chain A), the `destEid` would be Chain B (`currentEid`) where the collateral is held, and the `srcEid` would be Chain A where the borrow originated.
The current logic `collaterals[i].srcEid == currentEid` incorrectly requires the borrow to have also originated on the current chain (Chain B in this example).
This means that if a user provides collateral on Chain B (`destEid == currentEid`) for a borrow initiated on Chain A (`srcEid == Chain A`), the condition `collaterals[i].srcEid == currentEid` will be false. Consequently, `borrowedAmount` for this legitimate cross-chain collateralized position will not be incremented, effectively being treated as `0` by this function on Chain B.

This miscalculation leads the protocol to believe the user has borrowed less (against their cross-chain collateral) than they actually have, thereby allowing them to exceed their true borrowing limits or withdraw collateral that should be locked.

### Internal Pre-conditions

1. Admin needs to call `CoreRouter.setCrossChainRouter()` and set a cross chain router to be used.

### External Pre-conditions

none

### Attack Path

1. User initiates a cross chain borrow of Token A on Chain A to Chain B
2. User is able to borrow/redeem more of Token A than they should be able to as the borrowed amount is calculated incorrectly on Chain B

### Impact

The protocol is exposed to bad debt. Users can become over-leveraged by borrowing more than their collateral legitimately allows or by withdrawing collateral that is meant to secure existing cross-chain borrows. This can lead to insufficient collateralization for user positions, potentially resulting in losses for the protocol and its liquidity providers if these positions become insolvent.

### PoC

_No response_

### Mitigation

Consider changing the condition to which it checks if the collateral's destination (where it is accounted for) is the current chain.

```diff
function borrowWithInterest(address borrower, address _lToken) public view returns (uint256) {
    address _token = lTokenToUnderlying[_lToken];
    uint256 borrowedAmount;

    Borrow[] memory borrows = crossChainBorrows[borrower][_token];
    Borrow[] memory collaterals = crossChainCollaterals[borrower][_token];

    require(borrows.length == 0 || collaterals.length == 0, "Invariant violated: both mappings populated");
    // Only one mapping should be populated:
    if (borrows.length > 0) {
        for (uint256 i = 0; i < borrows.length; i++) {
            if (borrows[i].srcEid == currentEid) {
                borrowedAmount +=
                    (borrows[i].principle * LTokenInterface(_lToken).borrowIndex()) / borrows[i].borrowIndex;
            }
        }
    } else {
        for (uint256 i = 0; i < collaterals.length; i++) {
            // Only include a cross-chain collateral borrow if it originated locally.
-           if (collaterals[i].destEid == currentEid && collaterals[i].srcEid == currentEid) {
+           if (collaterals[i].destEid == currentEid) {
                borrowedAmount +=
                    (collaterals[i].principle * LTokenInterface(_lToken).borrowIndex()) / collaterals[i].borrowIndex;
            }
        }
    }
    return borrowedAmount;
}
```