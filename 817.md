Skinny Charcoal Chinchilla

High

# User's cant redeem the correct amount due to the function using outdated collateral values.

### Summary

When users redeem via [CoreRouter.sol::redeem()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L100), the check of how much the user is able to redeem is done using outdated values. This gives users less redeeming power as the amount user can redeem is compared with outdated borrowing and collateral value. The token and damount the user is redeeming is passed into `LendStorage.sol::getHypotheticalAccountLiquidityCollateral()` which will return the USD value using outdated exchange rate, making the amount user can redeem outdated. 

```solidity 
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        require(_amount > 0, "Zero redeem amount");
        require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");

        (uint256 borrowed, uint256 collateral) = 
@>          lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);

        require(collateral >= borrowed, "Insufficient liquidity");
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
        uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
        require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");
        IERC20(_token).transfer(msg.sender, expectedUnderlying); 
        lendStorage.distributeSupplierLend(_lToken, msg.sender);
        uint256 newInvestment = lendStorage.totalInvestment(msg.sender, _lToken) - _amount;
        lendStorage.updateTotalInvestment(msg.sender, _lToken, newInvestment);
        if (newInvestment == 0) {
            lendStorage.removeUserSuppliedAsset(msg.sender, _lToken);
        }
        emit RedeemSuccess(msg.sender, _lToken, expectedUnderlying, _amount);
        return 0;
    }
```

The [LendStorage.sol::getHypotheticalAccountLiquidityCollateral()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/LendStorage.sol#L385) calculates and return the USD value of both the total borrows and total collateral. However since the redeem is not updated via [Ltoken.sol::accrueInterest()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L324), the comparison does not reflect the real value of the collateral. [Ltoken.sol::exchangeRateStored()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LToken.sol#L281-L307) returns the last updated exchange rate.

```solidity 
    function getHypotheticalAccountLiquidityCollateral(
        address account,
        LToken lTokenModify,
        uint256 redeemTokens,
        uint256 borrowAmount
    ) public view returns (uint256, uint256) {
        AccountLiquidityLocalVars memory vars;
        address[] memory suppliedAssets = userSuppliedAssets[account].values();
        address[] memory borrowedAssets = userBorrowedAssets[account].values();
        for (uint256 i = 0; i < suppliedAssets.length;) {
            LToken asset = LToken(suppliedAssets[i]);
            uint256 lTokenBalanceInternal = totalInvestment[account][address(asset)];
            vars.collateralFactor =
                Exp({mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(asset))});
@>          vars.exchangeRate = Exp({mantissa: asset.exchangeRateStored()}); 
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
@>          vars.sumCollateral =
                mul_ScalarTruncateAddUInt(vars.tokensToDenom, lTokenBalanceInternal, vars.sumCollateral);

            unchecked {
                ++i;
            }
        }
        for (uint256 i = 0; i < borrowedAssets.length;) {
            LToken asset = LToken(borrowedAssets[i]);
            uint256 totalBorrow = borrowWithInterestSame(account, address(asset));
            totalBorrow += borrowWithInterest(account, address(asset));
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(asset);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
@>          vars.sumBorrowPlusEffects = 
                mul_ScalarTruncateAddUInt(vars.oraclePrice, totalBorrow, vars.sumBorrowPlusEffects);

            unchecked {
                ++i;
            }
        }
        if (address(lTokenModify) != address(0)) {
            vars.oraclePriceMantissa = UniswapAnchoredViewInterface(priceOracle).getUnderlyingPrice(lTokenModify);
            vars.oraclePrice = Exp({mantissa: vars.oraclePriceMantissa});
            if (redeemTokens > 0) {
                vars.collateralFactor = Exp({
                    mantissa: LendtrollerInterfaceV2(lendtroller).getCollateralFactorMantissa(address(lTokenModify))
                });
@>              vars.exchangeRate = Exp({mantissa: lTokenModify.exchangeRateStored()}); 
                vars.tokensToDenom = mul_(mul_(vars.collateralFactor, vars.exchangeRate), vars.oraclePrice);
@>              vars.sumBorrowPlusEffects = 
                    mul_ScalarTruncateAddUInt(vars.tokensToDenom, redeemTokens, vars.sumBorrowPlusEffects);
            }
            if (borrowAmount > 0) {
                vars.sumBorrowPlusEffects =
                    mul_ScalarTruncateAddUInt(vars.oraclePrice, borrowAmount, vars.sumBorrowPlusEffects);
            }
        }
        return (vars.sumBorrowPlusEffects, vars.sumCollateral);
    }
```


### Root Cause

The comparison is done using outdated values, causing the user to gain less redeeming power.

```solidity 
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {
        address _token = lendStorage.lTokenToUnderlying(_lToken);
        require(_amount > 0, "Zero redeem amount");
        require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");

        (uint256 borrowed, uint256 collateral) = 
@>          lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);

        require(collateral >= borrowed, "Insufficient liquidity");
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
        uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
        require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");
        IERC20(_token).transfer(msg.sender, expectedUnderlying); 
        lendStorage.distributeSupplierLend(_lToken, msg.sender);
        uint256 newInvestment = lendStorage.totalInvestment(msg.sender, _lToken) - _amount;
        lendStorage.updateTotalInvestment(msg.sender, _lToken, newInvestment);
        if (newInvestment == 0) {
            lendStorage.removeUserSuppliedAsset(msg.sender, _lToken);
        }
        emit RedeemSuccess(msg.sender, _lToken, expectedUnderlying, _amount);
        return 0;
    }
```

### Internal Pre-conditions

none

### External Pre-conditions

none

### Attack Path

none

### Impact


The user cannot redeem the true value of the collateral. 

### PoC

none

### Mitigation


Update both the borrows and collaterals of the user before any calculations / comparisons.

```diff
    function redeem(uint256 _amount, address payable _lToken) external returns (uint256) {

        
+       address[] memory suppliedAssets = userSuppliedAssets[msg.sender].values();
+       address[] memory borrowedAssets = userBorrowedAssets[msg.sender].values();

+       for (uint256 i = 0; i < suppliedAssets.length; i++) {
+           LTokenInterface(suppliedAssets[i]).accrueInterest();
        }

+       for (uint256 i = 0; i < borrowedAssets.length; i++) {
+           LTokenInterface(borrowedAssets[i]).accrueInterest();
        }


        address _token = lendStorage.lTokenToUnderlying(_lToken);
        require(_amount > 0, "Zero redeem amount");
        require(lendStorage.totalInvestment(msg.sender, _lToken) >= _amount, "Insufficient balance");

        (uint256 borrowed, uint256 collateral) = 
            lendStorage.getHypotheticalAccountLiquidityCollateral(msg.sender, LToken(_lToken), _amount, 0);

        require(collateral >= borrowed, "Insufficient liquidity");
        uint256 exchangeRateBefore = LTokenInterface(_lToken).exchangeRateStored();
        uint256 expectedUnderlying = (_amount * exchangeRateBefore) / 1e18;
        require(LErc20Interface(_lToken).redeem(_amount) == 0, "Redeem failed");
        IERC20(_token).transfer(msg.sender, expectedUnderlying); 
        lendStorage.distributeSupplierLend(_lToken, msg.sender);
        uint256 newInvestment = lendStorage.totalInvestment(msg.sender, _lToken) - _amount;
        lendStorage.updateTotalInvestment(msg.sender, _lToken, newInvestment);
        if (newInvestment == 0) {
            lendStorage.removeUserSuppliedAsset(msg.sender, _lToken);
        }
        emit RedeemSuccess(msg.sender, _lToken, expectedUnderlying, _amount);
        return 0;
    }
```