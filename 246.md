Creamy Pine Cow

High

# Users can claim accrued LEND multiple times to drain the contract balance

### Summary

The user's accrued `LEND` balance is not reset to zero after they claim, thus they can claim their balance multiple times to drain the contract of all the `LEND` tokens.

### Root Cause

Users can claim their accrued `LEND` balances by calling [CoreRouter::claimLend()](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/713372a1ccd8090ead836ca6b1acf92e97de4679/Lend-V2/src/LayerZero/CoreRouter.sol#L370-L408):

```solidity
    function claimLend(address[] memory holders, LToken[] memory lTokens, bool borrowers, bool suppliers) external {
        ...

        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
                grantLendInternal(holders[j], accrued);
            }
            unchecked {
                ++j;
            }
        }
    }

    /**
     * @dev Grants LEND tokens to a user
     * @param user The recipient
     * @param amount The amount to grant
     * @return uint256 Remaining amount if grant failed
     */
    function grantLendInternal(address user, uint256 amount) internal returns (uint256) {
        address lendAddress = LendtrollerInterfaceV2(lendtroller).getLendAddress();
        uint256 lendBalance = IERC20(lendAddress).balanceOf(address(this));

        if (amount > 0 && amount <= lendBalance) {
            IERC20(lendAddress).safeTransfer(user, amount);
            return 0;
        }
        return amount;
    }
```

In the above code snippet, the accrued `LEND` is sent to the user by `CoreRouter::grantLendInternal()`, which only sends the LEND if the contract has enough of it. The problem is that when the contract has enough `LEND` balance to send the user their accrued amount, their `LEND` balances are not reset. Thus a user can claim multiple times draining the contract of all its `LEND`.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. Attacker utilizes the protocol as normal and accrues some `LEND`.
2. Now the attacker can call `CoreRouter::claimLend()` as many times as they desire to repeatedly claim their balance.
3. Since the attacker's accrued `LEND` balance is not cleared after a successful transfer, they can repeat this to drain the contract `LEND` balance.

### Impact

Loss of funds (`LEND` token).

### PoC

_No response_

### Mitigation

Introduce a function to reset a user's `LEND` balance in `LendStorage.sol`:

```solidity
function resetAccruedLend(address user, uint256 amount) public onlyAuthorized {
    lendAccrued[user] = amount;
}
```

Then call this in `CoreRouter::claimLend()` if the user's `LEND` was actually transferred:

```solidity
    function claimLend(address[] memory holders, LToken[] memory lTokens, bool borrowers, bool suppliers) external {
        ...

        for (uint256 j = 0; j < holders.length;) {
            uint256 accrued = lendStorage.lendAccrued(holders[j]);
            if (accrued > 0) {
                uint256 amountLeft = grantLendInternal(holders[j], accrued);
                if (amountLeft == 0) {  // @audit-fix: `grantLendInternal` returns 0 if the contract actually send the LEND to the user
                    lendStorage.resetAccruedLend(holders[j], 0);
                }
            }
            unchecked {
                ++j;
            }
        }
    }
```