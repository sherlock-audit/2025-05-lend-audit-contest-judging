Rural Brick Lynx

High

# Missing On-Chain Collateral Seize Call in `liquidateBorrowInternal`

In the function [liquidateBorrowInternal](https://github.com/sherlock-audit/2025-05-lend-audit-contest/blob/main/Lend-V2/src/LayerZero/CoreRouter.sol#L256-L276), after computing the number of collateral tokens to seize via

```solidity
(amountSeizeError, seizeTokens) = LendtrollerInterfaceV2(lendtroller)
    .liquidateCalculateSeizeTokens(borrowedlToken, lTokenCollateral, repayAmount);
```

the contract updates its internal `LendStorage` balances for both borrower and liquidator but does not invoke the cToken’s `seize` method (e.g., `CErc20(lTokenCollateral).seize(liquidator, borrower, seizeTokens)`), leaving the borrower’s on-chain cToken balance unchanged and the liquidator unable to access the collateral held in the cToken contract.


```solidity
 function _setupLiquidationTest() internal returns (address borrower, address liquidator, address token, address lToken) {
        borrower = makeAddr("borrower");
        liquidator = makeAddr("liquidator");
        
        // Mint initial tokens to both users
        uint256 initialCollateral = 1000e18;
        uint256 borrowAmount = 800e18; // 80% of collateral to make position liquidatable
        
        vm.startPrank(deployer);
        (token, lToken) = _supply(initialCollateral);
        vm.stopPrank();
        
        // Transfer tokens to borrower and liquidator
        ERC20Mock(token).mint(borrower, initialCollateral);
        ERC20Mock(token).mint(liquidator, initialCollateral);
        
        // Borrower supplies collateral and borrows
        vm.startPrank(borrower);
        IERC20(token).approve(address(coreRouter), initialCollateral);
        coreRouter.supply(initialCollateral, token);
        coreRouter.borrow(borrowAmount, token);
        vm.stopPrank();
        
        // Make position liquidatable by dropping price
        priceOracle.setDirectPrice(token, 0.8e18); // 20% price drop
    }

    function test_liquidation_missing_seize_functionality() public {
        // Setup test
        (address borrower, address liquidator, address token, address lToken) = _setupLiquidationTest();
        
        // Calculate liquidation amount
        uint256 repayAmount = (lendStorage.borrowWithInterestSame(borrower, lToken) * 
            LendtrollerInterfaceV2(address(lendtroller)).closeFactorMantissa()) / (2 * 1e18);
        
        // Get initial balances
        uint256 borrowerBefore = LTokenInterface(lToken).balanceOf(borrower);
        uint256 liquidatorBefore = LTokenInterface(lToken).balanceOf(liquidator);
        uint256 borrowerStorageBefore = lendStorage.totalInvestment(borrower, lToken);
        uint256 liquidatorStorageBefore = lendStorage.totalInvestment(liquidator, lToken);
        
        // Liquidator performs liquidation
        vm.startPrank(liquidator);
        IERC20(token).approve(address(coreRouter), repayAmount);
        coreRouter.liquidateBorrow(borrower, repayAmount, lToken, token);
        vm.stopPrank();
        
        // Get final balances
        uint256 borrowerAfter = LTokenInterface(lToken).balanceOf(borrower);
        uint256 liquidatorAfter = LTokenInterface(lToken).balanceOf(liquidator);
        uint256 borrowerStorageAfter = lendStorage.totalInvestment(borrower, lToken);
        uint256 liquidatorStorageAfter = lendStorage.totalInvestment(liquidator, lToken);
        
        // Calculate storage changes
        uint256 borrowerStorageChange = borrowerStorageBefore - borrowerStorageAfter;
        uint256 liquidatorStorageChange = liquidatorStorageAfter - liquidatorStorageBefore;
        
        // Verify that on-chain balances remain unchanged
        assertEq(
            borrowerBefore,
            borrowerAfter,
            "Borrower's on-chain balance should remain unchanged"
        );
        assertEq(
            liquidatorBefore,
            liquidatorAfter,
            "Liquidator's on-chain balance should remain unchanged"
        );
        
        // Verify that LendStorage accounting was updated
        assertGt(
            borrowerStorageChange,
            0,
            "Borrower's storage balance should be reduced"
        );
        assertGt(
            liquidatorStorageChange,
            0,
            "Liquidator's storage balance should be increased"
        );
        
        // Verify that the storage changes are within 5% of each other
        // This accounts for potential fees or rounding differences
        uint256 maxDifference = borrowerStorageChange / 20; // 5% of borrower's change
        assertApproxEqAbs(
            borrowerStorageChange,
            liquidatorStorageChange,
            maxDifference,
            "Storage changes should be within 5% of each other"
        );
    }
}
```
```solidity
forge test -vvv --match-test test_liquid
ation_missing_seize_functionality
[⠒] Compiling...
No files changed, compilation skipped

Ran 1 test for test/TestBorrowing.t.sol:TestBorrowing
[PASS] test_liquidation_missing_seize_functionality() (gas: 1223641)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 20.53ms (5.84ms CPU time)

Ran 1 test suite in 27.76ms (20.53ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

**Impact:**
Collateral corresponding to each successful liquidation becomes permanently unmovable on-chain while internal records diverge, resulting in locked funds that neither the borrower nor liquidator can withdraw.

**Mitigation:**
After calculating `seizeTokens`, immediately call the cToken’s `seize(liquidator, borrower, seizeTokens)` function and require its successful execution before updating `LendStorage`.
